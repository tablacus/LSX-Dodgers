
;	LSX-Dodgers IO
;	PC-8801mkIISR

_SYS04	EQU	_ERROR		;(BDOS)外部出力
_SYS05	EQU	_ERROR		;(BDOS)プリンタ出力
_SYS1C	EQU	_ERROR		;(BDOS)ディスクの書き込み禁止化
_SYS1E	EQU	_ERROR		;(BDOS)ファイル属性の設定
_SYS20	EQU	_ERROR		;(BDOS)利用者番号の設定と読み出し
_SYS2E	EQU	_ERROR		;(BDOS)ベリファイ・フラグの設定

INT	EQU	0
INTCTCE	EQU	0
INTCTC2	EQU	0

BDOS_RF:
	IN	A,(032H)
	OR	010H		;高速RAMアクセスモードOFF
	OUT	(032H),A
	JP	BDOS0

KEYSET:
KEYBS:
	OR	A
	RET	Z
KEYBS1:
	CALL	KEYBC_IFBREAK
	PUSH	HL
	PUSH	AF
	LD	HL,(_KEYPS)
	INC	L
	LD	A,L
	CP	H
	JR	Z,POP_AF_HL_SCF_RET
	LD	(_KEYPS),A
	LD	H,KEYBF/256
	POP	AF
	LD	(HL),A
	POP	HL
	RET
KEYBC_IFBREAK:
	CP	3
	RET	NZ
KEYBC:
	PUSH	HL
	LD	HL,0
	LD	(_KEYPS),HL
	POP	HL
	RET

POP_AF_HL_SCF_RET:
	POP	AF
	POP	HL
	SCF
	RET

_SYS01:		;(BDOS)コンソール入力
	CALL	_SYS07
MSG_A:
	PUSH	AF
	PUSH	DE
	LD	E,A
	CALL	_SYS02
	POP	DE
	POP	AF
	RET

_SYS02:		;(BDOS)コンソール出力
	CALL	BREAK
	JR	PRINTX

_SYS06:		;(BDOS)コンソール直接入出力
	LD	A,E
	INC	A
	JP	Z,_INKEY
PRINTX:
	JP	_PRINT

_SYS08:		;(BDOS)エコーなしコンソール入力
	CALL	_SYS07
	JR	BREAK1
BREAK:
	CALL	CHKEY
BREAK1:
	CP	3		;CTRL+C
	JP	Z,CPM_BOOT
	CP	013H		;CTRL+S
	RET	NZ

PAUSE:
	CALL	KEYBC

FLGET:
	CALL	_FFLUSH
	PUSH	HL
	LD	HL,(_TXADR)
	CALL	CURSOR_ON
FLGET1:
	CALL	_INKEY
	JR	Z,FLGET1
	CALL	CURSOR_OFF
	POP	HL
	RET

_SYS0A:		;(BDOS)文字列入力
	PUSH	BC
	PUSH	HL
	PUSH	DE
	CALL	GETL
	LD	DE,KBUF
	POP	HL
	PUSH	HL
	LD	C,0
	JR	NC,SAX0
	INC	HL
	INC	HL
	JR	SAX4
SAX0:
	LD	B,(HL)
	INC	HL
SAX1:
	INC	HL
	LD	A,(DE)
	INC	DE
	OR	A
	JR	NZ,SAX2
SAX4:
	LD	(HL),00DH
	JR	SAX3
SAX2:
	LD	(HL),A
	INC	C
	DJNZ	SAX1
SAX3:
	POP	DE
	LD	A,C
	INC	DE
	LD	(DE),A
	DEC	DE
	POP	HL
	POP	BC
	AND	A
	RET

_SYS0B:		;(BDOS)コンソール状態のチェック
	CALL	BREAK
	LD	A,(_KEYD)
	OR	A
	RET	Z
CONSTX:
	CALL	_FFLUSH
	PUSH	HL
	LD	HL,(_KEYPS)
	LD	A,H
	XOR	L
	POP	HL
	CALL	Z,CHKEY
	ADD	A,0FFH
	SBC	A,A
	RET

_SYS2A:		;(BDOS)日付の獲得
	PUSH	BC
	CALL	RTCREAD
	LD	A,(RTC_YY)
	CALL	BCD
	LD	L,A
	LD	H,0
;	LD	DE,1900		;0076CH
;	CP	90		;90以上は1900年代 1990-1999
;	JR	NC,RDDATE1	;90未満は2000年代 2000-2089
;	LD	E,0D0H		;2000 & 0FFH D=007H
	LD	DE,2000		;すべて2000年代で処理 2000-2099
RDDATE1:
	ADD	HL,DE
	LD	A,(RTC_MW)
	LD	D,A
	LD	A,(RTC_DD)
	CALL	BCD
	LD	E,A
	LD	A,D
	LD	B,4
RDDATE2:
	SRL	D
	DJNZ	RDDATE2
	POP	BC
	AND	7
	RET

_SYS2C:		;(BDOS)時刻の獲得
	PUSH	BC
	CALL	RTCREAD
	LD	A,(RTC_TT)
	CALL	BCD
	LD	H,A
	LD	A,(RTC_MM)
	CALL	BCD
	LD	L,A
	LD	A,(RTC_SS)
	LD	D,A
	POP	BC
	XOR	A
	RET

RTCREAD:
	LD	C,0F9H
	LD	A,(WK40)
	LD	E,A
	LD	A,3		;コマンド３：タイムリードにセット
	CALL	RTCCMD
	LD	A,1		;コマンド１：レジスタシフトにセット
	CALL	RTCCMD
	LD	B,6
	LD	HL,RTC_SS
RTCREAD1:
	PUSH	BC
	LD	B,8
RTCREAD2:
	IN	A,(40H)		;システムモードセンス BIT4:CDI
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	RR	(HL)
	CALL	RTCCLK
	DJNZ	RTCREAD2
	POP	BC
	DEC	HL
	DJNZ	RTCREAD1
	XOR	A
	CALL	RTCCMD
	LD	A,E
	LD	(WK40),A
	RET

RTCCMD:
	LD	B,4
	RLCA
	RLCA
	RLCA
RTCCMD1:
	PUSH	AF
	OR	7
	OUT	(10H),A		;プリンタデータ出力 兼 カレンダクロック(μPD1990AC)出力
	CALL	RTCCLK
	POP	AF
	RRCA
	DJNZ	RTCCMD1
	LD	A,7
	OUT	(10H),A		;プリンタデータ出力 兼 カレンダクロック(μPD1990AC)出力
	LD	A,E
	AND	C
	OR	2
	OUT	(40H),A		;ストローブポート
	AND	C
	NOP
	OUT	(40H),A		;ストローブポート
	LD	E,A
	LD	B,13
RTCCMD3:
	DJNZ	RTCCMD3
	RET

RTCCLK:
	LD	A,E
	AND	C
	OR	04H
	OUT	(40H),A
	AND	C
	NOP
	OUT	(40H),A
	LD	E,A
	RET

;------------------------
;BCDの10進数化
;in
;	A  : BCD
;out
;	A  : 10進数
;------------------------
BCD:
	LD	C,A
	AND	0F0H	;10の位
	RRCA
	LD	B,A
	RRCA
	RRCA
	ADD	A,B
	LD	B,A
	LD	A,C
	AND	00FH	;1の位
	ADD	A,B
	RET

ESC1:
	LD	A,E
	CP	'A'
	JP	Z,CTRL1E
	CP	'B'
	JP	Z,CTRL1F
	CP	'C'
	JP	Z,CTRL1C
	CP	'D'
	JP	Z,CTRL1D
	CP	'E'
	JR	Z,CT0CX
	CP	'j'
CT0CX:
	JP	Z,CTRL0C
	CP	'H'
	JP	Z,CTRL0B
	CP	'J'
	JP	Z,CTRL06
	CP	'K'
	JP	Z,CTRL05
	CP	'L'
	JP	Z,CTRL0E
	CP	'T'
	JP	Z,CTRL05
	CP	'x'
	JR	Z,ESC1X
	CP	'y'
	JR	NZ,ESC1Y
ESC1X:
	LD	(HL),1
ESC1Y:
	CP	'='
	JR	Z,ESC1W
	CP	'Y'
	JR	NZ,ESC1Z
ESC1W:
	LD	(HL),2
ESC1Z:
	CP	020H
	JR	C,ESC1C
	ADD	A,A
	RET	NC
ESC1C:
	POP	HL
	JR	ANK

ESC:
	LD	HL,PRINTE5
	PUSH	HL
	LD	HL,ESCFLG
	LD	(HL),0
	DEC	A
	JR	Z,ESC1
	DEC	A
	JR	NZ,ESC2
	LD	(HL),3
	LD	A,E
	SUB	020H
	LD	(ESCYP+1),A
	RET
ESC2:
	DEC	A
	RET	NZ
ESCYP:	LD	H,0
	LD	A,E
	SUB	020H
	LD	L,A
	JP	_LOC

PRINT:
	PUSH	BC
	PUSH	HL
ESCFLG	EQU	$+1
	LD	A,000H		;自己書き換え
	OR	A
	JR	NZ,ESC

	LD	A,01FH
	CP	E
	JR	NC,CTRL
INSFLG	EQU	$
	LD	BC,INSERT	;自己書き換え
ANK:
	CALL	LOC0
	IN	A,(032H)
	PUSH	AF
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A
	LD	(HL),E
	POP	AF
	OUT	(032H),A
	CALL	CTRL1C
PRINTE5:
	POP	HL
	POP	BC
	XOR	A
	RET

CTRL:
	LD	A,E
	ADD	A,A
	OR	080H
	LD	L,A
	LD	H,CTRLTB/256
	LD	A,(HL)
	INC	L
	LD	H,(HL)
	LD	L,A
	CALL	JP_HL
	JR	PRINTE5

CTRL1C:
	LD	HL,(_TXADR)
	INC	L
PRINTE3:
	LD	A,(_WIDTH)
	DEC	A
	CP	L
	JR	NC,SET_TXADR_HL
	LD	L,0
	INC	H
PRINTE8:
	LD	A,(_LINE)
	CP	H
	JR	Z,PRINT2
LOC:
SET_TXADR_HL:
	LD	(_TXADR),HL
CTRL00:
	RET

PRINT2:
	CALL	SCR
	DEC	H
	JR	SET_TXADR_HL

CTRL08:
	LD	A,(INSFLG)
	CP	0CDH		;CALL ????
	JR	Z,CTRL02
CTRL1D:
	LD	HL,(_TXADR)
	LD	A,H
	OR	L
	RET	Z
	LD	A,L
	OR	A
	JR	Z,CTRL1DX1
	DEC	L
	JR	SET_TXADR_HL
CTRL1DX1:
	LD	A,(_WIDTH)
	DEC	A
	LD	L,A
	DEC	H
	JR	SET_TXADR_HL
CTRL1E:
	LD	HL,(_TXADR)
	LD	A,H
	OR	A
	RET	Z
	DEC	H
	JR	SET_TXADR_HL

CTRL09:
	LD	HL,(_TXADR)
	LD	A,L
	AND	0F8H
	ADD	A,8
	LD	L,A
	JR	PRINTE3

CTRL02:
	LD	A,(_TXADR)
	OR	A
	JR	Z,CTRL1D
	CALL	CTRL1D
	LD	HL,(_TXADR)
	LD	A,(_WIDTH)
	LD	C,A
	SUB	L
	LD	B,A
	LD	A,C
	DEC	A
	LD	L,A
	CALL	LOC1

	IN	A,(032H)
	PUSH	AF
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A

	LD	A,020H
C8X1:
	LD	C,(HL)
	LD	(HL),A
	DEC	HL
	LD	A,C
	DJNZ	C8X1

	POP	AF
	OUT	(032H),A
	RET

CTRL05:
	CALL	_POS
	LD	HL,(_TXADR)
	LD	A,(_WIDTH)
	SUB	L
	LD	B,A
	CALL	LOC0
CLLINE:
	IN	A,(032H)
	PUSH	AF
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A
	LD	A,020H
CLLINE1:
	LD	(HL),A
	INC	HL
	DJNZ	CLLINE1

	POP	AF
	OUT	(032H),A
	RET

CTRL0D:
	XOR	A
	LD	(_TXADR),A
	RET

CTRL12:
	LD	HL,INSFLG
	LD	A,(HL)
	XOR	0CCH		;CD:CALL ???? 1:LD BC,????
	LD	(HL),A
	RET

CTRL0B:
	LD	HL,0
	LD	(_TXADR),HL
	RET

CTRL1B:
	LD	A,1
	LD	(ESCFLG),A
	RET
LOC0:
	LD	HL,(_TXADR)
LOC1:
	PUSH	BC
	PUSH	DE
	LD	C,L
	LD	B,8
	LD	E,H
	LD	HL,120*256
	LD	D,L
LOC2:
	ADD	HL,HL
	JR	NC,LOC3
	ADD	HL,DE
LOC3:
	DJNZ	LOC2
	ADD	HL,BC
	LD	DE,(TVRAMTOP)
	ADD	HL,DE
	POP	DE
	POP	BC
	RET

CONOUT1:
	CALL	CURSOR_ON
	LD	E,C
	CALL	_PRINT
	LD	A,E
	CP	00AH
	JR	NZ,CURSOR_OFF
	CALL	CTRL0D
	CALL	CTRL05
CURSOR_OFF:
	PUSH	AF
	LD	A,080H
CURSOR0:
	OUT	(051H),A
	LD	A,(_TXADR)
	OUT	(050H),A
	LD	A,(_TXADR+1)
	OUT	(050H),A
	POP	AF
	RET
CURSOR_ON:
	PUSH	AF
	LD	A,081H
	JR	CURSOR0

CTRL07:
	LD	A,(WK40)
	PUSH	AF
	OR	20H
	OUT	(040H),A
	LD	B,16
C07X1:
	CALL	VBLANK
	DJNZ	C07X1
	POP	AF
	OUT	(040H),A
	RET

INSERT:
	LD	HL,(_TXADR)
	LD	A,(_WIDTH)
	SUB	L
	LD	B,A

	CALL	LOC0

	IN	A,(032H)
	PUSH	AF
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A

	LD	A,020H
INS1:
	LD	C,(HL)
	LD	(HL),A
	LD	A,C
	INC	HL
	DJNZ	INS1

	POP	AF
	OUT	(032H),A
	RET

CTRL0C:
	CALL	CTRL0B
CTRL06:
	LD	HL,(_TXADR)
CLS1:
	IN	A,(032H)
	PUSH	AF
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A
CLS2:
	PUSH	HL
	LD	A,(_WIDTH)
	SUB	L
	LD	B,A
	CALL	LOC1
	LD	A,020H
CLS3:
	LD	(HL),A
	INC	HL
	DJNZ	CLS3
	POP	HL
	LD	L,0
	INC	H
	LD	A,(_LINE)
	CP	H
	JR	NZ,CLS2
	POP	AF
	OUT	(032H),A
	RET

CTRL04:
	CALL	_POS
	LD	A,L
	OR	A
	RET	Z
CTRL03:
	CALL	CTRL0D
CTRL0A:
CTRL1F:
	LD	HL,(_TXADR)
	INC	H
	JP	PRINTE8

CTRL0E:
	CALL	_POS
	LD	A,H
	JR	SCR1
SCR:
	LD	A,(_LINE)
	DEC	A
SCR1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	AF
	LD	HL,0
	CALL	LOC1
	EX	DE,HL
	LD	HL,0100H
	CALL	LOC1
	POP	BC
	IN	A,(032H)
	PUSH	AF
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A
	LD	A,B
	OR	A
SCRUP1:
	JR	Z,SCRCL
	PUSH	HL
	PUSH	DE
	LD	B,8
	LD	E,A
	LD	HL,120*256
	LD	D,L
SCRH2:
	ADD	HL,HL
	JR	NC,SCRH3
	ADD	HL,DE
SCRH3:
	DJNZ	SCRH2
	LD	C,L
	LD	B,H
	POP	DE
	POP	HL
	LDIR

SCRCL:
	POP	AF
	OUT	(032H),A

	LD	A,(_WIDTH)
	LD	B,A
	EX	DE,HL
	CALL	CLLINE
	POP	HL
	POP	DE
	POP	BC
	RET
POS:
	LD	HL,(_TXADR)
	RET

GETL:
	CALL	_POS
	PUSH	HL
	LD	A,0CDH		;CALL ????
	LD	(INSFLG),A
GETL1:
	CALL	_SYS08
	CP	9
	JR	NZ,GETL1V
	LD	HL,KBUF
	CALL	MSX
	JR	GETL1
GETL1V:
	LD	E,A
	CALL	_PRINT

	LD	A,E
	CP	00DH
	JR	NZ,GETL1
	LD	A,1		;LD BC,????
	LD	(INSFLG),A
	LD	DE,KBUF
	LD	A,(_WIDTH)
	LD	B,A
	CALL	ZERO_MEMORY_DE

	POP	DE
	CALL	_POS
	LD	L,E
	LD	A,(_WIDTH)
	SUB	L
	LD	D,A
	CALL	LOC1
	LD	C,L
	LD	A,H
	OR	030H
	LD	B,A
	LD	E,D
	LD	HL,KBUF
GETL2:
	CALL	_SCRN
	INC	BC
	LD	(HL),A
	INC	HL
	DEC	E
	JR	NZ,GETL2
GETL3:
	DEC	HL
	LD	A,(HL)
	CP	021H
	RET	NC
	LD	(HL),0
	DEC	D
	JR	NZ,GETL3
	RET

INKEY:
	PUSH	HL
	LD	HL,(_KEYPS)
	LD	A,H
	XOR	L
	JR	Z,GETKEY
	LD	A,H
	INC	A
	LD	(_KEYPS+1),A
	LD	L,A
	LD	H,KEYBF/256
	LD	A,(HL)
	POP	HL
INKEYD:
	OR	A
	LD	(_KEYD),A
	RET
GETKEY:
	LD	A,(_KEYD)
	OR	A
	LD	L,A
GKSMC:	LD	H,16
GETKEY1:
	CALL	CHKEY		;キーを離すまで待つ
	JR	Z,GETKEY2
	CP	L
	JR	NZ,GETKEY2
	IN	A,(040H)
	BIT	5,A
	JR	Z,GETKEY1
GETKEY1X:
	CALL	CHKEY		;キーを離すまで待つ
	JR	Z,GETKEY2
	CP	L
	JR	NZ,GETKEY2
	IN	A,(040H)
	BIT	5,A
	JR	NZ,GETKEY1X
	DEC	H
	JR	NZ,GETKEY1
	LD	A,1
	JR	GETKEY3
GETKEY2:
	LD	A,16
GETKEY3:
	LD	(GKSMC+1),A
	LD	A,(_KEYD)
	OR	A
	POP	HL
	RET	NZ
CHKEY:
	PUSH	BC
	IN	A,(8)		;6:SHIFT,7:CTRL
	LD	C,A
	LD	A,(00EH)
	BIT	3,A
	JR	NZ,CHKEYRSHIFT
	RES	6,C
CHKEYRSHIFT:
	LD	A,C
	LD	(_KEYD+1),A
	PUSH	HL
	LD	C,0
	LD	HL,KEY_TABLE
	BIT	6,A
	JR	NZ,INKEY0
	LD	HL,SHIFT_TABLE
INKEY0:
	BIT	7,A
	JR	NZ,INKEY1
	LD	HL,CTRL_TABLE
INKEY1:
	IN	A,(C)
	LD	B,8
INKEY2:
	RRCA
	JR	NC,INKEY4
INKEY3:
	INC	HL
	DJNZ	INKEY2
	INC	C
	LD	A,C
	SUB	00DH
	JR	NZ,INKEY1
	JR	INKEY5
INKEY4:
	LD	A,(HL)
	OR	A
	JR	Z,INKEY3
INKEY5:
	POP	HL
	POP	BC
	JR	INKEYD

SCRN:
	PUSH	HL
	IN	A,(032H)
	LD	H,A
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A
	LD	A,(BC)
	LD	L,A
	LD	A,H
	OUT	(032H),A
	LD	A,L
	POP	HL
	RET

VBLANK:
	PUSH	AF
VBLANK0:
	IN	A,(040H)
	AND	020H
	JR	NZ,VBLANK0
VBLANK1:
	IN	A,(040H)
	AND	020H
	JR	Z,VBLANK1
	POP	AF
	RET

INIT_CRTC:
	LD	A,3		;80桁
	OUT	(030H),A
	LD	(WK30),A

	LD	A,022H		;25行 64K RAM
	OUT	(031H),A
	LD	(WK31),A

	LD	A,0FFH
	OUT	(0E4H),A	;割り込みレベル設定
	LD	(WKE4),A
	LD	A,0
	OUT	(0E6H),A	;割り込みマスク

	LD	A,019H
	OUT	(040H),A	;CRT I/F 同期ビットオン
	LD	(WK40),A
	CALL	VBLANK

	XOR	A
	OUT	(051H),A	;Reset CRTC
	LD	A,0A0H
	OUT	(068H),A	;DMAC: Mode Set(Reset)
	LD	HL,0F3C8H
	LD	(TVRAMTOP),HL
	LD	C,064H
	OUT	(C),L		;TVRAM LOW
	OUT	(C),H		;TVRAM HIGH
	LD	HL,08000H+120*25-1
	INC	C		;65H
	OUT	(C),L
	OUT	(C),H
	LD	A,080H+80-2
	OUT	(050H),A
	LD	A,080H+25-1
	OUT	(050H),A
	IN	A,(040H)
	BIT	1,A
	LD	HL,067DEH
	JR	NZ,RESO1
	LD	HL,06F58H
RESO1:
	LD	C,050H
	OUT	(C),H
	OUT	(C),L

	LD	A,053H
	OUT	(050H),A

	LD	A,043H
	OUT	(051H),A

	LD	A,0E4H
	OUT	(068H),A

	LD	A,020H
	OUT	(051H),A

	CALL	VBLANK
IVBL1:
	IN	A,(040H)
	BIT	5,A
	JR	NZ,IVBL1

	LD	A,011H
	OUT	(040H),A	;CRT I/F 同期ビットオフ
	LD	(WK40),A
	RET