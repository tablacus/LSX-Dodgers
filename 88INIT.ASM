
;	LSX-Dodgers INIT
;	PC-8801mkIISR

	JP	START
	DW	MACW
	DW	VER

START:
	DI
	IM	2
	LD	SP,START
	CALL	INIT		;NZならAUTOEXECを実行
	LD	HL,0
	PUSH	HL

	RET

	RET	Z
	LD	DE,AUTOD
	JP	_COMANL

INIT:
	IN	A,(032H)
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A

	LD	HL,0F3C8H	;テキストをクリア
	LD	DE,080E8H
	LD	C,' '
	LD	A,25
TVCL1:
	LD	B,80
TVCL2:
	LD	(HL),C
	INC	HL
	DJNZ	TVCL2
	LD	B,20
TVCL3:
	LD	(HL),D
	INC	L
	LD	(HL),E
	INC	HL
	DJNZ	TVCL3
	DEC	A
	JR	NZ,TVCL1

	IN	A,(032H)
	OR	010H		;高速RAMアクセスモードOFF
	OUT	(032H),A

	CALL	INIT_CRTC
BANK:
	IN	A,(0E2H)
	LD	B,0E3H		;拡張RAM バンク選択
	LD	HL,07F00H
	DI
BANK1:
	LD	A,011H
	OUT	(0E2H),A	;拡張RAM リード/ライト制御
	OUT	(C),L
	LD	E,(HL)
	LD	A,E
	ADD	A,0A5H
	LD	(HL),A
	CP	(HL)
	LD	(HL),E
	JR	NZ,BANK2
	INC	L
	BIT	4,L
	JR	Z,BANK1
BANK2:
	XOR	A
	OUT	(0E2H),A	;拡張RAM リード/ライト制御
	LD	A,L
	OR	A
	JR	Z,NOBANK
	LD	H,0
	ADD	HL,HL	;*2
	ADD	HL,HL	;*4
	ADD	HL,HL	;*8
	ADD	HL,HL	;*16
	ADD	HL,HL	;*32
	DEC	HL	;-1
	DEC	HL	;-2
	DEC	HL	;-3
	LD	(BANKCL),HL
	CALL	CALC_FATLN
	LD	(BANKFL),A
NOBANK:
	LD	A,0CH		;レシーブメモリ
	CALL	PUTCOM
	LD	A,DSS/256	;開始アドレスH
	CALL	PUTDAT
	XOR	A		;開始アドレスL
	CALL	PUTDAT
	LD	BC,AT_DSSE-AT_DSS
	LD	A,B		;バイト数H
	CALL	PUTDAT
	LD	A,C		;バイト数H
	CALL	PUTDAT
	LD	HL,AT_DSS
SUBPG1:
	LD	A,(HL)
	CALL	PUTDAT
	CPI
	JP	PE,SUBPG1

	LD	HL,0
	LD	B,05CH
	LD	A,0FFH		;RST 38H
	CALL	FILL_MEMORY
	LD	B,0A4H
	XOR	A
	CALL	FILL_MEMORY

	LD	A,0C3H		;JP
	LD	HL,CPM_WBOOT
	LD	(00000H),A
	LD	(00001H),HL	;IPL

	LD	HL,MACW		;3 bit7  DPB Compatible LA(1)
	LD	(00003H),HL	;  bit6-0 Machine
;				;4 LSX-Dodgers(01DH)
	LD	HL,BDOS
	LD	(00005H),A
	LD	(00006H),HL	;BDOS

	LD	HL,BIOS
	LD	(00018H),A
	LD	(00019H),HL	;BIOS-ROM

	LD	HL,TRAP38	;TRAP38
	LD	(00038H),A
	LD	(00039H),HL	;RST 038H

	LD	A,CPM_BOOT/256
	LD	(0000BH),A

	LD	A,0E9H		;JP (HL)
	LD	(0000FH),A

	LD	A,(_FATBF+1)
	LD	(00013H),A

	LD	A,(_DTBUF+1)
	LD	(00017H),A

	LD	A,KEYBF/256
	LD	(0001BH),A

	LD	A,KBUF/256
	LD	(0001FH),A

	LD	HL,VER
	LD	(0005AH),HL

	LD	HL,INIMES
	CALL	MSX
INIT1:
	DI
	LD	DE,0FF00H
	LD	B,0
	CALL	ZERO_MEMORY_DE
	LD	HL,EXTBIO
	LD	B,TRAP38-EXTBIO
	LD	A,0FFH		;RST 38H
	CALL	FILL_MEMORY
	LD	HL,AT_TRAP38
	LD	DE,TRAP38
	LD	BC,AT_TRAPE-AT_TRAP38
	LDIR

	CALL	CHKEY
	CP	3
	RET

CALC_FATLN:	;FAT12のクラスタ数から必要なFATサイズを求める(セクタサイズ512)
	LD	E,L
	LD	D,H
	ADD	HL,HL	;*2
	ADD	HL,DE	;*3
	SRL	H	;/2
	RR	L	;1クラスタ辺り1.5バイト必要
	LD	DE,511	;切り上げる
	ADD	HL,DE
	LD	A,H
	SRL	A
	RET

AUTOD:	DB	"AUTOEXEC "
AUTODV:	DB	"A:",0

INIMES:	DB	00CH,"LSX-Dodgers for PC-8801mkIISR v"
	DB	030H + VER_1, '.'
	DB	030H + VER_2 ,030H + VER_3
	DB	"PR2"
	DB	' Gaku',0DH,0AH
	DB	0

AT_TRAP38:
	ORG	TRAP38,AT_TRAP38-OFFSET
	LD	HL,0
	EX	(SP),HL
	LD	A,'$'
	CALL	MSG_A
	DEC	HL
	LD	A,H
	CALL	PRHX
	LD	A,L
PRHX:
	PUSH	AF
	RLCA
	RLCA
	RLCA
	RLCA
	CALL	PRHX2
	POP	AF
PRHX2:
	AND	00FH
	CP	10
	CCF
	ADC	A,'0'
	DAA
	JP	MSG_A
	DS	4
	ORG	$$+OFFSET	;$DEPHASE
AT_TRAPE:

AT_DSS:
	ORG	DSS,AT_DSS-OFFSET
READDISKEX:
	CALL	GETPARAMS
	XOR	A
	LD	(RETRY_RDDISK),A
	LD	HL,(S_PARAMS+2)
	LD	A,L
	DEC	A
	OR	H
	JR	NZ,RDDISK0
	LD	HL,(S_PARAMS+5)
	LD	A,H
	CP	L
	JR	NZ,RDDISK0
	LD	A,3
	LD	(RETRY_RDDISK),A
RDDISK0:
	LD	A,46H		;READ DATA
	CALL	FDCOMMAND
	JP	C,FINISH_DISK
RDDISK1:
	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	20H		;NO SERVICE
	JR	Z,RDDISK2
	IN	A,(0FBH)	;FDC μPD765A データレジスタ
	LD	(HL),A
	CPI
	JP	PE,RDDISK1
RDDISK2:
	LD	A,B
	OR	C
	LD	(S_LEFT),A
	LD	A,E
	DEC	A
	OR	D
	JR	NZ,WRDISK3
				;2D/2DDを見分ける
				;BPB
	LD	HL,(DSS_DATA+11);BPB_BytsPerSec
	LD	A,L
	OR	A
	JR	NZ,S_NOTBPB
	LD	A,H
	CP	2
	JR	Z,S_BPBOK
	CP	4
	JR	Z,S_BPBOK
	CP	1
	JR	Z,S_BPBOK
S_NOTBPB:	
	LD	A,(S_PARAMS+7)	;シリンダ数
	SUB	77
	CCF
	JR	DSSBPB3
S_BPBOK:
	LD	A,(DSS_DATA+12)	;BPB_BytsPerSec
	LD	HL,(DSS_DATA+19);BPB_TotSec16
	OR	4
DSSBPB1:
	RRCA
	JR	C,DSSBPB2
	ADD	HL,HL
	JR	DSSBPB1
DSSBPB2:
	LD	DE,-(40*2*5*4+1)
	XOR	A
	ADD	HL,DE
DSSBPB3:
	ADC	A,A
	ADD	A,A
	ADD	A,A
	LD	C,0FBH
	CALL	SET_WKF4
WRDISK3:
	IN	A,(0F8H)	;FDC μPD765A TC入力
	EI
	HALT
	CALL	CHECKRESULT
	LD	A,(S_LEFT)
	OR	A
	JR	Z,FINISH_DISK0
RETRY_RDDISK	EQU	$+1
	LD	A,0
	OR	A
	JR	Z,FINISH_DISK0
	DEC	A
	LD	(RETRY_RDDISK),A
	LD	A,(S_PARAMS+5)
	CP	4
	JR	NC,RETRY_RDDISK1
	ADD	A,A
	JR	RETRY_RDDISK2
RETRY_RDDISK1:
	LD	A,1
RETRY_RDDISK2:
	LD	H,A
	LD	L,A
	LD	(S_PARAMS+5),HL
	JP	RDDISK0
FINISH_DISK0:
	LD	A,(S_LEFT)
	SUB	1
	CCF
FINISH_DISK:
	SBC	A,A
	LD	(S_CMD_STATUS),A
	JP	MAIN

WRITEDISKEX:
	XOR	A
	LD	(RETRY_RDDISK),A
	CALL	GETPARAMS
	LD	A,45H		;WRITE DATA
	CALL	FDCOMMAND
	JR	C,FINISH_DISK
WRDISK1:
	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	20H
	JR	Z,WRDISK2
	LD	A,(HL)
	OUT	(0FBH),A	;FDC μPD765A データレジスタ
	CPI
	JP	PE,WRDISK1
WRDISK2:
	LD	A,B
	OR	C
	LD	(S_LEFT),A
	JR	WRDISK3

GETPARAMS:
	LD	B,8
	PUSH	HL
	LD	HL,S_PARAMS
GETPARAMS1:
	RST	10H
	LD	(HL),A
	INC	HL
	DJNZ	GETPARAMS1
	POP	HL
	RET

FDCOMMAND:
	PUSH	AF
	LD	A,(S_PARAMS+4)	;MFMモード切り替え $FF:2D/2DD $FE:2HD
	RRA
	CCF
	SBC	A,A
	AND	5
	LD	C,0FEH	;マスク
	CALL	SET_WKF4
	OUT	(0F4H),A	;ドライブ制御

	LD	BC,(S_PARAMS)
	LD	DE,(S_PARAMS+2)
	CALL	SEEK
	PUSH	HL
	LD	HL,04000H
WAIT_SEEK:
	CALL	GETDEVSTAT
	AND	8
	JR	NZ,SEEK_OK
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,WAIT_SEEK
	POP	HL
	POP	AF
	SCF
	RET
SEEK_OK:
	POP	HL
	POP	AF
	RST	20H
	CALL	MAPDRV		;HD US1 US0
	RST	20H
	LD	A,D		;C
	SRL	A
	RST	20H
	LD	A,D		;H
	AND	1
	RST	20H
	LD	A,E		;R
	RST	20H
	LD	A,(S_PARAMS+5)	;N
	CP	4
	JR	C,FDCOMMAND2
	LD	A,3
FDCOMMAND2:
	RST	20H
	LD	A,B		;EOT
	RST	20H
	LD	A,054H		;GSL
	RST	20H
	XOR	A		;DTL
	RST	20H
	LD	C,0
	LD	A,(S_PARAMS+6)
	LD	B,A		;バイト数H
	LD	HL,DSS_DATA	;開始アドレス
	AND	A
	RET

RESULTEX:
	LD	A,(S_CMD_STATUS)
	INC	A
	JR	Z,RESULTEX1
	LD	A,(S_PARAMS+6)	;バイト数H
RESULTEX1:
	RST	18H
	JP	MAIN

SET_WKF4:
	LD	B,A
	LD	A,(S_PARAMS)	;0:ドライブA 1:ドライブB
	OR	A
	JR	Z,SET_WKF4_1
	SLA	B
	SLA	C
SET_WKF4_1:
	LD	A,(S_WKF4)
	AND	C
	OR	B
	LD	(S_WKF4),A
	RET

S_PARAMS:
	DS	8
S_CMD_STATUS:
	DB	0
S_LEFT:
	DB	0
S_WKF4:
	DB	0

	ORG	$$+OFFSET	;$DEPHASE
AT_DSSE:

INITE:
;	ORG	BDOS,INITE-OFFSET
	DS	BDOS-INITE
	JP	BDOS_RF
