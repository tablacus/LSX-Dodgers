
;	LSX-Dodgers INIT
;	PC-8801mkIISR

	JP	START
	DW	MACW
	DW	VER

START:
	DI
	IM	2
	LD	SP,START
	CALL	INIT		;NZならAUTOEXECを実行
	LD	HL,0
	PUSH	HL
	RET	Z
	LD	DE,AUTOD
	JP	_COMANL

INIT:
	IN	A,(032H)
	RES	4,A		;高速RAMアクセスモード
	OUT	(032H),A

	LD	HL,0F3C8H	;テキストをクリア
	LD	DE,080E8H
	LD	C,' '
	LD	A,25
TVCL1:
	LD	B,80
TVCL2:
	LD	(HL),C
	INC	HL
	DJNZ	TVCL2
	LD	B,20
TVCL3:
	LD	(HL),D
	INC	L
	LD	(HL),E
	INC	HL
	DJNZ	TVCL3
	DEC	A
	JR	NZ,TVCL1

	IN	A,(032H)
	OR	010H		;高速RAMアクセスモードOFF
	OUT	(032H),A

	XOR	A
	LD	(WKE6),A
	CPL
	LD	(WKE4),A

	CALL	INIT_CRTC
BANK:
	IN	A,(0E2H)
	LD	B,0E3H		;拡張RAM バンク選択
	LD	HL,07F00H
	DI
	LD	A,011H
	OUT	(0E2H),A	;拡張RAM リード/ライト制御
	OUT	(C),L
	LD	A,(0)
	CP	0EBH
	JR	NZ,BANK1
	LD	A,02BH
	LD	(BANKDV),A
BANK1:
	LD	A,011H
	OUT	(0E2H),A	;拡張RAM リード/ライト制御
	OUT	(C),L
	LD	E,(HL)
	LD	A,E
	ADD	A,0A5H
	LD	(HL),A
	CP	(HL)
	LD	(HL),E
	JR	NZ,BANK2
	INC	L
	BIT	4,L
	JR	Z,BANK1
BANK2:
	XOR	A
	OUT	(0E2H),A	;拡張RAM リード/ライト制御
	LD	A,L
	OR	A
	JR	Z,NOBANK
	LD	H,0
	ADD	HL,HL	;*2
	ADD	HL,HL	;*4
	ADD	HL,HL	;*8
	ADD	HL,HL	;*16
	ADD	HL,HL	;*32
	DEC	HL	;-1
	DEC	HL	;-2
	DEC	HL	;-3
	LD	(BANKCL),HL
	CALL	CALC_FATLN
	LD	(BANKFL),A
NOBANK:
				;初期化
	XOR	A		;イニシャライズ
	CALL	PUTCOM
	LD	A,16H		;高速レシーブメモリ
	CALL	PUTCOM
	LD	A,DSS/256	;開始アドレスH
	CALL	PUTDAT
	XOR	A		;開始アドレスL
	CALL	PUTDAT
	LD	BC,AT_DSSE-AT_DSS
	BIT	0,C
	JR	Z,SUBPG1
	INC	BC		;偶数にする
SUBPG1:
	LD	A,B		;バイト数H
	CALL	PUTDAT
	LD	A,C
	CALL	PUTDAT
	LD	HL,AT_DSS
	CALL	FPUTDAT

	LD	HL,0
	LD	B,05CH
	LD	A,0FFH		;RST 38H
	CALL	FILL_MEMORY
	LD	B,0A4H
	XOR	A
	CALL	FILL_MEMORY

	LD	A,0C3H		;JP
	LD	HL,CPM_WBOOT
	LD	(00000H),A
	LD	(00001H),HL	;IPL

	LD	HL,MACW		;3 bit7  DPB Compatible LA(1)
	LD	(00003H),HL	;  bit6-0 Machine
;				;4 LSX-Dodgers(01DH)
	LD	HL,BDOS
	LD	(00005H),A
	LD	(00006H),HL	;BDOS

	LD	HL,BIOS
	LD	(00018H),A
	LD	(00019H),HL	;BIOS-ROM

	LD	HL,TRAP38	;TRAP38
	LD	(00038H),A
	LD	(00039H),HL	;RST 038H

	LD	A,CPM_BOOT/256
	LD	(0000BH),A

	LD	A,0E9H		;JP (HL)
	LD	(0000FH),A

	LD	A,(_FATBF+1)
	LD	(00013H),A

	LD	A,(_DTBUF+1)
	LD	(00017H),A

	LD	A,KEYBF/256
	LD	(0001BH),A

	LD	A,KBUF/256
	LD	(0001FH),A

	LD	HL,VER
	LD	(0005AH),HL

	LD	HL,INIMES
	CALL	MSX
INIT1:
	DI
	LD	DE,0FF00H
	LD	B,0
	CALL	ZERO_MEMORY_DE
	LD	HL,EXTBIO
	LD	B,TRAP38-EXTBIO
	LD	A,0FFH		;RST 38H
	CALL	FILL_MEMORY
	LD	HL,AT_TRAP38
	LD	DE,TRAP38
	LD	BC,AT_TRAPE-AT_TRAP38
	LDIR

	CALL	CHKEY
	CP	3
	RET

CALC_FATLN:	;FAT12のクラスタ数から必要なFATサイズを求める(セクタサイズ512)
	LD	E,L
	LD	D,H
	ADD	HL,HL	;*2
	ADD	HL,DE	;*3
	SRL	H	;/2
	RR	L	;1クラスタ辺り1.5バイト必要
	LD	DE,511	;切り上げる
	ADD	HL,DE
	LD	A,H
	SRL	A
	RET

AUTOD:	DB	"AUTOEXEC "
AUTODV:	DB	"A:",0

INIMES:	DB	00CH,"LSX-Dodgers for PC-8801mkIISR v"
	DB	030H + VER_1, '.'
	DB	030H + VER_2 ,030H + VER_3
;	DB	"PR3"
	DB	' Gaku',0DH,0AH
	DB	0

AT_TRAP38:
	ORG	TRAP38,AT_TRAP38-OFFSET
	LD	HL,0
	EX	(SP),HL
	LD	A,'$'
	CALL	MSG_A
	DEC	HL
	LD	A,H
	CALL	PRHX
	LD	A,L
PRHX:
	PUSH	AF
	RLCA
	RLCA
	RLCA
	RLCA
	CALL	PRHX2
	POP	AF
PRHX2:
	AND	00FH
	CP	10
	CCF
	ADC	A,'0'
	DAA
	JP	MSG_A
	DS	4
	ORG	$$+OFFSET	;$DEPHASE
AT_TRAPE:

AT_DSS:
	ORG	DSS,AT_DSS-OFFSET
READDISKEX:
	CALL	GETPARAMS
	XOR	A
	LD	(RETRY_RDDISK),A
	LD	HL,(S_PARAMS+2)
	LD	A,L
	DEC	A
	OR	H
	JR	NZ,RDDISK0
	LD	HL,(S_PARAMS+5)
	LD	A,H
	CP	L
	JR	NZ,RDDISK0
	LD	A,3
	LD	(RETRY_RDDISK),A
RDDISK0:
	LD	A,46H		;READ DATA
	CALL	FDCOMMAND
	JR	C,FINISH_DISK
	CALL	RDDISK1
WRDISK3:
	LD	A,B
	OR	D
	LD	(S_LEFT),A
	IN	A,(0F8H)	;FDC μPD765A TC入力
	EI
	HALT
	CALL	CHECKRESULT
	JR	NC,FINISH_DISK0
RETRY_RDDISK	EQU	$+1
	LD	A,0
	OR	A
	JR	Z,FINISH_DISK0
	DEC	A
	LD	(RETRY_RDDISK),A
	LD	A,(S_PARAMS+5)
	CP	4
	JR	NC,RETRY_RDDISK1
	ADD	A,A
	JR	RETRY_RDDISK2
RETRY_RDDISK1:
	LD	A,1
RETRY_RDDISK2:
	LD	H,A
	LD	L,A
	LD	(S_PARAMS+5),HL
	JP	RDDISK0
FINISH_DISK0:
	LD	A,(S_LEFT)
	SUB	1
	CCF
FINISH_DISK:
	SBC	A,A
	LD	(S_CMD_STATUS),A
	JP	MAIN

WRITEDISKEX:
	XOR	A
	LD	(RETRY_RDDISK),A
	CALL	GETPARAMS
	LD	A,45H		;WRITE DATA
	CALL	FDCOMMAND
	JR	C,FINISH_DISK
	CALL	WRDISK1
	JR	WRDISK3

GETPARAMS:
	LD	B,8
	PUSH	HL
	LD	HL,S_PARAMS
GETPARAMS1:
	RST	10H
	LD	(HL),A
	INC	HL
	DJNZ	GETPARAMS1
	POP	HL
	RET

FDCOMMAND:
	PUSH	AF
	LD	A,(S_PARAMS+4)	;MFMモード切り替え $FF:2D/2DD $FE:2HD
	RRA
	CCF
	SBC	A,A
	PUSH	AF
	AND	5
	LD	C,A
	LD	A,(S_PARAMS+7)	;シリンダ数
	ADD	A,0-50
	SBC	A,A
	AND	4
	OR	C
	LD	C,0FFH-5	;マスク
	LD	B,A
	LD	A,(S_PARAMS)	;0:ドライブA 1:ドライブB
	OR	A
	JR	Z,SET_WKF4
	SLA	B
	SLA	C
SET_WKF4:
	LD	A,(S_WKF4)
	AND	C
	OR	B
	LD	(S_WKF4),A
	POP	AF
	OR	020H		;bit5:CLK
	LD	C,A
	LD	A,(S_WKF4)
	AND	0FFH-020H	;20Hのマスク
	OR	C
	OUT	(0F4H),A	;ドライブ制御
	LD	(S_WKF4),A

	LD	BC,(S_PARAMS)
	LD	DE,(S_PARAMS+2)
	LD	A,D		;TRACK
	SRL	A		;CYLINDER
	ADD	A,0FFH		;0の場合、CF=0 それ以外CF=1
	SBC	A,A
	AND	8		;SEEK:0FH = 8 | 7
	OR	7		;RECALIBRATE:7
	RST	20H
	LD	A,C		;DR/HD
	RST	20H
	LD	A,D		;TRACK
	SRL	A		;CYLINDER
	CALL	NZ,20H		;SEEKの場合だけ呼ぶ(0シリンダの場合はRECALIBRATEを使う)
	EI
	HALT
	LD	A,8		;SENSE
	RST	20H
	CALL	RDFDC		;ST0
	CALL	RDFDC		;PCN(CYLINDER)
	PUSH	HL
	LD	HL,04000H
WAIT_SEEK:
	CALL	GETDEVSTAT
	AND	8
	JR	NZ,SEEK_OK
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,WAIT_SEEK
	POP	HL
	POP	AF
	SCF
	RET
SEEK_OK:
	POP	HL
	POP	AF
	RST	20H
	LD	A,D		;HD US1 US0
	AND	1
	ADD	A,A
	ADD	A,A
	OR	C
	RST	20H
	LD	A,D		;C
	SRL	A
	RST	20H
	LD	A,D		;H
	AND	1
	RST	20H
	LD	A,E		;R
	RST	20H
	LD	A,(S_PARAMS+5)	;N
	CP	4
	JR	C,FDCOMMAND2

	LD	A,3
FDCOMMAND2:
	LD	E,020H		;NO SERVICE
	RST	20H

	LD	HL,DSS_DATA	;開始アドレス
	LD	A,B		;EOT
	RST	20H

	LD	BC,000FBH	;B:バイト数L C:FDC μPD765A データレジスタ
	LD	A,054H		;GSL
	RST	20H

	LD	A,(S_PARAMS+6)
	LD	D,A		;バイト数H

	XOR	A		;DTL
	RST	20H
	AND	A
	RET

RDDISK1:			;高速化の為にループ展開x8
	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	JR	NZ,RDDISK1
	DEC	D
	JP	NZ,RDDISK1
	RET

WRDISK1:			;高速化の為にループ展開x8
	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	JR	NZ,WRDISK1
	DEC	D
	JP	NZ,WRDISK1
	RET

RESULTEX:
	LD	A,(S_CMD_STATUS)
	INC	A
	JR	Z,RESULTEX1
	LD	A,(S_PARAMS+6)	;バイト数H
RESULTEX1:
	RST	18H
	JP	MAIN

CHECKRESULT:
	PUSH	BC
	CALL	RDFDC		;ST0
	AND	0D8H
	LD	C,A
	CALL	RDFDC		;ST1
	AND	0B7H
	OR	C
	LD	C,A
	CALL	RDFDC		;ST2
	AND	073H
	OR	C
	LD	C,A
	LD	B,4
CHECKRESULT1:			;C H R N
	CALL	RDFDC
	DJNZ	CHECKRESULT1
	LD	A,C
	AND	A
	POP	BC
	RET	Z
	SCF
	RET

GETDEVSTAT:
	LD	A,4
	RST	20H
	LD	A,C
	RST	20H

RDFDC:
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	CPL
	AND	0C0H		;RQMとDIOが両方1になるまで待つ
	JR	NZ,RDFDC
	IN	A,(0FBH)	;FDC μPD765A データレジスタリード
	RET

S_PARAMS:
	DS	8
S_CMD_STATUS:
	DB	0
S_LEFT:
	DB	0
S_WKF4:
	DB	0

	ORG	$$+OFFSET	;$DEPHASE
AT_DSSE:

INITE:
;	ORG	BDOS,INITE-OFFSET
	DS	BDOS-INITE
	JP	BDOS_RF
