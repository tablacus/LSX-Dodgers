
;	LSX-Dodgers SUB DISK SYSTEM
;	PC-8801mkIISR

AT_DSS:
	ORG	DSS,AT_DSS-OFFSET
	JP	READDISKEX
	JP	WRITEDISKEX
	JP	GETPARAMS_N
	JP	SEEK
	JP	FDCOMMAND
	JP	CHECKRESULT
	JP	GETDEVSTAT
	JP	RDFDC

	DS	8*3

S_PARAMS:
	DS	16
S_UNITNO	EQU	S_PARAMS	;最大セクタサイズ << 1 + ユニット番号 0:ドライブA 1:ドライブB
S_DPS		EQU	S_PARAMS+1	;1トラックのセクタ数
S_SECTOR_R	EQU	S_PARAMS+2	;セクタ番号
S_TRACK		EQU	S_PARAMS+3	;トラック番号
S_FDMODE	EQU	S_PARAMS+4	;FDモード切り替え $FF:2D/2DD $FE:2HD
S_SECSIZE_N	EQU	S_PARAMS+5	;N セクタサイズ
S_BYTE_H	EQU	S_PARAMS+6	;バイト数H
S_MAXCL		EQU	S_PARAMS+7	;シリンダ数

S_CMD_STATUS:
	DB	0
S_LEFT:
	DB	0
S_WKF4:
	DB	0

READDISKEX:
	CALL	GETPARAMS
	XOR	A
	LD	(RETRY_RDDISK),A
	LD	HL,(S_PARAMS+2)
	LD	A,L
	DEC	A
	OR	H
	JR	NZ,RDDISKA1
	LD	HL,(S_SECSIZE_N)
	LD	A,H
	CP	L
	JR	NZ,RDDISKA1
	LD	A,(S_UNITNO)	;(MAX_SEC_SZ_H << 1) + UNITNO
	LD	(RETRY_RDDISK),A
	SRL	A
	LD	(RETRY_MAX_SEC_H),A
RDDISKA1:
	LD	A,(S_UNITNO)
	AND	1
	LD	(S_UNITNO),A
RDDISK0:
	CALL	SEEK
	LD	A,46H		;READ DATA
	CALL	FDCOMMAND
	JR	C,FINISH_DISK
	CALL	RDDISK1
WRDISK3:
	LD	A,B
	OR	D
	LD	(S_LEFT),A
	IN	A,(0F8H)	;FDC μPD765A TC入力
	EI
	HALT
	CALL	CHECKRESULT
	JR	NC,FINISH_DISK0
	LD	A,0
RETRY_RDDISK	EQU	$-1
	SRL	A
	JR	Z,FINISH_DISK0
	LD	(RETRY_RDDISK),A
	LD	A,(S_SECSIZE_N)
	CP	4
RETRY_MAX_SEC_H	EQU	$-1
	JR	NC,RETRY_RDDISK1
	ADD	A,A
	JR	RETRY_RDDISK2
RETRY_RDDISK1:
	LD	A,1
RETRY_RDDISK2:
	LD	H,A
	LD	L,A
	LD	(S_SECSIZE_N),HL
	JP	RDDISK0
FINISH_DISK0:
	LD	A,(S_LEFT)
	SUB	1
	CCF
FINISH_DISK:
	SBC	A,A
	LD	(S_CMD_STATUS),A
	JP	MAIN

WRITEDISKEX:
	XOR	A
	LD	(RETRY_RDDISK),A
	CALL	GETPARAMS
	CALL	SEEK
	LD	A,45H		;WRITE DATA
	CALL	FDCOMMAND
	JR	C,FINISH_DISK
	CALL	WRDISK1
	JR	WRDISK3

GETPARAMS:
	LD	B,8
GETPARAMS_N:
	PUSH	HL
	LD	HL,S_PARAMS
GETPARAMS1:
	RST	10H
	LD	(HL),A
	INC	HL
	DJNZ	GETPARAMS1
	POP	HL
	RET

SEEK:
	PUSH	HL
	LD	A,(S_FDMODE)	;FDモード切り替え $FF:2D/2DD $FE:2HD
	RRA
	CCF
	SBC	A,A
	PUSH	AF
	AND	5
	LD	C,A
	LD	A,(S_MAXCL)	;シリンダ数
	ADD	A,0-45		;オーバートラックを考慮して40+α
	SBC	A,A
	AND	4
	OR	C
	LD	C,0FFH-5	;マスク
	LD	B,A
	LD	A,(S_UNITNO)	;(MAX_SEC_SZ_H << 1) + UNITNO(0:ドライブA 1:ドライブB)
	OR	A
	JR	Z,SET_WKF4
	SLA	B
	RLC	C
SET_WKF4:
	LD	A,(S_WKF4)
	AND	C
	OR	B
	LD	C,A
	OUT	(0F4H),A	;ドライブ制御
	AND	0FFH-020H	;CLKの分は抜いておく
	LD	B,A
	POP	AF
	AND	020H		;bit5:CLK
	OR	B
	OUT	(0F4H),A	;ドライブ制御
	LD	(S_WKF4),A
	LD	B,A
	XOR	C
	AND	020H		;CLKが変化したら一定時間待つ
	JR	Z,NOCHGCLK
	LD	HL,0F060H
WAIT_CLK:
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,WAIT_CLK
NOCHGCLK:
	LD	A,B
	AND	020H
	JR	Z,SPECIFY2D
	LD	DE,0DF31H	;SPECIFY 2HD / SRT:13 HUT:15 HLT:24 NON-DMA
	JR	SPECIFYOK
SPECIFY2D:
	LD	DE,0DA19H	;SPECIFY 2D/2DD / SRT:13 HUT 10 HLT:12 NON-DMA
SPECIFYOK:
	LD	A,3		;SPECIFY
	RST	20H
	LD	A,D
	RST	20H
	LD	A,E
	RST	20H
NOSPECIFY:
	LD	BC,(S_PARAMS)
	LD	DE,(S_PARAMS+2)
	LD	A,D		;TRACK
	SRL	A		;CYLINDER
	LD	A,00FH		;SEEK
	JR	NZ,SEEK1	;0シリンダの場合はRECALIBRATE、それ以外はSEEKを使う
	LD	A,7		;RECALIBRATE
SEEK1:
	RST	20H
	LD	A,C		;DR/HD
	RST	20H
	LD	A,D		;TRACK
	SRL	A		;CYLINDER
	CALL	NZ,20H		;SEEKの場合だけ呼ぶ(0シリンダの場合はRECALIBRATEを使うのでシリンダの設定は不要)
	EI
	HALT
	LD	A,8		;SENSE
	RST	20H
	CALL	RDFDC		;ST0
	CALL	RDFDC		;PCN(CYLINDER)
	LD	HL,04000H
WAIT_SEEK:
	CALL	GETDEVSTAT
	AND	8
	JR	NZ,SEEK_OK
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,WAIT_SEEK
	SCF
SEEK_OK:
	POP	HL
	RET

FDCOMMAND:
	RST	20H
	LD	A,D		;HD US1 US0
	AND	1
	ADD	A,A
	ADD	A,A
	OR	C
	RST	20H
	LD	A,D		;C
	SRL	A
	RST	20H
	LD	A,D		;H
	AND	1
	RST	20H
	LD	A,E		;R
	RST	20H
	LD	A,(S_SECSIZE_N)	;N
	CP	4
	JR	C,FDCOMMAND2	;N:1=>1 2=>2 4=>3
	LD	A,3
FDCOMMAND2:
	LD	E,020H		;NO SERVICE
	RST	20H

	LD	HL,DSS_DATA	;開始アドレス
	LD	A,B		;EOT
	RST	20H

	LD	BC,000FBH	;B:バイト数L C:FDC μPD765A データレジスタ
	LD	A,054H		;GSL
	RST	20H

	LD	A,(S_BYTE_H)
	LD	D,A		;バイト数H

	XOR	A		;DTL
	RST	20H
	AND	A
	RET

RDDISK1:			;高速化の為にループ展開x8
	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E		;NO SERVICE
	RET	Z
	INI

	JR	NZ,RDDISK1
	DEC	D
	JP	NZ,RDDISK1
	RET

WRDISK1:			;高速化の為にループ展開x8
	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	E
	RET	Z
	OUTI

	JR	NZ,WRDISK1
	DEC	D
	JP	NZ,WRDISK1
	RET

RESULTEX:
	LD	A,(S_CMD_STATUS)
	INC	A
	JR	Z,RESULTEX1
	LD	A,(S_BYTE_H)	;バイト数H
RESULTEX1:
	RST	18H
	JP	MAIN

CHECKRESULT:
	PUSH	BC
	CALL	RDFDC		;ST0
	AND	0D8H
	LD	C,A
	CALL	RDFDC		;ST1
	AND	0B7H
	OR	C
	LD	C,A
	CALL	RDFDC		;ST2
	AND	073H
	OR	C
	LD	C,A
	LD	B,4
CHECKRESULT1:			;C H R N
	CALL	RDFDC
	DJNZ	CHECKRESULT1
	LD	A,C
	AND	A
	POP	BC
	RET	Z
	SCF
	RET

GETDEVSTAT:
	LD	A,4
	RST	20H
	LD	A,C
	RST	20H

RDFDC:
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	CPL
	AND	0C0H		;RQMとDIOが両方1になるまで待つ
	JR	NZ,RDFDC
	IN	A,(0FBH)	;FDC μPD765A データレジスタリード
	RET

	ORG	$$+OFFSET,$$	;$DEPHASE
AT_DSSE:
INITE:
	DS	BDOS-INITE
	JP	BDOS_RF
