
;	LSX-Dodgers IO
;	MZ-700/1500
;	VRAM/KEY/TIMER(0D000H-0EFFFH)にアクセスするので0D000H-0FFFFHに置けない部分
;

IO_BDOS:
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	JP	BDOS0

#IF EXISTS USE_8253
IO_INT8253:
	PUSH	AF
	LD	A,(EXTBIO)
	LD	(INTMEM_SWC),A
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	A,1
	LD	(0E006H),A	;8253 Ch.2 カウンタ
	XOR	A
	LD	(0E006H),A	;8253 Ch.2 カウンタ
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	JP	INT8253
ROM8253E:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	POP	AF
	RET

INITINT:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	HL,0E007H	;HL=0E007H 8253 コントロール
	LD	(HL),0B0H	;Ch.2 カウンタの下位バイト→上位バイトの順に連続してアクセス Mode 0 16ビットバイナリ
	LD	(HL),074H	;Ch.1 カウンタの下位バイト→上位バイトの順に連続してアクセス Mode 2 16ビットバイナリ
	DEC	L		;0E006H 8253 Ch.2 カウンタ
	LD	(HL),2
	LD	(HL),0
	DEC	L		;0E005H 8253 Ch.1 カウンタ
	LD	(HL),A
	LD	(HL),0
	LD	A,L		;L=5
	LD	(0E003H),A	;8255 コントロールポートでポートC(0E002H)のINTMSKを1にする
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET
#ENDIF

RDKEYDATA:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(0E000H),A
	LD	A,(E001H)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
#IF EXISTS USE_8253
#ELSE
IO_INT8253:
#ENDIF
	RET

RD556VSYNC:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	A,(0E002H)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

RDMMIO:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	A,(HL)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

WRMMIO:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(HL),A
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

RDMMIO_BC:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	A,(BC)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

WRMMIO_BC:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(BC),A
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

IO_PRINT:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(HL),A
	SET	3,H
	LD	(HL),C
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

IO_CLR:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
C1AX1:
	LD	A,D
	ADD	A,0D0H
	LD	D,A
	XOR	A
	LD	(DE),A		;Text
	SET	3,D
ICSMC	EQU	$+1
	LD	A,0		;自己書き換え
	LD	(DE),A		;Color
	INC	DE
	LD	A,D
	SUB	0D8H
	LD	D,A
	LD	HL,0-WIDTH*LINE
_NEG_PAGE_SWC	EQU	$-2
	ADD	HL,DE
	JR	NC,C1AX1
	POP	HL
	POP	DE
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

IO_CLLINE:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
CLLINE1:
	RES	3,H
	LD	(HL),A		;Text
	SET	3,H
	LD	(HL),C		;Color
	INC	HL
	DJNZ	CLLINE1
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

IO_INSBS:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
IO_INSBS1:
	RES	3,H
	LD	E,(HL)
	LD	(HL),A
	LD	A,E
	SET	3,H
	LD	E,(HL)
	LD	(HL),C
	LD	C,E
IO_INCDEC	EQU	$	; 自己書き換え
	INC	HL		;INS->INC HL or BS->DEC HL
	DJNZ	IO_INSBS1
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	EI
	RET

IO_SCRUP:
	DI
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
SCRUP1:
	JR	Z,SCRCL
	PUSH	DE
	PUSH	HL
	SET	3,D
	SET	3,H
	LD	BC,WIDTH
	LDIR
	POP	HL
	POP	DE
	LD	BC,WIDTH
	LDIR
	DEC	A
	JR	SCRUP1

SCRCL:
	LD	B,WIDTH
	EX	DE,HL
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	CALL	CLLINE
	POP	HL
	POP	DE
	POP	BC
	RET

RD_PCG:
	DI
	OUT	(0E5H),A	;MZ-1500: PCGバンク切り替え
	LD	C,(HL)
RW_PCG:
	OUT	(0E6H),A	;MZ-1500: PCGバンクを閉じる
	EI
	RET

WR_PCG:
	DI
	OUT	(0E5H),A	;MZ-1500: PCGバンク切り替え
	LD	(HL),C
	JR	RW_PCG

IO_MON:
	OUT	(0E4H),A	;MONITOR ROM(0000H-0FFFH) & VRAM/KEY/TIMER(D000H-FFFFH)
	RST	0

BANKE:
	DS	0CFFEH-BANKE
