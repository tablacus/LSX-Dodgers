
;	LSX-Dodgers IO
;	MZ-700/1500
;	VRAM/KEY/TIMER(0D000H-0EFFFH)にアクセスするので0D000H-0FFFFHに置けない部分
;

IO_BDOS:
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	JP	BDOS0

RDKEYDATA:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(0E000H),A
	LD	A,(E001H)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

RD556VSYNC:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	A,(0E002H)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

RDMMIO:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	A,(HL)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

WRMMIO:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(HL),A
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

RDMMIO_BC:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	A,(BC)
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

WRMMIO_BC:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(BC),A
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

IO_PRINT:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
	LD	(HL),A
	SET	3,H
	LD	(HL),C
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

IO_CLR:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
C1AX1:
	LD	A,D
	ADD	A,0D0H
	LD	D,A
	XOR	A
	LD	(DE),A		;Text
	SET	3,D
ICSMC	EQU	$+1
	LD	A,0		;自己書き換え
	LD	(DE),A		;Color
	INC	DE
	LD	A,D
	SUB	0D8H
	LD	D,A
	LD	HL,0-WIDTH*LINE
	ADD	HL,DE
	JR	NC,C1AX1
	POP	HL
	POP	DE
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

IO_CLLINE:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
CLLINE1:
	RES	3,H
	LD	(HL),A		;Text
	SET	3,H
	LD	(HL),C		;Color
	INC	HL
	DJNZ	CLLINE1
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

IO_INS:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
IO_INS1:
	CALL	C12S
	INC	HL
	DJNZ	IO_INS1
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

IO_BS:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
IO_BS1:
	CALL	C12S
	DEC	HL
	DJNZ	IO_BS
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	RET

C12S:
	RES	3,H
	LD	E,(HL)
	LD	(HL),A
	LD	A,E
	SET	3,H
	LD	E,(HL)
	LD	(HL),C
	LD	C,E
	RET

IO_SCRUP:
	OUT	(0E3H),A	;VRAM/KEY/TIMER(D000H-FFFFH)
SCRUP1:
	JR	Z,SCRCL
	PUSH	DE
	PUSH	HL
	SET	3,D
	SET	3,H
	LD	BC,WIDTH
	LDIR
	POP	HL
	POP	DE
	LD	BC,WIDTH
	LDIR
	DEC	A
	JR	SCRUP1

SCRCL:
	LD	B,WIDTH
	EX	DE,HL
	OUT	(0E1H),A	;DRAM(D000H-FFFFH)
	CALL	CLLINE
	POP	HL
	POP	DE
	POP	BC
	RET

C_MON:
	OUT	(0E4H),A	;MONITOR ROM(0000H-0FFFH) & VRAM/KEY/TIMER(D000H-FFFFH)
	RST	0

BANKE:
	DS	0CFFEH-BANKE		;スタックエリア
EXTSP:
