
;	LSX-Dodgers DISK2
;	X1/turbo/Z

;	各種RAMディスクドライバ

;	GRAPHIC RAM DISK DRIVER

GDRD:
	PUSH	BC
	CALL	GADR
	POP	AF
GDRD1:
	INI
	INC	B
	INC	C
	JR	NZ,GDRD1
	INC	B
	INC	DE
	DEC	A
	JR	NZ,GDRD1
GBANK0:
	LD	A,(WK1FD0)
	AND	0EFH		;BANK0
	JR	S1FD0

GDWT:
	PUSH	BC
	CALL	GADR
	POP	AF
GDWT1:
	INC	B
	OUTI
	INC	C
	JR	NZ,GDWT1
	INC	B
	INC	DE
	DEC	A
	JR	NZ,GDWT1
	JR	GBANK0

GADR:
	LD	C,0
	LD	A,E
	ADD	A,040H
	LD	B,A
	LD	A,(WK1FD0)
	OR	010H		;BANK1
S1FD0:
	PUSH	BC
	LD	(WK1FD0),A
	LD	BC,01FD0H
	OUT	(C),A
	POP	BC
	RET


;	BANK RAM DISK DRIVER

BDWT:
	LD	A,01EH	;LD E,??
	JR	BRW

BDRD:
	LD	A,018H	;JR ??
BRW:
	DI
	LD	(BRWPAT),A
	LD	(BANKSP),SP
	LD	A,(BANKSP_H)
	ADD	A,A
	JR	C,BRW0
	LD	SP,EXTSP
BRW0:
	EXX		;裏レジスタ
	 PUSH	 BC
	 PUSH	 DE
	 LD	 BC,00B00H	 ;バンクメモリ
	 LD	 DE,01010H
	 EXX		;表レジスタ
BRW1:
	PUSH	BC
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	A,H
	ADD	A,(IX+DPB_MAXCYL)
	AND	00FH	;バンク
	LD	H,L
	SRL	H	;/2
	LD	L,0
	EXX		;裏レジスタ
BRWPAT:	 JR	 BRW_RD	;自己書き換え(JR ??:読み出し/:LD E,??書き込み)
	 LD	E,A	 ;E=バンク
	 EXX		;表レジスタ
	CALL	BTFR
	EX	DE,HL
	JR	BRW2
BRW_RD:
	 LD	 D,A	 ;D=バンク
	 EXX		;表レジスタ
	EX	DE,HL
	CALL	BTFR
BRW2:
	POP	DE
	POP	BC
	INC	DE
	DJNZ	BRW1

	EXX		;裏レジスタ
	 LD	 A,10H
	 OUT	 (C),A		;メインメモリに切り替える
	 POP	 DE
	 POP	 BC
	 EXX		;表レジスタ
	XOR	A
	LD	SP,0
BANKSP_H	EQU	$-1
BANKSP		EQU	$-2
	EI
	RET

;メインメモリ←→バンクメモリ 512バイトの転送を行う
; DE:転送元アドレス
; HL:転送先アドレス
; 裏BC:バンク切り替えポート(0B00H)
; 裏D:転送元バンク(0-0FH:10Hはメインメモリ)
; 裏E:転送先バンク(0-0FH:10Hはメインメモリ)

BTFR:
	LD	B,0	;256回ループ(256*2)
BTFR1:
	EXX		;裏レジスタ
	 OUT	 (C),D	 ;BANK(MAIN)
	 EXX		;表レジスタ
	LD	A,(DE)
	LD	C,A
	INC	DE
	LD	A,(DE)
	INC	DE
	EXX		;裏レジスタ
	 OUT	 (C),E	 ;MAIN(BANK)
	 EXX		;表レジスタ
	LD	(HL),C
	INC	HL
	LD	(HL),A
	INC	HL
	DJNZ	BTFR1
	RET

;	EMM DRIVER(CPU)

EDWT:
	CALL	EADR
EDWT0:
	PUSH	AF
	XOR	A
EDWT1:
	INC	B
	OUTI
	INC	B
	OUTI
	DEC	A
	JR	NZ,EDWT1
	INC	DE
	POP	AF
	DEC	A
	JR	NZ,EDWT0
	RET

EDRD:
	CALL	EADR
EDRD0:
	PUSH	AF
	XOR	A
EDRD1:
	INI
	INC	B
	INI
	INC	B
	DEC	A
	JR	NZ,EDRD1
	INC	DE
	POP	AF
	DEC	A
	JR	NZ,EDRD0
	RET

EADR:
	PUSH	BC
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	LD	C,(IX+DPB_MAXSEC)
	LD	B,(IX+DPB_MAXCYL)
	ADD	HL,BC
	LD	C,(IX+DPB_UNITNO)
	LD	B,00DH
	DB	0EDH,071H	;OUT (C),0	Z80未定義命令
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H
	INC	C
	EX	DE,HL
	POP	DE
	POP	AF
	AND	A	;CF=0
	RET


