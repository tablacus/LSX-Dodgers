
;	VIEW	by Gaku Nakagawa

WIDTH	EQU	80
SYSTEM	EQU	0005H
FCB1	EQU	005CH
DTA	EQU	0080H
_SFTJIS	EQU	2F81H
_JISVRM	EQU	2FC3H
_COLORF	EQU	0EC96H

	ORG	0100H
	JP	START
TITLE:
	DB	"view v1.00 Lovers",0DH,0AH
	DB	"使用法:"
	DB	" VIEW ファイル名",0DH,0AH
	DB	"$"
EMES1:
	DB	"ファイルが見つかりません$"
EMES2:
	DB	"ファイルの読み込みに失敗しました$"
EMES:
	DB	7,0DH,0AH,"$"

START:
;				Read File
	LD	A,(FCB1+1)
	CP	21H
	JP	C,USAGE

	LD	DE,DTA
	LD	C,1AH
	CALL	SYSTEM

	LD	DE,FCB1
	LD	C,11H
	CALL	SYSTEM
	OR	A
	JP	NZ,ERROR1
FILES:
	LD	DE,DTA
	LD	C,0FH
	CALL	SYSTEM
	OR	A
	JP	NZ,ERROR1

	LD	HL,0
	LD	(DTA+33),HL
	LD	(DTA+35),HL
	INC	L
	LD	(DTA+14),HL

	LD	DE,DATA2
	LD	C,1AH
	CALL	SYSTEM

	LD	HL,(SYSTEM+1)
	LD	DE,DATA2
	SBC	HL,DE
	LD	DE,DTA
	LD	C,27H
	CALL	SYSTEM
	ADD	A,0FEH
	JP	C,ERROR2
;				Change Data
	EX	DE,HL		;DE=SIZE
	INC	DE
	LD	HL,DATA
	LD	IY,DATA2
	XOR	A
	CALL	WRITE
CHG1:
	LD	B,WIDTH
CHG2:
	DEC	DE
	LD	A,D
	OR	E
	JR	Z,CHGE

	LD	A,(IY)
	INC	IY
	CP	0AH
	JR	Z,CHG2
	CP	0DH
	JR	Z,CHG4

	CP	80H
	JR	C,CHGA
	CP	0A0H
	JR	C,CHGK
	CP	0E0H
	JR	C,CHGA
CHGK:
	INC	DE
	DEC	IY
	DEC	B
	JR	Z,CHG4
	INC	IY

	CALL	WRITE

	DEC	DE
	DEC	DE
	LD	A,D
	OR	E
	JR	Z,CHGE
	LD	A,(IY)
	INC	IY
CHGA:
	CALL	WRITE
	CP	9
	JR	NZ,CHG3

	LD	A,B
	DEC	A
	AND	0F8H
	INC	A
	LD	B,A
CHG3:
	DJNZ	CHG2

	LD	A,(IY)
	CP	0DH
	JR	Z,CHG2
CHG4:
	LD	A,0DH
	CALL	WRITE
	JR	CHG1
CHGE:
	LD	(HL),0

	LD	A,(_COLORF)
	PUSH	AF
	LD	A,0EH
	LD	(_COLORF),A

	LD	E,11
	LD	C,6
	CALL	SYSTEM
	LD	E,20H
	CALL	SYSTEM

	LD	HL,DTA+1
	LD	BC,8*256+6
NAME:
	LD	E,(HL)
	INC	HL
	PUSH	BC
	CALL	SYSTEM
	POP	BC
	DJNZ	NAME

	LD	E,"."
	CALL	SYSTEM

	LD	B,3
EXT:
	LD	E,(HL)
	INC	HL
	PUSH	BC
	CALL	SYSTEM
	POP	BC
	DJNZ	EXT

	LD	E,5
	CALL	SYSTEM

	POP	AF
	LD	(_COLORF),A

KBUF:
	LD	E,0FFH
	CALL	SYSTEM
	OR	A
	JR	NZ,KBUF

	LD	HL,DATA+1
MAIN:
	CALL	PAGE
MAIN1:
	LD	E,0FFH
	LD	C,6
	CALL	SYSTEM
	CP	3
	JP	Z,QUIT
	CP	1BH
	JP	Z,ESC

	CP	1FH
	JR	Z,DOWN
	CP	1EH
	JR	Z,UP
	CP	1DH
	JR	Z,PGUP
	CP	1CH
	JR	Z,PGDW
	CP	20H
	JR	Z,NFILES
	JR	MAIN1

DOWN:
	LD	HL,(TEXT2)
	LD	A,(HL)
	OR	A
	JR	Z,MAIN1

	PUSH	HL
	CALL	SCRUP
	POP	HL
	LD	BC,3000H+80*23
	CALL	PRINT
	LD	(TEXT2),HL

	LD	HL,(TEXT)
	CALL	NEXT
	LD	(TEXT),HL
	JR	MAIN1
UP:
	LD	HL,(TEXT)
	DEC	HL
	LD	A,(HL)
	OR	A
	JR	Z,MAIN1

	CALL	SCRDW
	LD	HL,(TEXT)
	CALL	BACK
	LD	(TEXT),HL
	LD	BC,3000H+80
	CALL	PRINT

	LD	HL,(TEXT2)
	CALL	BACK
	LD	(TEXT2),HL
	JR	MAIN1
PGDW:
	LD	B,22
PGDW1:
	LD	HL,(TEXT2)
	LD	A,(HL)
	OR	A
	JR	Z,PGDW2

	CALL	NEXT
	LD	(TEXT2),HL

	LD	HL,(TEXT)
	CALL	NEXT
	LD	(TEXT),HL
	DJNZ	PGDW1
PGDW2:
	LD	HL,(TEXT)
	JP	MAIN

PGUP:
	LD	HL,(TEXT)
	LD	B,22
PGUP1:
	CALL	BACK
	DJNZ	PGUP1
	JP	MAIN

NFILES:
	LD	DE,DTA
	LD	C,1AH
	CALL	SYSTEM

	LD	C,12H
	CALL	SYSTEM
	OR	A
	JP	NZ,QUIT
	JP	FILES

WRITE:
	PUSH	DE
	LD	DE,(SYSTEM+1)
	DEC	D
	AND	A
	SBC	HL,DE
	ADD	HL,DE
	POP	DE
	JR	NC,WRITE1
	LD	(HL),A
	INC	HL
	RET
WRITE1:
	POP	AF
	JP	CHGE

NEXT:
	LD	A,(HL)
	OR	A
	RET	Z
	INC	HL
	CP	0DH
	JR	NZ,NEXT
	RET

BACK:
	DEC	HL
	LD	A,(HL)
BACK1:
	OR	A
	JR	Z,BACKE
	DEC	HL
	LD	A,(HL)
	CP	0DH
	JR	NZ,BACK1
BACKE:
	INC	HL
	RET

PAGE:
	LD	(TEXT),HL
	LD	BC,3000H+80
	LD	D,23
PAGE1:
	CALL	PRINT
	DEC	D
	JR	NZ,PAGE1
	LD	(TEXT2),HL
	RET

PRINT:
	LD	E,80
PRINT1:
	LD	A,(HL)
	OR	A
	JR	Z,PRINTE2
	INC	HL
	CP	0DH
	JR	Z,PRINTE2

	CP	80H
	JR	C,ANK
	CP	0A0H
	JR	C,KANJI
	CP	0E0H
	JR	C,ANK
KANJI:
	PUSH	DE
	PUSH	BC
	LD	D,A
	LD	E,(HL)
	INC	HL
	LD	BC,_SFTJIS
	RST	18H
	LD	BC,_JISVRM
	RST	18H
	POP	BC
	OUT	(C),E
	INC	BC
	OUT	(C),E
	DEC	BC
	SET	3,B
	OUT	(C),D
	INC	BC
	SET	6,D
	OUT	(C),D
	RES	3,B
	POP	DE
	DEC	E
	JR	PRINTL
ANK:
	CP	9
	JR	Z,TAB
	OUT	(C),A
	SET	3,B
	DB	0EDH,71H
	RES	3,B
PRINTL:
	INC	BC
PRINTL1:
	DEC	E
	JR	NZ,PRINT1
PRINTE:
	LD	A,(HL)
	OR	A
	JR	Z,PRINTE2
	INC	HL
	CP	0DH
	JR	NZ,PRINTE
PRINTE2:
	LD	A,20H
	INC	E
PRINTE3:
	DEC	E
	RET	Z
	OUT	(C),A
	SET	3,B
	DB	0EDH,71H
	RES	3,B
	INC	BC
	JR	PRINTE3
TAB:
	LD	A,E
	DEC	A
	AND	0F8H
	SUB	A,E
TAB1:
	PUSH	AF
	LD	A,20H
	OUT	(C),A
	SET	3,B
	DB	0EDH,71H
	RES	3,B
	INC	BC
	POP	AF
	DEC	E
	INC	A
	JR	NZ,TAB1
	INC	E
	JR	PRINTL1

SCRUP:
	LD	HL,3000H+80
	CALL	SCR1

SCRUP1:
	OR	A
	RET	Z
	PUSH	AF
	CALL	UPSUB		;ﾃｷｽﾄ
	LD	DE,800H-80
	ADD	HL,DE
	CALL	UPSUB		;ｶﾝｼﾞ
	RES	3,H
	POP	AF
	DEC	A
	JR	SCRUP1

SCRDW:
	LD	HL,3000H+80*23
	CALL	SCR1
SCRDW1:
	OR	A
	RET	Z
	PUSH	AF
	CALL	DWSUB		;ﾃｷｽﾄ
	LD	DE,800H+80
	ADD	HL,DE
	CALL	DWSUB		;ｶﾝｼﾞ
	RES	3,H
	POP	AF
	DEC	A
	JR	SCRDW1
SCR1:
	LD	A,22
	LD	BC,1F80H	;DMA
	LD	E,0C3H
	OUT	(C),E
	LD	DE,659AH
	OUT	(C),E
	OUT	(C),D
	LD	DE,79
	OUT	(C),E
	OUT	(C),D

	LD	DE,1C18H
	OUT	(C),D
	OUT	(C),E
	RET
DWSUB:
	LD	DE,-80
	JR	DWSUB1
UPSUB:
	LD	DE,80
DWSUB1:
	LD	A,0CDH
	OUT	(C),A
	OUT	(C),L		;SOURCE
	OUT	(C),H
	ADD	HL,DE
	LD	A,1DH
	OUT	(C),A		;DIST
	OUT	(C),L
	OUT	(C),H
	LD	DE,0CF87H
	OUT	(C),D
	OUT	(C),E
	RET

USAGE:
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM
	JP	0

ERROR1:
	LD	DE,EMES1
	JR	ERROR
ERROR2:
	LD	DE,EMES2
ERROR:
	LD	C,9
	CALL	SYSTEM

	LD	DE,EMES
	LD	C,9
	CALL	SYSTEM
	JP	0

ESC:
	LD	E,0FFH
	LD	C,6
	CALL	SYSTEM
	OR	A
	JP	NZ,MAIN1

QUIT:
	LD	E,0FFH
	LD	C,6
	CALL	SYSTEM
	OR	A
	JR	NZ,QUIT

	LD	E,0CH
	LD	C,6
	CALL	SYSTEM
	JP	0

TEXT:	DW	DATA
TEXT2:	DW	DATA

DATA:
DATA2	EQU	DATA+1024
