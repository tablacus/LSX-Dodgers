
;	VIEW	by Gaku

WIDTH	EQU	80
LINE	EQU	24
SYSTEM	EQU	0005H
FCB1	EQU	005CH
DTA	EQU	0080H
_COLORF	EQU	0EF96H

	ORG	0100H
	JP	START
TITLE:
	DB	"view v1.02",0DH,0AH
	DB	"usage:"
	DB	" VIEW filename",0DH,0AH
	DB	"$"
EMES1:
	DB	"File not found.$"
EMES2:
	DB	"Read error.$"
EMES:
	DB	7,0DH,0AH,"$"

START:
	LD	A,(000BH)
	LD	(SMC01+2),A
	LD	(SMC02+2),A
	LD	(SMC03+2),A
;				Read File
	LD	A,(FCB1+1)
	CP	21H
	JP	C,USAGE

	LD	DE,DTA
	LD	C,1AH
	CALL	SYSTEM

	LD	DE,FCB1
	LD	C,11H
	CALL	SYSTEM
	OR	A
	JP	NZ,ERROR1
FILES:
	LD	DE,DTA
	LD	C,0FH
	CALL	SYSTEM
	OR	A
	JP	NZ,ERROR1

	LD	HL,0
	LD	(DTA+33),HL
	LD	(DTA+35),HL
	INC	L
	LD	(DTA+14),HL

	LD	DE,DATA2
	LD	C,1AH
	CALL	SYSTEM

	LD	HL,(SYSTEM+1)
	LD	DE,DATA2
	SBC	HL,DE
	LD	DE,DTA
	LD	C,27H
	CALL	SYSTEM
	ADD	A,0FEH
	JP	C,ERROR2
;				Change Data
	EX	DE,HL		;DE=SIZE
	INC	DE
	LD	HL,DATA
	LD	IY,DATA2
	XOR	A
	CALL	WRITE
CHG1:
	LD	B,WIDTH
CHG2:
	DEC	DE
	LD	A,D
	OR	E
	JR	Z,CHGE

	LD	A,(IY+0)
	INC	IY
	CP	0AH
	JR	Z,CHG2
	CP	0DH
	JR	Z,CHG4

	CP	80H
	JR	C,CHGA
	CP	0A0H
	JR	C,CHGK
	CP	0E0H
	JR	C,CHGA
CHGK:
	INC	DE
	DEC	IY
	DEC	B
	JR	Z,CHG4
	INC	IY

	CALL	WRITE

	DEC	DE
	DEC	DE
	LD	A,D
	OR	E
	JR	Z,CHGE
	LD	A,(IY+0)
	INC	IY
CHGA:
	CALL	WRITE
	CP	9
	JR	NZ,CHG3

	LD	A,B
	DEC	A
	AND	0F8H
	INC	A
	LD	B,A
CHG3:
	DJNZ	CHG2

	LD	A,(IY+0)
	CP	0DH
	JR	Z,CHG2
CHG4:
	LD	A,0DH
	CALL	WRITE
	JR	CHG1
CHGE:
	LD	(HL),0

SMC01:	LD	A,(_COLORF)
	PUSH	AF
	LD	A,0EH
SMC02:	LD	(_COLORF),A

	LD	E,11
	LD	C,6
	CALL	SYSTEM
	LD	E,20H
	CALL	SYSTEM

	LD	HL,DTA+1
	LD	BC,8*256+6
NAME:
	LD	E,(HL)
	INC	HL
	PUSH	BC
	CALL	SYSTEM
	POP	BC
	DJNZ	NAME

	LD	E,"."
	CALL	SYSTEM

	LD	B,3
EXT:
	LD	E,(HL)
	INC	HL
	PUSH	BC
	CALL	SYSTEM
	POP	BC
	DJNZ	EXT

	LD	E,5
	CALL	SYSTEM

	POP	AF
SMC03:	LD	(_COLORF),A

KBUF:
	LD	E,0FFH
	CALL	SYSTEM
	OR	A
	JR	NZ,KBUF

	LD	HL,DATA+1
MAIN:
	CALL	PAGE
MAIN1:
	LD	E,0FFH
	LD	C,6
	CALL	SYSTEM
	CP	3
	JP	Z,QUIT
	CP	1BH
	JP	Z,ESC

	CP	1FH
	JR	Z,DOWN
	CP	1EH
	JR	Z,UP
	CP	1DH
	JR	Z,PGUP
	CP	1CH
	JR	Z,PGDW
	CP	20H
	JR	Z,NFILES
	JR	MAIN1

DOWN:
	LD	HL,(TEXT2)
	LD	A,(HL)
	OR	A
	JR	Z,MAIN1

	PUSH	HL
	CALL	SCRUP
	POP	HL
	LD	BC,3000H+WIDTH*LINE
	CALL	PRINT
	LD	(TEXT2),HL

	LD	HL,(TEXT)
	CALL	NEXT
	LD	(TEXT),HL
	JR	MAIN1
UP:
	LD	HL,(TEXT)
	DEC	HL
	LD	A,(HL)
	OR	A
	JR	Z,MAIN1

	CALL	SCRDW
	LD	HL,(TEXT)
	CALL	BACK
	LD	(TEXT),HL
	LD	BC,3000H+WIDTH
	CALL	PRINT

	LD	HL,(TEXT2)
	CALL	BACK
	LD	(TEXT2),HL
	JR	MAIN1
PGDW:
	LD	B,LINE-1
PGDW1:
	LD	HL,(TEXT2)
	LD	A,(HL)
	OR	A
	JR	Z,PGDW2

	CALL	NEXT
	LD	(TEXT2),HL

	LD	HL,(TEXT)
	CALL	NEXT
	LD	(TEXT),HL
	DJNZ	PGDW1
PGDW2:
	LD	HL,(TEXT)
	JP	MAIN

PGUP:
	LD	HL,(TEXT)
	LD	B,LINE-1
PGUP1:
	CALL	BACK
	DJNZ	PGUP1
	JP	MAIN

NFILES:
	LD	DE,DTA
	LD	C,1AH
	CALL	SYSTEM

	LD	C,12H
	CALL	SYSTEM
	OR	A
	JP	NZ,QUIT
	JP	FILES

WRITE:
	PUSH	DE
	LD	DE,(SYSTEM+1)
	DEC	D
	AND	A
	SBC	HL,DE
	ADD	HL,DE
	POP	DE
	JR	NC,WRITE1
	LD	(HL),A
	INC	HL
	RET
WRITE1:
	POP	AF
	JP	CHGE

NEXT:
	LD	A,(HL)
	OR	A
	RET	Z
	INC	HL
	CP	0DH
	JR	NZ,NEXT
	RET

BACK:
	DEC	HL
	LD	A,(HL)
BACK1:
	OR	A
	JR	Z,BACKE
	DEC	HL
	LD	A,(HL)
	CP	0DH
	JR	NZ,BACK1
BACKE:
	INC	HL
	RET

PAGE:
	LD	(TEXT),HL
	LD	BC,3000H+WIDTH
	LD	D,LINE
PAGE1:
	CALL	PRINT
	DEC	D
	JR	NZ,PAGE1
	LD	(TEXT2),HL
	RET

PRINT:
	LD	E,WIDTH
PRINT1:
	LD	A,(HL)
	OR	A
	JR	Z,PRINTE2
	INC	HL
	CP	0DH
	JR	Z,PRINTE2

	CP	80H
	JR	C,ANK
KANFLG:	CP	0A0H
	JR	C,KANJI
	CP	0E0H
	JR	C,ANK
KANJI:
	PUSH	DE
	PUSH	BC
	LD	D,A
	LD	E,(HL)
	INC	HL
	CALL	SFTJIS
	CALL	JISVRM
	POP	BC
	OUT	(C),E
	INC	BC
	OUT	(C),E
	DEC	BC
	SET	3,B
	OUT	(C),D
	INC	BC
	SET	6,D
	OUT	(C),D
	RES	3,B
	POP	DE
	DEC	E
	JR	PRINTL
ANK:
	CP	9
	JR	Z,TAB
	OUT	(C),A
	SET	3,B
	DB	0EDH,71H
	RES	3,B
PRINTL:
	INC	BC
PRINTL1:
	DEC	E
	JR	NZ,PRINT1
PRINTE:
	LD	A,(HL)
	OR	A
	JR	Z,PRINTE2
	INC	HL
	CP	0DH
	JR	NZ,PRINTE
PRINTE2:
	LD	A,20H
	INC	E
PRINTE3:
	DEC	E
	RET	Z
	OUT	(C),A
	SET	3,B
	DB	0EDH,71H
	RES	3,B
	INC	BC
	JR	PRINTE3
TAB:
	LD	A,E
	DEC	A
	AND	0F8H
	SUB	E
TAB1:
	PUSH	AF
	LD	A,20H
	OUT	(C),A
	SET	3,B
	DB	0EDH,71H
	RES	3,B
	INC	BC
	POP	AF
	DEC	E
	INC	A
	JR	NZ,TAB1
	INC	E
	JR	PRINTL1

SCRUP:
	LD	HL,3000H+WIDTH
	CALL	SCR1

SCRUP1:
	OR	A
	RET	Z
	PUSH	AF
	CALL	UPSUB		;テキスト
	LD	DE,800H-WIDTH
	ADD	HL,DE
	CALL	UPSUB		;漢字
	RES	3,H
	POP	AF
	DEC	A
	JR	SCRUP1

SCRDW:
	LD	HL,3000H+WIDTH*LINE
	CALL	SCR1
SCRDW1:
	OR	A
	RET	Z
	PUSH	AF
	CALL	DWSUB		;テキスト
	LD	DE,800H+WIDTH
	ADD	HL,DE
	CALL	DWSUB		;漢字
	RES	3,H
	POP	AF
	DEC	A
	JR	SCRDW1
SCR1:
	LD	A,LINE-1
	LD	BC,1F80H	;DMA
	LD	E,0C3H
	OUT	(C),E
	LD	DE,659AH
	OUT	(C),E
	OUT	(C),D
	LD	DE,79
	OUT	(C),E
	OUT	(C),D

	LD	DE,1C18H
	OUT	(C),D
	OUT	(C),E
	RET
DWSUB:
	LD	DE,-WIDTH
	JR	DWSUB1
UPSUB:
	LD	DE,WIDTH
DWSUB1:
	LD	A,0CDH
	OUT	(C),A
	OUT	(C),L		;SOURCE
	OUT	(C),H
	ADD	HL,DE
	LD	A,1DH
	OUT	(C),A		;DIST
	OUT	(C),L
	OUT	(C),H
	LD	DE,0CF87H
	OUT	(C),D
	OUT	(C),E
	RET

USAGE:
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM
	JP	0

ERROR1:
	LD	DE,EMES1
	JR	ERROR
ERROR2:
	LD	DE,EMES2
ERROR:
	LD	C,9
	CALL	SYSTEM

	LD	DE,EMES
	LD	C,9
	CALL	SYSTEM
	JP	0

ESC:
	LD	E,0FFH
	LD	C,6
	CALL	SYSTEM
	OR	A
	JP	NZ,MAIN1

QUIT:
	LD	E,0FFH
	LD	C,6
	CALL	SYSTEM
	OR	A
	JR	NZ,QUIT

	LD	E,0CH
	LD	C,6
	CALL	SYSTEM
	LD	BC,03000H+WIDTH*24
	LD	A,020H
QUIT1:
	OUT	(C),A
	INC	BC
	BIT	3,B
	JR	Z,QUIT1
	LD	BC,03800H+WIDTH*24
QUIT2:
	OUT	(C),0
	INC	BC
	BIT	3,B
	JR	NZ,QUIT2
	JP	0

SFTJIS:
	SLA	D
	LD	A,E
	CP	$80
	ADC	A,$61
	LD	E,A
	LD	A,D
	ADC	A,$1F
	LD	D,A
	LD	A,E
	ADD	A,$7F
	LD	E,A
	JR	C,SFTJIS1
	ADD	A,$A2
SFTJIS1:
	LD	E,A
	RES	7,D
	RET

;in
;DE:	JISコード
;out
;D:	RH
;E:	RL
JISVRM:
	LD	A,D
	RRCA
	RRCA
	RRCA
	RRCA
	AND	7	;JH6,JH5,JH4
	CP	2
	JR	Z,JR2
	JR	C,JR0
	CP	7
	JR	Z,JR7
			;3-6
	SUB	3
	LD	B,A
	ADD	A,A	;*2
	ADD	A,B	;*3
	INC	A
	LD	B,A
	LD	A,E
	RLCA
	RLCA
	RLCA
	AND	3	;JL6,JL5
	ADD	A,B
	ADD	A,A	;*2
JRE:
	BIT	3,D	;JH3
	JR	Z,JR3_1
	INC	A
JR3_1:
	LD	B,D
	OR	$80	;RH7
	LD	D,A
	LD	A,E
	AND	$1F
	LD	E,A
	LD	A,B
	RRCA
	RRCA
	RRCA
	AND	$E0
	OR	E
	LD	E,A
JR0:
	RET
JR7:
	LD	B,$1C
	JR	JR2_1
JR2:
	LD	B,0
JR2_1:
	LD	A,E
	AND	$60		;JL6,JL5
	CP	2*$20
	JR 	NZ,JR2_2
	SET	1,B
JR2_2:
	CP	3*$20
	LD	A,B
	JR	NZ,JR2_3
	XOR	1
JR2_3:
	JR	JRE

TEXT:	DW	DATA
TEXT2:	DW	DATA

DATA:
DATA2	EQU	DATA+1024

