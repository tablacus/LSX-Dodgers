;
;	EMM controler by Gaku
;
SYSTEM	EQU	$0005
DTA	EQU	$0080

	ORG	$0100
	JP	START
START:
	LD	DE,TITLE
	LD	C,9		;(BDOS)文字列出力(_STROUT)
	CALL	SYSTEM
	LD	A,(DTA)
	OR	A
	JP	Z,USAGE
MAIN:
	LD	C,$6F 		;(BDOS)MSX-DOS のバージョン番号の獲得(_DOSVER)
	CALL	SYSTEM
	LD	A,C
	CP	$1D
	RET	NZ
	LD	A,L
	AND	$7F
	RET	NZ

	LD	SP,(SYSTEM+1)
	LD	DE,DTA+1
	CALL	AHEX
	JP	C,USAGE
	LD	(LEVEL),A

	CALL	FIND
	CALL	EMM
	JP	NC,0

	LD	A,(LEVEL)
	LD	(IX+$0C),A	;(DPB)フロッピーディスクのシリンダ数(DPB_MAXCYL)
	LD	(IX+$08),L	;(DPB)総クラスタ数(DPB_MAXCL)
	LD	(IX+$09),H	; 		//
AREA:
	LD	DE,MAREA
	LD	C,9		;(BDOS)文字列出力(_STROUT)
	CALL	SYSTEM

	LD	A,(IX+$0C)	;(DPB)フロッピーディスクのシリンダ数(DPB_MAXCYL)
	CALL	PRTHX

	LD	DE,CRLF
	LD	C,9		;(BDOS)文字列出力(_STROUT)
	CALL	SYSTEM

	JP	0

FIND:
	XOR	A
FIND1:
	PUSH	AF
	LD	E,A
	LD	C,$1F		;(BDOS)ディスク装置のパラメータの読み出し
	CALL	SYSTEM

	LD	A,(IX+$12)	;(DPB)デバイス情報(DPB_DEVICE)
	AND	$0F
	CP	$06
	JR	Z,EXIT
	POP	AF
	INC	A
	CP	8
	JR	C,FIND1
	JP	0
EXIT:
	POP	AF
	RET

EMM:
	CALL	EADR0
	IN	A,(C)
	PUSH	AF

	LD	DE,0
ECHECK1:
	INC	DE
	CALL	EADR2
	IN	H,(C)
	CALL	EADR2
	LD	A,H
	ADD	A,$E5
	OUT	(C),A
	INC	A
	CALL	EADR0
	OUT	(C),A
	DEC	A
	CALL	EADR2
	IN	L,(C)
	CALL	EADR2
	OUT	(C),H
	CP	L
	JR	NZ,ECHECK2
	BIT	3,D
	JR	Z,ECHECK1
ECHECK2:
	CALL	EADR0
	POP	AF
	OUT	(C),A

	LD	HL,-9
	ADD	HL,DE
	RET

EADR2:
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,(LEVEL-1)
	LD	C,0
	ADD	HL,BC
	LD	C,(IX+$13)	;(DPB)デバイスドライバ内におけるユニット番号(DPB_UNITNO)
	LD	B,$0D
	OUT	(C),C
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H
	INC	C
	EX	DE,HL
	POP	DE
	RET

EADR0:
	LD	C,(IX+$13)	;(DPB)デバイスドライバ内におけるユニット番号(DPB_UNITNO)
	LD	B,$0D
	OUT	(C),0		;Z80未定義命令
	INC	C
	OUT	(C),0		;Z80未定義命令
	INC	C
	OUT	(C),0		;Z80未定義命令
	INC	C
	RET

HLHEX4:
	INC	DE
HLHEX5:
	LD	A,(DE)
	CP	$20
	JR	Z,HLHEX4
HLHEX6:
	LD	A,(DE)
	CALL	HEX
	RET	C
	INC	DE
	RET
AHEX:
	PUSH	BC
	PUSH	DE
	CALL	HLHEX5
	JR	C,AHEX3
	LD	C,A
	CALL	HLHEX6
	JR	C,AHEX1
	LD	B,A
	LD	A,C
	RLCA
	RLCA
	RLCA
	RLCA
	ADD	A,B
	LD	C,A
AHEX1:
	LD	A,C
AHEX2:
	POP	BC
	POP	BC
	OR	A
	RET
AHEX3:
	LD	A,(DE)
	CP	$3B
	JR	Z,AHEX4
	POP	DE
	POP	BC
	SCF
	RET
AHEX4:
	INC	DE
	LD	A,(DE)
	INC	DE
	JR	AHEX2

HEX:
	CALL	CAP
	SUB	"0"
	RET	C
	CP	10
	JR	C,HEX1
	CP	17
	RET	C
	SUB	7
	CP	$10
HEX1:
	CCF
	RET

CAP:
	CP	"a"
	RET	C
	CP	"z"+1
	RET	NC
	SUB	$20
	RET

PRTHX:
	PUSH	AF
	RLCA
	RLCA
	RLCA
	RLCA
	CALL	PRTA2
	POP	AF
PRTA2:
	CALL	ASC
	LD	E,A
	LD	C,2		;(BDOS)コンソール出力(_CONOUT)
	JP	SYSTEM

ASC:
	AND	$0F
	OR	$30
	CP	$3A
	RET	C
	ADD	A,7
	RET

USAGE:
	LD	DE,MUSAGE
	LD	C,9		;(BDOS)文字列出力(_STROUT)
	CALL	SYSTEM
	CALL	FIND
	JP	AREA

TITLE:
	DB	"X1 EMM controler v1.01"
CRLF:
	DB	$0D,$0A
	DB	"$"
MUSAGE:
	DB	"usage: EMM ｴﾘｱ(0-FF)",$0D,$0A
	DB	"$"
MAREA:
	DB	"EMM area: $"

LEVEL:	DB	0

