;
;	screen controler by Gaku
;
OSFLG	EQU	$0003
SYSTEM	EQU	$0005
BASE	EQU	$000B
JP_HL	EQU	$000F
DTA	EQU	$0080

	ORG	$0100
	JP	START
START:
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM
	LD	A,(DTA)
	OR	A
	JR	NZ,MAIN
	LD	DE,USAGE
	LD	C,9
	JP	SYSTEM
MAIN:
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	RET	NZ
	LD	A,L
	AND	$7F
	RET	NZ

	LD	SP,(SYSTEM+1)

	LD	IX,DTA+1
	JR	MAIN4
MAIN1:
	LD	A,(IX+0)
	SUB	"0"
	JR	C,MAIN2
	CP	9
	JR	NC,MAIN2
	INC	IX
	JR	MAIN3
MAIN2:
	XOR	A
MAIN3:
	CALL	JP_HL
MAIN4:
	LD	A,(IX+0)
	INC	IX
	CALL	CAP
	CP	$20
	JP	C,0
	CP	"A"
	LD	HL,ANALOG
	JR	Z,MAIN1
	CP	"B"
	LD	HL,BANK
	JR	Z,MAIN1
	CP	"C"
	LD	HL,CLS
	JR	Z,MAIN1
	CP	"G"
	LD	HL,GRAPH
	JR	Z,MAIN1
	CP	"L"
	LD	HL,LINE
	JR	Z,MAIN1
	CP	"R"
	LD	HL,RESO
	JR	Z,MAIN1
	CP	"T"
	LD	HL,TEXT
	JR	Z,MAIN1
	CP	"V"
	LD	HL,VIDEO
	JR	Z,MAIN1
	JR	MAIN4

RESO:
	OR	A
	LD	HL,C8025L	;0
	JR	Z,SETCRT1
	DEC	A
	LD	HL,C8025S	;1
	JR	Z,SETCRT1
	DEC	A
	LD	HL,C8025I	;2
	JR	Z,SETCRT1
	DEC	A
	LD	HL,C8025L	;3
	JR	Z,SETCRTC
	DEC	A
	LD	HL,C8025S	;4
	JR	Z,SETCRTC
	DEC	A
	LD	HL,C8025I	;5
	JR	Z,SETCRTC
	DEC	A
	LD	HL,_C8025H	;6
	JR	Z,SETCRTC
	DEC	A
	LD	HL,C4025L	;7
	JR	Z,SETCRTC
	DEC	A
	LD	HL,C6432P	;8
	JR	Z,SETCRTC
	RET
SETCRT1:
	LD	A,$1F
	IN	A,($F0)
	RRCA
	JR	C,SETCRTC
	LD	HL,_C8025H
SETCRTC:
	LD	DE,(BASE-1)
	LD	E,$B1
	LD	A,(DE)
	INC	HL
	CP	(HL)
	PUSH	DE
	LD	E,0CH
	LD	C,2
	CALL	NZ,SYSTEM
	POP	DE
	DEC	HL
	DEC	DE
	PUSH	DE
	LD	BC,16
	LDIR
	LD	A,(HL)
	LD	HL,(BASE-1)
	LD	L,$97
	LD	(HL),A
	POP	HL
SETCRT2:
	CALL	G1FD0
	LD	BC,$1800
	XOR	A
SETCRTL:
	LD	D,(HL)
	OUT	(C),A
	INC	C
	OUT	(C),D
	DEC	C
	INC	HL
	INC	A
	CP	12
	JR	NZ,SETCRTL
	INC	HL
	INC	HL
	LD	A,(HL)
	LD	BC,$1A03
	OUT	(C),A
	INC	HL
	LD	A,(HL)
	JP	S1FD0

VIDEO:
	LD	A,$E7
	CALL	COMOUT
	LD	A,$1E
	CALL	OT49SB

	LD	E,$0C
	LD	C,6
	CALL	SYSTEM
;
	CALL	G1FD0
	PUSH	AF
	LD	HL,C4025L
	CALL	SETCRT2
VIDEO1:
	LD	A,$88
	LD	BC,$1FB0
	OUT	(C),A

	CALL	FLGET

	LD	A,$80
	LD	BC,$1FB0
	OUT	(C),A

	CALL	FLGET
	JR	NC,VIDEO1
VIDEOE:
	POP	AF
	CALL	S1FD0
	LD	BC,$1FB0
	DB	$ED,$71
	LD	A,$E7
	CALL	COMOUT
	LD	A,$1D
OT49SB:
	PUSH	BC
	CALL	CANW
	LD	BC,$1900
	OUT	(C),A
	POP	BC
	RET

CLS:
	CP	3
	RET	NC
	PUSH	AF
	CALL	CLS0
	POP	AF
	DEC	A
	CP	1
	RET	NZ
CLS0:
	LD	HL,(BASE-1)
	LD	L,$BF
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	AND	$10
	LD	B,A
	LD	A,(HL)
	AND	$EF
	OR	B
	LD	(HL),A
	LD	BC,$1FD0
	OUT	(C),A

	XOR	A
	LD	C,A
	LD	HL,$4003
CLS1:
	LD	B,H
CLS2:
	DB	$ED,$71
	INC	B
	JR	NZ,CLS2
;
	ADD	A,L
	LD	C,A
	JR	NZ,CLS1
	RET

BANK:
	CALL	G1FD0

	SET	4,A
	LD	E,A	;BANK1
	RES	4,A
	LD	D,A	;BANK0
	CALL	S1FD0
	LD	BC,$4000
BANK1:
	IN	H,(C)	;BANK0
	LD	A,E
	CALL	S1FD0	;BANK1
	IN	L,(C)
	OUT	(C),H
	LD	A,D
	CALL	S1FD0	;BANK0
	OUT	(C),L
	INC	BC
	LD	A,B
	OR	C
	JR	NZ,BANK1
	RET

ANALOG:
	CP	2
	RET	NC
	RRCA
	AND	$80
	LD	BC,$1FB0
	OUT	(C),A
	RET

LINE:
	CP	2
	RET	NC
	ADD	A,A
	CPL
	AND	2
	LD	B,A

	CALL	G1FD0
	AND	$FD
	OR	B
S1FD0:
	PUSH	BC
	LD	BC,$1FD0
	OUT	(C),A
	LD	(IY+0),A
	POP	BC
	RET

GRAPH:
	CP	3
	RET	NC
	OR	A
	JR	NZ,GRAPH1

	LD	B,$10
	OUT	(C),A
	INC	B
	OUT	(C),A
	INC	B
	OUT	(C),A
	INC	B
	OUT	(C),A

	CALL	G1FD0
	OR	$80
	JR	S1FD0
GRAPH1:
	DEC	A
	JR	NZ,GRAPH2

	CALL	PALET1
	AND	$77
	JR	S1FD0
GRAPH2:
	CALL	PALET1
	AND	$77
	OR	8
	JR	S1FD0

TEXT:
	LD	BC,$1FB0
	IN	E,(C)
	LD	A,$80
	OUT	(C),A
	LD	D,7
	LD	C,$B9
	LD	HL,TEXTDT
TEXT1:
	INC	B
	OUTI
	INC	C
	DEC	D
	JR	NZ,TEXT1
	LD	BC,$1FB0
	OUT	(C),E
	RET

PALET1:
	LD	BC,$10AA
	OUT	(C),C
	LD	BC,$11CC
	OUT	(C),C
	LD	BC,$12F0
	OUT	(C),C
	INC	B
	DB	$ED,$71
G1FD0:
	LD	A,(BASE)
	LD	IYH,A
	LD	IYL,$BF
	LD	A,(IY+0)
	RET

FLGET:
	LD	A,(VIDEOH)
	LD	BC,$1FC1
	OUT	(C),A
	LD	C,7
	CALL	SYSTEM
	CP	$0B
	JR	NZ,FLGET0
	LD	A,40
	JR	FLGET2
FLGET0:
	SUB	$1C
	RET	C
	JR	NZ,FLGET1
	LD	A,(VIDEOH)
	OR	A
	JR	Z,FLGET
	DEC	A
	JR	FLGET2
FLGET1:
	DEC	A
	RET	NZ
	LD	A,(VIDEOH)
	INC	A
	JR	Z,FLGET
FLGET2:
	LD	(VIDEOH),A
	JR	FLGET

CANW:
	LD	BC,$1A01
	IN	C,(C)
	BIT	6,C
	JR	NZ,CANW
	RET

COMOUT:
	EI
	CALL	OT49SB
	CALL	CANW
	DI
	RET

CAP:
	CP	"a"
	RET	C
	CP	"z"+1
	RET	NC
	SUB	$20
	RET

TITLE:
	DB	"X1 screen controler v1.03"
	DB	$0D,$0A
	DB	"$"
USAGE:
	DB	"usage: SC ºÏÝÄÞ",$0D,$0A
	DB	9,"An ÃÞ¼ÞÀÙ/±ÅÛ¸Þ(n:0-1)",$0D,$0A
	DB	9,"B  ¸Þ×Ì¨¯¸ ÊÞÝ¸ º³¶Ý",$0D,$0A
	DB	9,"Cn ¸Þ×Ì¨¯¸ ¸Ø±(n:0-2)",$0D,$0A
	DB	9,"Gn ¸Þ×Ì¨¯¸ µÌ/µÝ(n:0-2)",$0D,$0A
	DB	9,"Ln ¸Þ×Ì¨¯¸ ×²Ý(n:0-1)",$0D,$0A
	DB	9,"Rn ¶²¿Þ³ÄÞ(n:0-8)",$0D,$0A
	DB	9,"T  Ã·½ÄÊßÚ¯Ä ¼®·¶",$0D,$0A
	DB	9,"V  ËÞÃÞµ ÄØºÐ",$0D,$0A
	DB	"$"

C8025L:	DB	$6F,$50,$59,$38,$1F,$02,$19,$1C
	DB	$00,$07,$80,$F8,$B0,$FF,$0C,$A0
	DB	24

C8025S:	DB	$6F,$50,$59,$38,$1F,$00,$19,$1C
	DB	$03,$0E,$80,$F8,$B0,$FF,$0C,$A4
	DB	24

C8025I:	DB	$A7,$50,$74,$38,$1F,$01,$19,$1C
	DB	$03,$0E,$80,$F8,$B0,$FF,$0C,$A3
	DB	24

_C8025H:	DB	$6B,$50,$59,$88,$1B,$01,$19,$1A
	DB	$00,$0F,$80,$F8,$B0,$FF,$0C,$A3
	DB	24

C4025L:	DB	$37,$28,$2D,$34,$1F,$02,$19,$1C
	DB	$00,$07,$40,$FC,$D8,$FF,$0D,$A0
	DB	24

C6432P:	DB	$53,$40,$47,$34,$1F,$07,$1D,$1E
	DB	$03,$0E,$40,$F8,$C0,$FF,$0D,$A1
	DB	31

TEXTDT:	DB	$03,$0C,$0F,$30,$33,$3C,$3F

VIDEOH:	DB	40

