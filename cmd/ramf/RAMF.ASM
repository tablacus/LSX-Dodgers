; ramf

OSFLG	EQU	0003H
SYSTEM	EQU	0005H
BASE	EQU	000BH
JP_HL	EQU	000FH
BIOS	EQU	0018H
FCB1	EQU	005CH
DTA	EQU	0080H

	ORG	0100H
	JP	START
START:
	LD	DE,TITLE
	LD	C,09H
	CALL	SYSTEM
	LD	A,(FCB1)
	OR	A
	JR	NZ,MAIN
	LD	A,(FCB1+1)
	CP	20H
	JR	NZ,MAIN
	LD	DE,MSG_USAGE
	LD	C,09H
	JP	SYSTEM
MAIN:
	LD	HL,(OSFLG)
	LD	A,H
	CP	1DH
	RET	NZ
	LD	A,L
	AND	0F0H
	CP	090H
	RET	NZ
	LD	HL,BUF
	LD	DE,BUF+1
	LD	BC,4000H
	LDIR
	CALL	SWITCH
	CALL	CAP
	CP	'I'
	JR	NZ,NOT_I
	LD	(I_PAT+1),A
NOT_I:
	LD	DE,DTA+1
	CALL	SKIP_SP
RM0145:
	LD	A,(DE)
	INC	DE
	OR	A
	RET	Z
	CP	':'
	JR	Z,RM0145
	CALL	CAP
	SUB	'A'
	RET	C
	CP	8
	RET	NC
	LD	(DRIVE),A
	PUSH	DE
	PUSH	AF
	ADD	A,'A'
	LD	E,A
	LD	C,2
	CALL	SYSTEM
	LD	E,':'
	LD	C,2
	CALL	SYSTEM
	POP	AF
	PUSH	AF
	LD	HL,(BASE-1)
	LD	L,DCH
	CALL	JP_HL
	POP	AF
	INC	A
	LD	E,A
	LD	A,(IX+00H)	;FAT領域のセクタ単位でのサイズ(DPB_00_FATLN)
	OR	A
	JR	Z,SKIP
	BIT	6,(IX+0FH)	;FAT情報と論理セクタのサイズ(DPB_0F_BPS)
	JR	NZ,SKIP		;FAT16
	LD	A,(IX+12H)	;デバイス情報(DPB_12_DEVICE)
	AND	0FH
	CP	05H
	JR	Z,RAMDISK
	CP	06H
	JR	Z,RAMDISK
	CP	0BH
	JR	NZ,SKIP
RAMDISK:
I_PAT:	LD	A,00H
	OR	A
	JR	NZ,FORMAT
	LD	C,1BH
	CALL	SYSTEM
	INC	A
	JR	NZ,SKIP
FORMAT:
	LD	DE,FDATA
	LD	C,1AH
	CALL	SYSTEM
	LD	A,(IX+06H)	;メディアバイト(DPB_06_FATID)
	LD	(FDATA),A
	LD	E,(IX+0EH)	;FAT領域の先頭論理セクタ番号(DPB_0E_FATPS)
	LD	D,00H
	LD	HL,(DRIVE)
	LD	H,(IX+00H)	;FAT領域のセクタ単位でのサイズ(DPB_00_FATLN)
	LD	C,30H
	CALL	SYSTEM
	LD	DE,BUF
	LD	C,1AH
	CALL	SYSTEM
	LD	E,(IX+10H)	;ルートディレクトリ領域の先頭論理セクタ番号(DPB_10_DIRPS)
	LD	D,00H
	LD	HL,(DRIVE)
	LD	A,(IX+0BH)	;ルートディレクトリ領域の論理セクタ数(DPB_0B_DIRSCNT)
	LD	H,A
	LD	C,30H
	CALL	SYSTEM
	LD	DE,MSG_FORMAT
	JR	SHOW_MSG
SKIP:
	LD	DE,MSG_SKIP
SHOW_MSG:
	LD	C,09H
	CALL	SYSTEM
	POP	DE
	JP	RM0145
SKIP_SP1:
	INC	DE
SKIP_SP:
	LD	A,(DE)
	CP	20H
	JR	Z,SKIP_SP1
	RET
CAP:
	CP	'a'
	RET	C
	CP	'z'+1
	RET	NC
	SUB	20H
	RET
SWITCH:
	LD	DE,DTA+1
SWITCH1:
	LD	A,(DE)
	CP	20H
	RET	C
	INC	DE
	CP	2FH
	JR	NZ,SWITCH1
	LD	A,(DE)
	INC	DE
	RET
TITLE:
	DB	"LD ram disk format v1.02"
	DB	0DH, 0AH
	DB	'$'
MSG_USAGE:
	DB	"usage: RAMF ﾄﾞﾗｲﾌﾞ[ﾄﾞﾗｲﾌﾞ･･･][ｽｲｯﾁ]"
	DB	00DH, 00AH
	DB	9,"/I ｷｮｳｾｲ ｼｮｷｶ"
	DB	0DH, 0AH
	DB	'$'
MSG_FORMAT:
	DB	" format"
	DB	0DH, 0AH
	DB	'$'
MSG_SKIP:
	DB	" skip"
	DB	0DH, 0AH
	DB	'$'
DRIVE:
	DB	0
FDATA:
	DB	0
	DB	0FFH, 0FFH
BUF:
	DB	0
