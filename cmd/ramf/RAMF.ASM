; ramf

SYSTEM	EQU	0005H
BASE	EQU	0002H
JP_HL	EQU	000FH
BIOS	EQU	0018H
FCB1	EQU	005CH
DTA	EQU	0080H

DPB_FATLN	EQU	00H
DPB_DRD		EQU	02H
DPB_DWT		EQU	04H
DPB_FATID	EQU	06H
DPB_SECPCL	EQU	07H
DPB_MAXCL	EQU	08H
DPB_FDMODE	EQU	0AH
DPB_DIRSCNT	EQU	0BH
DPB_MAXCYL	EQU	0CH
DPB_MAXSEC	EQU	0DH
DPB_FATPS	EQU	0EH
DPB_BPS		EQU	0FH
DPB_DIRPS	EQU	10H
DPB_DEVICE	EQU	12H
DPB_UNITNO	EQU	13H
DPB_ADDCL	EQU	14H
DPB_NAME	EQU	1CH

	ORG	0100H
	JP	START
START:
	LD	DE,TITLE
	LD	C,09H
	CALL	SYSTEM
	LD	A,(FCB1)
	OR	A
	JR	NZ,MAIN
	LD	A,(FCB1+1)
	CP	20H
	JR	NZ,MAIN
	LD	DE,MSG_USAGE
	LD	C,09H
	JP	SYSTEM
MAIN:
	LD	C,06FH 		;(BDOS)MSX-DOS のバージョン番号の獲得(_DOSVER)
	CALL	SYSTEM
	LD	A,C
	CP	01DH
	RET	NZ

	LD	HL,BUF
	LD	DE,BUF+1
	LD	BC,4000H
	LDIR
	CALL	SWITCH
	CALL	CAP
	CP	'I'
	JR	NZ,NOT_I
	LD	(I_PAT+1),A
NOT_I:
	LD	DE,DTA+1
	CALL	SKIP_SP
RM0145:
	LD	A,(DE)
	INC	DE
	OR	A
	RET	Z
	CP	':'
	JR	Z,RM0145
	CALL	CAP
	SUB	'A'
	RET	C
	CP	8
	RET	NC
	LD	(DRIVE),A
	PUSH	DE
	PUSH	AF
	ADD	A,'A'
	LD	E,A
	LD	C,2
	CALL	SYSTEM
	LD	E,':'
	LD	C,2
	CALL	SYSTEM
	POP	AF
	PUSH	AF
	LD	E,A
	LD	C,01FH		;(BDOS)ディスク装置のパラメータの読み出し
	CALL	SYSTEM
	POP	AF
	INC	A
	LD	E,A
	LD	A,(IX+DPB_FATLN)	;FAT領域のセクタ単位でのサイズ
	OR	A
	LD	A,(IX+DPB_DEVICE)	;デバイス情報
	JR	NZ,CHECK_DEVICE
	AND	0FH
	CP	06H			;EMM
	JR	Z,FORMAT
	JR	SKIP
CHECK_DEVICE:
	AND	0FH
	CP	05H
	JR	Z,RAMDISK
	CP	0BH
	JR	Z,RAMDISK
	CP	0CH
	JR	Z,RAMDISK
	CP	0FH
	JR	Z,RAMDISK
	CP	06H
	JR	NZ,SKIP
INIT_EMM:

RAMDISK:
I_PAT:	LD	A,00H
	OR	A
	JR	NZ,FORMAT
	LD	C,1BH
	CALL	SYSTEM
	INC	A
	JR	NZ,SKIP
FORMAT:
	LD	A,(IX+DPB_DEVICE)	;デバイス情報
	AND	0FH
	CP	06H

	CALL	Z,BPB_FROM_EMM
	LD	A,(IX+DPB_FATLN)	;FAT領域のセクタ単位でのサイズ
	OR	A
	JR	Z,SKIP

	LD	DE,FDATA
	LD	C,1AH
	CALL	SYSTEM
	LD	A,(IX+DPB_FATID)	;メディアバイト
	LD	HL,FDATA
	LD	(HL),A
	INC	HL
	LD	(HL),$FF
	INC	HL
	LD	(HL),$FF
	INC	HL
	LD	A,(IX+DPB_BPS)	;FAT情報と論理セクタのサイズ
	ADD	A,A
	ADD	A,A
	ADD	A,A
	SBC	A,A
	LD	(HL),A		;FAT12=00 FAT16=FF

	LD	E,(IX+DPB_FATPS)	;FAT領域の先頭論理セクタ番号
	LD	D,00H
	LD	HL,(DRIVE)
	LD	H,(IX+DPB_FATLN)	;FAT領域のセクタ単位でのサイズ
	LD	C,30H
	CALL	SYSTEM
	LD	DE,BUF
	LD	C,1AH
	CALL	SYSTEM
	LD	E,(IX+DPB_DIRPS)	;ルートディレクトリ領域の先頭論理セクタ番号
	LD	D,00H
	LD	HL,(DRIVE)
	LD	A,(IX+DPB_DIRSCNT)	;ルートディレクトリ領域の論理セクタ数
	LD	H,A
	LD	C,30H
	CALL	SYSTEM
	LD	DE,MSG_FORMAT
	JR	SHOW_MSG
SKIP:
	LD	DE,MSG_SKIP
SHOW_MSG:
	LD	C,09H
	CALL	SYSTEM
	POP	DE
	JP	RM0145
SKIP_SP1:
	INC	DE
SKIP_SP:
	LD	A,(DE)
	CP	20H
	JR	Z,SKIP_SP1
	RET
CAP:
	CP	'a'
	RET	C
	CP	'z'+1
	RET	NC
	SUB	20H
	RET
SWITCH:
	LD	DE,DTA+1
SWITCH1:
	LD	A,(DE)
	CP	20H
	RET	C
	INC	DE
	CP	2FH
	JR	NZ,SWITCH1
	LD	A,(DE)
	INC	DE
	RET

DWT:
	PUSH	HL
	PUSH	BC
	PUSH	DE
	EX	DE,HL
	LD	C,$1A		;DTAの設定
	CALL	SYSTEM
	POP	DE
	PUSH	DE
	LD	HL,(DRIVE)
	LD	H,1
	LD	C,$30		;論理セクタを用いた書き込み
	CALL	SYSTEM
	POP	DE
	INC	DE
	POP	BC
	POP	HL
	RET

ERROR:
	LD	DE,ERRMES
	LD	C,9		;文字列出力
	CALL	SYSTEM
	JP	0

BPB_FROM_EMM:				;EMMのサイズからBPBを作成する
	LD	DE,EMMMES
	LD	C,9		;文字列出力
	CALL	SYSTEM

	LD	A,(IX+DPB_MAXSEC)	;フロッピーディスクの1トラックのセクタ数
	LD	(EMMAL),A
	LD	A,(IX+DPB_MAXCYL)	;フロッピーディスクのシリンダ数
	LD	(EMMBL),A
	LD	A,(IX+DPB_UNITNO)	;デバイスドライバ内におけるユニット番号
	LD	(EMMDV),A

	CALL	EADR0
	IN	A,(C)
	PUSH	AF

	LD	DE,0
ECHECK1:
	INC	DE
	CALL	EADR2
	IN	H,(C)
	CALL	EADR2
	LD	A,H
	ADD	A,0E5H
	OUT	(C),A
	INC	A
	CALL	EADR0
	OUT	(C),A
	DEC	A
	CALL	EADR2
	IN	L,(C)
	CALL	EADR2
	OUT	(C),H
	CP	L
	JR	NZ,ECHECK2
	BIT	7,D
	JR	Z,ECHECK1
ECHECK2:
	CALL	EADR0
	POP	AF
	OUT	(C),A

	LD	HL,0-9
	ADD	HL,DE
	JP	NC,ERROR

	LD	BC,4086
	AND	A
	SBC	HL,BC
	JR	C,EFAT12
	LD	HL,CALC_FATLN16
	LD	(CALC_FATLN),HL
EFAT12:
	PUSH	DE
	POP	HL
	LD	(EMM_TotSec16),HL	;総セクタ数
	EX	DE,HL
CALC_FATLN	EQU	$+1
	CALL	CALC_FATLN12		;自己書き換え
	LD	(EMM_FATSz16),A		;FATのサイズ
	LD	(IX+DPB_FATLN),A	;FAT領域のセクタ単位でのサイズ

	LD	HL,FDATA
	LD	DE,FDATA+1
	LD	(HL),0
	LD	BC,512-FDATA+EMM_BPB
	LDIR

	LD	DE,0
	LD	HL,EMM_BPB
	CALL	DWT
	OR	A
	JP	NZ,ERROR

	LD	A,(FCB1)
	LD	E,A
	LD	C,$1B		;ディスク情報の獲得
	JP	SYSTEM

EADR0:
	PUSH	DE
	EX	DE,HL
	LD	HL,0
	JR	EADR1
EADR2:
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	LD	BC,(EMMAL)
	ADD	HL,BC
EADR1:
	LD	BC,(EMMDV)
	OUT	(C),0		;Z80未定義命令
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H
	INC	C
	EX	DE,HL
	POP	DE
	RET

CALC_FATLN12:	;FAT12 - クラスタ数から必要なFATサイズを求める(セクタサイズ512)
	LD	E,L
	LD	D,H
	ADD	HL,HL	;*2
	ADD	HL,DE	;*3
	SRL	H	;/2
	RR	L	;1クラスタ辺り1.5バイト必要
	LD	DE,511	;切り上げる
	ADD	HL,DE
	LD	A,H
	SRL	A
	RET

CALC_FATLN16:	;FAT16 - クラスタ数から必要なFATサイズを求める(セクタサイズ512)
	LD	A,L
	ADD	A,$FF
	SBC	A,A
	AND	1
	ADD	A,H
	RET

TITLE:
	DB	"LD ram disk format v1.04"
	DB	0DH, 0AH
	DB	'$'
MSG_USAGE:
	DB	"usage: RAMF ﾄﾞﾗｲﾌﾞ[ﾄﾞﾗｲﾌﾞ･･･][ｽｲｯﾁ]"
	DB	00DH, 00AH
	DB	9,"/I ｷｮｳｾｲ ｼｮｷｶ"
	DB	0DH, 0AH
	DB	'$'
MSG_FORMAT:
	DB	" format"
	DB	0DH, 0AH
	DB	'$'
MSG_SKIP:
	DB	" skip"
	DB	0DH, 0AH
	DB	'$'
EMMMES:
	DB	" analysis EMM",'$'
ERRMES:
	DB	7,"Error!",0DH,0AH,'$'

EMMDV:	DW	$0D00
EMMAL:	DB	0
EMMBL:	DB	0

EMM_BPB:
	DB	0EBH,0FEH,090H
EMM_OEMName:
	DB	"LD-EMM  "
EMM_BytsPerSec:
	DW	512
EMM_SecPerClus:
	DB	2
EMM_RsvdSecCnt:
	DW	1
EMM_NumFATs:
	DB	1
EMM_RootEntCnt:
	DW	512
EMM_TotSec16:
	DW	0
EMM_Media:
	DB	0FAH
EMM_FATSz16:
	DW	0

DRIVE:
	DB	0
FDATA:
	DB	0
	DB	0FFH, 0FFH
BUF:
	DB	0
