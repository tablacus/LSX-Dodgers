;
;	PWD v1.01	カレントディレクトリの表示
;
SYSTEM	EQU	$0005
FCB1	EQU	$005C

	ORG	$0100
	JP	START		;0C3HはZ80ではJPだが、i8086ではRETなので
START:				;間違えてMS-DOS環境で実行しても即終了できる
	LD	C,$6F		;MSX-DOS のバージョン番号の獲得(_DOSVER)
	CALL	SYSTEM
	JP	C,DOS1
	LD	A,B
	CP	2
	JP	NC,DOS2
	LD	A,C
	CP	$1D
	JP	NZ,DOS1

	LD	DE,DIR_MATCH
	LD	C,$1A		;DTAの設定(_SETDTA)
	CALL	SYSTEM
LOOP1:
	LD	A,$10
	LD	(FCB_PARENT+13),A
	LD	DE,FCB_PARENT
	LD	C,$0F		;ファイルのオープン(_FOPEN)
	CALL	SYSTEM
	CP	$FF
	JP	Z,SHOW
	LD	A,(FCB_PARENT+13)
	BIT	4,A
	JR	Z,SHOW
SETED:
	LD	A,(FCB_PARENT)	;ドライブ番号
	LD	(FCB_SEARCH),A

	LD	HL,(FCB_PARENT+26)	;ファイルの先頭クラスタ
	INC	HL
	LD	(FCB_SEARCH+14),HL	;アクセスするディレクトリのクラスタ番号+1
	LD	A,$10
	LD	(FCB_SEARCH+13),A

	LD	DE,FCB_SEARCH
	LD	C,$11		;ファイル検索(_SFIRST)
	CALL	SYSTEM
LOOP2:
	CP	$FF
	JR	Z,SHOW
	LD	A,(DIR_MATCH+2)
	CP	'.'			;..
	JR	Z,SKIP
	LD	HL,(FCB_PARENT+14)	;アクセスするディレクトリのクラスタ番号+1
	LD	DE,(DIR_MATCH+1+$1A)	;ファイルの先頭クラスタ
	SCF
	SBC	HL,DE
	JR	Z,FOUND
SKIP:
	LD	C,$12		;次のファイル検索(_SNEXT)
	CALL	SYSTEM
	JR	LOOP2
FOUND:
	LD	HL,DIR_MATCH+1	;ファイル名
	LD	DE,(PATH_IX)
	LD	BC,11
	LDIR
	LD	HL,(PATH_IX)
	LD	BC,11
	ADD	HL,BC
	LD	(PATH_IX),HL

	LD	HL,(FCB_SEARCH+14)	;アクセスするディレクトリのクラスタ番号
	LD	(FCB_PARENT+14),HL

	JP	LOOP1
DOS2:				;MSX-DOS2
	CALL	SHOW_DRIVE
	LD	DE,PATH_STACK
	LD	C,$59		;カレントディレクトリの獲得(_GETCD)
	CALL	SYSTEM
	LD	HL,PATH_STACK
	LD	BC,0
	CALL	SHOWX
	JR	FIN
DOS1:				;CP/M MSX-DOS1
	CALL	SHOW_DRIVE
	JR	FIN
SHOW:
	CALL	SHOW_DRIVE
	LD	HL,(PATH_IX)
	LD	DE,PATH_STACK
	AND	A
	SBC	HL,DE
	JR	Z,FIN
SHOW1:
	LD	HL,(PATH_IX)
	LD	DE,PATH_STACK
	AND	A
	SBC	HL,DE
	JR	Z,FIN

SEPSMC	EQU	$+1
	LD	A,0
	OR	A
	CALL	NZ,CONOUT_A
	LD	A,$5C		;\
	LD	(SEPSMC),A

	LD	DE,PATH_STACK-11
	ADD	HL,DE
	LD	(PATH_IX),HL
	LD	B,8
	LD	C,'.'
	CALL	SHOWX
	LD	HL,(PATH_IX)
	LD	DE,8
	ADD	HL,DE
	LD	BC,$0300
	CALL	SHOWX
	JR	SHOW1
SHOWX:
	LD	A,(HL)
	INC	HL
	CP	$21
	RET	C
	CALL	CONOUT_A
	DJNZ	SHOWX
	LD	A,C
	OR	A
	RET	Z
CONOUT_A:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	E,A
	LD	C,$02		;コンソール出力(_CONOUT)
	CALL	SYSTEM
	POP	HL
	POP	DE
	POP	BC
	RET
SHOW_DRIVE:
	LD	C,$19		;カレントドライブ番号の獲得(_CURDRV)
	CALL	SYSTEM
	PUSH	AF
	ADD	A,'A'
	CALL	CONOUT_A
	LD	A,':'
	CALL	CONOUT_A
	LD	A,$05C		;\
	CALL	CONOUT_A
	POP	BC
	RET

FIN:
	LD	A,$0D
	CALL	CONOUT_A
	LD	A,$0A
	CALL	CONOUT_A
	JP	0

PATH_IX:	DW	PATH_STACK

FCB_PARENT:
	DB	0,"..         "
	DB	0,$10,0,0, 0,0,0,0, 0,0,0,0	;残りは0で埋める
	DB	0,0,0,0, 0,0,0,0, 0,0,0,0, 0

	DS	11

FCB_SEARCH:
	DB	0,"???????????"
	DB	0,$10,0,0, 0,0,0,0, 0,0,0,0	;残りは0で埋める
	DB	0,0,0,0, 0,0,0,0, 0,0,0,0, 0

	DS	11

DIR_MATCH:
	DS	33
PATH_STACK:
