;
;	GL3 loader/saver  by Gaku
;
OSFLG	EQU	$0003
SYSTEM	EQU	$0005
JP_HL	EQU	$000F
BIOS	EQU	$0018
FCB1	EQU	$005C
DTA	EQU	$0080

	ORG	$0100
	JP	START
START:
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM
	LD	A,(FCB1+1)
	CP	$20
	JR	NZ,MAIN
USAGE:
	LD	DE,MUSAGE
	LD	C,9
	CALL	SYSTEM
	RST	0
MAIN:
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	RET	NZ
	LD	A,L
	AND	$7F
	RET	NZ

	LD	SP,(SYSTEM+1)

	CALL	EXT
	CALL	SWITCH
	CP	$20
	JR	C,LOAD1

	CP	"S"
	JP	Z,SAVE

	CP	"D"
	JR	NZ,USAGE
	XOR	A
	LD	(MAGIC+1),A
LOAD1:
	LD	DE,FILE
	LD	C,$1A
	CALL	SYSTEM

	LD	DE,FCB1
	LD	C,$11
	CALL	SYSTEM
	OR	A
	JP	NZ,END
WILD:
	LD	DE,FILE
	LD	C,$0F
	CALL	SYSTEM
	OR	A
	JP	NZ,END

	CALL	SETCRTC

	LD	HL,0
	LD	(FILE+33),HL
	LD	(FILE+35),HL
	INC	L
	LD	(FILE+14),HL

	LD	DE,DATA
	LD	C,$1A
	CALL	SYSTEM
	LD	HL,$4000
	LD	(GRAM),HL

	LD	A,25	;25line
LOOP1:
	PUSH	AF
	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	CP	$1B
	JP	Z,NEXT
	CP	3
	JP	Z,END

	LD	HL,8*1024
	LD	A,(FILE+11)
	CALL	CAP
	CP	"Z"
	JR	NZ,GLZL1
	LD	HL,5*1024
GLZL1:
	LD	DE,FILE
	LD	C,$27
	CALL	SYSTEM
	LD	A,H
	OR	L
	JP	Z,END
	DI
	LD	IX,DATA
	LD	D,8		;Y=8dot
LOOP2:
	LD	E,40		;40chr
LOOP3:
	PUSH	DE
	LD	C,8		;X=8dot
LOOP4:
	LD	H,(IX+0)
	INC	IX
	LD	L,(IX+0)
	INC	IX
MAGIC:	LD	A,2		;DITHER
	XOR	1
	LD	(MAGIC+1),A
	JP	NZ,MAGICE
	LD	A,H
	ADD	A,8
	JR	C,MAGIC1
	LD	H,A
MAGIC1:
	RES	3,H
	LD	DE,$40
	ADD	HL,DE
	BIT	3,H
	JR	Z,MAGIC2
	SBC	HL,DE
MAGIC2:
	RES	6,L
	INC	L
	INC	L
	BIT	6,L
	JR	Z,MAGICE
	DEC	L
	DEC	L
MAGICE:
	LD	IY,G3
	LD	B,12+2		;4x3+2
LOOP5:
	ADD	HL,HL
	RL	(IY+0)
	DEC	IY
	DJNZ	LOOP5
	DEC	C
	JR	NZ,LOOP4
;
	LD	BC,$1FD0
	XOR	A
	LD	E,A
	RES	4,E
	OUT	(C),E		;BANK0
	EXX
	LD	BC,(GRAM)
	LD	IY,B3
	CALL	PSET1
	SET	2,B
	LD	IY,B2
	CALL	PSET1
	EXX
	SET	4,E
	OUT	(C),E
	EXX
	LD	IY,B0
	CALL	PSET1
	RES	2,B
	LD	IY,B1
	CALL	PSET1
;
	LD	HL,(GRAM)
	INC	HL
	LD	(GRAM),HL
	POP	DE
	DEC	E
	JP	NZ,LOOP3
	LD	A,(MAGIC+1)
	XOR	1
	LD	(MAGIC+1),A
	LD	BC,$800-40
	ADD	HL,BC
	LD	(GRAM),HL
	LD	A,(FILE+11)
	CALL	CAP
	CP	"Z"
	JR	Z,GLZL2
	LD	BC,1024-640
	ADD	IX,BC
GLZL2:
	DEC	D
	JP	NZ,LOOP2
	LD	BC,$C000+40
	ADD	HL,BC
	LD	(GRAM),HL
	POP	AF
	DEC	A
	JP	NZ,LOOP1
	PUSH	AF
NEXT:
	POP	AF
	CALL	KEYBC

	LD	C,7
	CALL	SYSTEM
	CP	3
	JR	Z,END

	LD	DE,FILE
	LD	C,$1A
	CALL	SYSTEM

	LD	C,$12
	CALL	SYSTEM
	OR	A
	JP	Z,WILD
END:
	LD	D,7
	LD	BC,$1FB9
	LD	HL,TEXTDT
TEXT1:
	INC	B
	OUTI
	INC	C
	DEC	D
	JR	NZ,TEXT1
	LD	BC,$1FC0
	DB	$ED,$71
	LD	C,$B0
	DB	$ED,$71
	RST	0

PSET1:
	RES	7,B
	LD	A,(IY+0)		;B
	OUT	(C),A
;
	SET	7,B
	RES	6,B
	LD	A,(IY+5)	;R
	OUT	(C),A
;
	SET	6,B
	LD	A,(IY+10)	;G
	OUT	(C),A
	RET
;
SETCRTC:
	LD	BC,$1FB0
	LD	A,$80
	OUT	(C),A

	LD	HL,C4025L
	LD	BC,$1800
	XOR	A
SETCRTL:
	LD	D,(HL)
	OUT	(C),A
	INC	C
	OUT	(C),D
	DEC	C
	INC	HL
	INC	A
	CP	14
	JR	NZ,SETCRTL
	LD	A,(HL)
	LD	BC,$1A03
	OUT	(C),A
	INC	HL
	LD	A,(HL)
	LD	BC,$1FD0
	OUT	(C),A
	LD	C,$C0
	LD	A,$11
	OUT	(C),A

	LD	D,7
	LD	BC,$1FB9
TEXT0:
	DB	$ED,$71
	INC	C
	DEC	D
	JR	NZ,TEXT0
	RET

KEYBC:
	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	OR	A
	JR	NZ,KEYBC
	RET

SWITCH:
	LD	DE,DTA+1
SWITCH1:
	LD	A,(DE)
	CP	$20
	RET	C
	INC	DE
	CP	"/"
	JR	NZ,SWITCH1
	LD	A,(DE)
	INC	DE
CAP:
	CP	"a"
	RET	C
	CP	"z"+1
	RET	NC
	SUB	$20
	RET

SAVE:
	LD	BC,25*256+6
PRT:
	LD	E,"."
	PUSH	BC
	CALL	SYSTEM
	POP	BC
	DJNZ	PRT

	LD	E,$0D
	LD	C,6
	CALL	SYSTEM

	LD	DE,FCB1
	LD	C,$16
	CALL	SYSTEM
	OR	A
	JP	NZ,0

	LD	HL,0
	LD	(FCB1+33),HL
	LD	(FCB1+35),HL
	INC	L
	LD	(FCB1+14),HL

	LD	DE,DATA
	LD	C,$1A
	CALL	SYSTEM
	LD	HL,$4000
	LD	(GRAM),HL
	LD	HL,DATA
	LD	BC,1024*8
	LD	E,0
;
SAVE0:
	LD	(HL),E
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JP	NZ,SAVE0

	LD	A,25		;25line
SAVE1:
	PUSH	AF

	LD	IX,DATA
	LD	D,8		;Y=8dot
SAVE2:
	LD	E,40		;40chr
SAVE3:
	PUSH	DE
;
	LD	C,$BF
	LD	A,($000B)
	LD	B,A
	LD	A,(BC)
	LD	BC,$1FD0
	LD	E,A
	RES	4,E		;BANK0
	OUT	(C),E
	EXX
	LD	BC,(GRAM)
	LD	IY,B3
	CALL	POINT
	SET	2,B
	LD	IY,B2
	CALL	POINT
	EXX
	SET	4,E
	OUT	(C),E		;BANK1
	EXX
	LD	IY,B0
	CALL	POINT
	RES	2,B
	LD	IY,B1
	CALL	POINT

	LD	C,8		;X=8dot

SAVE4:
	LD	IY,G3
	LD	B,15
	LD	HL,0
;
SAVE5:
	RL	(IY+0)
	ADC	HL,HL
	DEC	IY
	DJNZ	SAVE5

	ADD	HL,HL
	LD	(IX+0),H
	INC	IX
	LD	(IX+0),L
	INC	IX

	DEC	C
	JR	NZ,SAVE4

	LD	HL,(GRAM)
	INC	HL
	LD	(GRAM),HL

	POP	DE
	DEC	E
	JP	NZ,SAVE3

	LD	BC,$800-40
	ADD	HL,BC
	LD	(GRAM),HL

	LD	A,(FCB1+11)
	CP	"Z"
	JR	Z,GLZS1
	LD	BC,1024-640
	ADD	IX,BC
GLZS1:
	DEC	D
	JP	NZ,SAVE2

	LD	BC,$C000+40
	ADD	HL,BC
	LD	(GRAM),HL

	LD	E,"o"
	LD	C,6
	CALL	SYSTEM

	LD	DE,FCB1
	LD	HL,8*1024
	LD	A,(FCB1+11)
	CP	"Z"
	JR	NZ,GLZS2
	LD	HL,5*1024
GLZS2:
	LD	C,$26
	CALL	SYSTEM
	OR	A
	JP	NZ,0
	POP	AF
	DEC	A
	JP	NZ,SAVE1

	LD	DE,FCB1
	LD	C,$10
	CALL	SYSTEM

	LD	DE,COMP
	LD	C,9
	CALL	SYSTEM
	RST	0

POINT:	RES	7,B
	IN	A,(C)		;B
	LD	(IY+0),A

	SET	7,B
	RES	6,B
	IN	A,(C)		;R
	LD	(IY+5),A

	SET	6,B
	IN	A,(C)		;G
	LD	(IY+10),A
	RET

EXT:
	LD	A,(FCB1+9)
	CP	$20
	RET	NZ
	LD	HL,"L"*256+"G"
	LD	(FCB1+9),HL
	LD	A,"3"
	LD	(FCB1+11),A
	RET
;
TITLE:
	DB	"X1 GL3 loader/saver v1.01",$0D,$0A
	DB	"$"
MUSAGE:
	DB	"usage: GL3 Ãß≤Ÿ»∞—[Ω≤Ø¡]",$0D,$0A
	DB	9,"/D √ﬁ®ªﬁ",$0D,$0A
	DB	9,"/S æ∞Ãﬁ",$0D,$0A
	DB	"$"

COMP:	DB	" Ok",$0D,$0A,"$"

C4025L:	DB	$37,$28,$2D,$34,$1F,$02,$19,$1C
	DB	$00,$07,$00,$00,$00,$00,$0D,$A0

TEXTDT:	DB	$03,$0C,$0F,$30,$33,$3C,$3F
;
;			WORKS
;
GRAM:	DS	2
;
BASE:	DB	0	; 1
B0:	DB	0	; 2
B1:	DB	0	; 3
B2:	DB	0	; 4
B3:	DB	0	; 5
	DB	0	; 6
R0:	DB	0	; 7
R1:	DB	0	; 8
R2:	DB	0	; 9
R3:	DB	0	;10
	DB	0	;11
G0:	DB	0	;12
G1:	DB	0	;13
G2:	DB	0	;14
G3:	DB	0	;15
;
FILE:	DS	37
DATA:
