
;	Remote Link Client

WBOOT	EQU	00000H
SYSTEM	EQU	00005H
PGM	EQU	0CE06H
_DVSW	EQU	00003H		;LD v1.60以前は xx87H
_DTA	EQU	0008AH
_FCB	EQU	00098H

	ORG	0100H
	JP	START
TITLE:
	DB	"X1 Remote Link Client v1.05"
	DB	0DH,0AH,"$"
INSTALL:
	DB	"Installed",0DH,0AH,"$"

START:
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM

	LD	C,06FH		;MSX-DOS のバージョン番号の獲得(_DOSVER)
	CALL	SYSTEM

	LD	A,C
	CP	01DH		;LSX-Dodgersのチェック
	RET	NZ

	LD	A,L		;機種ID
	OR	A		;X1
	RET	NZ

	LD	HL,00160H
	SBC	HL,DE
	JR	C,V161
	LD	HL,(WBOOT+1)	;LD v1.60以前の処理
	LD	L,087H		;_DVSW
	LD	(DV001-PGM+TSR),HL
	LD	(DV002-PGM+TSR),HL
V161:
	LD	BC,1FA1H
	LD	A,47H
	OUT	(C),A
	LD	A,1		;ボーレート 125000
	OUT	(C),A

	LD	BC,1F91H
	LD	HL,SIODATA
SINIT1:
	LD	A,(HL)
	INC	HL
	CP	0FFH
	JR	Z,SINIT2
	OUT	(C),A
	JR	SINIT1
SINIT2:
	LD	HL,(SYSTEM+1)
	LD	DE,PGM
	SCF
	SBC	HL,DE
	RET	C

	LD	SP,PGM
	LD	HL,0
	PUSH	HL

	DI
	LD	HL,TSR
	LD	DE,PGM
	LD	BC,FIN-PGM
	LDIR
	LD	HL,(SYSTEM+1)
	LD	(PGM+3),HL

	INC	HL
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	(PGM+1),HL

	LD	HL,PGM
	LD	(SYSTEM+1),HL

	LD	A,(WBOOT+2)
	LD	(DTA001),A
	LD	(DTA002),A
	LD	(DTA003),A
	LD	(FCB001),A
	LD	(FCB002),A
	LD	(FCB003),A
	LD	(LDBIOS_SWC),A

	LD	IYL,0
	INC	A
	LD	IYH,A		;IY=STABLE

	LD	L,(IY+2*0EH)
	LD	H,(IY+2*0EH+1)
	LD	(O0E+1),HL
	LD	HL,S0E
	LD	(IY+2*0EH),L
	LD	(IY+2*0EH+1),H

	LD	L,(IY+2*0FH)
	LD	H,(IY+2*0FH+1)
	LD	(O0F+1),HL
	LD	HL,S0F
	LD	(IY+2*0FH),L
	LD	(IY+2*0FH+1),H

	LD	L,(IY+2*10H)
	LD	H,(IY+2*10H+1)
	LD	(O10+1),HL
	LD	HL,S10
	LD	(IY+2*10H),L
	LD	(IY+2*10H+1),H

	LD	L,(IY+2*11H)
	LD	H,(IY+2*11H+1)
	LD	(O11+1),HL
	LD	HL,S11
	LD	(IY+2*11H),L
	LD	(IY+2*11H+1),H

	LD	L,(IY+2*12H)
	LD	H,(IY+2*12H+1)
	LD	(O12+1),HL
	LD	HL,S12
	LD	(IY+2*12H),L
	LD	(IY+2*12H+1),H

	LD	L,(IY+2*13H)
	LD	H,(IY+2*13H+1)
	LD	(O13+1),HL
	LD	HL,S13
	LD	(IY+2*13H),L
	LD	(IY+2*13H+1),H

	LD	L,(IY+2*16H)
	LD	H,(IY+2*16H+1)
	LD	(O16+1),HL
	LD	HL,S16
	LD	(IY+2*16H),L
	LD	(IY+2*16H+1),H

	LD	L,(IY+2*17H)
	LD	H,(IY+2*17H+1)
	LD	(O17+1),HL
	LD	HL,S17
	LD	(IY+2*17H),L
	LD	(IY+2*17H+1),H

	LD	L,(IY+2*1BH)
	LD	H,(IY+2*1BH+1)
	LD	(O1B+1),HL
	LD	HL,S1B
	LD	(IY+2*1BH),L
	LD	(IY+2*1BH+1),H

	LD	L,(IY+2*26H)
	LD	H,(IY+2*26H+1)
	LD	(O26+1),HL
	LD	HL,S26
	LD	(IY+2*26H),L
	LD	(IY+2*26H+1),H

	LD	L,(IY+2*27H)
	LD	H,(IY+2*27H+1)
	LD	(O27+1),HL
	LD	HL,S27
	LD	(IY+2*27H),L
	LD	(IY+2*27H+1),H

	EI
	LD	DE,INSTALL
	LD	C,9
	CALL	SYSTEM
	JP	0

SIODATA:
	DB	18H
	DB	1,0
	DB	2,0
	DB	3,0C1H
	DB	4,44H
	DB	5,0E8H
	DB	0FFH

TSR:
	ORG	PGM,TSR-0100H

	JP	0		;+1	SYSTEM CALL
	DW	0		;+3	NEXT
	DB	"[RLC",0	;+5	NAME

O0E:	JP	0
O0F:	JP	0
O10:	JP	0
O11:	JP	0
O12:	JP	0
O13:	JP	0
O16:	JP	0
O17:	JP	0
O1B:	JP	0
O26:	JP	0
O27:	JP	0
WR5:	DB	0E8H
S0E:
	LD	A,E
	CP	26
	JR	NC,O0E
	CP	8
	JR	C,O0E

	LD	(_DVSW),A
DV001	EQU	$-2
	LD	A,26
	RET

S0F:
	CALL	GETDRV
	LD	(DE),A
	JR	C,O0F

	LD	A,0FH
	CALL	COMMAND

	LD	E,L
	CALL	NC,XMIT
	LD	E,H
	CALL	NC,XMIT

	PUSH	HL
	LD	B,16
	CALL	NC,XMITX

	CALL	NC,RECV
	POP	HL
	OR	A
	JR	NZ,SERR

	LD	B,32
	CALL	RECVX

	JR	NC,SOK
	JR	SERR

S10:
	CALL	GETDRV
	LD	(DE),A
	JR	C,O10

	LD	A,10H
	CALL	COMMAND

	PUSH	HL
	LD	B,32
	CALL	NC,XMITX

	CALL	NC,RECV
	POP	HL
	OR	A
	JR	Z,SOK
	JR	SERR

S11:
	CALL	GETDRV
	LD	(DE),A
	JR	C,O11

	LD	(_FCB),DE
FCB001	EQU	$-1
	LD	A,11H
	CALL	COMMAND

	LD	B,16
	CALL	NC,XMITX

	CALL	NC,RECV
	OR	A
	JR	NZ,SERR

	LD	IX,(_FCB)
FCB002	EQU	$-1
	CALL	RECV
	JR	C,SERR
	LD	(IX+14),A
	CALL	RECV
	JR	C,SERR
	LD	(IX+15),A
S12A:
	LD	HL,(_DTA)
DTA001	EQU	$-1
	LD	B,33
	CALL	RECVX
	JR	C,SERR
SOK:
	XOR	A
SERR:
	POP	IX
	POP	BC
	POP	DE
	POP	HL
	RET

S12:
	PUSH	DE
	LD	DE,(_FCB)
FCB003	EQU	$-1
	CALL	GETDRV
	LD	(DE),A
	POP	DE
	JP	C,O12

	LD	A,12H
	CALL	COMMAND
	JP	C,SERR

	CALL	RECV
	OR	A
	JR	Z,S12A
	JR	SERR

S13:
	CALL	GETDRV
	LD	(DE),A
	JP	C,O13

	LD	A,13H
S17A:
	CALL	COMMAND

	LD	B,16
	CALL	NC,XMITX

	CALL	NC,RECV
	JR	NC,SOK
	JR	SERR

S16:
	CALL	GETDRV
	LD	(DE),A
	JP	C,O16

	LD	A,16H
	CALL	COMMAND

	LD	E,L
	CALL	NC,XMIT
	LD	E,H
	CALL	NC,XMIT

	PUSH	HL
	LD	B,16
	CALL	NC,XMITX

	CALL	NC,RECV
	POP	HL
	OR	A
	JR	NZ,SERR

	LD	B,32
	CALL	RECVX

	JR	NC,SOK
	JR	SERR

S17:
	CALL	GETDRV
	LD	(DE),A
	JP	C,O17

	LD	A,17H
	CALL	COMMAND

	LD	B,16+12
	CALL	NC,XMITX

	CALL	NC,RECV
	JP	NC,SOK
	JR	SERR

S1B:
	LD	A,E
	CALL	GETDRV1
	JP	C,O1B

	LD	D,A
	LD	E,1BH
	CALL	XMIT

	CALL	NC,RECV
	OR	A
	RET	NZ

	LD	E,D
	CALL	XMIT
	RET	C
	LD	HL,DATA
	LD	B,8
	CALL	RECVX
	RET	C
	LD	A,(DATA)
	LD	BC,(DATA+2)
	LD	DE,(DATA+4)
	LD	HL,(DATA+6)
	LD	IY,DATA+1
	LD	IX,DATA
	LD	(IX+0),0
	RET

S26:
	CALL	GETDRV
	LD	(DE),A
	JP	C,O26

	LD	(PAT26+1),HL
	LD	A,26H
	CALL	COMMAND2

	PUSH	HL
	LD	B,36
	CALL	NC,XMITX

PAT26:	LD	HL,0
	LD	E,L
	CALL	NC,XMIT
	LD	E,H
	CALL	NC,XMIT

	EX	DE,HL
	LD	HL,(_DTA)
DTA002	EQU	$-1
	CALL	NC,XMITW

	CALL	NC,RECV
	CP	$FF
	POP	HL
	JR	Z,RBWERR

	LD	B,36
	CALL	RECVX
RBWERR:
	POP	IX
	POP	BC
	POP	DE
	RET

S27:
	CALL	GETDRV
	LD	(DE),A
	JP	C,O27

	LD	(PAT27+1),HL
	LD	A,27H
	CALL	COMMAND2

	PUSH	HL
	LD	B,36
	CALL	NC,XMITX

PAT27:	LD	HL,0
	LD	E,L
	CALL	NC,XMIT
	LD	E,H
	CALL	NC,XMIT

	CALL	NC,RECV
	CP	$FF
	POP	HL
	JR	Z,RBRERR

	PUSH	AF
	LD	B,36
	CALL	RECVX

	CALL	NC,RECV
	LD	E,A
	CALL	NC,RECV

	LD	D,A
	PUSH	DE
	LD	HL,(_DTA)
DTA003	EQU	$-1

	CALL	NC,RECVW

	POP	HL
	JR	NC,RBRERR2
	POP	AF
	SCF
	SBC	A,A
	JR	RBRERR
RBRERR2:
	POP	AF
	AND	A
RBRERR:
	POP	IX
	POP	BC
	POP	DE
	RET

COMMAND2:
	POP	HL
	JR	COMMAND3
COMMAND:
	EX	(SP),HL		;PUSH	HL
COMMAND3:
	PUSH	DE
	PUSH	BC
	PUSH	IX
	PUSH	HL		;PC
	EX	DE,HL
	LD	E,A		;A=Command
	LD	BC,1F91H
COM1:
	IN	A,(C)
	RRCA			;アライベル
	JR	NC,COM2
	DEC	C
	IN	A,(C)		;データ読み出し
	INC	C
	JR	COM1
COM2:
	CALL	XMIT

	CALL	NC,RECV
	OR	A
	RET

GETDRV:
	LD	A,(DE)
GETDRV1:
	AND	7FH
	SUB	01H
	JR	NC,GETDRV2
	LD	A,(_DVSW)
DV002	EQU	$-2
GETDRV2:
	INC	A
	CP	9
	RET

XMITW:
	INC	D
XMITW1:
	DEC	D
	JR	Z,XMITW2
	PUSH	DE
	LD	B,0
	CALL	XMITX
	POP	DE
	JR	NC,XMITW1
	RET
XMITW2:
	LD	A,E
	OR	E
	RET	Z
	LD	B,E

XMITX:
	LD	IX,0
	LD	D,0
	LD	C,B
XMITX1:
	LD	E,(HL)
	INC	HL
	ADD	IX,DE
	CALL	XMIT
	RET	C
	DJNZ	XMITX1

	LD	E,IXL
	CALL	XMIT
	LD	E,IXH
	CALL	NC,XMIT
	RET	C

	CALL	RECV
	OR	A
	RET	Z

	SBC	HL,BC
	LD	B,C
	JR	XMITX

RECVW:
	INC	D
RECVW1:
	DEC	D
	JR	Z,RECVW2
	PUSH	DE
	LD	B,0
	CALL	RECVX
	POP	DE
	JR	NC,RECVW1
	RET
RECVW2:
	LD	A,E
	OR	E
	RET	Z
	LD	B,E

RECVX:
	LD	IX,0
	LD	D,0
	LD	C,B
RECVX1:
	CALL	RECV
	RET	C
	LD	(HL),A
	INC	HL
	LD	E,A
	ADD	IX,DE
	DJNZ	RECVX1

	CALL	RECV
	RET	C
	LD	E,A
	CALL	RECV
	RET	C
	LD	D,A

	LD	A,IXL
	CP	E
	JR	NZ,RECVX2
	LD	A,IXH
	SUB	D
	JR	NZ,RECVX2
	LD	E,A
	JR	XMIT
RECVX2:
	LD	A,0FFH
	CALL	XMIT
	JR	NC,RECV
	RET

XMIT:
	PUSH	BC
	PUSH	DE
	LD	BC,1F91H
	LD	A,10H		;ステータス・リセット
	OUT	(C),A
	IN	D,(C)
	DEC	C
	LD	A,E
	RRCA
	RRCA
	RRCA
	RRCA
	AND	0FH
	OUT	(C),A
	INC	C
XMIT1:
	CALL	BREAK2
	LD	A,10H		;ステータス・リセット
	OUT	(C),A
	IN	A,(C)
	XOR	D
	AND	8
	JR	Z,XMIT1
	DEC	C
	LD	A,E
	AND	0FH
	OUT	(C),A
	INC	C
XMIT2:
	CALL	BREAK2
	LD	A,10H		;ステータス・リセット
	OUT	(C),A
	IN	A,(C)
	XOR	D
	AND	8
	JR	NZ,XMIT2
	POP	DE
	POP	BC
	RET

RECV:
	PUSH	BC
	PUSH	DE
	LD	BC,1F91H
REC1:
	CALL	BREAK2
	IN	A,(C)
	RRCA			;アライベル
	JR	NC,REC1
	DEC	C
	IN	E,(C)		;データ読み出し
	INC	C
	LD	A,5
	OUT	(C),A
	LD	A,(WR5)
	XOR	80H
	OUT	(C),A
	LD	(WR5),A
REC2:
	CALL	BREAK2
	IN	A,(C)
	RRCA			;アライベル
	JR	NC,REC2
	DEC	C
	IN	A,(C)		;データ読み出し
	AND	0FH
	LD	D,A
	INC	C
	LD	A,5
	OUT	(C),A
	LD	A,(WR5)
	XOR	80H
	OUT	(C),A
	LD	(WR5),A
	LD	A,E
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,D
BREAK2X:
	POP	DE
	POP	BC
	RET

BREAK2:
	LD	A,(00092H)	;LDBBIOS+92H
LDBIOS_SWC	EQU	$-1
	CP	1BH
	JR	Z,BREAK2Z
	CP	3
	RET	NZ
BREAK2Z:
	SCF
	SBC	A,A
	POP	DE
	JR	BREAK2X

DATA:
	DS	8
FIN:
