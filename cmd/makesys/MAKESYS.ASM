;
;	MAKESYS by Gaku
;
;
SYSTEM	EQU	$0005
JP_HL	EQU	$000F
FCB1	EQU	$005C
FCB2	EQU	$006C
DTA	EQU	$0080

TSD	EQU	$FF51
BUFSZ	EQU	$4000
	ORG	$0100
	JP	START
START:
	LD	DE,TITLE
	LD	C,9		;文字列出力
	CALL	SYSTEM
MAIN:
	LD	C,$6F		;MSX-DOS のバージョン番号の獲得(_DOSVER)
	CALL	SYSTEM
	JP	C,NOT_SUPPOTED
	LD	A,L		;機種フラグ
	OR	A
	JP	NZ,NOT_SUPPOTED
	LD	HL,$011D	;LSX-Dodgers
	SBC	HL,BC
	JP	NZ,NOT_SUPPOTED

	LD	A,(FCB1+1)
	CP	' '+1
	JP	C,SHOW_USGAE
	LD	A,(FCB2+1)
	CP	' '+1
	JP	Z,SHOW_USGAE
	LD	HL,FCB2		;引数が2つの場合はFCB1とFCB2が重なっているので
	LD	DE,FCB_DEST	;FCB2を移動させて使う
	LD	BC,12		;ドライブ(1)＋ファイル名(8)＋拡張子(3)=12
	LDIR

	LD	DE,FCB_DEST	;転送先FCB
	LD	C,016H		;ファイルの作成(_FMAKE) CP/M MSX-DOS
	CALL	SYSTEM
	OR	A
	JP	NZ,SHOW_ERROR	;失敗したら終了

	LD	HL,1
	LD	(FCB_DEST+14),HL	;レコードサイズを1にする

	LD	DE,IPL		;データの読み書き先
	LD	C,01AH		;DTAの設定(_SETDTA) CP/M MSX-DOS
	CALL	SYSTEM
	LD	HL,IPL_E-IPL
	LD	DE,FCB_DEST
	LD	C,026H		;ランダムブロック書き込み(_WRBLK) MSX-DOS
	CALL	SYSTEM
	CP	0FFH
	JP	Z,SHOW_ERROR	;失敗したら終了

	LD	IY,FCB1
	CALL	COPY

	LD	IY,FCB_LD
	CALL	COPY

	LD	HL,(BUFAD+3)
	LD	(LAST),HL
	LD	HL,(BUFAD+5)
	LD	(RUN_LD),HL
	LD	DE,$0B-4
	ADD	HL,DE
	LD	(_INIT1),HL
	LD	(_INIT2),HL

	LD	HL,(BUFAD+1)
	LD	DE,-7
	ADD	HL,DE
	LD	DE,(FCB1+16)
	LD	(COM_SIZE),DE
	AND	A
	SBC	HL,DE
	LD	(COM_START),HL
	LD	DE,IPL-IPL_E+7
	ADD	HL,DE
	LD	(RUN),HL
	LD	(EXEC),HL
	LD	DE,INITX-_START
	ADD	HL,DE
	LD	(_INITX),HL
	INC	HL
	LD	(_INIT_X1),HL
	LD	DE,TSS-INITX1
	ADD	HL,DE
	LD	(_TSS),HL

	LD	HL,0
	LD	(FCB_DEST+33),HL
	LD	(FCB_DEST+35),HL

	LD	DE,DTA+1
	LD	HL,FCB1
	LD	C,$5C		;ファイル名の解析(_PFILE)
	CALL	SYSTEM

	LD	HL,FCB1
	LD	C,$5C		;ファイル名の解析(_PFILE)
	CALL	SYSTEM

	LD	HL,FCB1
	LD	C,$5C		;ファイル名の解析(_PFILE)
	CALL	SYSTEM

	LD	A,(FCB1)
	LD	(DRIVE),A

	LD	DE,IPL		;データの読み書き先
	LD	C,01AH		;DTAの設定(_SETDTA) CP/M MSX-DOS
	CALL	SYSTEM
	LD	HL,IPL_E-IPL
	LD	DE,FCB_DEST
	LD	C,026H		;ランダムブロック書き込み(_WRBLK) MSX-DOS
	CALL	SYSTEM
	CP	0FFH
	JP	Z,SHOW_ERROR	;失敗したら終了

	LD	DE,FCB_DEST
	LD	C,010H		;ファイルのクローズ(_FCLOSE) CP/M MSX-DOS
	CALL	SYSTEM
SHOW_COMPLETED:
	LD	DE,COMPLETED
SHOW_MESSAGE:
	LD	C,9		;文字列出力
	CALL	SYSTEM
	JP	0
NOT_SUPPOTED:
	LD	DE,ERRMES
	JR	SHOW_MESSAGE
SHOW_USGAE:
	LD	DE,USAGE
	JR	SHOW_MESSAGE
SHOW_ERROR:
	LD	DE,ERRMES1
	JR	SHOW_MESSAGE

COPY:
	PUSH	IY
	POP	DE
	LD	C,00FH		;ファイルのオープン(_FOPEN) CP/M MSX-DOS
	CALL	SYSTEM
	OR	A
	JP	NZ,SHOW_ERROR	;失敗したら終了

	LD	(IY+14),1	;レコードサイズを1にする
	XOR	A
	LD	(IY+15),A

	LD	(IY+33),A	;ランダムレコード
	LD	(IY+34),A
	LD	(IY+35),A
	LD	(IY+36),A

	LD	DE,BUFAD	;データの読み書き先
	LD	C,01AH		;DTAの設定(_SETDTA) CP/M MSX-DOS
	CALL	SYSTEM
LOOP1:
	PUSH	IY
	POP	DE
	LD	HL,BUFSZ	;データを読み出すサイズ（バイト単位）
	LD	C,027H		;ランダムブロック読み出し(_RDBLK) MSX-DOS
	CALL	SYSTEM
	CP	0FFH
	JP	Z,SHOW_ERROR	;失敗したら終了
	PUSH	AF
				;HLには読み込んだデータ数が入っている
	LD	DE,FCB_DEST
	LD	C,026H		;ランダムブロック書き込み(_WRBLK) MSX-DOS
	CALL	SYSTEM
	CP	0FFH
	JP	Z,SHOW_ERROR	;失敗したら終了

	POP	AF
	OR	A		;A=0の場合はバッファサイズすべて読み込みできたなので、まだ読み込みできていない残りがある
	JR	Z,LOOP1		;A=1の場合はすべて読み込み完了している

	LD	DE,FCB1
	LD	C,010H		;ファイルのクローズ(_FCLOSE) CP/M MSX-DOS
	JP	SYSTEM

IPL:
;	MSX BINARY HEADER
	DB	0FEH
;	MSX BINARY START ADDRESS
RUN:	DW	0
;	MSX BINARY END ADDRESS
LAST:	DW	0
;	MSX BINARY EXEC ADDRESS
EXEC:	DW	0
_START:
COM_START	EQU	$+1
	LD	HL,0
	LD	DE,$0100
COM_SIZE	EQU	$+1
	LD	BC,0
	LDIR
_INIT1	EQU	$+1
	LD	HL,($CB0B)
_INIT_X1	EQU	$+1
	LD	(INITX1),HL
_INITX	EQU	$+1
	LD	HL,0
_INIT2	EQU	$+1
	LD	($CB0B),HL

RUN_LD	EQU	$+1
	JP	0
INITX:	
INITX1	EQU	$+1
	CALL	0
_TSS	EQU	$+1
	LD	HL,TSS
	LD	DE,TSD
	LD	BC,TSS_E-TSS
	LDIR

	LD	HL,(1)
	LD	A,H
	LD	(_COMANL),A
	INC	HL
	LD	(CWBOOT),HL	;EF04
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	DE
	LD	(WBOOT),DE
	LD	DE,TSD
	LD	(HL),D
	DEC	HL
	LD	(HL),E
	PUSH	DE
	LD	HL,$2002
	LD	(DTA),HL
	LD	A,$20
	LD	HL,FCB1+1
	LD	B,11
CLRFCB1:
	LD	(HL),A
	DJNZ	CLRFCB1
	LD	HL,FCB2+1
	LD	B,11
CLRFCB2:
	LD	(HL),A
	DJNZ	CLRFCB2

	JP	$0100
TSS:				;TSD
CWBOOT	EQU	$+TSD-TSS+1
	LD	HL,0
WBOOT	EQU	$+TSD-TSS+1
	LD	DE,0
	LD	(HL),E
	INC	HL
	LD	(HL),D
	PUSH	DE
DRIVE	EQU	$+1
	LD	E,0
	LD	C,$0E		;カレントドライブの設定(_SELDSK)
	LD	A,E
	DEC	E
	OR	A
	CALL	NZ,SYSTEM
	LD	DE,AUTOD+TSD-TSS
_COMANL	EQU	$+TSD-TSS+2
	JP	$EFFD
AUTOD:	DB	"AUTOEXEC "
TSS_E:
IPL_E:

TITLE:
	DB	"makesys v1.00"
CRLF:
	DB	$0D,$0A
	DB	'$'
USAGE:
	DB	"usage: MAKESYS com out [drive]"
	DB	$0D,$0A
	DB	'$'

ERRMES:
	DB	"Not supported.",$0D,$0A,'$'
ERRMES1:
	DB	"Error.",$0D,$0A,'$'
COMPLETED:
	DB	"Completed.",$0D,$0A,'$'

FCB_DEST:
	DS	37
FCB_LD:
	DB	0,"LD      BIN"	;カレントドライブ=0,ファイル名8文字,拡張子3文字
	DB	0,0,0,0, 0,0,0,0, 0,0,0,0	;残りは0で埋める
	DB	0,0,0,0, 0,0,0,0, 0,0,0,0, 0
BUFAD: