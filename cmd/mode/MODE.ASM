;
;	MODE by Gaku
;
WBOOT	EQU	$0000
SYSTEM	EQU	$0005
FCB1	EQU	$005C
FCB2	EQU	$006C
DTA	EQU	$0080

_WIDTH	EQU	$B1
_HEIGHT	EQU	$97
CRTCD	EQU	$B0
_PAGE_MINUS	EQU	$BA
_WIDTH_MINUS	EQU	$BC
WK1FD0	EQU	$BF

			;X1
X1CRTC	EQU	$1800

			;MSX
CALSLT	EQU	$001C
INITXT	EQU	$006C
INIT32	EQU	$006F
SETTXT	EQU	$0078
SETT32	EQU	$007B

LINL40	EQU	$F3AE
LINL32	EQU	$F3AF
LINLEN	EQU	$F3B0
BUF	EQU	$F55E
INTFLG	EQU	$FC9B
EXPTBL	EQU	$FCC1
EXBRSA	EQU	$FAF8

	ORG	$0100
	JP	START
START:
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM
	LD	A,(DTA)
	OR	A
	JR	NZ,MAIN
	LD	DE,USAGE
	LD	C,9
	JP	SYSTEM
MAIN:
	LD	DE,FCB1+1
	CALL	GETNUM
	LD	(NEW_W),HL
	LD	DE,FCB2+1
	CALL	GETNUM
	LD	(NEW_H),HL
	LD	A,L
	OR	A
	JR	NZ,MAIN1
	LD	HL,(NEW_W)
	LD	A,L
	CALL	IS_HEIGHT_OK
	JR	NZ,MAIN1
	LD	(NEW_H),HL
	LD	HL,0
	LD	(NEW_W),HL
MAIN1:
	LD	C,$6F 		;(BDOS)MSX-DOS のバージョン番号の獲得(_DOSVER)
	CALL	SYSTEM
	LD	A,C
	CP	$1D
	RET	NZ
	LD	A,L
	OR	A
	JR	Z,X1
	CP	1
	JR	Z,MZ7
	CP	2
	JR	Z,PC88
	CP	3
	JP	Z,MSX

	JP	WBOOT
X1:				;X1
	CALL	CLS
	LD	A,(WBOOT+2)
	LD	L,WK1FD0
	LD	H,A
	LD	C,(HL)
	LD	A,(NEW_W)
	CP	80
	JR	Z,X1_80
	CP	40
	JP	NZ,X1_HEIGHT
X1_40:				;X1 40桁
	LD	HL,X4024L
	BIT	0,C
	JR	Z,X1_40L
	LD	HL,X4024H
X1_40L:
	JR	X1_WIDTH
X1_80:				;X1 80桁
	LD	HL,X8024L
	BIT	0,C
	JR	Z,X1_WIDTH
	LD	HL,X8024H
X1_WIDTH:
	LD	A,(WBOOT+2)
	LD	E,CRTCD
	LD	D,A
	LD	BC,(WK1FD0-CRTCD)+1
	LDIR
X1_HEIGHT:
	CALL	SET_HEIGHT

X1SETCRTC:
	LD	L,CRTCD
	LD	A,(WBOOT+2)
	LD	H,A
	LD	BC,X1CRTC
	XOR	A
SETCRTL:
	LD	D,(HL)
	OUT	(C),A
	INC	C
	OUT	(C),D
	DEC	C
	INC	HL
	INC	A
	CP	12
	JR	NZ,SETCRTL
	INC	HL
	INC	HL
	LD	A,(HL)
	LD	BC,$1A03
	OUT	(C),A
	INC	HL
	LD	A,(HL)
	LD	BC,$1FD0
	OUT	(C),A
	LD	HL,(WBOOT+1)
	LD	L,WK1FD0
	LD	(HL),A
	RET

MZ7:
PC88:
	CALL	CLS
SET_HEIGHT:
	LD	A,(WBOOT+2)
	LD	(H_SWC1),A
	LD	(H_SWC2),A
	LD	(H_SWC3),A
	LD	(H_SWC4),A
	LD	(H_SWC5),A

	LD	A,(NEW_H)
	CALL	IS_HEIGHT_OK
	JR	NZ,SETHEIGHT1
	LD	(_HEIGHT),A
H_SWC1	EQU	$-1
SETHEIGHT1:
	LD	A,(_WIDTH)
H_SWC2	EQU	$-1
	LD	C,A
	XOR	A
	LD	B,A
	LD	L,A
	LD	H,A
	SBC	HL,BC
	LD	(_WIDTH_MINUS),HL
H_SWC3	EQU	$-1
	LD	C,L
	LD	B,H
	LD	A,(_HEIGHT)
H_SWC4	EQU	$-1
	LD	HL,0
SETHEIGHT2:
	ADD	HL,BC
	DEC	A
	JR	NZ,SETHEIGHT2
	LD	(_PAGE_MINUS),HL
H_SWC5	EQU	$-1
	RET

MSX:
	LD	C,40
	LD	A,(EXBRSA)
	OR	A
	JR	Z,MODE_MSX1
	LD	C,80
MODE_MSX1:
	LD	A,(NEW_W)
	CP	C
	JR	Z,MODE_MSX2
	CP	40
	JR	NZ,MODE1
MODE_MSX2:
	LD	(LINL40),A
	LD	HL,(WBOOT+1)
	LD	L,_WIDTH
	LD	(HL),A
	LD	IX,INITXT
	CALL	CALLROM
	LD	IX,SETTXT
CALLROM:
	LD	IY,(EXPTBL)	;メインROMスロット
	JP	CALSLT
MODE1:
	LD	DE,USAGE
	LD	C,9
	JP	SYSTEM

CLS:
	LD	E,$0C		;画面消去
	LD	C,2		;(BDOS)コンソール出力(_CONOUT)
	JP	SYSTEM

;	in
;	DE:	文字列のアドレス
;	out
;	HL:	10進数数値化した値
GETNUM:
	LD	HL,0
GETNUM1:
	LD	A,(DE)
	INC	DE
	SUB	'0'
	RET	C
	CP	10
	RET	NC
	LD	C,L
	LD	B,H
	ADD	HL,HL	;*2
	ADD	HL,HL	;*4
	ADD	HL,BC	;*5
	ADD	HL,HL	;*10
	LD	C,A
	LD	B,0
	ADD	HL,BC
	JR	GETNUM1

IS_HEIGHT_OK:
	CP	24
	RET	Z
	CP	25
	RET

TITLE:
	DB	"mode v1.00"
	DB	$0D,$0A
	DB	"$"
USAGE:
	DB	"usage: MODE [40/80/-] [24/25]",$0D,$0A
	DB	"$"

;		X1 CRTC DATA
X8024L:		;80x25 15kHz
	DB	$6F,$50,$59,$38,$1F,$02,$19,$1C,$00,$07
	DW	0-80*24
	DW	-80
	DB	$0C,$20

X4024L:		;40x25 15kHz
	DB	$37,$28,$2D,$34,$1F,$02,$19,$1C,$00,$07
	DW	0-40*24
	DW	-40
	DB	$0D,$20

X8024H:		;80x25 24kHz
	DB	$6B,$50,$59,$88,$1B,$00,$19,$1A,$00,$0F
	DW	0-80*24
	DW	-80
	DB	$0C,$23

X4024H:		;40x25 24kHz
	DB	$35,$28,$2D,$84,$1B,$00,$19,$1A,$00,$0F
	DW	0-40*24
	DW	-40
	DB	$0D,$23

NEW_W	DW	0
NEW_H	DW	0




