;
;	MAG  by Gaku
;
THOLD	EQU	11
WIDTH	EQU	80
;
OSFLG	EQU	$0003
SYSTEM	EQU	$0005
JP_HL	EQU	$000F
BIOS	EQU	$0018
FCB1	EQU	$005C
DTA	EQU	$0080

FLAGBF	EQU	$1000		;256
LINEBF	EQU	FLAGBF+256	;16*512	リングバッファ

KWABF1:	EQU	LINEBF+16*512
KWABF2:	EQU	KWABF1+2048

BUFSIZ	EQU	4*1024
DTBUF	EQU	KWABF2+2048
DTBUFE	EQU	DTBUF+BUFSIZ

PDSIZE	EQU	8*1024
PDBUF	EQU	DTBUFE
PDBUFE	EQU	PDBUF+PDSIZE

HEADBF	EQU	PDBUFE
PALBF	EQU	HEADBF+32

	ORG	$0100
	JP	START
START:
	LD	A,(000BH)
	LD	(SMC01+2),A
	LD	(SMC02+2),A
	LD	(SMC03+2),A
	LD	(SMC04+2),A
	LD	(SMC05+2),A
	LD	(SMC06+2),A
	LD	(SMC07+2),A
	LD	(SMC08+2),A
	LD	(SMC09+2),A
	LD	(SMC10+2),A
	LD	(SMC11+2),A
	LD	(SMC12+2),A
	LD	(SMC13+2),A
	LD	(SMC14+2),A
	LD	(SMC15+2),A
	LD	(SMC16+2),A
	LD	(SMC17+2),A
	LD	(SMC18+2),A
	LD	(SMC19+2),A
	LD	(SMC20+2),A
	LD	(SMC21+2),A
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM
	LD	A,(FCB1)
	OR	A
	JR	NZ,MAIN
	LD	A,(FCB1+1)
	CP	$20
	JR	NZ,MAIN
USAGE:
	LD	DE,MUSAGE
	LD	C,9
	CALL	SYSTEM
	RST	0
MAIN:
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	RET	NZ
	LD	A,L
	AND	$7F
	RET	NZ

	LD	SP,(SYSTEM+1)
	LD	HL,FCB1+1
	LD	A,(HL)
	CP	$20
	JR	NZ,ALL2
	LD	B,8
ALL1:
	LD	(HL),"?"
	INC	HL
	DJNZ	ALL1
ALL2:
				;拡張子 MAG
	LD	A,(FCB1+9)
	CP	$20
	JR	NZ,EXT1
	LD	HL,"A"*256+"M"
	LD	(FCB1+9),HL
	LD	A,"G"
	LD	(FCB1+11),A
EXT1:
	LD	DE,DTA+1
SW0:
	CALL	SWITCH
	CP	$21
	JP	C,LOAD
	CP	"C"
	JR	NZ,SW1
	LD	(CLSSW+1),A
	JR	SW0
SW1:
	CP	"R"
	JR	NZ,SW2
	LD	A,6
	LD	(RGBSW+1),A
	JR	SW0
SW2:
	CP	"S"
	JR	NZ,SW3
	LD	(SAVESW+1),A
	JR	SW0
SW3:
	CP	"H"
	JR	NZ,SW4
	LD	(SAVESW+1),A

	LD	HL,200
	LD	(L200X1+1),HL
	LD	(L200X2+1),HL
	LD	(L200X3+1),HL
	DEC	HL
	LD	(L200X4),HL

	LD	A,$23		;INC HL
	LD	(S200X1),A
	LD	(S200X2),A

	LD	HL,1999
	LD	(M200X1+1),HL
	INC	HL
	LD	(M200X2+1),HL

	LD	HL,STABLE2-2
	LD	(T200X1+1),HL

	LD	A,7
	LD	(SCMODE),A

	LD	HL,$0050+2000
	LD	(FBST),HL
	JR	SW0
SW4:
	CP	"U"
	JP	NZ,USAGE
	LD	(SAVESW+1),A
	LD	HL,USER
	LD	B,18
	LD	A,(DE)
	CP	$22
	JR	NZ,SW4X
	INC	DE
	LD	A,(DE)
SW4X:
	CP	"'"
	JR	NZ,SW4L
	INC	DE
SW4L:
	LD	A,(DE)
	CP	$20
	JR	C,SW4E
	CP	"/"
	JR	Z,SW4E
	INC	DE
	CP	$22
	JR	Z,SW4E
	CP	"'"
	JR	Z,SW4E
	LD	(HL),A
	INC	HL
	DJNZ	SW4L
	JP	SW0
SW4E:
	LD	A,$20
	LD	(HL),A
	INC	HL
	DJNZ	SW4E
	JP	SW0

LOAD:
SAVESW:	LD	A,$00
	OR	A
	JP	NZ,SAVE

	LD	DE,FILE
	LD	C,$1A
	CALL	SYSTEM

	LD	DE,FCB1
	LD	C,$11
	CALL	SYSTEM
	OR	A
	JP	NZ,0

	CALL	GRAPH1
WILD:
	LD	DE,FILE
	LD	C,$0F
	CALL	SYSTEM
	OR	A
	JP	NZ,END

	LD	HL,0
	LD	(FILE+33),HL
	LD	(FILE+35),HL
	INC	L
	LD	(FILE+14),HL

	LD	DE,DTBUF
	LD	C,$1A
	CALL	SYSTEM

	LD	HL,FLAGBF
	LD	DE,FLAGBF+1
	LD	BC,16*512+256+4096-1
	LD	(HL),0
	LDIR

	LD	A,1
	LD	(FLAGAX+1),A
	LD	HL,0
	LD	(ZURI+1),HL

	LD	HL,LINEBF
	LD	(LINEX+1),HL

	LD	HL,$FFFF
	LD	DE,$FFFF
	LD	BC,0
	EXX

	LD	HL,HEADBF
	LD	BC,8
	CALL	READ
	LD	HL,HEADBF
	LD	DE,MAGID
	LD	B,8
	CALL	CPSTR
	JP	NZ,NEXT

	LD	E,$0C
	LD	C,2
	CALL	SYSTEM

CLSSW:	LD	A,$00
	OR	A
	CALL	NZ,CLS

	LD	BC,$1800
	LD	A,12
	OUT	(C),A
	INC	C
	DB	$ED,$71
	DEC	C
	INC	A
	OUT	(C),A
	INC	C
	DB	$ED,$71
	LD	HL,8
COMMENT:
	INC	HL
	CALL	FGETC
	CP	$1A
	JR	Z,HEADER
	LD	E,A
	LD	C,2
	CALL	SYSTEM
	JR	COMMENT
HEADER:
	LD	(ADDHD+1),HL
	LD	E,$0D
	LD	C,2
	CALL	SYSTEM
	LD	E,$0A
	LD	C,2
	CALL	SYSTEM

	CALL	FGETC
	OR	A
	JP	NZ,NEXT
	LD	HL,HEADBF
	LD	(HL),A
	INC	HL
	LD	BC,31+48	;ヘッダ パレット
	CALL	READ
	PUSH	HL

	LD	HL,PALBF
	LD	DE,DEGI8
	LD	B,48
	CALL	CPSTR
	LD	A,(HEADBF+3)	;スクリーンモード
	JR	NZ,NOTD
	OR	6
	LD	(HEADBF+3),A
NOTD:
	BIT	7,A		;256色
	JP	NZ,NEXT
	AND	1		;200ライン
	LD	(L200+1),A

	LD	HL,(HEADBF+16)
	LD	BC,(HEADBF+12)
	AND	A
	SBC	HL,BC
	LD	B,H
	LD	C,L
	POP	HL
	CALL	READ		;フラグA
				;フラグB
	LD	HL,FILE
	LD	DE,FILE2
	LD	BC,37
	LDIR

	LD	A,(HEADBF+26)
	LD	HL,(HEADBF+24)
ADDHD:	LD	DE,0
	ADD	HL,DE
	ADC	A,0
	LD	(FILE2+33),HL
	LD	(FILE2+35),A

	LD	HL,(HEADBF+8)
	LD	BC,(HEADBF+4)
	AND	A
	SBC	HL,BC
	LD	BC,8
	ADD	HL,BC
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	A,H
	LD	(XSIZE+1),A

	LD	HL,(HEADBF+10)
	LD	BC,(HEADBF+6)
	AND	A
	SBC	HL,BC
	INC	HL
	LD	(YSIZE+1),HL

RGBSW:	LD	B,$00
	LD	A,(HEADBF+3)
	OR	B
	LD	(HEADBF+3),A

	LD	HL,KUWANO
	LD	DE,KUWANO

SMC01:	LD	A,($ECBF)
	RRCA
	JR	NC,LOW
				;High Reso
	LD	A,(HEADBF+3)	;スクリーンモード
	LD	B,0
	AND	6
	CP	6
	JR	NZ,HIGH3
				;デジタル 8 色
	LD	A,(HEADBF+3)	;スクリーンモード
	LD	DE,RGB

	ADD	A,A
	AND	2
	JR	Z,HIGH2

	LD	DE,NUL
HIGH2:
	LD	HL,RGB
HIGH3:
	LD	B,A
SMC02:	LD	A,($ECBF)
	AND	$FD
	OR	B
SMC03:	LD	($ECBF),A
	LD	BC,$1FD0
	OUT	(C),A

	JR	FA

LOW:
	LD	HL,KUWANO2
	LD	DE,NUL

	LD	A,(HEADBF+3)	;スクリーンモード
	AND	7
	CP	7
	JR	NZ,FA

	LD	HL,RGB
FA:
	LD	(MODE1+1),HL
	LD	(MODE2+1),DE
;
				;パレット 8bit->4bit
	LD	B,48
	LD	HL,PALBF
PALI:
	LD	A,(HL)
	RRCA
	RRCA
	RRCA
	RRCA
	AND	$0F
	SUB	4
	JR	NC,PLUS
	XOR	A
PLUS:
	LD	(HL),A
	INC	HL
	DJNZ	PALI

	LD	IX,(HEADBF+12)	;フラグA
	LD	DE,HEADBF
	ADD	IX,DE
	LD	IY,(HEADBF+16)	;フラグB
	ADD	IY,DE

	LD	B,25
LOOP1:
	PUSH	BC

	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	CP	$1B
	JP	Z,NEXT
	CP	3
	JP	Z,END
	CP	$13
	CALL	Z,WAIT1

ZURI:	LD	HL,0
	LD	A,H
	OR	L
	JR	Z,ZURI1

	LD	BC,$1800
	LD	A,12
	OUT	(C),A
	INC	C
	OUT	(C),H
	DEC	C
	INC	A
	OUT	(C),A
	INC	C
	OUT	(C),L
	DEC	C
	LD	A,6
	OUT	(C),A
	INC	C
	LD	A,24
	OUT	(C),A
	LD	BC,WIDTH
	ADD	HL,BC
	LD	(ZURI+1),HL
ZURI1:
	LD	B,8
LOOP2:
	PUSH	BC
	EXX
	PUSH	BC
	PUSH	BC
	EXX
SMC04:	LD	A,($ECBF)
	AND	$EF
SMC05:	LD	($ECBF),A
	LD	BC,$1FD0
	OUT	(C),A

	CALL	MAG
	LD	HL,(LINEX+1)
	PUSH	HL
L200:	LD	A,$00
	OR	A
	CALL	Z,MAG
	LD	HL,(LINEX+1)
	EX	(SP),HL
MODE1:	CALL	KUWANO

SMC06:	LD	A,($ECBF)
	OR	$10
SMC07:	LD	($ECBF),A
	LD	BC,$1FD0
	OUT	(C),A

	POP	HL
	EXX
	POP	BC
	EXX
MODE2:	CALL	KUWANO

	EXX
	POP	BC
	LD	A,B
	ADD	A,8
	LD	B,A
	EXX

	POP	BC
	DEC	B
	JP	NZ,LOOP2

	LD	HL,(YSIZE+1)
	LD	A,H
	OR	L
	JR	Z,WAIT

	EXX
	PUSH	HL
	LD	HL,WIDTH
	ADD	HL,BC
	LD	A,H
	AND	7
	LD	B,A
	LD	C,L
	POP	HL
	EXX
	POP	BC
	DEC	B
	JP	NZ,LOOP1

	CALL	WAIT1
	LD	HL,(ZURI+1)
	LD	BC,WIDTH
	ADD	HL,BC
	LD	(ZURI+1),HL

	LD	BC,$2000
	LD	HL,$800
CLRAT:
	DB	$ED,$71
	INC	BC
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,CLRAT

	LD	B,25
	JP	LOOP1

WAIT:
	DB	$3E
NEXT:
	XOR	A
	OR	A
	EX	AF,AF'
	LD	SP,(SYSTEM+1)

	LD	DE,FILE
	LD	C,$1A
	CALL	SYSTEM

	LD	C,$12
	CALL	SYSTEM
	OR	A
	EX	AF,AF'
	CALL	NZ,WAIT1
	CALL	WAIT2
	EX	AF,AF'
	JP	Z,WILD
END:
	CALL	MOTOFF
	CALL	GRAPH0
	RST	0

WAIT1:
	CALL	MOTOFF
	LD	BC,$1800
	LD	A,6
	OUT	(C),A
	INC	C
	LD	A,25
	OUT	(C),A

	CALL	WAIT2
WAIT3:
	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	OR	A
	JR	Z,WAIT3
	CP	3
	JR	Z,END
	RET

WAIT2:
	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	OR	A
	JR	NZ,WAIT2

SMC08:	LD	A,($EC92)
	OR	A
	JR	NZ,WAIT2
	RET

RGB:
	LD	C,WIDTH
DIS1:
	LD	DE,$4004
	CALL	DISS		;G
	EXX
	SET	7,B
	SET	6,B
	OUT	(C),A
	EXX
	LD	DE,$2002
	CALL	DISS		;R
	EXX
	RES	6,B
	OUT	(C),A
	EXX
	LD	DE,$1001
	CALL	DISS		;B
	EXX
	RES	7,B
	SET	6,B
	OUT	(C),A
	LD	A,B
	INC	BC
	XOR	B
	AND	8
	JR	Z,INCXX
	LD	A,B
	SUB	8
	LD	B,A
INCXX:
	EXX
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	DEC	C
	JR	NZ,DIS1

DISS:
	PUSH	BC
	PUSH	HL
	LD	BC,$0400
DISS1:
	LD	A,(HL)
	AND	D
	JR	Z,DISS2
	SCF
DISS2:
	RL	C
	LD	A,(HL)
	AND	E
	JR	Z,DISS3
	SCF
DISS3:
	RL	C

	INC	HL
	DJNZ	DISS1

	LD	A,C
	POP	HL
	POP	BC
	RET

KUWANO:
	PUSH	IX
	PUSH	IY

KLINE1:	LD	IX,KWABF1+4
KLINE2:	LD	IY,KWABF2+4

	LD	B,80
KUWA1:
	PUSH	BC

	LD	B,4
KUWA2:
	PUSH	BC

	LD	A,(HL)
	INC	HL
	PUSH	HL

	PUSH	AF
	RRCA
	RRCA
	RRCA
	RRCA
	CALL	PALET1
	POP	AF
	CALL	PALET1

	POP	HL
	POP	BC
	DJNZ	KUWA2

	EXX
	SET	7,B
	SET	6,B
	LD	A,(GREEN)
	OUT	(C),A
	RES	6,B
	LD	A,(RED)
	OUT	(C),A
	RES	7,B
	SET	6,B
	LD	A,(BLUE)
	OUT	(C),A
	LD	A,B
	INC	BC
	XOR	B
	AND	8
	JR	Z,INCX
	LD	A,B
	SUB	8
	LD	B,A
INCX:
	EXX
	POP	BC
	DJNZ	KUWA1
KUWAE:
	LD	HL,(KLINE1+2)
	LD	IX,(KLINE2+2)
	LD	(KLINE2+2),HL
	LD	(KLINE1+2),IX

	POP	IY
	POP	IX
	RET

PALET1:
				;C=GREEN D=RED E=BLUE
	AND	$0F
	LD	E,A
	ADD	A,A
	ADD	A,E
	LD	E,A
	LD	D,0
	LD	HL,PALBF
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	E,(HL)
PALETX:
	LD	HL,GREEN
	CALL	KUWANO1
	LD	A,D
	LD	HL,RED		;RED
	CALL	KUWANO1
	LD	A,E
	LD	HL,BLUE		;BLUE
KUWANO1:
	ADD	A,(IX+0)		;桑野
	LD	(IX+0),0
	ADD	A,-THOLD
	JR	C,PSET
	SUB	-THOLD
PSET:
	RL	(HL)

	LD	B,A
	SRL	B
	SUB	B
	LD	C,B		;C=A/2
	SRL	B
	SUB	B
	PUSH	BC		;B=A/4
	SRL	B
	SUB	B
	PUSH	BC		;B=A/8

	ADD	A,(IY+0)		;Y+1
	LD	(IY+0),A

	POP	AF		;A=A/8
	ADD	A,(IY+3*1)	;Y+1 X+1
	LD	(IY+3*1),A

	POP	AF		;A=A/4
	ADD	A,(IY-3*1)	;Y+1 X-1
	LD	(IY-3*1),A

	LD	A,C		;A=A/2
	ADD	A,(IX+3*1)	;X+1
	LD	(IX+3*1),A

	INC	IX
	INC	IY
NUL:
	RET

KUWANO2:
	POP	BC
	POP	DE
	PUSH	DE
	PUSH	BC

	PUSH	IX
	PUSH	IY

	LD	IX,(KLINE1+2)
	LD	IY,(KLINE2+2)

	LD	B,80
LKUWA1:
	PUSH	BC

	LD	B,4
LKUWA2:
	PUSH	BC

	LD	A,(DE)
	INC	DE
	LD	B,(HL)
	INC	HL
	PUSH	DE
	PUSH	HL

	PUSH	BC
	PUSH	AF
	LD	A,B
	RRCA
	RRCA
	RRCA
	RRCA
	LD	B,A
	POP	AF
	PUSH	AF
	RRCA
	RRCA
	RRCA
	RRCA
	CALL	PALET2
	POP	AF
	POP	BC
	CALL	PALET2

	POP	HL
	POP	DE
	POP	BC
	DJNZ	LKUWA2

	EXX
	SET	7,B
	SET	6,B
	LD	A,(GREEN)
	OUT	(C),A
	RES	6,B
	LD	A,(RED)
	OUT	(C),A
	RES	7,B
	SET	6,B
	LD	A,(BLUE)
	OUT	(C),A
	LD	A,B
	INC	BC
	XOR	B
	AND	8
	JR	Z,LINCX
	LD	A,B
	SUB	8
	LD	B,A
LINCX:
	EXX
	POP	BC
	DJNZ	LKUWA1
	JP	KUWAE

PALET2:
				;C=GREEN D=RED E=BLUE
	AND	$0F
	LD	E,A
	ADD	A,A
	ADD	A,E
	LD	E,A
	LD	D,0
	LD	HL,PALBF
	ADD	HL,DE
	LD	C,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	E,(HL)

	LD	A,B
	AND	$0F
	PUSH	DE
	LD	E,A
	ADD	A,A
	ADD	A,E
	LD	E,A
	LD	D,0
	LD	HL,PALBF
	ADD	HL,DE
	POP	DE

	LD	A,(HL)
	INC	HL
	ADD	A,C
	RRA
	LD	C,A

	LD	A,(HL)
	INC	HL
	ADD	A,D
	RRA
	LD	D,A

	LD	A,(HL)
	ADD	A,E
	RRA
	LD	E,A

	LD	A,C
	JP	PALETX

;				フラグ&ピクセル 展開
MAG:
	LD	A,(LINEX+2)
	SUB	LINEBF/256-2
	AND	$1F
	ADD	A,LINEBF/256
	LD	(LINEX+2),A

LINEX:	LD	DE,LINEBF
XSIZE:	LD	B,80
YSIZE:	LD	HL,0

	LD	A,H
	OR	L
	JR	Z,CLEAR
	DEC	HL
	LD	(YSIZE+1),HL

	LD	HL,FLAGBF
FLAGA1:
	RLC	(IX+0)
	JR	C,FLAGA3
	XOR	A
	JR	FLAGA4
FLAGA3:
	CALL	FGETC
FLAGA4:
	XOR	(HL)
	LD	(HL),A
	CP	$11
	JR	NZ,FLAGA5
	PUSH	BC
	PUSH	HL
	LD	H,D
	LD	L,E
	DEC	HL
	DEC	HL
	LDI
	LDI
	LDI
	LDI
	POP	HL
	POP	BC
	JR	FLAGA6
FLAGA5:
	RRCA
	RRCA
	RRCA
	CALL	PIC
	LD	A,(HL)
	ADD	A,A
	CALL	PIC
FLAGA6:
	INC	HL
FLAGAX:	LD	A,$01
	RLCA
	LD	(FLAGAX+1),A
	JR	NC,FLAGA2
	INC	IX
FLAGA2:
	DJNZ	FLAGA1
	RET

CLEAR:
	LD	C,4
CLEAR1:
	LD	(DE),A
	INC	DE
	DJNZ	CLEAR1
	DEC	C
	JR	NZ,CLEAR1
	RET

PIC:
	AND	$1E
	JR	NZ,PICT

	CALL	FGETP
	LD	(DE),A
	INC	DE
	CALL	FGETP
	LD	(DE),A
	INC	DE
	RET
PICT:
	PUSH	BC
	PUSH	HL
	LD	C,A
	LD	B,0
	LD	HL,TABLE
	ADD	HL,BC

	LD	A,E
	SUB	(HL)		;1
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	A,D
	SBC	A,LINEBF/256	;2
	SUB	H
	AND	$1F
	ADD	A,LINEBF/256
	LD	H,A

	LDI
	LDI
	POP	HL
	POP	BC
	RET

READ:
	PUSH	HL
	ADD	HL,BC
	JP	C,END
	LD	DE,(SYSTEM+1)
	DEC	D
	SBC	HL,DE
	JP	NC,NEXT
	POP	HL
READ1:
	LD	A,B
	OR	C
	RET	Z
	CALL	FGETC
	LD	(HL),A
	INC	HL
	DEC	BC
	JR	READ1

FGETC:
	EXX
	LD	A,H
	CP	DTBUFE/$100
	JR	NC,FGETC2
FGETC1:
	LD	A,(HL)
	INC	HL
	EXX
	RET
FGETC2:
	PUSH	BC
	PUSH	DE
	LD	DE,DTBUF
	LD	C,$1A
	CALL	SYSTEM
	LD	DE,FILE
	LD	HL,BUFSIZ
	LD	C,$27
	CALL	SYSTEM
	ADD	A,$FE
	JP	C,NEXT

SMC09:	LD	A,($EC88)
	PUSH	IX
SMC10:	CALL	$ECDC
	LD	A,(IX+$12)
	POP	IX
	AND	$7F
	CP	7
	JR	NZ,FGETC3
SMC11:	LD	A,($EC84)
	OR	$80
	LD	BC,$FFC
	OUT	(C),A
FGETC3:
	LD	A,H
	OR	L
	JP	Z,WAIT
	LD	HL,DTBUF
	POP	DE
	POP	BC
	JR	FGETC1

FGETP:
	EXX
	LD	A,D
	CP	PDBUFE/$100
	JR	NC,FGETP2
FGETP1:
	LD	A,(DE)
	INC	DE
	EXX
	RET
FGETP2:
	PUSH	BC
	PUSH	HL
	LD	DE,PDBUF
	LD	C,$1A
	CALL	SYSTEM
	LD	DE,FILE2
	LD	HL,PDSIZE
	LD	C,$27
	CALL	SYSTEM
	ADD	A,$FE
	JP	C,NEXT

SMC12:	LD	A,($EC88)
	PUSH	IX
SMC13:	CALL	$ECDC
	LD	A,(IX+$12)
	POP	IX
	AND	$7F
	CP	7
	JR	NZ,FGETP3
SMC14:	LD	A,($EC84)
	OR	$80
	LD	BC,$FFC
	OUT	(C),A
FGETP3:
	LD	A,H
	OR	L
	JP	Z,WAIT
	LD	DE,PDBUF
	POP	HL
	POP	BC
	JR	FGETP1

MOTOFF:
SMC15:	LD	A,($EC84)
	AND	$7F
	LD	BC,$FFC
	OUT	(C),A
	RET

CPSTR:
	LD	A,(DE)
	INC	DE
	CP	(HL)
	INC	HL
	RET	NZ
	DJNZ	CPSTR
	RET

CLS:
SMC16:	LD	A,($ECBF)
	RES	4,A
	CALL	CLS0
SMC17:	LD	A,($ECBF)
	SET	4,A
CLS0:
	LD	BC,$1FD0
	OUT	(C),A
SMC18:	LD	($ECBF),A
	XOR	A
	LD	C,A
	LD	HL,$4003
CLS1:
	LD	B,H
CLS2:
	DB	$ED,$71
	INC	B
	JR	NZ,CLS2
;
	ADD	A,L
	LD	C,A
	JR	NZ,CLS1
	RET

SWITCH:
	LD	A,(DE)
	CP	$20
	RET	C
	INC	DE
	CP	"/"
	JR	NZ,SWITCH
	LD	A,(DE)
	INC	DE
CAP:
	CP	"a"
	RET	C
	CP	"z"+1
	RET	NC
	SUB	$20
	RET

GRAPH0:
	LD	B,$10
	DB	$ED,$71
	INC	B
	DB	$ED,$71
	INC	B
	DB	$ED,$71
	INC	B
	DB	$ED,$71
	LD	A,(BIOS)
	INC	A
	RET	Z
GR1:
	CALL	G1FD0
	OR	$80
	JR	GR3
GRAPH1:
	LD	BC,$1FB0
	DB	$ED,$71
	LD	BC,$10AA
	OUT	(C),C
	LD	BC,$11CC
	OUT	(C),C
	LD	BC,$12F0
	OUT	(C),C
	INC	B
	DB	$ED,$71
	CALL	G1FD0
	AND	$77
GR3:
	LD	(HL),A
	LD	BC,$1FD0
	OUT	(C),A
	RET
G1FD0:
SMC19:	LD	HL,$ECBF
	LD	A,(HL)
	RET

	INCLUDE	"MAGS.ASM"

TITLE:
	DB	"X1 MAG loader/saver v1.01",$0D,$0A
	DB	"$"
MUSAGE:
	DB	"usage: MAG ﾌｧｲﾙﾈｰﾑ[.MAG][ｽｲｯﾁ]",$0D,$0A
	DB	"ﾛｰﾄﾞ:"
	DB	9,"/C ｽｸﾘｰﾝ･ｸﾘｱ",$0D,$0A
	DB	9,"/R RGB",$0D,$0A
	DB	"ｾｰﾌﾞ:"
	DB	9,"/S ｾｰﾌﾞ",$0D,$0A
	DB	9,"/H 200ﾗｲﾝ",$0D,$0A
	DB	9,"/U ﾕｰｻﾞｰ",$0D,$0A
	DB	"$"
COMP:
	DB	" Ok",$0D,$0A,"$"

TABLE:
	DB	0  ,0		; 0
	DB	1*2,0		; 1
	DB	2*2,0		; 2
	DB	4*2,0		; 3
	DB	0  ,1*2		; 4
	DB	1*2,1*2		; 5
	DB	0  ,2*2		; 6
	DB	1*2,2*2		; 7
	DB	2*2,2*2		; 8
	DB	0  ,4*2		; 9
	DB	1*2,4*2		;10
	DB	2*2,4*2		;11
	DB	0  ,8*2		;12
	DB	1*2,8*2		;13
	DB	2*2,8*2		;14
	DB	0  ,0		;15

FILE:
	DS	37
FILE2:
	DS	37

GREEN:	DB	0
RED:	DB	0
BLUE:	DB	0
