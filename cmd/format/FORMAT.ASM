;
;	FORMAT by Gaku
;
WBOOT	EQU	$0000
SYSTEM	EQU	$0005
JP_HL	EQU	$000F
FCB1	EQU	$005C
DTA	EQU	$0080

DPB_FATLN	EQU	00H
DPB_DRD		EQU	02H
DPB_DWT		EQU	04H
DPB_FATID	EQU	06H
DPB_SECPCL	EQU	07H
DPB_MAXCL	EQU	08H
DPB_FDMODE	EQU	0AH
DPB_DIRSCNT	EQU	0BH
DPB_MAXCYL	EQU	0CH
DPB_MAXSEC	EQU	0DH
DPB_FATPS	EQU	0EH
DPB_BPS		EQU	0FH
DPB_DIRPS	EQU	10H
DPB_DEVICE	EQU	12H
DPB_UNITNO	EQU	13H
DPB_ADDCL	EQU	14H
DPB_NAME	EQU	1CH

;	PC-8801mkIISR
S_MAIN	EQU	$002A
DSS	EQU	$6000

GETPARAMS_N	EQU	$7C06
SEEK		EQU	$7C09
FDCOMMAND	EQU	$7C0C
CHECKRESULT	EQU	$7C0F
GETDEVSTAT	EQU	$7C12
RDFDC		EQU	$7C15

S_PARAMS	EQU	$7C30
S_UNITNO	EQU	S_PARAMS	;ユニット番号 0:ドライブA 1:ドライブB
S_DPS		EQU	S_PARAMS+1	;1トラックのセクタ数
S_SECTOR_R	EQU	S_PARAMS+2	;セクタ番号
S_TRACK		EQU	S_PARAMS+3	;トラック番号
S_FDMODE	EQU	S_PARAMS+4	;FDモード切り替え $FF:2D/2DD $FE:2HD
S_SECSIZE_N	EQU	S_PARAMS+5	;N セクタサイズ
S_BYTE_H	EQU	S_PARAMS+6	;バイト数H
S_MAXCL		EQU	S_PARAMS+7	;シリンダ数

S_CMD_STATUS	EQU	$7C40
S_LEFT		EQU	$7C41
S_WKF4		EQU	$7C42

PUTCOM	EQU	$FFBB
PUTDAT	EQU	$FFBE
GETDAT	EQU	$FFC1
FPUTDAT	EQU	$FFC4
FGETDAT	EQU	$FFC7

;	for MSX
CALSLT	EQU	$001C
FORMAT	EQU	$0147
EXPTBL	EQU	$FCC1

	ORG	$0100
	JP	START
START:
	LD	DE,TITLE
	LD	C,9		;文字列出力
	CALL	SYSTEM
	LD	A,(WBOOT+2)
	LD	(WTTRKP),A
	LD	(RDFATXP),A
	LD	A,(FCB1)
	OR	A
	JR	NZ,MAIN
USAGE:
	LD	DE,MUSAGE
	LD	C,9		;文字列出力
	CALL	SYSTEM
	JP	0
MAIN:
	LD	C,$6F		;MSX-DOS のバージョン番号の獲得(_DOSVER) *1 MSX-DOS
	CALL	SYSTEM
	RET	C		;CP/M
	LD	HL,$011D
	AND	A
	SBC	HL,BC
	JP	NZ,MSX
	LD	HL,(SYSTEM+1)
	DEC	H
	DEC	H
	DEC	H
	DEC	H
	LD	SP,HL
	LD	HL,0
	PUSH	HL

	LD	A,(FCB1)
	DEC	A
	LD	E,A
	LD	C,$1F		;ディスク装置のパラメータの読み出し
	CALL	SYSTEM
	OR	A
	JR	NZ,MAIN0
	LD	A,(IX+DPB_DEVICE)	;デバイス情報
	CP	$26			;デバイスはEMMでBPBを使っている
	JR	NZ,USAGE
MAIN0:
	CALL	SWITCH
	CP	$20
	JR	C,MAIN1
	SUB	'C'
	JR	NZ,USAGE
	LD	(LOGSW),A
MAIN1:
	LD	DE,FORMES	;format
	LD	C,9		;文字列出力
	CALL	SYSTEM

	LD	BC,$0406
	PUSH	IX
PRNAME:
	LD	E,(IX+DPB_NAME)	;デバイスの名前
	LD	C,6		;コンソール直接入出力
	PUSH	BC
	CALL	SYSTEM
	POP	BC
	INC	IX
	DJNZ	PRNAME
	POP	IX
	CALL	NL

	LD	A,(IX+DPB_DEVICE)	;デバイス情報
	CP	$26			;デバイスはEMMでBPBを使っている
	JR	Z,EMM
	AND	$0F
	CP	7		;FDD
	JR	Z,FDD
	JP	LOGIC
EMM:
	LD	A,(LOGSW)
	OR	A
	JP	Z,LOGICY
	CALL	SURE
	CALL	BPB_FROM_EMM
	JP	LOGICX
FDD:
	LD	A,1
LOGSW	EQU	$-1
	OR	A
	JP	Z,LOGICY

	LD	C,$6F		;MSX-DOS のバージョン番号の獲得(_DOSVER) *1 MSX-DOS
	CALL	SYSTEM
	LD	A,L
	OR	A		;X1
	JR	Z,ENABLE_PHISIC
	CP	1		;MZ-700/1500
	JR	Z,ENABLE_PHISIC
	CP	2		;PC-8801mkIISR
	JR	Z,ENABLE_PHISIC88
	JP	TYPEY
ENABLE_PHISIC88:
	LD	A,(PUTCOM)
	CP	$C3		;JP
	JP	NZ,TYPEY
	DB	$3E		;LD A,?
	SCF
	LD	(MAKE),A
	LD	A,16H		;高速レシーブメモリ
	CALL	PUTCOM
	LD	A,DSS/256	;開始アドレスH
	CALL	PUTDAT
	XOR	A		;開始アドレスL
	CALL	PUTDAT
	LD	BC,AT_DSSE-AT_DSS
	BIT	0,C
	JR	Z,SUBPG1
	INC	BC		;偶数にする
SUBPG1:
	LD	A,B		;バイト数H
	CALL	PUTDAT
	LD	A,C
	CALL	PUTDAT
	LD	HL,AT_DSS
	CALL	FPUTDAT
ENABLE_PHISIC:
	BIT	0,(IX+DPB_FDMODE)	;フロッピーディスクのモード
	JR	Z,ENABLE_2HD
	BIT	7,(IX+DPB_DEVICE)	;デバイス情報
	JR	NZ,ENABLE_2HD
	LD	DE,FOTMESL	;format type
	JR	DISABLE_2HD
ENABLE_2HD:
	LD	DE,FOTMES	;format type
DISABLE_2HD:
	LD	C,9		;文字列出力
	CALL	SYSTEM
TYPE:
	LD	C,8		;エコーなしコンソール入力
	CALL	SYSTEM
	CALL	CAP
	CP	'0'
	JR	Z,TYPE0
	CP	'1'
	JR	Z,TYPE1
	CP	'2'
	JR	Z,TYPE2
	CP	'3'
	JR	Z,TYPE3
	CP	'4'
	JR	Z,TYPE4
	CP	'L'
	JP	Z,TYPEY
	JR	TYPE
TYPE0:
	LD	HL,M2D
TYPE1X:
	LD	BC,LGAP1
	LD	DE,SEC512
	JR	TYPEX
TYPE1:
	LD	HL,M2DD
	JR	TYPE1X
TYPE2:
	CALL	JAPAN2HD
	LD	BC,HGAP1
	LD	DE,SEC1024
	LD	HL,M2HD
	JR	TYPEX
TYPE3:
	LD	BC,HGAP1
	LD	DE,SEC512
	LD	HL,M2HC
	JR	TYPEX
TYPE4:
	LD	BC,HGAP1
	LD	DE,SEC512
	LD	HL,M2HQ
	JR	TYPEX
TYPEX:
	LD	(GAP1),BC
	LD	(SEC),DE
	PUSH	HL
	POP	IY

	LD	E,A
	LD	C,2		;コンソール出力
	CALL	SYSTEM
	CALL	NL

	PUSH	IX
	POP	DE
	LDI
	LDI
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	LD	BC,12
	LDIR

	LD	A,(IX+DPB_MAXCYL)	;フロッピーディスクのシリンダ数
	LD	(MAXCYL),A

	LD	HL,(WBOOT+1)
	LD	L,$89			;_DSK
	LD	(HL),$FF

	LD	A,(FCB1)
	DEC	A
	PUSH	HL
	LD	L,$D9			;_CHGDRV
	CALL	JP_HL
	POP	HL
	LD	A,(IX+DPB_UNITNO)	;デバイスドライバ内におけるユニット番号
	AND	3
	OR	$80			;_CYL0
	LD	L,A
	LD	(HL),$FF
	CALL	SURE

	LD	DE,PHYMES
	LD	C,9		;文字列出力
	CALL	SYSTEM

	CALL	PHYSIC
	CALL	NL
	JP	C,ERROR
	CALL	VERIFY
	CALL	NL
	JP	C,ERROR

	LD	HL,FDATA
	LD	DE,FDATA+1
	LD	(HL),0
	LD	BC,$400
	LDIR
	LD	A,(IX+DPB_FATID)	;メディアバイト
	CP	$FD
	LD	HL,BPB_2D
	JR	Z,IPL
	CP	$FE
	LD	HL,BPB_2HD
	JR	Z,IPL
	CP	$F0
	LD	HL,BPB_2HQ
	JR	Z,IPL
	LD	A,(IX+DPB_FDMODE)	;フロッピーディスクのモード
	LD	HL,BPB_2DD
	INC	A
	JR	Z,IPL
	LD	HL,BPB_2HC
IPL:
	LD	DE,FDATA
	LD	C,$22
	LDIR
	LD	HL,IPLPRG
	LD	C,IPLPEND-IPLPRG
	LDIR
	LD	HL,FDATA+$1E0
	LD	B,$10
LOW1:
	LD	(HL),$22
	INC	HL
	LD	(HL),$FE
	INC	HL
	DJNZ	LOW1

	LD	DE,0
	LD	HL,FDATA
	CALL	DWT
	OR	A
	JP	NZ,ERROR

	JR	LOGICX
TYPEY:
	LD	DE,CRLF
	LD	C,9		;文字列出力
	CALL	SYSTEM
LOGICY:
	LD	A,(FCB1)
	DEC	A
	LD	E,A
	LD	C,$1F		;(BDOS)ディスク装置のパラメータの読み出し
	CALL	SYSTEM
	CALL	$00E2		;_RDFATX
RDFATXP	EQU	$-1
	JP	C,ERROR
LOGIC:
	CALL	SURE
LOGICX:
	LD	DE,LOGMES
	LD	C,9		;文字列出力
	CALL	SYSTEM

	LD	HL,FDATA
	LD	E,L
	LD	D,H
	INC	DE
	XOR	A
	LD	(HL),A
	LD	BC,$03FF+4
	LDIR
;
	LD	HL,FDATA
	LD	A,(IX+DPB_FATID)	;メディアバイト
	LD	(HL),A		;FATID
	INC	HL
	LD	(HL),$FF
	INC	HL
	LD	(HL),$FF
	INC	HL
	LD	A,(IX+DPB_BPS)	;FAT情報と論理セクタのサイズ
	ADD	A,A
	ADD	A,A
	ADD	A,A
	SBC	A,A
	LD	(HL),A			;FAT12=00 FAT16=FF
	LD	E,(IX+DPB_FATPS)	;FAT領域の先頭論理セクタ番号
	LD	D,0

	LD	A,(IX+DPB_BPS)	;FAT情報と論理セクタのサイズ
	AND	$80		;bit7:予備FAT
LOGIC0:
	PUSH	AF
	LD	C,(IX+DPB_FATLN)	;FAT領域のセクタ単位でのサイズ
	LD	B,(IX+DPB_FATLN+1)
	LD	HL,FDATA
LOGIC1:
	CALL	DWT
	OR	A
	JP	NZ,ERROR
	LD	HL,FDATA+4
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,LOGIC1

	POP	AF
	ADD	A,A		;bit7:予備FAT
	JR	C,LOGIC0
LOGIC2:				;ディレクトリエントリ
	LD	E,(IX+DPB_DIRPS)	;ルートディレクトリ領域の先頭論理セクタ番号
	LD	D,(IX+DPB_DIRPS+1)

	LD	B,(IX+DPB_DIRSCNT)	;ルートディレクトリ領域の論理セクタ数
LOGIC3:
	LD	HL,FDATA+4
	CALL	DWT
	OR	A
	JP	NZ,ERROR
	DJNZ	LOGIC3

	LD	DE,COMMES
	LD	C,9		;文字列出力
	CALL	SYSTEM
LOGICL:
	JP	0
SURE:
	LD	DE,HITMES
	LD	C,9		;文字列出力
	CALL	SYSTEM
SURE1:
	LD	C,8		;エコーなしコンソール入力
	CALL	SYSTEM
	CP	$0D
	JR	NZ,SURE1
NL:
	PUSH	AF
	LD	E,4
	LD	C,6		;コンソール直接入出力
	CALL	SYSTEM
	POP	AF
	RET

PHYSIC:
	CALL	GUIDE
	LD	DE,0
	EXX
	LD	L,0		;CYLINDER
PH1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	E,'w'
	LD	C,2		;コンソール出力
	CALL	SYSTEM
	POP	HL
	POP	DE
	POP	BC
PH2:
	EXX
	CALL	MAKE
	RET	C
	EXX
	INC	L
	LD	A,L
	CP	0
MAXCYL	EQU	$-1
	JR	C,PH1
	RET

WRITE_ID88:		;PC-8801mkIISR
	LD	A,0DH		;エグゼキュート
	CALL	PUTCOM
	LD	A,DSS/256
	CALL	PUTDAT
	XOR	A
	CALL	PUTDAT
	LD	A,(IX+DPB_UNITNO)	;デバイスドライバ内におけるユニット番号
	CALL	PUTDAT		;+0
	LD	A,(IX+DPB_MAXSEC)	;フロッピーディスクの1トラックのセクタ数
	CALL	PUTDAT		;+1 SC
	LD	A,(IY+18)	;GSL
	CALL	PUTDAT		;+2 GPL
	EXX
	LD	A,L		;シリンダ番号
	EXX
	ADD	A,A		;トラック番号
	CALL	PUTDAT		;+3
	LD	A,(IX+DPB_FDMODE)	;フロッピーディスクのモード
	CALL	PUTDAT		;+4
	LD	A,(IX+DPB_BPS)	;FAT情報と論理セクタのサイズ
	AND	7
	CP	3
	JR	C,W88X1
	LD	A,3
W88X1:
	CALL	PUTDAT		;+5 N
	LD	A,1
	CALL	PUTDAT		;+6
	LD	A,(MAXCYL)
	CALL	PUTDAT		;+7

	LD	A,0DH		;エグゼキュート
	CALL	PUTCOM
	LD	A,RESULTEX/256
	CALL	PUTDAT
	LD	A,RESULTEX%256
	CALL	PUTDAT

	CALL	GETDAT
	LD	C,A
	ADD	A,0FFH
	RET	C
	XOR	A
	RET

MAKE:
	AND	A
	JR	C,WRITE_ID88
	LD	IY,FDATA
	EXX
	LD	C,0		;SIDE0
	CALL	MKDATA
	LD	(GDATA+1),IY
	EXX
	LD	C,1		;SIDE1
	CALL	MKDATA

	LD	HL,FDATA
	CALL	WTTRK
	RET	C
GDATA:	LD	HL,FDATA
WTTRK:
	PUSH	DE
	CALL	$00E8		;_WTTRK
WTTRKP	EQU	$-1
	POP	DE
;
	LD	L,(IX+DPB_MAXSEC)	;フロッピーディスクの1トラックのセクタ数
	LD	H,0
	ADD	HL,DE
	EX	DE,HL
	AND	A
	RET

MKDATA:
	LD	H,0
	EXX
	LD	HL,LGAP1
GAP1	EQU	$-2
	CALL	SETD
MAKE1:
	LD	HL,SEC512
SEC	EQU	$-2
	CALL	SETD
	EXX
	LD	(IY+0),L		;CYLINDER
	INC	IY
	LD	(IY+0),C		;SIDE
	INC	IY
	LD	A,L
	OR	C
	OR	H
	LD	A,1
	JR	Z,MAKES1
	LD	A,H
	INC	A
MAKES1:
	INC	H
	LD	(IY+0),A		;SECTOR
	PUSH	HL
	INC	IY
	LD	A,(IX+DPB_BPS)	;FAT情報と論理セクタのサイズ
	AND	7
	CP	3
	JR	C,SIZE
SIZE1024:
	LD	A,3
SIZE:
	LD	(IY+0),A	;SIZE
	INC	IY
	EXX
	CALL	SETD
	POP	AF		;AF=HL
	CP	(IX+DPB_MAXSEC)	;フロッピーディスクの1トラックのセクタ数
	JR	C,MAKE1

	LD	HL,HGAP4
SETD:
	LD	A,(HL)
	INC	HL
	LD	B,A
	INC	A
	RET	Z
	LD	A,(HL)
	INC	HL
SETD1:
	LD	(IY+0),A
	INC	IY
	DJNZ	SETD1
	JR	SETD

VERIFY:
	LD	DE,FDATA
	LD	C,$1A		;DTAの設定
	CALL	SYSTEM

	CALL	GUIDE

	LD	DE,0
	LD	A,(MAXCYL)
	LD	B,A

VE2:
	PUSH	BC
	PUSH	DE
	LD	E,'v'
	LD	C,2		;コンソール出力
	CALL	SYSTEM
	POP	DE
	PUSH	DE
	LD	A,(IX+DPB_MAXSEC)	;フロッピーディスクの1トラックのセクタ数
	ADD	A,A
	LD	H,A
	LD	A,(FCB1)
	DEC	A
	LD	L,A
	LD	C,$2F		;論理セクタを用いた読み出し
	CALL	SYSTEM
	POP	DE
	POP	BC
	RET	C
	LD	H,0
	LD	A,(IX+DPB_MAXSEC)	;フロッピーディスクの1トラックのセクタ数
	ADD	A,A
	LD	L,A
	ADD	HL,DE
	EX	DE,HL
	DJNZ	VE2
VERE:
	RET

DWT:
	PUSH	HL
	PUSH	BC
	PUSH	DE
	EX	DE,HL
	LD	C,$1A		;DTAの設定
	CALL	SYSTEM
	POP	DE
	PUSH	DE
	LD	A,(FCB1)
	DEC	A
	LD	L,A
	LD	H,1
	LD	C,$30		;論理セクタを用いた書き込み
	CALL	SYSTEM
	POP	DE
	INC	DE
	POP	BC
	POP	HL
	RET

SWITCH:
	LD	DE,DTA+1
SWITCH1:
	LD	A,(DE)
	CP	$20
	RET	C
	INC	DE
	CP	'/'
	JR	NZ,SWITCH1
	LD	A,(DE)
	INC	DE
CAP:
	CP	'a'
	RET	C
	CP	'z'+1
	RET	NC
	SUB	$20
	RET

GUIDE:
	LD	A,(MAXCYL)
	LD	B,A
	LD	C,2
	LD	E,'.'
	CALL	GUIDE1
	LD	A,(MAXCYL)
	LD	B,A
	LD	E,8		;コンソール直接入出力
GUIDE1:
	PUSH	BC
	PUSH	DE
	CALL	SYSTEM
	POP	DE
	POP	BC
	DJNZ	GUIDE1
	RET

ERROR:
	LD	DE,ERRMES
	LD	C,9		;文字列出力
	CALL	SYSTEM
	JP	0

BPB_FROM_EMM:				;EMMのサイズからBPBを作成する
	LD	DE,EMMMES
	LD	C,9		;文字列出力
	CALL	SYSTEM

	LD	A,(IX+DPB_MAXSEC)	;フロッピーディスクの1トラックのセクタ数
	LD	(EMMAL),A
	LD	A,(IX+DPB_MAXCYL)	;フロッピーディスクのシリンダ数
	LD	(EMMBL),A
	LD	A,(IX+DPB_UNITNO)	;デバイスドライバ内におけるユニット番号
	LD	(EMMDV),A

	CALL	EADR0
	IN	A,(C)
	PUSH	AF

	LD	DE,0
ECHECK1:
	INC	DE
	CALL	EADR2
	IN	H,(C)
	CALL	EADR2
	LD	A,H
	ADD	A,0E5H
	OUT	(C),A
	INC	A
	CALL	EADR0
	OUT	(C),A
	DEC	A
	CALL	EADR2
	IN	L,(C)
	CALL	EADR2
	OUT	(C),H
	CP	L
	JR	NZ,ECHECK2
	BIT	7,D
	JR	Z,ECHECK1
ECHECK2:
	CALL	EADR0
	POP	AF
	OUT	(C),A

	LD	HL,0-9
	ADD	HL,DE
	JP	NC,ERROR

	LD	BC,4086
	AND	A
	SBC	HL,BC
	JR	C,EFAT12
	LD	HL,CALC_FATLN16
	LD	(CALC_FATLN),HL
EFAT12:
	PUSH	DE
	POP	HL
	LD	(EMM_TotSec16),HL	;総セクタ数
	EX	DE,HL
CALC_FATLN	EQU	$+1
	CALL	CALC_FATLN12		;自己書き換え
	LD	(EMM_FATSz16),A		;FATのサイズ
	LD	(IX+DPB_FATLN),A	;FAT領域のセクタ単位でのサイズ

	LD	HL,FDATA
	LD	DE,FDATA+1
	LD	(HL),0
	LD	BC,512-FDATA+EMM_BPB
	LDIR

	LD	DE,0
	LD	HL,EMM_BPB
	CALL	DWT
	OR	A
	JP	NZ,ERROR

	LD	A,(FCB1)
	LD	E,A
	LD	C,$1B		;ディスク情報の獲得
	JP	SYSTEM

EADR0:
	PUSH	DE
	EX	DE,HL
	LD	HL,0
	JR	EADR1
EADR2:
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	LD	BC,(EMMAL)
	ADD	HL,BC
EADR1:
	LD	BC,(EMMDV)
	OUT	(C),0		;Z80未定義命令
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H
	INC	C
	EX	DE,HL
	POP	DE
	RET

CALC_FATLN12:	;FAT12 - クラスタ数から必要なFATサイズを求める(セクタサイズ512)
	LD	E,L
	LD	D,H
	ADD	HL,HL	;*2
	ADD	HL,DE	;*3
	SRL	H	;/2
	RR	L	;1クラスタ辺り1.5バイト必要
	LD	DE,511	;切り上げる
	ADD	HL,DE
	LD	A,H
	SRL	A
	RET

CALC_FATLN16:	;FAT16 - クラスタ数から必要なFATサイズを求める(セクタサイズ512)
	LD	A,L
	ADD	A,$FF
	SBC	A,A
	AND	1
	ADD	A,H
	RET

MSX:
	LD	IY,(EXPTBL)	;メインROMスロット
	LD	IX,FORMAT
	CALL	CALSLT
	JP	0

JAPAN2HD:
	LD	HL,(1)
	INC	H
	LD	L,070H
	PUSH	HL
	POP	IY
	LD	A,(IY+00BH)	;_MAX_SEC_SZ_H
	CP	4
	RET	Z

	LD	E,(IY+00CH)	;_FATBF
	LD	D,(IY+00DH)

	LD	L,(IY+00EH)	;_DTBUF
	LD	H,(IY+00FH)

	AND	A
	SBC	HL,DE

	LD	DE,512*3
	AND	A
	SBC	HL,DE

	RET	NZ		;_FATBFが1.5KBかチェック

	LD	C,05FH	 	;ディスクバッファのフラッシュ(_FLUSH)
	CALL	SYSTEM

	LD	HL,(SYSTEM+1)
	LD	A,(HL)
	CP	0C3H		;JP
	RET	NZ

	DI

	LD	E,L
	LD	D,H
	DEC	D
	DEC	D
	DEC	D
	DEC	D
	LD	(SYSTEM+1),DE
	LD	BC,3
	LDIR

	LD	(IY+00BH),4	;_MAX_SEC_SZ_H
	LD	(IY+00EH),E	;_DTBUF
	LD	(IY+00FH),D
	RET

AT_DSS:
	ORG	DSS,AT_DSS-$0100

	LD	B,8
	CALL	GETPARAMS_N
WRITE_ID1:
	CALL	SEEK
	JR	C,FINISH_DISK
	LD	A,4DH		;WRITE ID
	RST	20H
	LD	A,D		;HD US1 US0
	AND	1
	ADD	A,A
	ADD	A,A
	OR	C
	RST	20H
	LD	A,(S_PARAMS+5)	;N
	LD	C,A
	RST	20H
	LD	A,(S_PARAMS+1)	;SC
	LD	B,A
	RST	20H
	LD	A,(S_PARAMS+2)	;GPL
	RST	20H
	LD	A,0E5H		;D
	RST	20H
	LD	E,1		;R
WRITE_ID2:
	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	020H		;NO SERVICE
	JR	Z,ERROR_DISK
	LD	A,D		;C
	SRL	A
	OUT	(0FBH),A	;FDC μPD765A データレジスタ

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	020H		;NO SERVICE
	JR	Z,ERROR_DISK
	LD	A,D		;H
	AND	1
	OUT	(0FBH),A	;FDC μPD765A データレジスタ

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	020H		;NO SERVICE
	JR	Z,ERROR_DISK
	LD	A,E		;R
	OUT	(0FBH),A	;FDC μPD765A データレジスタ

	EI
	HALT
	IN	A,(0FAH)	;FDC μPD765A ステータスレジスタリード
	AND	020H		;NO SERVICE
	JR	Z,ERROR_DISK
	LD	A,C		;N
	OUT	(0FBH),A	;FDC μPD765A データレジスタ

	INC	E
	DJNZ	WRITE_ID2

	CALL	CHECKRESULT
	JR	C,FINISH_DISK

	LD	A,D
	INC	A
	LD	(S_TRACK),A
	AND	1
	JR	NZ,WRITE_ID1

FINISH_DISK:
	SBC	A,A
	LD	(S_CMD_STATUS),A
	JP	S_MAIN
ERROR_DISK:
	SCF
	JR	FINISH_DISK

RESULTEX:
	LD	A,(S_CMD_STATUS)
	RST	18H
	JP	S_MAIN

	ORG	$$+$0100	;$DEPHASE
AT_DSSE:

TITLE:
	DB	"format v1.22"
CRLF:
	DB	$0D,$0A
	DB	'$'
MUSAGE:
	DB	"usage: FORMAT ﾄﾞﾗｲﾌﾞ:[ｽｲｯﾁ]",$0D,$0A
	DB	9,"/C  FAT,ﾃﾞｨﾚｸﾄﾘ ｼｮｷｶ",$0D,$0A
	DB	'$'
FORMES:
	DB	"format $"
FOTMES:
	DB	"format type (2D=0,2DD=1,2HD=2,2HC=3,2HQ=4,logical only=L)? $"
FOTMESL:
	DB	"format type (2D=0,2DD=1,logical only=L)? $"
PHYMES:
	DB	"physical format",$0D,$0A,'$'
LOGMES:
	DB	"logical  format",$0D,$0A,'$'
EMMMES:
	DB	"analysis EMM",$0D,$0A,'$'
COMMES:
	DB	"completed!",$0D,$0A,'$'
HITMES:
	DB	"hit [CR] to start$"
ERRMES:
	DB	7,"Error!",$0D,$0A,'$'
;
;	DPB Data
;

M2D:
	DW	2			;FAT領域のセクタ単位でのサイズ(DPB_FATLN)
	DB	$FD			;メディアバイト(DPB_FATID)
	DB	2			;1クラスタの論理セクタ数(DPB_SECPCL)
	DW	356			;総クラスタ数(DPB_MAXCL)
	DB	$FF			;フロッピーディスクのモード(DPB_FDMODE)
	DB	7			;ルートディレクトリ領域の論理セクタ数(DPB_DIRSCNT)
	DB	40			;フロッピーディスクのシリンダ数(DPB_MAXCYL)
	DB	9			;フロッピーディスクの1トラックのセクタ数(DPB_MAXSEC)
	DB	1			;FAT領域の先頭論理セクタ番号(DPB_FATPS)
	DB	$82			;FAT情報と論理セクタのサイズ(DPB_BPS)
	DW	5			;ルートディレクトリ領域の先頭論理セクタ番号(DPB_DIRPS)
	DB	$A7			;デバイス情報(DPB_DEVICE)
	DB	0			;デバイスドライバ内におけるユニット番号(DPB_UNITNO)
	DW	8			;データ格納領域の先頭論理セクタ番号(DPB_ADDCL)
M2DGSL:
	DB	84

M2DD:
	DW	3			;FAT領域のセクタ単位でのサイズ(DPB_FATLN)
	DB	$F9			;メディアバイト(DPB_FATID)
	DB	2			;1クラスタの論理セクタ数(DPB_SECPCL)
	DW	715			;総クラスタ数(DPB_MAXCL)
	DB	$FF			;フロッピーディスクのモード(DPB_FDMODE)
	DB	7			;ルートディレクトリ領域の論理セクタ数(DPB_DIRSCNT)
	DB	80			;フロッピーディスクのシリンダ数(DPB_MAXCYL)
	DB	9			;フロッピーディスクの1トラックのセクタ数(DPB_MAXSEC)
	DB	1			;FAT領域の先頭論理セクタ番号(DPB_FATPS)
	DB	$82			;FAT情報と論理セクタのサイズ(DPB_BPS)
	DW	7			;ルートディレクトリ領域の先頭論理セクタ番号(DPB_DIRPS)
	DB	$A7			;デバイス情報(DPB_DEVICE)
	DB	0			;デバイスドライバ内におけるユニット番号(DPB_UNITNO)
	DW	10			;データ格納領域の先頭論理セクタ番号(DPB_ADDCL)
M2DDGSL:
	DB	84

M2HD:
	DW	2			;FAT領域のセクタ単位でのサイズ(DPB_FATLN)
	DB	$FE			;メディアバイト(DPB_FATID)
	DB	1			;1クラスタの論理セクタ数(DPB_SECPCL)
	DW	1223			;総クラスタ数(DPB_MAXCL)
	DB	$FE			;フロッピーディスクのモード(DPB_FDMODE)
	DB	6			;ルートディレクトリ領域の論理セクタ数(DPB_DIRSCNT)
	DB	77			;フロッピーディスクのシリンダ数(DPB_MAXCYL)
	DB	8			;フロッピーディスクの1トラックのセクタ数(DPB_MAXSEC)
	DB	1			;FAT領域の先頭論理セクタ番号(DPB_FATPS)
	DB	$84			;FAT情報と論理セクタのサイズ(DPB_BPS)
	DW	5			;ルートディレクトリ領域の先頭論理セクタ番号(DPB_DIRPS)
	DB	$A7			;デバイス情報(DPB_DEVICE)
	DB	0			;デバイスドライバ内におけるユニット番号(DPB_UNITNO)
	DW	9			;データ格納領域の先頭論理セクタ番号(DPB_ADDCL)
M2HDGSL:
	DB	116

M2HC:
	DW	7			;FAT領域のセクタ単位でのサイズ(DPB_FATLN)
	DB	$F9			;メディアバイト(DPB_FATID)
	DB	1			;1クラスタの論理セクタ数(DPB_SECPCL)
	DW	2373			;総クラスタ数(DPB_MAXCL)
	DB	$FE			;フロッピーディスクのモード(DPB_FDMODE)
	DB	14			;ルートディレクトリ領域の論理セクタ数(DPB_DIRSCNT)
	DB	80			;フロッピーディスクのシリンダ数(DPB_MAXCYL)
	DB	15			;フロッピーディスクの1トラックのセクタ数(DPB_MAXSEC)
	DB	1			;FAT領域の先頭論理セクタ番号(DPB_FATPS)
	DB	$82			;FAT情報と論理セクタのサイズ(DPB_BPS)
	DW	15			;ルートディレクトリ領域の先頭論理セクタ番号(DPB_DIRPS)
	DB	$A7			;デバイス情報(DPB_DEVICE)
	DB	0			;デバイスドライバ内におけるユニット番号(DPB_UNITNO)
	DW	27			;データ格納領域の先頭論理セクタ番号(DPB_ADDCL)
M2HCGSL:
	DB	84

M2HQ:
	DW	9			;FAT領域のセクタ単位でのサイズ(DPB_FATLN)
	DB	$F0			;メディアバイト(DPB_FATID)
	DB	1			;1クラスタの論理セクタ数(DPB_SECPCL)
	DW	2849			;総クラスタ数(DPB_MAXCL)
	DB	$FE			;フロッピーディスクのモード(DPB_FDMODE)
	DB	14			;ルートディレクトリ領域の論理セクタ数(DPB_DIRSCNT)
	DB	80			;フロッピーディスクのシリンダ数(DPB_MAXCYL)
	DB	18			;フロッピーディスクの1トラックのセクタ数(DPB_MAXSEC)
	DB	1			;FAT領域の先頭論理セクタ番号(DPB_FATPS)
	DB	$82			;FAT情報と論理セクタのサイズ(DPB_BPS)
	DW	19			;ルートディレクトリ領域の先頭論理セクタ番号(DPB_DIRPS)
	DB	$A7			;デバイス情報(DPB_DEVICE)
	DB	0			;デバイスドライバ内におけるユニット番号(DPB_UNITNO)
	DW	31
M2HQGSL:
	DB	84

;M2HDE:
;	DW	3			;FAT領域のセクタ単位でのサイズ(DPB_FATLN)
;	DB	$03			;メディアバイト(DPB_FATID)
;	DB	1			;1クラスタの論理セクタ数(DPB_SECPCL)
;	DW	1429			;総クラスタ数(DPB_MAXCL)
;	DB	$FE			;フロッピーディスクのモード(DPB_FDMODE)
;	DB	6			;ルートディレクトリ領域の論理セクタ数(DPB_DIRSCNT)
;	DB	80			;フロッピーディスクのシリンダ数(DPB_MAXCYL)
;	DB	89			;フロッピーディスクの1トラックのセクタ数(DPB_MAXSEC)
;	DB	1			;FAT領域の先頭論理セクタ番号(DPB_FATPS)
;	DB	$84			;FAT情報と論理セクタのサイズ(DPB_BPS)
;	DW	7			;ルートディレクトリ領域の先頭論理セクタ番号(DPB_DIRPS)
;	DB	$A7			;デバイス情報(DPB_DEVICE)
;	DB	0			;デバイスドライバ内におけるユニット番号(DPB_UNITNO)
;	DW	11			;データ格納領域の先頭論理セクタ番号(DPB_ADDCL)
;M2HDEGSL:
;	DB	53

;M2HS:	DB	3
;	DW	$FB,8
;	DW	1432
;	DB	$FE,10,80,9,1,4,4,10
;
;	Format Data
;
LGAP1:
	DB	32,$4E,255
HGAP1:
	DB	80,$4E,12,$00,3,$F6,1,$FC,50,$4E,255
HGAP4:
	DB	0,$4E,0,$4E,0,$4E,255
;
;	Sector Data
;
SEC512:
	DB	12,$00,3,$F5,1,$FE,255

	DB	1,$F7,22,$4E,12,$00,3,$F5,1,$FB
	DB	0,$E5,0,$E5
	DB	1,$F7,84,$4E,255
SEC1024:
	DB	12,$00,3,$F5,1,$FE,255

	DB	1,$F7,22,$4E,12,$00,3,$F5,1,$FB
	DB	0,$E5,0,$E5,0,$E5,0,$E5
	DB	1,$F7,116,$4E,255
;SEC1024E:
;	DB	12,$00,3,$F5,1,$FE,255
;
;	DB	1,$F7,22,$4E,12,$00,3,$F5,1,$FB
;	DB	0,$E5,0,$E5,0,$E5,0,$E5
;	DB	1,$F7,53,$4E,255
;
;	Format Data 2HS
;
;SSEC:	DB	12,$00,3,$F5,1,$FE,255
;
;	DB	1,$F7,22,$4E,12,$00,3,$F5,1,$FB
;	DB	0,$E5,0,$E5,0,$E5,0,$E5
;	DB	1,$F7,53,$4E,255
;
;	BOOT-DATA
;
BPB_2D:
	DB	0EBH,0FEH,090H	;BS_JmpBoot
	DB	"LD      "	;BS_OEMName
	DW	512		;BPB_BytsPerSec
	DB	2		;BPB_SecPerClus
	DW	1		;BPB_RsvdSecCnt
	DB	2		;BPB_NumFATs
	DW	112		;BPB_RootEntCnt
	DW	720		;BPB_TotSec16
	DB	0FDH		;BPB_Media
	DW	2		;BPB_FATSz16
	DW	9		;BPB_SecPerTrk
	DW	2		;BPB_NumHeads
	DW	0		;BPB_HiddSec(2バイト)
	RET			;for MSX
	DB	0,0,0

BPB_2DD:
	DB	0EBH,0FEH,090H	;BS_JmpBoot
	DB	"LD      "	;BS_OEMName
	DW	512		;BPB_BytsPerSec
	DB	2		;BPB_SecPerClus
	DW	1		;BPB_RsvdSecCnt
	DB	2		;BPB_NumFATs
	DW	112		;BPB_RootEntCnt
	DW	1440		;BPB_TotSec16
	DB	0F9H		;BPB_Media
	DW	3		;BPB_FATSz16
	DW	9		;BPB_SecPerTrk
	DW	2		;BPB_NumHeads
	DW	0		;BPB_HiddSec(2バイト)
	RET			;for MSX
	DB	0,0,0

BPB_2HD:
	DB	0EBH,0FEH,090H	;BS_JmpBoot
	DB	"LD      "	;BS_OEMName
	DW	1024		;BPB_BytsPerSec
	DB	1		;BPB_SecPerClus
	DW	1		;BPB_RsvdSecCnt
	DB	2		;BPB_NumFATs
	DW	192		;BPB_RootEntCnt
	DW	1232		;BPB_TotSec16
	DB	0FEH		;BPB_Media
	DW	2		;BPB_FATSz16
	DW	8		;BPB_SecPerTrk
	DW	2		;BPB_NumHeads
	DW	0		;BPB_HiddSec(2バイト)
	RET			;for MSX
	DB	0,0,0

BPB_2HC:
	DB	0EBH,0FEH,090H	;BS_JmpBoot
	DB	"LD      "	;BS_OEMName
	DW	512		;BPB_BytsPerSec
	DB	1		;BPB_SecPerClus
	DW	1		;BPB_RsvdSecCnt
	DB	2		;BPB_NumFATs
	DW	224		;BPB_RootEntCnt
	DW	2400		;BPB_TotSec16
	DB	0F9H		;BPB_Media
	DW	7		;BPB_FATSz16
	DW	15		;BPB_SecPerTrk
	DW	2		;BPB_NumHeads
	DW	0		;BPB_HiddSec(2バイト)
	RET			;for MSX
	DB	0,0,0

BPB_2HQ:
	DB	0EBH,0FEH,090H	;BS_JmpBoot
	DB	"LD      "	;BS_OEMName
	DW	512		;BPB_BytsPerSec
	DB	1		;BPB_SecPerClus
	DW	1		;BPB_RsvdSecCnt
	DB	2		;BPB_NumFATs
	DW	224		;BPB_RootEntCnt
	DW	2880		;BPB_TotSec16
	DB	0F0H		;BPB_Media
	DW	9		;BPB_FATSz16
	DW	18		;BPB_SecPerTrk
	DW	2		;BPB_NumHeads
	DW	0		;BPB_HiddSec(2バイト)
	RET			;for MSX
	DB	0,0,0

;SIPL:	DB	$EB,$1E,$90,'L','D',$20,$20,$20
;	DB	$20,$20,$20,"2","H","S",$20,$20
;	DB	$20,$20,$04,$00,$01,$03,$00,$01
;	DB	$00,$C0,$05,$A0,$FB,$01,$C9,$00
;	DB	$EB,$FE

IPLPRG:
	LD	BC,$FFC
	DB	$ED,$71
	LD	A,7
	LD	($FF86),A
	JP	$F5
IPLPEND:

EMMDV:	DW	$0D00
EMMAL:	DB	0
EMMBL:	DB	0

EMM_BPB:
	DB	0EBH,0FEH,090H
EMM_OEMName:
	DB	"LD-EMM  "
EMM_BytsPerSec:
	DW	512
EMM_SecPerClus:
	DB	2
EMM_RsvdSecCnt:
	DW	1
EMM_NumFATs:
	DB	1
EMM_RootEntCnt:
	DW	512
EMM_TotSec16:
	DW	0
EMM_Media:
	DB	0FAH
EMM_FATSz16:
	DW	0

FDATA:
