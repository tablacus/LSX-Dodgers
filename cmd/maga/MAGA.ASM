;
;	MAG  by Gaku
;
DIZ1	EQU	$08
DIZ2	EQU	$08
WIDTH	EQU	40

OSFLG	EQU	$0003
SYSTEM	EQU	$0005
JP_HL	EQU	$000F
BIOS	EQU	$0018
FCB1	EQU	$005C
DTA	EQU	$0080

CTABLE	EQU	$0C00
APALET	EQU	$0D00
FLAGBF	EQU	$1000		;256
LINEBF	EQU	FLAGBF+256	;16*512	リングバッファ

BUFSIZ	EQU	4*1024
DTBUF	EQU	LINEBF+16*512
DTBUFE	EQU	DTBUF+BUFSIZ

PDSIZE	EQU	8*1024
PDBUF	EQU	DTBUFE
PDBUFE	EQU	PDBUF+PDSIZE

HEADBF	EQU	PDBUFE
PALBF	EQU	HEADBF+32

	ORG	$0100
	JP	START
START:
	LD	A,(000BH)
	LD	(SMC01+2),A
	LD	(SMC02+2),A
	LD	(SMC03+2),A
	LD	(SMC04+2),A
	LD	(SMC05+2),A
	LD	(SMC06+2),A
	LD	(SMC07+2),A
	LD	(SMC08+2),A
	LD	(SMC09+2),A
	LD	(SMC10+2),A
	LD	(SMC11+2),A
	LD	(SMC12+2),A
	LD	(SMC13+2),A
	LD	(SMC14+2),A
	LD	(SMC15+2),A
	LD	(SMC16+2),A
	LD	(SMC17+2),A
	LD	(SMC18+2),A
	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM
	LD	A,(FCB1)
	OR	A
	JR	NZ,MAIN
	LD	A,(FCB1+1)
	CP	$20
	JR	NZ,MAIN
USAGE:
	LD	DE,MUSAGE
	LD	C,9
	CALL	SYSTEM
	RST	0
MAIN:
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	RET	NZ
	LD	A,L
	AND	$7F
	RET	NZ

	LD	SP,(SYSTEM+1)
	LD	HL,FCB1+1
	LD	A,(HL)
	CP	$20
	JR	NZ,ALL2
	LD	B,8
ALL1:
	LD	(HL),"?"
	INC	HL
	DJNZ	ALL1
ALL2:
				;拡張子 MAG
	LD	HL,CTABLE1
	LD	DE,CTABLE
	LD	BC,256
	LDIR

	LD	A,(FCB1+9)
	CP	$20
	JR	NZ,EXT1
	LD	HL,"A"*256+"M"
	LD	(FCB1+9),HL
	LD	A,"G"
	LD	(FCB1+11),A
EXT1:
	CALL	SWITCH
	CP	$21
	JR	C,LOAD
	CP	"C"
	JR	NZ,USAGE
	LD	(CLSSW+1),A
LOAD:
	LD	DE,FILE
	LD	C,$1A
	CALL	SYSTEM

	LD	DE,FCB1
	LD	C,$11
	CALL	SYSTEM
	OR	A
	JP	NZ,0

	LD	HL,(1)
	LD	(BOOT+1),HL
	LD	HL,END
	LD	(1),HL

	CALL	SETCRTC
WILD:
	LD	DE,FILE
	LD	C,$0F
	CALL	SYSTEM
	OR	A
	JP	NZ,END

	LD	HL,0
	LD	(FILE+33),HL
	LD	(FILE+35),HL
	INC	L
	LD	(FILE+14),HL

	LD	HL,FLAGBF
	LD	DE,FLAGBF+1
	LD	BC,16*512+256-1
	LD	(HL),0
	LDIR
	LD	A,1
	LD	(FLAGAX+1),A
	LD	HL,0
	LD	(ZURI+1),HL

	LD	HL,LINEBF
	LD	(LINEX+1),HL

	LD	HL,$FFFF
	LD	DE,$FFFF
	LD	BC,0
	EXX

	LD	HL,HEADBF
	LD	BC,8
	CALL	READ
	LD	HL,HEADBF
	LD	DE,MAGID
	LD	B,8
	CALL	CPSTR
	JP	NZ,NEXT

CLSSW:	LD	A,$00
	OR	A
	CALL	NZ,CLS

	LD	BC,$1800
	LD	A,12
	OUT	(C),A
	INC	C
	DB	$ED,$71
	DEC	C
	INC	A
	OUT	(C),A
	INC	C
	DB	$ED,$71
	LD	HL,8
COMMENT:
	INC	HL
	CALL	FGETC
	CP	$1A
	JR	Z,HEADER
	LD	E,A
	LD	C,2
	CALL	SYSTEM
	JR	COMMENT
HEADER:
	LD	(ADDHD+1),HL
	LD	E,$0D
	LD	C,2
	CALL	SYSTEM
	LD	E,$0A
	LD	C,2
	CALL	SYSTEM

	CALL	FGETC
	OR	A
	JP	NZ,NEXT
	LD	HL,HEADBF
	LD	(HL),A
	INC	HL
	LD	BC,31+48	;ﾍｯﾀﾞ ﾊﾟﾚｯﾄ
	CALL	READ

	LD	A,(HEADBF+3)	;ｽｸﾘｰﾝ･ﾓｰﾄﾞ
	BIT	7,A		;256ｼｮｸ
	JP	NZ,NEXT
	AND	1		;200ﾗｲﾝ
	LD	(L200+1),A

	PUSH	HL
	LD	HL,(HEADBF+16)
	LD	BC,(HEADBF+12)
	AND	A
	SBC	HL,BC
	LD	B,H
	LD	C,L
	POP	HL
	CALL	READ		;フラグA
				;フラグB
	LD	HL,FILE
	LD	DE,FILE2
	LD	BC,37
	LDIR

	LD	A,(HEADBF+26)
	LD	HL,(HEADBF+24)
ADDHD:	LD	DE,0
	ADD	HL,DE
	ADC	A,0
	LD	(FILE2+33),HL
	LD	(FILE2+35),A

	LD	HL,(HEADBF+8)
	LD	BC,(HEADBF+4)
	AND	A
	SBC	HL,BC
	LD	BC,8
	ADD	HL,BC
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	A,H
	LD	(XSIZE+1),A

	LD	HL,(HEADBF+10)
	LD	BC,(HEADBF+6)
	AND	A
	SBC	HL,BC
	INC	HL
	LD	(YSIZE+1),HL
				;ﾊﾟﾚｯﾄ 8bit->4bit
	LD	E,0
PALI1:
	LD	A,E
	AND	$F0
	RRCA
	RRCA
	RRCA
	LD	B,A
	RRCA
	ADD	A,B
	LD	C,A
	LD	B,0
	LD	HL,PALBF
	ADD	HL,BC

	LD	B,3
	LD	D,APALET/256
PALI2:
	LD	A,(HL)
	INC	HL
	LD	(DE),A
	INC	D
	DJNZ	PALI2

	LD	A,E
	AND	$0F
	LD	B,A
	ADD	A,A
	ADD	A,B	;*3

	LD	C,A
	LD	B,0
	LD	HL,PALBF
	ADD	HL,BC
	EX	DE,HL
	LD	B,3
	LD	H,APALET/256
PALI3:
	LD	A,(DE)
	INC	DE
	CALL	XADD1
	LD	(HL),A
	INC	H
	DJNZ	PALI3

	EX	DE,HL
	INC	E
	JR	NZ,PALI1
;
	LD	IX,(HEADBF+12)	;フラグA
	LD	DE,HEADBF
	ADD	IX,DE

	LD	B,25
LOOP1:
	PUSH	BC

	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	CP	$1B
	JP	Z,NEXT
	CP	3
	JP	Z,END
	CP	$13
	CALL	Z,WAIT1

ZURI:	LD	HL,0
	LD	A,H
	OR	L
	JR	Z,ZURI1

	LD	BC,$1800
	LD	A,12
	OUT	(C),A
	INC	C
	OUT	(C),H
	DEC	C
	INC	A
	OUT	(C),A
	INC	C
	OUT	(C),L
	DEC	C
	LD	A,6
	OUT	(C),A
	INC	C
	LD	A,24
	OUT	(C),A
	LD	BC,WIDTH
	ADD	HL,BC
	LD	(ZURI+1),HL
ZURI1:
	LD	B,8
LOOP2:
	PUSH	BC
	EXX
	PUSH	BC
	EXX

	CALL	MAG
	LD	HL,(LINEX+1)
	PUSH	HL
L200:	LD	A,$00
	OR	A
	CALL	Z,MAG

;			ピクセル 表示

	EX	(SP),IX
	LD	IY,GBUF
	LD	HL,(LINEX+1)

	LD	B,WIDTH
ANA1:
	PUSH	BC
	LD	B,8
ANA2:
	LD	DE,0
	LD	C,D

				;C=GREEN D=RED E=BLUE
	PUSH	HL
	LD	L,(HL)
	LD	H,APALET/256

	LD	C,(HL)
	INC	H
	LD	D,(HL)
	INC	H
	LD	A,(HL)

	LD	L,(IX+0)
	INC	IX

	ADD	A,(HL)
	PUSH	HL
	LD	L,A
	LD	H,CTABLE/256
	LD	A,(HL)
	POP	HL

	LD	E,A
	DEC	H
	LD	A,D
	ADD	A,(HL)
	PUSH	HL
	LD	L,A
	LD	H,CTABLE/256
	LD	A,(HL)
	POP	HL

	LD	D,A
	DEC	H
	LD	A,C
	ADD	A,(HL)
	PUSH	HL
	LD	L,A
	LD	H,CTABLE/256
	LD	A,(HL)
	POP	HL

	LD	C,A
	POP	HL
	INC	HL

DG:	LD	A,$00
	ADD	A,C
	LD	C,A
	JR	NC,DG1
	LD	C,$FF
DG1:
	LD	A,(DG+1)
	ADD	A,D
	LD	D,A
	JR	NC,DR1
	LD	D,$FF
DR1:
	LD	A,(DG+1)
	ADD	A,E
	LD	E,A
	JR	NC,DB1
	LD	E,$FF
DB1:
	LD	A,(DG+1)
	XOR	DIZ1
	LD	(DG+1),A

	RL	C
	RL	(IY+0)
	RL	C
	RL	(IY+1)
	RL	C
	RL	(IY+2)
	RL	C
	RL	(IY+3)

	RL	D
	RL	(IY+4)
	RL	D
	RL	(IY+5)
	RL	D
	RL	(IY+6)
	RL	D
	RL	(IY+7)

	RL	E
	RL	(IY+8)
	RL	E
	RL	(IY+9)
	RL	E
	RL	(IY+10)
	RL	E
	RL	(IY+11)

	DEC	B
	JP	NZ,ANA2

SMC01:	LD	A,($ECBF)
	RES	4,A
	LD	BC,$1FD0
	OUT	(C),A
SMC02:	LD	($ECBF),A

	EXX
	RES	2,B
	CALL	PSET1
	SET	2,B
	CALL	PSET1
	EXX

SMC03:	LD	A,($ECBF)
	SET	4,A
	LD	BC,$1FD0
	OUT	(C),A
SMC04:	LD	($ECBF),A

	EXX
	RES	2,B
	CALL	PSET1
	SET	2,B
	CALL	PSET1
	RES	2,B
	INC	BC
	EXX

	POP	BC
	DEC	B
	JP	NZ,ANA1

	POP	IX

	LD	A,(DG+1)
	XOR	DIZ2
	LD	(DG+1),A

	EXX
	POP	BC
	LD	A,B
	ADD	A,8
	LD	B,A
	EXX
	POP	BC
	DEC	B
	JP	NZ,LOOP2

	LD	HL,(YSIZE+1)
	LD	A,H
	OR	L
	JR	Z,WAIT

	EXX
	PUSH	HL
	LD	HL,WIDTH
	ADD	HL,BC
	LD	B,H
	LD	C,L
	POP	HL
	EXX
	POP	BC
	DEC	B
	JP	NZ,LOOP1

	LD	HL,(YSIZE+1)
	LD	A,H
	OR	L
	JR	Z,WAIT

	CALL	WAIT1
	LD	HL,(ZURI+1)
	LD	BC,WIDTH
	ADD	HL,BC
	LD	(ZURI+1),HL

	LD	B,25
	JP	LOOP1

WAIT:
	DB	$3E
NEXT:
	XOR	A
	OR	A
	EX	AF,AF'
	LD	SP,(SYSTEM+1)

	LD	DE,FILE
	LD	C,$1A
	CALL	SYSTEM

	LD	C,$12
	CALL	SYSTEM
	OR	A
	EX	AF,AF'
	CALL	NZ,WAIT1
	CALL	WAIT2
	EX	AF,AF'
	JP	Z,WILD

END:
	CALL	MOTOFF
BOOT:	LD	HL,0
	LD	(1),HL

B1FD0:	LD	A,$00
SMC05:	LD	($ECBF),A

	LD	D,7
	LD	BC,$1FB9
	LD	HL,TEXTDT
TEXT1:
	INC	B
	OUTI
	INC	C
	DEC	D
	JR	NZ,TEXT1
	LD	BC,$1FC0
	DB	$ED,$71
	LD	C,$B0
	DB	$ED,$71

	LD	B,$10
	DB	$ED,$71
	INC	B
	DB	$ED,$71
	INC	B
	DB	$ED,$71
	INC	B
	DB	$ED,$71
	RST	0

WAIT1:
	CALL	MOTOFF

	LD	BC,$1800
	LD	A,6
	OUT	(C),A
	INC	C
	LD	A,25
	OUT	(C),A

	CALL	WAIT2
WAIT3:
	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	OR	A
	JR	Z,WAIT3
	CP	3
	JR	Z,END
	RET

WAIT2:
	LD	E,$FF
	LD	C,6
	CALL	SYSTEM
	OR	A
	JR	NZ,WAIT2

SMC06:	LD	A,($EC92)	;KEYD
	OR	A
	JR	NZ,WAIT2
	RET

PSET1:
	SET	7,B
	SET	6,B
	LD	A,(IY+0)	;G
	OUT	(C),A
	RES	6,B
	LD	A,(IY+4)	;R
	OUT	(C),A
	RES	7,B
	SET	6,B
	LD	A,(IY+8)	;B
	OUT	(C),A
	INC	IY
	RET

;				フラグ&ピクセル展開
MAG:
	LD	A,(LINEX+2)
	SUB	LINEBF/256-2
	AND	$1F
	ADD	A,LINEBF/256
	LD	(LINEX+2),A

LINEX:	LD	DE,LINEBF
XSIZE:	LD	B,80
YSIZE:	LD	HL,0

	LD	A,H
	OR	L
	JR	Z,CLEAR
	DEC	HL
	LD	(YSIZE+1),HL

	LD	HL,FLAGBF
FLAGA1:
	RLC	(IX+0)
	JR	C,FLAGA3
	XOR	A
	JR	FLAGA4
FLAGA3:
	CALL	FGETC
FLAGA4:
	XOR	(HL)
	LD	(HL),A
	CP	$11
	JR	NZ,FLAGA5
	PUSH	BC
	PUSH	HL
	LD	H,D
	LD	L,E
	DEC	HL
	DEC	HL
	LDI
	LDI
	LDI
	LDI
	POP	HL
	POP	BC
	JR	FLAGA6
FLAGA5:
	RRCA
	RRCA
	RRCA
	CALL	PIC
	LD	A,(HL)
	ADD	A,A
	CALL	PIC
FLAGA6:
	INC	HL
FLAGAX:	LD	A,$01
	RLCA
	LD	(FLAGAX+1),A
	JR	NC,FLAGA2
	INC	IX
FLAGA2:
	DJNZ	FLAGA1
	RET

CLEAR:
	LD	C,4
CLEAR1:
	LD	(DE),A
	INC	DE
	DJNZ	CLEAR1
	DEC	C
	JR	NZ,CLEAR1
	RET

PIC:
	AND	$1E
	JR	NZ,PICT

	CALL	FGETP
	LD	(DE),A
	INC	DE
	CALL	FGETP
	LD	(DE),A
	INC	DE
	RET
PICT:
	PUSH	BC
	PUSH	HL
	LD	C,A
	LD	B,0
	LD	HL,TABLE
	ADD	HL,BC

	LD	A,E
	SUB	(HL)		;1
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	A,D
	SBC	A,LINEBF/256	;2
	SUB	H
	AND	$1F
	ADD	A,LINEBF/256
	LD	H,A

	LDI
	LDI
	POP	HL
	POP	BC
	RET

READ:
	PUSH	HL
	ADD	HL,BC
	JP	C,END
	LD	DE,(SYSTEM+1)
	DEC	D
	SBC	HL,DE
	JP	NC,NEXT
	POP	HL
READ1:
	LD	A,B
	OR	C
	RET	Z
	CALL	FGETC
	LD	(HL),A
	INC	HL
	DEC	BC
	JR	READ1

FGETC:
	EXX
	LD	A,H
	CP	DTBUFE/$100
	JR	NC,FGETC2
FGETC1:
	LD	A,(HL)
	INC	HL
	EXX
	RET
FGETC2:
	PUSH	BC
	PUSH	DE
	LD	DE,DTBUF
	LD	C,$1A
	CALL	SYSTEM
	LD	DE,FILE
	LD	HL,BUFSIZ
	LD	C,$27
	CALL	SYSTEM
	ADD	A,$FE
	JP	C,NEXT

SMC07:	LD	A,($EC88)
	PUSH	IX
SMC08:	CALL	$ECDC
	LD	A,(IX+$12)
	POP	IX
	AND	$7F
	CP	7
	JR	NZ,FGETC3
SMC09:	LD	A,($EC84)
	OR	$80
	LD	BC,$FFC
	OUT	(C),A
FGETC3:
	LD	A,H
	OR	L
	JP	Z,WAIT
	LD	HL,DTBUF
	POP	DE
	POP	BC
	JR	FGETC1

FGETP:
	EXX
	LD	A,D
	CP	PDBUFE/$100
	JR	NC,FGETP2
FGETP1:
	LD	A,(DE)
	INC	DE
	EXX
	RET
FGETP2:
	PUSH	BC
	PUSH	HL
	LD	DE,PDBUF
	LD	C,$1A
	CALL	SYSTEM
	LD	DE,FILE2
	LD	HL,PDSIZE
	LD	C,$27
	CALL	SYSTEM
	ADD	A,$FE
	JP	C,NEXT

SMC10:	LD	A,($EC88)
	PUSH	IX
SMC11:	CALL	$ECDC
	LD	A,(IX+$12)
	POP	IX
	AND	$7F
	CP	7
	JR	NZ,FGETP3
SMC12:	LD	A,($EC84)
	OR	$80
	LD	BC,$FFC
	OUT	(C),A
FGETP3:
	LD	A,H
	OR	L
	JP	Z,WAIT
	LD	DE,PDBUF
	POP	HL
	POP	BC
	JR	FGETP1

MOTOFF:
SMC13:	LD	A,($EC84)
	AND	$7F
	LD	BC,$FFC
	OUT	(C),A
	RET

CPSTR:
	LD	A,(DE)
	INC	DE
	CP	(HL)
	INC	HL
	RET	NZ
	DJNZ	CPSTR
	RET

CLS:
SMC14:	LD	A,($ECBF)
	RES	4,A
	CALL	CLS0
SMC15:	LD	A,($ECBF)
	SET	4,A
CLS0:
	LD	BC,$1FD0
	OUT	(C),A
SMC16:	LD	($ECBF),A
	XOR	A
	LD	C,A
	LD	HL,$4003
CLS1:
	LD	B,H
CLS2:
	DB	$ED,$71
	INC	B
	JR	NZ,CLS2
;
	ADD	A,L
	LD	C,A
	JR	NZ,CLS1
	RET

SWITCH:
	LD	DE,DTA+1
SWITCH1:
	LD	A,(DE)
	CP	$20
	RET	C
	INC	DE
	CP	"/"
	JR	NZ,SWITCH1
	LD	A,(DE)
	INC	DE
CAP:
	CP	"a"
	RET	C
	CP	"z"+1
	RET	NC
	SUB	$20
	RET

SETCRTC:
	LD	BC,$1FB0
	LD	A,$80
	OUT	(C),A
SMC17:	LD	A,($ECBF)
	LD	(B1FD0+1),A

	LD	HL,C4025L
	LD	BC,$1800
	XOR	A
SETCRTL:
	LD	D,(HL)
	OUT	(C),A
	INC	C
	OUT	(C),D
	DEC	C
	INC	HL
	INC	A
	CP	14
	JR	NZ,SETCRTL
	LD	A,(HL)
	LD	BC,$1A03
	OUT	(C),A
	INC	HL
	LD	A,(HL)
	LD	BC,$1FD0
	OUT	(C),A
SMC18:	LD	($ECBF),A
	LD	C,$C0
	LD	A,$11
	OUT	(C),A

	LD	D,7
	LD	BC,$1FB9
TEXT0:
	DB	$ED,$71
	INC	C
	DEC	D
	JR	NZ,TEXT0
	RET

XADD1:
	PUSH	DE
	PUSH	HL
	CPL
	LD	E,A

	LD	A,(HL)
	CPL
	LD	L,A
	LD	H,CTABLE/256	;CTABLE/256
	LD	A,(HL)
	CPL
	LD	L,E
	LD	E,A
	LD	A,(HL)
	CPL
	ADD	A,E
	RRA
	SRL	A
	POP	HL
	POP	DE
	RET

TITLE:
	DB	"X1 analog MAG loader v1.01",$0D,$0A
	DB	"$"
MUSAGE:
	DB	"usage: MAGA ﾌｧｲﾙﾈｰﾑ[.MAG][ｽｲｯﾁ]",$0D,$0A
	DB	9,"/C ｽｸﾘｰﾝ･ｸﾘｱ",$0D,$0A
	DB	"$"
COMP:
	DB	" Ok",$0D,$0A,"$"

MAGID:
	DB	"MAKI02  "

TABLE:
	DB	0  ,0		; 0
	DB	1*2,0		; 1
	DB	2*2,0		; 2
	DB	4*2,0		; 3
	DB	0  ,1*2		; 4
	DB	1*2,1*2		; 5
	DB	0  ,2*2		; 6
	DB	1*2,2*2		; 7
	DB	2*2,2*2		; 8
	DB	0  ,4*2		; 9
	DB	1*2,4*2		;10
	DB	2*2,4*2		;11
	DB	0  ,8*2		;12
	DB	1*2,8*2		;13
	DB	2*2,8*2		;14
	DB	0  ,0		;15

CTABLE1:
	DB	$00,$01,$03,$05,$07,$09,$0B,$0D
	DB	$0F,$11,$13,$15,$16,$18,$1A,$1C
	DB	$1E,$1F,$21,$23,$25,$26,$28,$2A
	DB	$2B,$2D,$2F,$30,$32,$34,$35,$37
	DB	$38,$3A,$3C,$3D,$3F,$40,$42,$43
	DB	$45,$46,$48,$49,$4B,$4C,$4D,$4F
	DB	$50,$52,$53,$55,$56,$57,$59,$5A
	DB	$5B,$5D,$5E,$5F,$61,$62,$63,$65
	DB	$66,$67,$68,$6A,$6B,$6C,$6D,$6F
	DB	$70,$71,$72,$74,$75,$76,$77,$78
	DB	$79,$7B,$7C,$7D,$7E,$7F,$80,$81
	DB	$82,$84,$85,$86,$87,$88,$89,$8A
	DB	$8B,$8C,$8D,$8E,$8F,$90,$91,$92
	DB	$93,$94,$95,$96,$97,$98,$99,$9A
	DB	$9B,$9C,$9D,$9E,$9F,$A0,$A1,$A2
	DB	$A3,$A4,$A5,$A6,$A7,$A7,$A8,$A9
	DB	$AA,$AB,$AC,$AD,$AE,$AF,$AF,$B0
	DB	$B1,$B2,$B3,$B4,$B5,$B5,$B6,$B7
	DB	$B8,$B9,$B9,$BA,$BB,$BC,$BD,$BD
	DB	$BE,$BF,$C0,$C1,$C1,$C2,$C3,$C4
	DB	$C4,$C5,$C6,$C7,$C7,$C8,$C9,$CA
	DB	$CA,$CB,$CC,$CD,$CD,$CE,$CF,$CF
	DB	$D0,$D1,$D1,$D2,$D3,$D4,$D4,$D5
	DB	$D6,$D6,$D7,$D8,$D8,$D9,$DA,$DA
	DB	$DB,$DC,$DC,$DD,$DE,$DE,$DF,$DF
	DB	$E0,$E1,$E1,$E2,$E3,$E3,$E4,$E4
	DB	$E5,$E6,$E6,$E7,$E7,$E8,$E9,$E9
	DB	$EA,$EA,$EB,$EC,$EC,$ED,$ED,$EE
	DB	$EE,$EF,$F0,$F0,$F1,$F1,$F2,$F2
	DB	$F3,$F3,$F4,$F5,$F5,$F6,$F6,$F7
	DB	$F7,$F8,$F8,$F9,$F9,$FA,$FA,$FB
	DB	$FB,$FC,$FC,$FD,$FD,$FE,$FE,$FF

C4025L:
	DB	$37,$28,$2D,$34,$1F,$02,$19,$1C
	DB	$00,$07,$00,$00,$00,$00,$0D,$A0

TEXTDT:	DB	$03,$0C,$0F,$30,$33,$3C,$3F

FILE:
	DS	37,0
FILE2:
	DS	37,0

GBUF:
G3:	DB	0
G2:	DB	0
G1:	DB	0
G0:	DB	0
R3:	DB	0
R2:	DB	0
R1:	DB	0
R0:	DB	0
B3:	DB	0
B2:	DB	0
B1:	DB	0
B0:	DB	0

