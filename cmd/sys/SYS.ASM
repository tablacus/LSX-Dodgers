;	SYS	by Gaku

SYSTEM	EQU	0005H
FCB1	EQU	005CH

CR	EQU	0DH
LF	EQU	0AH

GETDPB	EQU	0ECDCH

DTBUF	EQU	0000H
FATBF	EQU	0D000H
IPL	EQU	0FE00H

	ORG	0100H
	JP	START
TITLE:
	DB	"X1 systemcopy v1.01"
	DB	CR,LF,"$"
USAGE:
	DB	"usage:"
	DB	" SYS ﾄﾞﾗｲﾌﾞ:",CR,LF,"$"
OK:
	DB	" Ok",CR,LF,"$"
ERROR:
	DB	" Error",CR,LF,"$"
SKIP:
	DB	" Skip",CR,LF,"$"
LSX:
	DB	"LD      .BIN$"
WIPL:
	DB	"Write IPL $"
M2D:
	DB	"2D$"
M2HD:
	DB	"2HD$"
M2HS:
	DB	"2HS$"
M2HC:
	DB	"2HC$"
MEMM:
	DB	"EMM$"

START:
	LD	SP,(SYSTEM+1)

	LD	DE,TITLE
	LD	C,9
	CALL	SYSTEM

	LD	DE,DATA
	LD	C,1AH
	CALL	SYSTEM

	LD	A,(FCB1)
	OR	A
	JR	NZ,INIT

	LD	DE,USAGE
	LD	C,9
	CALL	SYSTEM
	JP	0
INIT:
	LD	(FCB3),A

	LD	DE,LSX
	LD	C,9
	CALL	SYSTEM

	LD	DE,FCB2
	LD	C,0FH
	CALL	SYSTEM
	OR	A
	JR	NZ,CCHECK

	LD	HL,(FCB2+14)
	LD	(SDIR),HL
	LD	HL,1
	LD	(FCB2+14),HL
	LD	(FCB3+14),HL

	LD	HL,(SYSTEM+1)
	DEC	H
	DEC	H
	LD	DE,DATA
	SBC	HL,DE
	LD	DE,FCB2
	LD	C,27H
	CALL	SYSTEM
	DEC	A
	JR	NZ,CCHECK
	LD	(SIZE),HL

	LD	DE,FCB2
	LD	C,10H
	CALL	SYSTEM
	OR	A
	JR	NZ,CCHECK

	LD	A,(FCB2)
	LD	HL,FCB3
	CP	(HL)
	JR	NZ,CREATE

	LD	DE,SKIP
	LD	C,9
	CALL	SYSTEM
	JR	WTIPL

CREATE:
	LD	DE,FCB3
	LD	C,16H
	CALL	SYSTEM
	OR	A
	JR	NZ,CCHECK

	LD	HL,1
	LD	(FCB3+14),HL

	LD	DE,FCB3
	LD	HL,(SIZE)
	LD	C,26H
	CALL	SYSTEM
	OR	A
	JR	NZ,CCHECK

	LD	HL,FCB2+20
	LD	DE,FCB3+20
	LD	BC,4
	LDIR

	LD	DE,FCB3
	LD	C,10H
	CALL	SYSTEM

CCHECK:
	CALL	CHECK
WTIPL:
	LD	A,(FCB3)
	LD	E,A
	LD	C,1BH
	CALL	SYSTEM
	INC	A
	JP	Z,0

	LD	HL,354
	AND	A
	SBC	HL,DE
	JR	Z,W2D

	LD	HL,1221
	AND	A
	SBC	HL,DE
	JP	Z,W2HD

	LD	HL,1430
	SCF
	SBC	HL,DE
	JR	NC,WTIPL1

	LD	A,(IY+0)
	CP	0FBH
	JP	Z,W2HS
WTIPL1:
	LD	HL,2371
	AND	A
	SBC	HL,DE
	JP	Z,W2HC

	LD	HL,(3)
	LD	A,H
	CP	$1D
	JP	NZ,0

	LD	A,(FCB3)
	DEC	A
	CALL	GETDPB
	JP	C,0
	LD	A,(IX+12H)
	CP	6
	JP	Z,WEMM

	JP	0

W2D:
	LD	DE,WIPL
	LD	C,9
	CALL	SYSTEM
	LD	DE,M2D
	LD	C,9
	CALL	SYSTEM

	LD	HL,DATA
	LD	DE,DATA+1
	LD	BC,1023
	LD	(HL),0
	LDIR

	LD	HL,LIPL
	LD	DE,DATA
	LD	BC,30H
	LDIR

	LD	HL,MAIN
	LD	BC,RDDLE-MAIN
	LDIR

WJUMP:
	LD	HL,JUMP
	LD	DE,DATA+01E0H
	LD	BC,32
	LDIR

	CALL	EAREA

	LD	DE,0
	LD	HL,(FCB3)
	DEC	L
	LD	H,1
	LD	C,30H
	CALL	SYSTEM

	CALL	CHECK
	JP	0
CHECK:
	OR	A
	LD	DE,OK
	JR	Z,CHECK2
DISERR:
	LD	DE,ERROR
CHECK2:
	LD	C,9
	CALL	SYSTEM
	RET

W2HD:
	LD	DE,WIPL
	LD	C,9
	CALL	SYSTEM
	LD	DE,M2HD
	LD	C,9
	CALL	SYSTEM

	LD	HL,DATA
	LD	DE,DATA+1
	LD	BC,1023
	LD	(HL),0
	LDIR

	LD	HL,HIPL
	LD	DE,DATA
	LD	BC,30H
	LDIR
WMAIN:
	LD	HL,MAIN
	LD	BC,MAINE-MAIN
	LDIR

	LD	HL,RDDH
	LD	BC,RDDHE-RDDH
	LDIR

	JR	WJUMP

W2HS:
	LD	DE,WIPL
	LD	C,9
	CALL	SYSTEM
	LD	DE,M2HS
	LD	C,9
	CALL	SYSTEM

	LD	HL,DATA
	LD	DE,DATA+1
	LD	BC,1023
	LD	(HL),0
	LDIR

	LD	HL,SIPL
	LD	DE,DATA
	LD	BC,30H
	LDIR
	JR	WMAIN

W2HC:
	LD	DE,WIPL
	LD	C,9
	CALL	SYSTEM
	LD	DE,M2HC
	LD	C,9
	CALL	SYSTEM

	LD	HL,DATA
	LD	DE,DATA+1
	LD	BC,1023
	LD	(HL),0
	LDIR

	LD	HL,CIPL
	LD	DE,DATA
	LD	BC,30H
	LDIR
	JR	WMAIN

WEMM:
	LD	DE,WIPL
	LD	C,9
	CALL	SYSTEM
	LD	DE,MEMM
	LD	C,9
	CALL	SYSTEM

	LD	HL,DATA
	LD	DE,DATA+1
	LD	BC,1023
	LD	(HL),0
	LDIR

	LD	HL,EIPL
	LD	DE,DATA
	LD	BC,30H
	LDIR

	LD	HL,MAIN
	LD	BC,MAINE-MAIN
	LDIR

	LD	HL,RDDE
	LD	BC,RDDEE-RDDE
	LDIR

	CALL	EAREA

	XOR	A
	LD	BC,0D00H
	OUT	(C),A
	INC	C
	OUT	(C),A
	INC	C
	OUT	(C),A
	INC	C

	LD	HL,DATA
EMML:
	INC	B
	OUTI
	INC	B
	OUTI
	DEC	A
	JR	NZ,EMML

	LD	DE,OK
	LD	C,9
	CALL	SYSTEM
	JP	0

EAREA:
	LD	HL,(3)
	LD	A,H
	CP	$1D
	RET	NZ
	LD	A,4
	CALL	GETDPB
	RET	C
	LD	A,(IX+12H)
	CP	6
	RET	NZ

	LD	A,(IX+0CH)
	LD	(DATA+27H),A
	RET

MAIN:
	ORG	IPL+30H,MAIN-100H
FNAME:
	DB	"LD",20H,20H,20H,20H,20H,20H
	DB	"BIN"

SA:
	DI
	LD	SP,0
	LD	IX,IPL
	LD	A,1EH
	OUT	(0),A
	LD	BC,3000H+36
	IN	A,(C)
	AND	3
	LD	(DRIVE+1),A
	CALL	FILE
	JR	C,IPLERR
	LD	HL,(DTBUF+3)
	LD	DE,(DTBUF+1)
	SBC	HL,DE
	LD	B,H
	LD	C,L
	INC	BC
	LD	HL,DTBUF+7
	LDIR
	LD	A,(DRIVE+1)
	LD	(0FF87H),A
	LD	A,(IX+27H)
	LD	(0FF8CH),A

	LD	BC,1A03H
	LD	A,82H
	OUT	(C),A
	IM	2
	LD	HL,(0005H)
	JP	(HL)
IPLERR:
	LD	A,(DRIVE+1)
	LD	BC,0FFCH
	OUT	(C),A
	LD	A,1DH
	OUT	(0),A
	LD	A,7
	LD	(0FF86H),A
	JP	00F5H

FILE:
	LD	D,0
	LD	E,(IX+29H)	;FAT
	LD	HL,FATBF
	LD	B,(IX+2AH)	;FAT
	CALL	DRDSB
	RET	C

	LD	HL,DTBUF
	LD	D,0
	LD	E,(IX+2BH)	;ディレクトリ
	PUSH	HL
	LD	B,(IX+2CH)	;ディレクトリ
	CALL	DRDSB
	POP	HL
	RET	C

	LD	C,(IX+2DH)	;ディレクトリ
FILE1:
	LD	B,11
	LD	DE,FNAME
	PUSH	HL
COMP:
	LD	A,(DE)
	CP	(HL)
	JR	NZ,COMP1
	INC	DE
	INC	HL
	DJNZ	COMP
COMP1:
	POP	HL
	JR	Z,FILE2
	LD	DE,20H
	ADD	HL,DE
	DEC	C
	JR	NZ,FILE1
	SCF
	RET

FILE2:
	LD	DE,1AH
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)

	LD	HL,DTBUF
RDD1:
	PUSH	DE
	CALL	DRDC
	POP	DE
	RET	C
	PUSH	HL
	CALL	GNCL
	POP	HL
	LD	A,D
	CP	0FH
	JR	C,RDD1
	RET

GNCL:
	LD	L,E		;GET NEXT CLUSTER
	LD	H,D
	SRL	H
	RR	L
	LD	BC,FATBF
	JR	C,GNC1
	ADD	HL,DE
	ADD	HL,BC
	LD	E,(HL)
	INC	HL
	LD	A,(HL)
	AND	0FH
	LD	D,A
	RET
GNC1:
	ADD	HL,DE
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	D,(HL)
	LD	B,4
GNC1L:
	SRL	D
	RRA
	DJNZ	GNC1L

	LD	E,A
	AND	A
	RET

DRDSB:
	PUSH	BC
	PUSH	DE
	CALL	DRD
	POP	DE
	POP	BC
	RET	C
	INC	DE
	DJNZ	DRDSB
	RET
SEEK:
	LD	B,16
	LD	C,(IX+28H)		;MAX SECTOR
	XOR	A
	EX	DE,HL
DIV1:
	ADD	HL,HL
	ADC	A,A
	CP	C
	JR	C,DIV2
	SUB	C
	INC	L
DIV2:
	DJNZ	DIV1

	ADD	A,(IX+2FH)
	LD	E,A		;余り

	LD	D,L		;商
	SRL	D
	SBC	A,A
	AND	10H
DRIVE:	OR	00H
	LD	BC,0FFCH
	OR	80H		;モーターオン
	OUT	(C),A
	CALL	BUSY
	RET	C

	LD	C,0FBH
	OUT	(C),D
	LD	A,D
	ADD	A,0FFH
	SBC	A,A
	AND	18H		;シーク/リストア
	LD	C,0F8H
	OUT	(C),A

BUSY:
	LD	A,18
BUSY1:
	DEC	A
	JR	NZ,BUSY1
	LD	C,0F8H
	LD	H,C
BUSY2:
	IN	A,(C)
	AND	81H
	RET	Z
	EX	(SP),HL
	EX	(SP),HL
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,BUSY2
	SCF
	RET

DRD:
DRDC	EQU	DRD+2
	ORG	$$+0100H	;$DEPHASE
MAINE:

RDDL:
	ORG	DRD,RDDL-100H
__RDDL:
	JR	DRDL
DRDCL:
	EX	DE,HL
	LD	A,(IX+2EH)	;DE=DE+8 (2D)  !ADDCL 自己書き換え
	ADD	HL,HL		;DE=DE*2 (512) !DBLCL 自己書き換え
	EX	DE,HL
	ADD	A,E
	LD	E,A
	ADC	A,D
	SUB	E
	LD	D,A

	PUSH	DE
	CALL	DRDL
	POP	DE
	RET	C
	INC	DE
DRDL:
	PUSH	HL
	CALL	SEEK		;ディスクリード
	POP	HL
	RET	C
	LD	A,80H
	LD	BC,0FFAH
	OUT	(C),E
	LD	C,0F8H
	OUT	(C),A
	LD	A,18
DELAY:
	DEC	A
	JR	NZ,DELAY

	LD	C,0FBH
DRD1:
	LD	A,B
	IN	A,(0F8H)
	RRCA
	JR	NC,DRD3
	RRCA
	JR	NC,DRD1
	INI
	INC	B
	JR	DRD1

DRD3:
	ADD	A,0FFH
	RET
	ORG	$$+0100H	;$DEPHASE
RDDLE:

RDDH:
	ORG	DRD,RDDH-100H
__DRDCH:
	JR	DRDH
DRDCH:
	LD	A,(IX+2EH)
	ADD	A,E
	LD	E,A
	ADC	A,D
	SUB	E
	LD	D,A
DRDH:
	LD	(DMAD+11),HL
	CALL	SEEK
	RET	C

	LD	A,15
	LD	HL,DMAD

	LD	BC,$1F87
SDMA:
	INC	B
	OUTI
	DEC	A
	JR	NZ,SDMA

	LD	A,80H
	PUSH	BC
	LD	BC,0FFAH
	OUT	(C),E
	LD	C,0F8H
	OUT	(C),A

	CALL	BUSY
	IN	A,(C)

	LD	HL,(DMAD+11)
	INC	HL
	LD	DE,$BB06	;READ DMA

	POP	BC
	OUT	(C),D
	OUT	(C),E
	IN	E,(C)
	IN	D,(C)
	ADD	HL,DE
	ADD	A,0FFH
	RET

DMAD:
	DB	0C3H,7DH,0FBH,0FH,0FFH,0FFH,2CH,10H	;X1turbo
	DB	80H,92H,8DH,00H,00H,0CFH,87H		;DISK DMA
	ORG	$$+0100H	;$DEPHASE
RDDHE:

RDDE:
	ORG	DRD,RDDE-100H
	JR	DRDE
DRDCE:
	LD	A,4
	LD	(DRIVE+1),A
	LD	A,(IX+2EH)
	ADD	A,E
	LD	E,A
	ADC	A,D
	SUB	E
	LD	D,A
DRDE:
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	B,(IX+20H)
	LD	C,0
	ADD	HL,BC
	LD	B,0DH
	OUT	(C),C
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H
	INC	C
	EX	DE,HL
	XOR	A
EDRDC1:
	INI
	INC	B
	INI
	INC	B
	INI
	INC	B
	INI
	INC	B
	DEC	A
	JR	NZ,EDRDC1
	XOR	A
	RET
ESA:
	LD	HL,0FC00H
	LD	DE,IPL
	LD	BC,ESA-IPL
	LDIR
	JP	SA
EED:
	ORG	$$+0100H
RDDEE:

LIPL:
	DB	0EBH,0FEH,90H,"LD",20H,20H,20H
	DB	20H,20H,20H,00H,02H,02H,01H,00H
	DB	02H,70H,00H,0D0H,02H,0FDH,02H,00H
	DB	09H,00H,02H,00H,00H,00H,18H,0FEH
	DB	0,0,0,0,0,0,0,0
	DB	9,1,2,5,7,112,8,1

HIPL:
	DB	0EBH,0FEH,90H,"LD",20H,20H,20H
	DB	20H,20H,20H,00H,04H,01H,01H,00H
	DB	02H,0C0H,00H,0D0H,04H,0FEH,02H,00H
	DB	08H,00H,02H,00H,00H,00H,18H,0FEH
	DB	0,0,0,0,0,0,0,0
	DB	8,1,2,5,6,192,9,1

SIPL:
	DB	0EBH,1EH,90H,"LD",20H,20H,20H
	DB	20H,20H,20H,"2","H","S",20H,20H
	DB	20H,20H,04H,00H,01H,03H,00H,01H
	DB	00H,0C0H,05H,0A0H,0FBH,01H,18H,0FEH
	DB	0EBH,0FEH,0,0,0,0,0,0
	DB	9,1,3,4,6,192,8,10

CIPL:
	DB	0EBH,0FEH,90H,"LD",20H,20H,20H
	DB	20H,20H,20H,00H,02H,01H,01H,00H
	DB	02H,0E0H,00H,060H,09H,0F9H,07H,00H
	DB	0FH,00H,02H,00H,00H,00H,18H,0FEH
	DB	0,0,0,0,0,0,0,0
	DB	15,1,7,15,14,224,27,1

EIPL:
	DB	01H,"LSX-Dodgers  Sy"
	DB	"s",20H,EED,01H,00H,0FCH,ESA,ESA/256-2
	DB	00H,00H,00H,00H,00H,00H,00H,00H
	DB	00H,00H,00H,00H,00H,00H,00H,00H
	DB	8,1,2,5,6,192,9,1

JUMP:
	DW	SA,SA,SA,SA,SA,SA,SA,SA
	DW	SA,SA,SA,SA,SA,SA,SA,SA

FCB2:
	DB	0,"LD      BIN"
	DS	25,0

FCB3:
	DB	0,"LD      BIN"
	DS	25,0

SDIR:
	DW	0
SIZE:
	DW	0
DATA:
