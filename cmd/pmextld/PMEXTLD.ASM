; pmextld
;
; PMEXT.COM にLSX-Dodgers用のタイムスタンプパッチをあてる

OSFLG	EQU	0003H
SYSTEM	EQU	0005H
BASE	EQU	000BH
JP_HL	EQU	000FH
BIOS	EQU	0018H
FCB1	EQU	005CH
DTA	EQU	0080H

	ORG	0100H

	LD	SP,(SYSTEM+1)
	LD	DE,TITLE
	LD	C,9		;STROUT 文字列出力 CP/M
	CALL	SYSTEM
	LD	DE,FCB
	LD	C,0FH		;FOPEN FCB ファイルオープン CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_NOTFOUND
	LD	DE,DTA
	LD	C,1AH		;SETDTA ディスク転送アドレスの設定 CP/M
	CALL	SYSTEM
	LD	DE,FCB
	LD	C,21H		;RDRND FCB ランダム読み出し CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	HL,PATCH_START
	LD	DE,DTA+10H
	LD	BC,PATCH_END-PATCH_START
	LDIR
	LD	DE,FCB
	LD	C,22H		;WRRND FCB ランダム書き込み CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	A,01H
	LD	(RANDOM_RECORD),A
	LD	DE,FCB
	LD	C,21H		;RDRND FCB ランダム読み出し CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	HL,OSNAME
	LD	DE,DTA+2
	LD	BC,OSNAME_END-OSNAME
	LDIR
	LD	DE,FCB
	LD	C,22H		;WRRND FCB ランダム書き込み CP/M
	CALL	SYSTEM
	OR	A
	JR	Z,FINISH
SHOW_ERROR:
	LD	DE,MSG_ERROR
	JR	SHOW_MESSAGE
SHOW_NOTFOUND:
	LD	DE,MSG_NOT_FOUND
SHOW_MESSAGE:
	LD	C,9		;STROUT 文字列出力 CP/M
	CALL	SYSTEM
FINISH:
	LD	DE,FCB
	LD	C,10H		;FCLOSE FCB ファイルクローズ CP/M
	CALL	SYSTEM
	JP	0
FCB:				;PMEXT.COMのFCB
	DB	0,"PMEXT   COM",0
	DW	0,0,0,0,0,0,0,0,0,0
RANDOM_RECORD:
	DW	0,0
PATCH_START:			;パッチ
	PUSH	DE
	EX	(SP),IX
	POP	DE
	LD	A,(HL)
	INC	HL
	LD	(IX+22),A	;(FCB)ファイルを最後に変更した時刻 2バイト
	LD	A,(HL)
	INC	HL
	LD	(IX+23),A
	LD	A,(HL)
	INC	HL
	LD	(IX+20),A	;(FCB)ファイルを最後に変更した日付 2バイト
	LD	A,(HL)
	LD	(IX+21),A
	LD	HL,(0509H)
	LD	(IX+16),L	;(FCB)ファイルサイズ 4バイト
	LD	(IX+17),H
	LD	HL,(050BH)
	LD	(IX+18),L
	LD	(IX+19),H
	PUSH	DE
	EX	(SP),IX
	POP	DE
	RET
PATCH_END:
OSNAME:
	DB	"LSX-Dodgers"
OSNAME_END:
TITLE:
	DB	"PMEXTLD v1.00 Lovers"
	DB	0DH,0AH
	DB	'$'
MSG_NOT_FOUND:
	DB	"Not found PMEXT.COM"
	DB	0DH,0AH
	DB	'$'
MSG_ERROR:
	DB	"Error!"
	DB	0DH,0AH
	DB	'$'
