; pmextld
;
; PMEXT.COM にLSX-Dodgers用のタイムスタンプパッチをあてる

SYSTEM	EQU	0005H
FCB1	EQU	005CH
DTA	EQU	0080H

	ORG	0100H

	LD	SP,(SYSTEM+1)
	LD	DE,TITLE
	LD	C,9		;STROUT 文字列出力 CP/M
	CALL	SYSTEM

	LD	A,(FCB1)
	LD	(FCB),A
	LD	A,(FCB1+1)
	CP	020H
	JR	Z,SHOW_DRV
	LD	HL,FCB1
	LD	DE,FCB
	LD	BC,16
	LDIR
SHOW_DRV:
	LD	A,(FCB)
	OR	A
	JR	NZ,SHOW_DRV1

	LD	C,019H		;カレントドライブ番号の獲得(_CURDRV) CP/M MSX-DOS
	CALL	SYSTEM
	INC	A
	LD	(FCB),A
SHOW_DRV1:
	LD	A,(FCB)
	ADD	A,040H
	LD	E,A
	LD	C,2		;コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	LD	E,':'
	LD	C,2		;コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	LD	B,8
	LD	HL,FCB+1
	CALL	SHOW_STRN
	PUSH	HL
	LD	E,'.'
	LD	C,2		;コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	POP	HL
	LD	B,3
	CALL	SHOW_STRN

	LD	DE,FCB
	LD	C,00FH		;FOPEN FCB ファイルオープン CP/M
	CALL	SYSTEM
	INC	A
	JR	Z,SHOW_NOTFOUND
	LD	DE,DTA
	LD	C,01AH		;SETDTA ディスク転送アドレスの設定 CP/M
	CALL	SYSTEM
	LD	DE,FCB
	LD	C,021H		;RDRND FCB ランダム読み出し CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR

	LD	HL,TITLE
	LD	DE,DTA+2
	LD	B,5
	CALL	CPSTRN
	JR	NZ,SHOW_INCORRECT

	LD	HL,PATCH_START
	LD	DE,DTA+010H
	LD	BC,PATCH_END-PATCH_START
	LDIR
	LD	DE,FCB
	LD	C,022H		;WRRND FCB ランダム書き込み CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	A,001H
	LD	(RANDOM_RECORD),A
	LD	DE,FCB
	LD	C,021H		;RDRND FCB ランダム読み出し CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	HL,OSNAME
	LD	DE,DTA+2
	LD	BC,OSNAME_END-OSNAME
	LDIR
	LD	DE,FCB
	LD	C,22H		;WRRND FCB ランダム書き込み CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	DE,FCB
	LD	C,10H		;FCLOSE FCB ファイルクローズ CP/M
	CALL	SYSTEM
	INC	A
	JR	Z,SHOW_ERROR
	LD	DE,MSG_OK
	JR	SHOW_MESSAGE
SHOW_ERROR:
	LD	DE,FCB
	LD	C,10H		;FCLOSE FCB ファイルクローズ CP/M
	CALL	SYSTEM
	LD	DE,MSG_ERROR
	JR	SHOW_MESSAGE
SHOW_INCORRECT:
	LD	DE,MSG_INCORRECT
	JR	SHOW_MESSAGE
SHOW_NOTFOUND:
	LD	DE,MSG_NOT_FOUND
SHOW_MESSAGE:
	LD	C,9		;STROUT 文字列出力 CP/M
	CALL	SYSTEM
	JP	0

SHOW_STRN:
	LD	E,(HL)
	INC	HL
	PUSH	BC
	PUSH	HL
	LD	C,2		;コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	POP	HL
	POP	BC
	DJNZ	SHOW_STRN
	RET

CPSTRN:
	LD	A,(DE)
	INC	DE
	CP	(HL)
	RET	NZ
	INC	HL
	DJNZ	CPSTRN
	RET

PATCH_START:			;パッチ
	ORG	00110H,PATCH_START-00100H

	LD	A,(0000BH)
	DEC	A
	LD	BC,27		;for CDOS2
	JR	Z,CDOS2
	PUSH	HL
	PUSH	DE
	LD	C,06FH	 	;(BDOS) MSX-DOS のバージョン番号の獲得(_DOSVER) *1 MSX-DOS
	CALL	SYSTEM
	POP	DE
	POP	HL
	LD	A,B
	CP	2
	JR	NC,MSXDOS2	;MSX-DOS2ではFCBにタイムスタンプは無い
	PUSH	DE
	LD	A,2
	LD	BC,22		;(FCB)ファイルを最後に変更した時刻 2バイト
	CALL	ADD_LDIR
;	LD	A,2		;ここでA=2のハズ
	LD	BC,20-(22+2)	;(FCB)ファイルを最後に変更した日付 2バイト
	CALL	ADD_LDIR
	POP	DE
MSXDOS2:
	LD	HL,00509H
	LD	BC,16		;(FCB)ファイルサイズ 4バイト
CDOS2:
	LD	A,4
ADD_LDIR:
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	LD	C,A
	LD	B,0
	LDIR
	RET

	ORG	$$+00100H	;$DEPHASE
PATCH_END:
OSNAME:
	DB	"CDOS2/MSX-DOS/LD"
OSNAME_END:
TITLE:
	DB	"PMextLD v1.10"
	DB	0DH,0AH
	DB	'$'
MSG_NOT_FOUND:
	DB	" Not found"
	DB	0DH,0AH
	DB	'$'
MSG_INCORRECT:
	DB	" Incorrect file"
	DB	0DH,0AH
	DB	'$'
MSG_ERROR:
	DB	" Error!"
	DB	0DH,0AH
	DB	'$'
MSG_OK:
	DB	" OK"
	DB	0DH,0AH
	DB	'$'
FCB:				;PMEXT.COMのFCB
	DB	0,"PMEXT   COM",0
	DW	0,0,0,0,0,0,0,0,0,0
RANDOM_RECORD:
	DW	0,0,0
