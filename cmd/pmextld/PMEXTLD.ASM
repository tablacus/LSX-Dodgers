; pmextld
;
; PMEXT.COM にCDOS2/MSX-DOS/MSX-DOS2/Nextro/LSX-Dodgers用のタイムスタンプパッチをあてる

SYSTEM	EQU	00005H
FCB1	EQU	0005CH
DTA	EQU	00080H

	ORG	00100H
	JP	START		;0C3HはZ80ではJPだが、i8086ではRETなので
START:				;間違えてMS-DOS環境で実行しても即終了できる
	LD	SP,(SYSTEM+1)
	LD	DE,TITLE
	LD	C,9		;(BDOS) STROUT 文字列出力 CP/M
	CALL	SYSTEM

	LD	A,(FCB1)
	LD	(FCB),A
	LD	A,(FCB1+1)
	CP	020H
	JR	Z,SHOW_DRV
	LD	HL,FCB1
	LD	DE,FCB
	LD	BC,16
	LDIR
SHOW_DRV:
	LD	A,(FCB)
	OR	A
	JR	NZ,SHOW_DRV1

	LD	C,019H		;(BDOS) カレントドライブ番号の獲得(_CURDRV) CP/M MSX-DOS
	CALL	SYSTEM
	INC	A
	LD	(FCB),A
SHOW_DRV1:
	LD	A,(FCB)
	ADD	A,040H
	LD	E,A
	LD	C,2		;(BDOS) コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	LD	E,':'
	LD	C,2		;(BDOS) コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	LD	B,8
	LD	HL,FCB+1
	CALL	SHOW_STRN
	PUSH	HL
	LD	E,'.'
	LD	C,2		;(BDOS) コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	POP	HL
	LD	B,3
	CALL	SHOW_STRN

	LD	DE,FCB
	LD	C,00FH		;(BDOS) FOPEN FCB ファイルオープン CP/M
	CALL	SYSTEM
	INC	A
	JP	Z,SHOW_NOTFOUND
	LD	DE,DTA
	LD	C,01AH		;(BDOS) SETDTA ディスク転送アドレスの設定 CP/M
	CALL	SYSTEM
	LD	DE,FCB
	LD	C,021H		;(BDOS) RDRND FCB ランダム読み出し CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR

	LD	HL,TITLE
	LD	DE,DTA+2
	LD	B,5
	CALL	CPSTRN
	JR	NZ,SHOW_INCORRECT

	LD	HL,PATCH_START
	LD	DE,DTA+010H
	LD	BC,PATCH_END-PATCH_START
	LDIR

	LD	DE,FCB
	LD	C,022H		;(BDOS) WRRND FCB ランダム書き込み CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	A,001H
	LD	(RANDOM_RECORD),A
	LD	DE,FCB
	LD	C,021H		;(BDOS) RDRND FCB ランダム読み出し CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR

	LD	HL,OSNAME
	LD	DE,DTA+2
	LD	BC,OSNAME_END-OSNAME
	LDIR

	LD	HL,PATCH2_START
	LD	DE,DTA+0C0H-080H
	LD	BC,PATCH2_END-PATCH2_START
	LDIR

	LD	DE,FCB
	LD	C,22H		;(BDOS) WRRND FCB ランダム書き込み CP/M
	CALL	SYSTEM
	OR	A
	JR	NZ,SHOW_ERROR
	LD	DE,FCB
	LD	C,10H		;(BDOS) FCLOSE FCB ファイルクローズ CP/M
	CALL	SYSTEM
	INC	A
	JR	Z,SHOW_ERROR
	LD	DE,MSG_OK
	JR	SHOW_MESSAGE
SHOW_ERROR:
	LD	DE,FCB
	LD	C,10H		;(BDOS) FCLOSE FCB ファイルクローズ CP/M
	CALL	SYSTEM
	LD	DE,MSG_ERROR
	JR	SHOW_MESSAGE
SHOW_INCORRECT:
	LD	DE,MSG_INCORRECT
	JR	SHOW_MESSAGE
SHOW_NOTFOUND:
	LD	DE,MSG_NOT_FOUND
SHOW_MESSAGE:
	LD	C,9		;(BDOS) STROUT 文字列出力 CP/M
	CALL	SYSTEM
	JP	0

SHOW_STRN:
	LD	E,(HL)
	INC	HL
	PUSH	BC
	PUSH	HL
	LD	C,2		;(BDOS) コンソール出力(_CONOUT) CP/M MSX-DOS
	CALL	SYSTEM
	POP	HL
	POP	BC
	DJNZ	SHOW_STRN
	RET

CPSTRN:
	LD	A,(DE)
	INC	DE
	CP	(HL)
	RET	NZ
	INC	HL
	DJNZ	CPSTRN
	RET

PATCH_START:			;パッチ
	ORG	00110H,PATCH_START-00100H

	LD	A,(0000BH)
	DEC	A
	LD	BC,27		;for CDOS2
	JR	Z,CDOS2
	PUSH	HL
	PUSH	DE
	LD	C,06FH	 	;(BDOS) MSX-DOS のバージョン番号の獲得(_DOSVER) *1 MSX-DOS
	CALL	SYSTEM
	POP	DE
	POP	HL
	LD	A,B
	CP	2
	JR	NC,MSXDOS2	;MSX-DOS2
	PUSH	DE
	LD	A,2
	LD	BC,22		;(FCB)ファイルを最後に変更した時刻 2バイト
	CALL	ADD_LDIR
;	LD	A,2		;ここでA=2のハズ
	LD	BC,20-(22+2)	;(FCB)ファイルを最後に変更した日付 2バイト
	CALL	ADD_LDIR
	POP	DE
SET_FILESIZE:
	LD	HL,00509H
	LD	BC,16		;(FCB)ファイルサイズ 4バイト
CDOS2:
	LD	A,4
ADD_LDIR:
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	LD	C,A
	LD	B,0
	LDIR
	RET

MSXDOS2:
	LD	(TIME),HL
	INC	HL
	INC	HL
	LD	(DATE),HL
	JR	MSXDOS2_2

STRNSET:
	LD	A,(DE)
	INC	DE
	CP	021H
	JR	C,STRNSET1
	LD	(HL),A
	INC	HL
STRNSET1:
	DJNZ	STRNSET
	RET

PATH:
	DB	"A:FILENAME.EXT"
	DB	0

	ORG	$$+00100H	;$DEPHASE
PATCH_END:

PATCH2_START:			;パッチ
	ORG	001C0H,PATCH2_START-00100H
	RET
				;MSX-DOS2の処理　タイムスタンプはファイルの日付および時刻の獲得・セットを使う
MSXDOS2_2:
	LD	HL,PATH		;FCBからファイル名を取り出す (A:FILENAME.EXTの形式に)
	PUSH	HL
	PUSH	DE
	LD	A,(DE)
	INC	DE
	OR	A
	JR	Z,SET_NAME
	ADD	A,040H		;ドライブ名
	LD	(HL),A
	INC	HL
	LD	(HL),':'
	INC	HL
SET_NAME:
	LD	B,8		;ファイル名
	CALL	STRNSET
	LD	(HL),'.'
	INC	HL
	LD	B,3		;拡張子
	CALL	STRNSET
	LD	(HL),B		;B=0
	POP	DE
	PUSH	DE
	CALL	SET_FILESIZE	;ファイルサイズ
	POP	DE
	LD	C,010H		;(BDOS) ファイルのクローズ(_FCLOSE) CP/M MSX-DOS
	CALL	SYSTEM

	LD	HL,(0)
DATE	EQU	$-2
	LD	IX,(0)
TIME	EQU	$-2
	POP	DE
	LD	A,1		;日付と時刻のセット
	LD	C,051H		;(BDOS) ファイルの日付および時刻の獲得・セット(_FTIME) MSX-DOS2
	EX	(SP),HL
	INC	HL		;LD DE,FCB
	INC	HL
	INC	HL
	INC	HL		;LD C,010H
	INC	HL
	EX	(SP),HL
	RET

	ORG	$$+00100H	;$DEPHASE
PATCH2_END:

OSNAME:
	DB	"CDOS2/MSX-DOS/LD"
OSNAME_END:
TITLE:
	DB	"PMextLD v1.20"
	DB	0DH,0AH
	DB	'$'
MSG_NOT_FOUND:
	DB	" Not found"
	DB	0DH,0AH
	DB	'$'
MSG_INCORRECT:
	DB	" Incorrect file"
	DB	0DH,0AH
	DB	'$'
MSG_ERROR:
	DB	" Error!"
	DB	0DH,0AH
	DB	'$'
MSG_OK:
	DB	" OK"
	DB	0DH,0AH
	DB	'$'
FCB:				;PMEXT.COMのFCB
	DB	0,"PMEXT   COM",0
	DW	0,0,0,0,0,0,0,0,0,0
RANDOM_RECORD:
	DW	0,0,0
