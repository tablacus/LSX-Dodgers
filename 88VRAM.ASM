
;	LSX-Dodgers VRAM
;	PC-8801mkIISR

AT_VRAM:
	ORG	0F000H,AT_VRAM-OFFSET
ESC1:
	LD	A,E
	CP	'A'
	JP	Z,CTRL1E
	CP	'B'
	JP	Z,CTRL1F
	CP	'C'
	JP	Z,CTRL1C
	CP	'D'
	JP	Z,CTRL1D
	CP	'E'
	JR	Z,CT0CX
	CP	'j'
CT0CX:
	JP	Z,CTRL0C
	CP	'H'
	JP	Z,CTRL0B
	CP	'J'
	JP	Z,CTRL06
	CP	'K'
	JP	Z,CTRL05
	CP	'L'
	JP	Z,CTRL0E
	CP	'T'
	JP	Z,CTRL05
	CP	'x'
	JR	Z,ESC1X
	CP	'y'
	JR	NZ,ESC1Y
ESC1X:
	LD	(HL),1
ESC1Y:
	CP	'='
	JR	Z,ESC1W
	CP	'Y'
	JR	NZ,ESC1Z
ESC1W:
	LD	(HL),2
ESC1Z:
	CP	020H
	JR	C,ESC1C
	ADD	A,A
	RET	NC
ESC1C:
	POP	HL
	JP	ANK

ESC:
	LD	HL,PRINTE5
	PUSH	HL
	LD	HL,ESCFLG
	LD	(HL),0
	DEC	A
	JR	Z,ESC1
	DEC	A
	JR	NZ,ESC2
	LD	(HL),3
	LD	A,E
	SUB	020H
	LD	(ESCYP+1),A
	RET
ESC2:
	DEC	A
	RET	NZ
ESCYP:	LD	H,0
	LD	A,E
	SUB	020H
	LD	L,A
	JP	_LOC

CTRL:
	LD	A,E
	ADD	A,A
	OR	080H
	LD	L,A
	LD	H,CTRLTB/256
	LD	A,(HL)
	INC	L
	LD	H,(HL)
	LD	L,A
	CALL	JPHL
	JP	PRINTE5

CTRL1C:
	LD	HL,(_TXADR)
	INC	L
PRINTE3:
	LD	A,WIDTH
	DEC	A
	CP	L
	JR	NC,SET_TXADR_HL
	LD	L,0
	INC	H
PRINTE8:
	LD	A,(_LINE)
	CP	H
	JR	Z,PRINT2
LOC:
SET_TXADR_HL:
	LD	(_TXADR),HL
CTRL00:
	RET

PRINT2:
	CALL	SCR
	DEC	H
	JR	SET_TXADR_HL

CTRL08:
	LD	A,(INSFLG)
	CP	0CDH		;CALL ????
	JR	Z,CTRL02
CTRL1D:
	LD	HL,(_TXADR)
	LD	A,H
	OR	L
	RET	Z
	LD	A,L
	OR	A
	JR	Z,CTRL1DX1
	DEC	L
	JR	SET_TXADR_HL
CTRL1DX1:
	LD	A,WIDTH
	DEC	A
	LD	L,A
	DEC	H
	JR	SET_TXADR_HL
CTRL1E:
	LD	HL,(_TXADR)
	LD	A,H
	OR	A
	RET	Z
	DEC	H
	JR	SET_TXADR_HL

CTRL09:
	LD	HL,(_TXADR)
	LD	A,L
	AND	0F8H
	ADD	A,8
	LD	L,A
	JR	PRINTE3

CTRL02:
	LD	A,(_TXADR)
	OR	A
	JR	Z,CTRL1D
	CALL	CTRL1D
	LD	HL,(_TXADR)
	LD	A,WIDTH
	LD	C,A
	SUB	L
	LD	B,A
	LD	A,C
	DEC	A
	LD	L,A
	CALL	LOC1

	LD	A,020H
C8X1:
	LD	C,(HL)
	LD	(HL),A
	DEC	HL
	LD	A,C
	DJNZ	C8X1
	RET

CTRL05:
	CALL	_POS
	LD	HL,(_TXADR)
	LD	A,WIDTH
	SUB	L
	LD	B,A
	CALL	LOC0
CLLINE:
	LD	A,020H
CLLINE1:
	LD	(HL),A
	INC	HL
	DJNZ	CLLINE1
	RET

CTRL0D:
	XOR	A
	LD	(_TXADR),A
	RET

CTRL12:
	LD	HL,INSFLG
	LD	A,(HL)
	XOR	0CCH		;CD:CALL ???? 1:LD BC,????
	LD	(HL),A
	RET

CTRL0B:
	LD	HL,0
	LD	(_TXADR),HL
	RET

CTRL1B:
	LD	A,1
	LD	(ESCFLG),A
	RET

CTRL07:
	LD	A,(WK40)
	PUSH	AF
	OR	20H
	OUT	(040H),A
	LD	B,16
C07X1:
	CALL	VBLANK
	DJNZ	C07X1
	POP	AF
	OUT	(040H),A
	RET

INSERT:
	LD	HL,(_TXADR)
	LD	A,WIDTH
	SUB	L
	LD	B,A

	CALL	LOC0

	LD	A,020H
INS1:
	LD	C,(HL)
	LD	(HL),A
	LD	A,C
	INC	HL
	DJNZ	INS1
	RET

CTRL0C:
	CALL	CTRL0B
CTRL06:
	LD	HL,(_TXADR)
CLS1:
CLS2:
	PUSH	HL
	LD	A,WIDTH
	SUB	L
	LD	B,A
	CALL	LOC1
	LD	A,020H
CLS3:
	LD	(HL),A
	INC	HL
	DJNZ	CLS3
	POP	HL
	LD	L,0
	INC	H
	LD	A,(_LINE)
	CP	H
	JR	NZ,CLS2
	RET

CTRL04:
	CALL	_POS
	LD	A,L
	OR	A
	RET	Z
CTRL03:
	CALL	CTRL0D
CTRL0A:
CTRL1F:
	LD	HL,(_TXADR)
	INC	H
	JP	PRINTE8

CTRL0E:
	CALL	_POS
	LD	A,H
	JR	SCR1
SCR:
	LD	A,(_LINE)
	DEC	A
SCR1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	AF
	LD	HL,0
	CALL	LOC1
	EX	DE,HL
	LD	HL,0100H
	CALL	LOC1
	POP	BC
	LD	A,B
	OR	A
SCRUP1:
	JR	Z,SCRCL
	PUSH	HL
	PUSH	DE
	LD	B,8
	LD	E,A
	LD	HL,120*256
	LD	D,L
SCRH2:
	ADD	HL,HL
	JR	NC,SCRH3
	ADD	HL,DE
SCRH3:
	DJNZ	SCRH2
	LD	C,L
	LD	B,H
	POP	DE
	POP	HL
	LDIR

SCRCL:
	LD	A,WIDTH
	LD	B,A
	EX	DE,HL
	CALL	CLLINE
	POP	HL
	POP	DE
	POP	BC
	RET
