
;	LSX-Dodgers OS

BDOS0:
	PUSH	HL
	LD	A,C
	CP	03CH
	JR	C,DOS1
	LD	A,03CH
DOS1:
	ADD	A,A
	LD	L,A
	LD	H,STABLE/256
	LD	A,(HL)
	INC	L
	LD	H,(HL)
	LD	L,A
	EX	(SP),HL
	LD	A,C
;確認用
;	LD	(0050H),A
	RET
_DOS2:
	CP	05AH		;(BDOS)カレントディレクトリの変更(_CHDIR)
	JP	Z,_SYS5A
	CP	05CH		;(BDOS)ファイル名の解析(_PFILE)
	JP	Z,_SYS5C
	CP	05FH		;(BDOS)ディスクバッファのフラッシュ(_FLUSH)
	JP	Z,_SYS5F
	CP	06FH		;(BDOS)MSX-DOS のバージョン番号の獲得(_DOSVER)
	JR	NZ,_ERROR
	LD	BC,011DH
	LD	DE,VER
	LD	HL,MACD
C_REM:
	XOR	A
_ERROR:
	AND	A
	RET

_SYS09:		;文字列出力
	PUSH	DE
_SX09:
	LD	A,(DE)
	INC	DE
	CP	'$'
	JR	Z,SX0E2
	CALL	MSG_A
	JR	_SX09

MSX1:
	CALL	MSG_A
MSX:
	LD	A,(HL)
	INC	HL
	OR	A
	JR	NZ,MSX1
	RET

_SYS0C:		;(BDOS)バージョン番号の獲得
	LD	HL,00022H
	LD	A,L
	RET

_SYS0D:		;(BDOS)ディスクリセット
	CALL	_FFLUSH
	PUSH	HL
	LD	HL,00080H
	LD	(_DTA),HL
	POP	HL
	PUSH	DE
	LD	E,0
	DB	03EH

_SYS0E:		;(BDOS)カレントドライブの設定
	PUSH	DE
	LD	A,E
	PUSH	IX
	CALL	_GETDPB
	POP	IX
	JR	C,SX0E2
	LD	A,E
	LD	(_DVSW),A
SX0E2:
	POP	DE
	RET

_SYS0F:		;(BDOS)ファイルのオープン
	CALL	SETDRV
	LD	A,(IY+28)	;(FCB)オープンモード
	ADD	A,002H		;Write
	JR	Z,FEND0F
	CALL	CHKWILDX

	CALL	NC,SOPENX
_S16XX:
	JR	C,FEND0F
				;Directory location
	LD	A,(_FILEN)
	LD	(IY+25),A	;(FCB)ディレクトリロケーション
;				_DIRF
	LD	A,(_DIRF)
	LD	(IY+29),A	;(FCB)ディレクトリモード
;				_FBPS
	LD	(IY+30),E	;(FCB)ディレクトリポジション
	LD	(IY+31),D
;				Open(Read)
	LD	(IY+28),0FFH	;(FCB)オープンモード
;				FILENAME
	PUSH	IY
	POP	DE
	INC	DE
	LD	BC,11
	LDIR
;				Attribute
	LD	A,(HL)
	LD	(IY+13),A	;(FCB)属性(アトリビュート)
	LD	DE,11		;Reserved
	ADD	HL,DE
					;(FCB)ファイルを最後に変更した時刻
	LD	DE,SDDATA		;(FCB)ファイルを最後に変更した日付
	LD	B,SDDATAE-SDDATA	;(FCB)ファイルの先頭クラスタ
S16LOOP:				;(FCB)ファイルのサイズ(バイト単位)
	LD	A,(DE)
	INC	DE
	LD	(S16PAT+2),A
	LD	A,(HL)
	INC	HL
S16PAT:	LD	(IY+0),A
	DJNZ	S16LOOP

	XOR	A
	LD	(IY+12),A	;(FCB)カレントブロック
	LD	(OFCB+1),IY
FEND0F:
	JR	FEND10

_SYS10:		;(BDOS)ファイルのクローズ
	CALL	SETDRV
	LD	A,(IY+28)	;(FCB)オープンモード
	SUB	0FEH
	SCF
	CCF
	JR	NZ,FEND10
_S10A:				;Write
	LD	(IY+14),A	;(FCB)レコードサイズ/アクセスするディレクトリのクラスタ番号+1
	LD	(IY+15),A

	INC	(IY+28)		;0FEH->0FFH ;オープンモード
	CALL	SETTMS

	LD	A,(IY+29)	;(FCB)ディレクトリモード
	LD	(_DIRF),A
	AND	07FH
	LD	(_SECPS+1),A
	LD	E,(IY+30)	;(FCB)ディレクトリポジション
	LD	D,(IY+31)
	CALL	READ_DIR
	JR	C,FEND10

SDECM1:	LD	A,(SDECPAT+1)	;1セクタ辺りのディレクトリエントリ数
 	DEC	A		;1024=01FH / 512=00FH / 256=7
	AND	(IY+25)		;(FCB)ディレクトリロケーション
	LD	L,A
	LD	H,0
	ADD	HL,HL		;*2
	ADD	HL,HL		;*4
	ADD	HL,HL		;*8
	ADD	HL,HL		;*16
	ADD	HL,HL		;*32
	LD	BC,(_DTBUF)
	ADD	HL,BC

	CALL	SDIRENT
FEND10:
	JR	FEND

_SYS11:		;(BDOS)最初のファイルの検索
	CALL	SETDRV
	CALL	SOPEN
_S12X1:
	JR	C,FEND
	CALL	SOPENE2
	LD	DE,(_DTA)
	LD	A,(_DRV)
	INC	A
	LD	(DE),A
	PUSH	DE
	INC	DE
	LD	BC,32
	LDIR
	LD	L,(IY+14)	;(FCB)レコードサイズ/アクセスするディレクトリのクラスタ番号+1
	LD	H,(IY+15)
	LD	(SCDIR),HL
	LD	HL,(_FBPS)
	LD	(SFBPS),HL
	LD	HL,(_FILEN)
	LD	A,H
	OR	A
	JR	Z,_S12DIR
	LD	A,(_SECPS+1)
	OR	080H
	LD	H,A
_S12DIR:
	LD	(SFNDF),HL	;Directory position and Flags
	POP	HL
	LD	DE,SFILE
	LD	C,33
_S11B:
	LDIR
FEND:
	SBC	A,A		;CF=0 A=0 : CF=1 A=0FFH
FENDE:
	POP	IY
	POP	BC
	POP	DE
	POP	HL
	RET

_SYS12:		;(BDOS)次のファイルの検索
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	IY
	CALL	NOPEN
	JR	_S12X1

_S16K:
	LD	A,(IY+28)	;(FCB)オープンモード
	CP	0FDH		;Device(0FDH)
	SCF
	RET	Z

OFCB:	LD	DE,0
	PUSH	DE
	LD	B,26		;First cluster
S16K1:
	INC	DE
	INC	HL
	DJNZ	S16K1

	LD	A,(DE)
	CP	(HL)
	JR	NZ,S16K2
	INC	DE
	INC	HL
	LD	A,(DE)
	CP	(HL)
	JR	NZ,S16K2

	POP	HL
	PUSH	IY
	POP	DE
	LD	A,(DE)		;Drive
	CP	(HL)
	JR	NZ,S16K3
				;ここはCFが必ず0
	SBC	HL,DE		;CP HL,DE
	SCF
	RET	NZ
	PUSH	HL
S16K2:
	POP	HL
S16K3:
	PUSH	IY
	POP	DE
_SYS13:		;(BDOS)ファイルの削除
	CALL	SETDRV
	CALL	KILL
FEND13:
	JR	FEND

_SYS14:		;(BDOS)シーケンシャルな読み出し
	CALL	SETDRV
	CALL	INIT_SEQ
SREAD:
	CALL	RB_READ

	ADD	A,001H
	JR	C,FEND

	LD	A,L
	SUB	001H
FENDX:
	SBC	A,A
	AND	3
	RRA
	JR	FENDE

_SYS15:		;(BDOS)シーケンシャルな書き込み
	CALL	SETDRV
	CALL	INIT_SEQ
SWRITE:
	CALL	RB_WRITE

	ADD	A,0FFH
	JR	FENDX

_SYS17:		;(BDOS)ファイル名の変更
	CALL	SETDRV
	CALL	NAME
	JR	FEND13

_SYS16:		;(BDOS)ファイルの作成
	CALL	SETDRV

	CALL	CHKWILDX
	JR	C,FEND13
	LD	A,(IY+1)	;(FCB)主ファイル名
	CP	5
	JR	Z,_S16X2
	CP	021H
	JP	C,FEND13
_S16X2:
;				Clear FCB
	PUSH	IY
	POP	HL
	LD	BC,16
	ADD	HL,BC
_S16X1:
	LD	(HL),B		;B=0
	INC	HL
	DEC	C
	JR	NZ,_S16X1

	CALL	SETTMSX
	LD	(IY+13),0FFH	;(FCB)Attribute
	CALL	SOPENX
	CCF
	CALL	Z,_S16K
	CALL	NC,COPEN
	CALL	NC,SDIRENT
	JP	_S16XX

_SYS21:		;(BDOS)ランダムな読み出し
	CALL	SETDRV
	CALL	INIT_RND
	JR	SREAD

_SYS22:		;(BDOS)ランダムな書き込み
_SYS28:		;(BDOS)ランダムな書き込み・その2
	CALL	SETDRV
	CALL	INIT_RND
	JR	SWRITE

_SYS18:		;(BDOS)ログインベクトルの獲得
	LD	HL,000FFH
	XOR	A
	RET

_SYS19:		;(BDOS)カレントドライブ番号の獲得
	LD	A,(_DVSW)
	AND	A
	RET

_SYS1A:		;(BDOS)DTAの設定
	LD	(_DTA),DE
	XOR	A
	RET

_SYS1B:		;(BDOS)ディスク情報の獲得
	LD	A,E
	CALL	GETDRV
	CALL	_GETDPB
	CALL	NC,_RDFATX
	LD	HL,0
	CALL	NC,DSKF

	LD	DE,(MAXCLP+1)	;DPB_08_MAXCL-1
	DEC	DE		;DE←クラスタの総数
	LD	IY,(_FATBF)	;IY←メモリ上のFATの先頭アドレス
	SBC	A,A
	RET	C
	LD	C,A		;C=0
	LD	A,(IX+00FH)	;DPB_0F_BPS
	AND	00FH
	LD	B,A
	LD	A,(IX+7)	;DPB_07_SECPCL
	RET

_SYS1D:		;(BDOS)書き込みが禁止されているディスクの調査
	XOR	A
	LD	H,A
	LD	L,A
	RET

_SYS1F:		;(BDOS)ディスク装置のパラメータの読み出し
	LD	HL,DEVICE
	LD	A,E
	JP	_GETDPB

_SYS23:		;(BDOS)ファイルサイズの獲得
	PUSH	HL
	LD	HL,(STABLE+2*00FH)	;ファイルのオープン
	CALL	JP_HL
	JR	C,_S23X1

	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	LD	A,(IY+16)	;(FCB)ファイルのサイズ(バイト単位)
	LD	L,(IY+17)	;
	LD	H,(IY+18)	;
	PUSH	BC		;
	LD	B,(IY+19)	;
	CALL	GET_CPM_R_SIZE
	POP	BC
_S24X1:
	CALL	SET_RR_AHL
;	POP	DE		;^^^^^^^^^^^^^^^^^^^^^^^^
;	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
	POP	DE		;

	XOR	A
_S23X1:
	POP	HL
	RET

_SYS24:		;(BDOS)ランダムレコードフィールドの設定
	PUSH	HL
	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	CALL	GETSEQ
	JR	_S24X1		;^^^^^^^^^^^^^^^^^^^^^^^^

_SYS5C:		;(BDOS)ファイル名の解析
	PUSH	BC
	PUSH	HL
	CALL	FILE
	POP	HL
	POP	BC
S29X1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	EX	DE,HL
	LD	HL,FDRV
	LD	BC,13
	LDIR
	LD	(HL),B		;B=0
	LD	C,3
	LDIR
	POP	HL
	POP	DE
	POP	BC
	XOR	A
	RET

_SYS30:		;(BDOS)論理セクタを用いた書き込み
	PUSH	BC
	LD	BC,DWT24B
	JR	S2FX
_SYS2F:
	PUSH	BC
	LD	BC,DRD24B
S2FX:
	LD	(S2FPAT+1),BC
	CALL	_FFLUSH

	PUSH	DE
	PUSH	HL
	LD	A,L		;Drive
	CALL	_CHGDRV
	JR	C,S30X2
	LD	B,H		;Number of clusters
	LD	HL,(_DTA)

	LD	C,0
S2FPAT:	CALL	DRD24B
S30X2:
	POP	HL
	POP	DE
	POP	BC
	SBC	A,A
	RET

_SYS5A:		;(BDOS)カレントディレクトリの変更
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	FILEC
	LD	HL,0
	LD	DE,FDRV
	LD	HL,(STABLE+2*027H)	;ランダムブロック読み込み
	CALL	JP_HL
	JR	S30X2
