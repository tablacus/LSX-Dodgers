
;	LSX-Dodgers DISK


SEEK:
	LD	A,D
	OR	E
	LD	A,$01
	JR	Z,DIV3

	LD	B,16
MAXSEC:	LD	C,$00
	XOR	A
	EX	DE,HL
DIV1:
	ADD	HL,HL
	ADC	A,A
	CP	C
	JR	C,DIV2
	SUB	C
	INC	L
DIV2:
	DJNZ	DIV1

SECPAT:	ADD	A,$01		;(Self-rewriting)
	LD	D,L	;Quotient(TRACK)
DIV3:
	LD	E,A	;Remainder
	SRL	D	;CYLINDER
	SBC	A,A
	AND	$10	;SIDE
	LD	C,A

	LD	A,(_DRIVE)
	CP	4
	CCF
	RET	C
	OR	C
	LD	(MOPAT+1),A	;Motor off data
	LD	BC,$0FFC
	OR	$80		;Mortor on
	OUT	(C),A

	CALL	SETMODE
	RET	C

	CALL	DRIVE
	CP	$FF		;A=$FF Unknown CYLINDER
	CALL	Z,FDCRES	;Restore
	LD	C,$F9
	OUT	(C),A		;Currrent CYLINDER
	SUB	D
	RET	Z		;No seek

	LD	A,(_ISEEK)
	LD	C,A

	LD	A,(_MAXCYL)	;Max CYLINDER
	DEC	A
	CP	D		;Over CYLINDER
	RET	C

	CP	C		;for Built-in 2DD
	JR	C,SETM2
	LD	A,B		;B=$0F
	IN	A,($FE)		;2HD mode for 2DD seek
SETM2:
	LD	A,B
	IN	A,($FD)		;MFM mode

	LD	C,$FB
	OUT	(C),D
	LD	(HL),D
	LD	C,$18		;Seek
FDCCMD2:
	LD	A,(_SEEKSP)
FSPAT:	AND	$FF
	OR	C

FDCCMD:
	CALL	COMSET

@FDCPAT:LD	A,(FDCPAT+1)
	AND	$20		;Write data Write track
	INC	A

SST:
	LD	C,0
SST1:
	DEC	C		; 4
	JR	NZ,SST1		;12 * 256 / 4 = About 1ms
	DEC	A
	JR	NZ,SST1

	INC	A		;A=1
	CALL	READY
SETMODE:
	LD	A,B		;B=$0F
FDMODE:	IN	A,($FF)		;Floppy mode(2D/2HD) (Self-rewriting)

	LD	A,B
	IN	A,($FD)		;MFM mode

	CALL	DELAY0		;Head select time
	LD	A,$81
READY:
	LD	(WAITA+1),A
	PUSH	HL
	LD	C,$F8
	LD	H,C
READY2:
	IN	A,(C)
WAITA:	AND	$81		;NOT READY,BUSY
	JR	Z,READYE
	EX	(SP),HL		;wait
	EX	(SP),HL		; //
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,READY2
	SCF
READYE:
	POP	HL
	RET

FDCRES:
	LD	(HL),0
	LD	C,8
	JR	FDCCMD2

FDMOFF:
	PUSH	AF
	PUSH	BC
	LD	BC,$0FFC	;Motor off
MOPAT:	LD	A,$00		;(Self-rewriting)
	OUT	(C),A
	POP	BC
	POP	AF
	RET

SECSET:
	LD	BC,$0FFA
	OUT	(C),E
COMSET:
	LD	C,$F8
	OUT	(C),A
DELAY0:
	LD	A,26
DELAY:
	DEC	A
	JR	NZ,DELAY
	RET

DRIVE:
	LD	HL,(_DRIVE)
	LD	H,_CYL0/256
	SET	7,L
	LD	A,(HL)
	RET

RNF:
	CALL	DRIVE
	LD	(HL),$FF
	RET

DEMES:	DB	3,7
	DB	"¥Device I/O Error!",5,3
	DB	"Abort Retry Ignore?",5,3,0

RETRY:	DB	2


;	FLOPPY DISK DRIVER(DMA)


WTTRK:
	LD	A,$F4
	DB	1	;JR	WTTRK1
FDWT:
	LD	A,$A0
WTTRK1:
	LD	(FDCPAT+1),A
	LD	A,16
	DI
	JR	DISK

FDRD:
	LD	A,$80
	NOP

	LD	(FDCPAT+1),A
	LD	A,14

DISK:
	LD	(DMAPAT+1),A

	LD	(DMAD+11),HL
DISKR:
	LD	A,2		;Retry count
	LD	(RETRY),A
DISK2:
	PUSH	DE
	CALL	SEEK
	JR	C,ERRF

DMAPAT:	LD	A,14
	LD	HL,DMAD
	CALL	SETDMA
	OUT	(C),C		;OUT $1F87,$87
FDCPAT:	LD	A,$80		;FDC COMMAND(Self-rewriting)
	PUSH	BC
	CALL	SECSET
	LD	HL,(DMAD+11)
	INC	HL
	LD	DE,$BB06	;READ DMA

	LD	A,3
	CALL	READY
	IN	A,(C)

	POP	BC
	OUT	(C),D
	OUT	(C),E
	IN	E,(C)
	IN	D,(C)
	ADD	HL,DE
	POP	DE
	INC	DE
	EI
	CALL	FDMOFF
	OR	A
	RET	Z
	DEC	DE
	BIT	4,A
	CALL	NZ,RNF
DISKE2:
	LD	HL,RETRY
	DEC	(HL)
	JR	NZ,DISK2
	OR	A
	JR	Z,ERR
	PUSH	DE
ERRF:
	POP	DE
DELP:
	CALL	RNF
	CALL	FDMOFF
	LD	A,(_SEEKSP)
	ADD	A,A
	SBC	A,A
	JR	C,ERR
DELP1:
	PUSH	DE
	LD	HL,DEMES
	CALL	MSX
	CALL	KEYBC
DELP2:
	CALL	INKEY
	JR	Z,DELP2

	POP	DE
	CALL	CAP
	CP	"R"
	JP	Z,DISKR
	CP	"I"
	JR	Z,IGNORE
	CP	"A"
	JR	NZ,DELP1
	JP	BOOT
IGNORE:
	LD	A,$FF
ERR:
	CP	A
	SCF
	RET

SETDMA:
	LD	BC,$1F87
SDMA:
	INC	B
	OUTI
	DEC	A
	JR	NZ,SDMA
	RET

DMAD:	DB	$C3,$7D,$FB,$0F,$FF,$FF,$2C,$10		;X1turbo
	DB	$80,$92,$8D,$00,$00,$CF,$01,$CF		;DISK DMA


;	EMM DRIVER(DMA)


EDWTC:
	LD	A,17
	DB	1
EDRDC:
	LD	A,15
EDWTC1:
	PUSH	DE
	LD	(EDMAD+12),HL
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,(_MAXCYL-1)
	LD	C,0
	ADD	HL,BC
	LD	BC,(_DRIVE)
	LD	B,$0D
	OUT	(C),C
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H

	LD	HL,EDMAD
	CALL	SETDMA
	LD	A,$B3
	OUT	(C),A
	OUT	(C),C		;OUT $1F87,$87
	EX	DE,HL
	INC	H
	INC	H
	INC	H
	INC	H
	POP	DE
	INC	DE
	XOR	A
	RET

EDMAD:	DB	$C3,$7D,$03,$0D,$FF,$03,$2C,$50,$02
	DB	$80,$9A,$AD,$00,$00,$CF,$01,$CF


;	BANK RAM DISK DRIVER


BDRDC:
	LD	(BANKSP+1),SP
	LD	A,(BANKSP+2)
	ADD	A,A
	JR	C,BDRDC1
	LD	SP,EXTSP
BDRDC1:
	PUSH	DE
	EXX
	 PUSH	 BC
	 PUSH	 DE
	 CALL	 BADR
	CALL	BTFR
	EXX
BDWTCE:
	 POP	 DE
	 POP	 BC
	 EXX
	POP	DE
	INC	DE
	EI
BANKSP:	LD	SP,0
	XOR	A
	RET

BDWTC:
	LD	(BANKSP+1),SP
	LD	A,(BANKSP+2)
	ADD	A,A
	JR	C,BDWTC1
	LD	SP,EXTSP
BDWTC1:
	PUSH	DE
	EXX
	 PUSH	 BC
	 PUSH	 DE
	 CALL	 BADR
	EXX
	 LD	D,E
	 LD	E,A
	 EXX
	EX	DE,HL
	CALL	BTFR
	EX	DE,HL
	EXX
	 OUT	 (C),D	 ;MAIN
	 JR	 BDWTCE

BADR:
	 EXX
	LD	A,E
	ADD	A,A
	RL	D
	ADD	A,A
	RL	D
	PUSH	AF
	ADD	A,A
	LD	A,D
	ADC	A,A
	AND	$0F
	EXX
	 LD	 BC,$0B00
	 LD	 D,A	  ;BANK
	 LD	 E,$10	  ;MAIN
	 EXX
	POP	DE
	LD	B,0
	LD	E,B
	RES	7,D
	RET

BTFR:
	PUSH	DE
	DI

BTFR1:
	EXX
	 OUT	 (C),D	 ;BANK(MAIN)
	 EXX
	EX	(SP),HL
	LD	A,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	EX	(SP),HL
	EXX
	 OUT	 (C),E	 ;MAIN(BANK)
	 EXX
	LD	(HL),A
	INC	HL
	LD	(HL),C
	INC	HL
	LD	(HL),D
	INC	HL
	LD	(HL),E
	INC	HL
	DJNZ	BTFR1
	POP	DE
	RET


;	GRAPHIC RAM DISK DRIVER


GDRDC:
	CALL	GADR
	PUSH	DE
	LD	DE,$400
GDRDC1:
	INI
	INC	B
	INC	BC
	DEC	E
	JR	NZ,GDRDC1
	DEC	D
	JR	NZ,GDRDC1
GDWTC2:
	POP	DE
	AND	$EF		;BANK0
	JR	S1FD0

GDWTC:
	CALL	GADR
	PUSH	DE
	LD	DE,$400
GDWTC1:
	INC	B
	OUTI
	INC	BC
	DEC	E
	JR	NZ,GDWTC1
	DEC	D
	JR	NZ,GDWTC1
	JR	GDWTC2

GADR:
	LD	C,0
	LD	A,E
	ADD	A,A
	ADD	A,A
	ADD	A,$40
	LD	B,A
	INC	DE
	LD	A,(WK1FD0)
	OR	$10		;BANK1
S1FD0:
	PUSH	BC
	LD	(WK1FD0),A
	LD	BC,$1FD0
	OUT	(C),A
	POP	BC
	RET

@R	EQU	WTTRK-@WTTRK

PE:
