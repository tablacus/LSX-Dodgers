
;	LSX-Dodgers IO
;	MSX

_SYS04	EQU	_ERROR		;(BDOS)外部出力
_SYS05	EQU	_ERROR		;(BDOS)プリンタ出力
_SYS1C	EQU	_ERROR		;(BDOS)ディスクの書き込み禁止化
_SYS1E	EQU	_ERROR		;(BDOS)ファイル属性の設定
_SYS20	EQU	_ERROR		;(BDOS)利用者番号の設定と読み出し
_SYS2E	EQU	_ERROR		;(BDOS)ベリファイ・フラグの設定

INT	EQU	0
INTCTCE	EQU	0
INTCTC2	EQU	0

CTRL00	EQU	0
CTRL03	EQU	0
CTRL04	EQU	0
CTRL05	EQU	0
CTRL06	EQU	0
CTRL07	EQU	0
CTRL08	EQU	0
CTRL09	EQU	0
CTRL0A	EQU	0
CTRL0B	EQU	0
CTRL0C	EQU	0
CTRL0D	EQU	0
CTRL0E	EQU	0
CTRL12	EQU	0
CTRL1B	EQU	0
CTRL1C	EQU	0
CTRL1D	EQU	0
CTRL1E	EQU	0
CTRL1F	EQU	0

PRINT:
	LD	A,E
	JR	MSG_A

_SYS01:		;(BDOS)コンソール入力
	CALL	_SYS07
MSG_A:
	CP	3
	JR	Z,MSG_BR
MSGA1:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHPUT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
MSGA2:
	POP	AF
	RET
MSG_BR:
	PUSH	AF
	LD	A,(CSRX)
	CP	2
	JR	C,MSGA2
	POP	AF
MSG_CR:
	PUSH	AF
	LD	A,00DH
	CALL	MSGA1
	LD	A,00AH
	CALL	MSGA1
	POP	AF
	RET

_SYS02:		;(BDOS)コンソール出力
	CALL	BREAK
	JP	_PRINT

_SYS06:		;(BDOS)コンソール直接入出力
	LD	A,E
	INC	A
	JP	Z,_INKEY
	JP	_PRINT

_SYS08:		;(BDOS)エコーなしコンソール入力
	CALL	_SYS07
	CP	3
	CALL	Z,_BREAK
	CP	013H		;CTRL+S
	RET	NZ
PAUSE:
	CALL	KEYBC

BREAK:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,BREAKX
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CALL	C,_BREAK
	POP	AF
	RET
KEYBC:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,KILBUF
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET
FLGET:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	CALL	_FFLUSH
	JR	FLGET1
CBIOS_SWC1	EQU	$-1
	CALL	GETVRAM

	PUSH	HL
	LD	IX,RDVRM
	CALL	CALSLT_R
	POP	HL
	LD	(SWCCHR),A

	PUSH	HL
	LD	A,0FFH
	LD	IX,WRTVRM
	CALL	CALSLT_R
	POP	HL

	PUSH	HL
FLGET1:
	LD	IX,CHGET
	CALL	CALSLT_R
	JR	FLGET2
CBIOS_SWC2	EQU	$-1
	POP	HL

	PUSH	AF
	LD	A,0
SWCCHR	EQU	$-1
	LD	IX,WRTVRM
	CALL	CALSLT_R
	POP	AF
FLGET2:
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CP	3
	RET	NZ
	JP	CPM_BOOT

INKEY:
	CALL	CPM_CONST
	RET	Z
	JR	FLGET

_SYS0A:		;(BDOS)文字列入力
	PUSH	BC
	PUSH	HL
	PUSH	DE
	CALL	GETL
	LD	DE,KBUF
	POP	HL
	PUSH	HL
	LD	C,0
	JR	NC,SAX0
	INC	HL
	INC	HL
	JR	SAX4
SAX0:
	LD	B,(HL)
	INC	HL
SAX1:
	INC	HL
	LD	A,(DE)
	INC	DE
	OR	A
	JR	NZ,SAX2
SAX4:
	LD	(HL),00DH
	JR	SAX3
SAX2:
	LD	(HL),A
	INC	C
	DJNZ	SAX1
SAX3:
	POP	DE
	LD	A,C
	INC	DE
	LD	(DE),A
	DEC	DE
	POP	HL
	POP	BC
	AND	A
	RET
GETL:
	PUSH	IX
	PUSH	IY

	LD	HL,(CSRY)
	LD	(FSTPOS),HL
GETL1:
	CALL	_SYS08
	CP	9
	JR	NZ,GETL1V
	LD	HL,KBUF
	CALL	MSX
	JR	GETL1
GETL1V:
	LD	E,A
	CP	8
	CALL	Z,CTRL02
	CP	020H
	CALL	NC,INSERT
	CALL	MSGA1

	LD	A,E
	CP	00DH
	JR	NZ,GETL1

	LD	DE,KBUF
	LD	A,(LINLEN)
	LD	B,A
	CALL	ZERO_MEMORY_DE

	LD	HL,(FSTPOS)
	LD	A,(CSRY)
	LD	L,A
	PUSH	HL
	CALL	LOC0
	LD	C,L
	LD	B,H
	POP	HL
	LD	A,(LINLEN)
	SUB	H
	DEC	A
	LD	E,A
	LD	HL,KBUF
GETL2:
	CALL	_SCRN
	INC	BC
	LD	(HL),A
	INC	HL
	DEC	E
	JR	NZ,GETL2
	CALL	MSG_CR

	POP	IY
	POP	IX
GETL3:
	DEC	HL
	LD	A,(HL)
	CP	021H
	RET	NC
	LD	(HL),0
	DEC	D
	JR	NZ,GETL3
	RET

_SYS0B:		;(BDOS)コンソール状態のチェック
CONSTX:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHSNS
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	RET

_SYS2A:		;(BDOS)日付の獲得
	LD	C,11		;年/Year→HL
	CALL	RDCLK_BCD
	LD	L,A
	LD	H,0
	LD	A,(EXBRSA)
	OR	A
	JR	Z,SX2A1
	LD	DE,1980		;1980年〜2079年
	ADD	HL,DE
SX2A1:
	LD	C,9		;月/Month→D
	CALL	RDCLK_BCD
	LD	D,A

	LD	C,7		;日/Day→E
	CALL	RDCLK_BCD
	LD	E,A

	LD	C,6		;曜日/Week→A
RDCLK:
	PUSH	IX
	LD	IX,REDCLK
WRCLK1:
	LD	A,(EXBRSA)
	OR	A
	JR	Z,RDCLK1	;MSX1
	PUSH	IY
	LD	IYH,A
	LD	A,B
	CALL	CALSLTR
	POP	IY
RDCLK1:
	POP	IX
	RET
WRCLK:
	PUSH	IX
	LD	IX,WRTCLK
	JR	WRCLK1

RDCLK_BCD:
	CALL	RDCLK		;1の位
	LD	B,A
	INC	C
	CALL	RDCLK		;10の位
	ADD	A,A		;*2
	LD	C,A
	ADD	A,A		;*4
	ADD	A,A		;*8
	ADD	A,C		;*10(8+2)
	ADD	A,B		;1の位
	RET

_SYS2C:		;(BDOS)時刻の獲得
	LD	BC,00115H		;12時間計/24時間計の設定を24時間計に
	CALL	WRCLK
	LD	C,4		;H=時/Hour
	CALL	RDCLK_BCD
	LD	H,A
	LD	C,2		;L=分/Minute
	CALL	RDCLK_BCD
	LD	L,A
	LD	C,0		;D=秒/Second
	CALL	RDCLK_BCD
	LD	D,A
	XOR	A		;E=1/100秒
	LD	E,A
	RET

POS:
	PUSH	AF
	LD	HL,(CSRY)
	LD	A,L
	LD	L,H
	LD	H,A
	INC	L
	INC	H
	POP	AF
	RET

LOC:
	PUSH	AF
	PUSH	HL
	LD	A,L
	LD	L,H
	LD	H,A
	DEC	L
	DEC	H
	PUSH	IX
	LD	IX,POSIT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	AF
	RET
SCRN:
	PUSH	HL
	PUSH	IX

	LD	L,C
	LD	H,B
	LD	IX,RDVRM
	CALL	CALSLT_R

	POP	IX
	POP	HL
	RET

CTRL02:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	ADD	A,2
	LD	B,A	;カーソルより右の文字数
	LD	H,C	;LINLEN
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
C8X1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	DEC	HL
	LD	D,C
	DJNZ	C8X1
	POP	DE
	POP	AF
	RET

INSERT:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	INC	A
	LD	B,A	;カーソルより右の文字数
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
INS1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	INC	HL
	LD	D,C
	DJNZ	INS1
	POP	DE
	POP	AF
	RET

CONOUT1:
	LD	E,C
	JP	_PRINT

GETVRAM:
	LD	HL,(CSRY)
LOC0:
	DEC	L
	DEC	H
	LD	C,H	;CSRX-1
	LD	E,L	;CSRY-1
LOC1:
	LD	A,(LINLEN)
	LD	H,A
	LD	D,0
	LD	L,D	;0
	LD	B,8
LOC2:
	ADD	HL,HL
	JR	NC,LOC3
	ADD	HL,DE
LOC3:
	DJNZ	LOC2
	ADD	HL,BC
	RET

CALSLT_R:
	PUSH	IY
	LD	IY,(EXPTBL-1)	;メインROMスロット
	CALL	CALSLTR
	POP	IY
	RET

CALLF:
	EX	AF,AF'
	EX	(SP),HL
	LD	A,(HL)
	LD	IYH,A
	INC	HL
	LD	A,(HL)
	LD	IXL,A
	INC	HL
	LD	A,(HL)
	LD	IXH,A
	INC	HL
	EX	(SP),HL
	EX	AF,AF'
CALSLTR:
	EX	AF,AF'
	LD	A,_CALSLT
;
;メインROMのインタースロットコールを呼び出す
;in
;A:インタースロットコールのアドレス
;A':インタースロットコールの際に渡すAの値
;
SLTCALL:
	DI
	LD	(SLTCALLX),A
	XOR	A
	LD	(SLTCALLX_H),A
	LD	(CSSP),SP
	LD	A,(CSSP+1)
	CP	040H+1
	JR	C,CALSLTR1
	CALL	CALSLTR2
	EI
	RET
CALSLTR1:
	LD	SP,BUF
	CALL	CALSLTR2
	LD	SP,0
CSSP	EQU	$-2
	EI
	RET
CALSLTR2:
	PUSH	BC
	PUSH	HL
	LD	A,(SLTCALLX)
	CP	_CALSLT
	JR	NZ,CALSLTR3
	LD	A,(EXPTBL)
	CP	IYH
	JR	NZ,CALSLTR3	;_CALSLTでメインROMの場合はIXをコールする
	LD	(SLTCALLX),IX
CALSLTR3:
	LD	A,(EXPTBL)	;メインROMに切り替える
	CALL	ENASLT0
	POP	HL
	POP	BC
	PUSH	AF		;A:元のスロット
	EX	AF,AF'
	CALL	_CALSLT		;自己書き換え
SLTCALLX	EQU	$-2
SLTCALLX_H	EQU	$-1
	EX	AF,AF'
	POP	AF
	EX	AF,AF'		;裏A:元のスロット
	PUSH	BC
	PUSH	HL
	EX	AF,AF'
	CALL	ENASLT0
	EX	AF,AF'
	POP	HL
	POP	BC
	RET

RDSLTR:
	EX	AF,AF'
	LD	A,_RDSLT
	JR	SLTCALL

WRSLTR:
	EX	AF,AF'
	LD	A,_WRSLT
	JR	SLTCALL

ENASLT1:
	EX	AF,AF'
	LD	A,_ENASLT
	JR	SLTCALL
ENASLTR:
	BIT	7,H
	JR	NZ,ENASLT1
	BIT	6,H
	JR	NZ,ENASLT1
;
;ページ0専門のENASLT
;in
;A←スロット
;out
;A←切り替え前のスロット
;破壊
;BCHL
;
ENASLT0:
	DI
	LD	L,A
	AND	3
	LD	C,A
	IN	A,(0A8H)
	LD	H,A
	AND	0FCH
	OR	C
	OUT	(0A8H),A
	LD	A,H
	AND	3
	BIT	7,L
	RET	Z
	OR	080H
	LD	H,A
	LD	A,L
	RRCA
	RRCA
	AND	3
	LD	C,A
	LD	A,(0FFFFH)
	CPL
	LD	B,A
	AND	0FCH
	OR	C
	LD	(0FFFFH),A
	LD	A,B
	AND	3
	ADD	A,A
	ADD	A,A
	OR	H
	LD	C,A

	LD	A,L
	AND	3
	ADD	A,LOW SLTTBL
	LD	L,A
	LD	H,HIGH SLTTBL
	LD	(HL),B
	LD	A,C
	RET

INT38H:
	PUSH	AF
	PUSH	BC
	PUSH	HL
	LD	HL,010000H-04100H	;メインROMを呼び出すのでスタックがPage1に被っていないかチェック
	ADD	HL,SP
	JR	C,INT38H1
	LD	HL,(INTSP)		;別の割り込み中の場合は飛ばす
	LD	A,H
	OR	L
	JR	NZ,INT38H2

	LD	(INTSP),SP	;スタックポインタがページ1
	LD	SP,BUF
	LD	A,(EXPTBL)	;メインROMに切り替える
	CALL	ENASLT0
	CALL	00038H
	CALL	ENASLT0
	LD	SP,0
INTSP	EQU	$-2
	LD	HL,0
	LD	(INTSP),HL
	JR	INT38H2

INT38H1:
	LD	A,(EXPTBL)	;メインROMに切り替える
	CALL	ENASLT0
	CALL	00038H
	CALL	ENASLT0
INT38H2:
	POP	HL
	POP	BC
	POP	AF
	EI
	RET





