
;	LSX-Dodgers IO
;	MSX

_SYS04	EQU	_ERROR		;(BDOS)外部出力
_SYS05	EQU	_ERROR		;(BDOS)プリンタ出力
_SYS1C	EQU	_ERROR		;(BDOS)ディスクの書き込み禁止化
_SYS1E	EQU	_ERROR		;(BDOS)ファイル属性の設定
_SYS20	EQU	_ERROR		;(BDOS)利用者番号の設定と読み出し
_SYS2E	EQU	_ERROR		;(BDOS)ベリファイ・フラグの設定

INT	EQU	0
INTCTCE	EQU	0
INTCTC2	EQU	0

CTRL00	EQU	0
CTRL03	EQU	0
CTRL04	EQU	0
CTRL05	EQU	0
CTRL06	EQU	0
CTRL07	EQU	0
CTRL08	EQU	0
CTRL09	EQU	0
CTRL0A	EQU	0
CTRL0B	EQU	0
CTRL0C	EQU	0
CTRL0D	EQU	0
CTRL0E	EQU	0
CTRL12	EQU	0
CTRL1B	EQU	0
CTRL1C	EQU	0
CTRL1D	EQU	0
CTRL1E	EQU	0
CTRL1F	EQU	0

PRINT:
	LD	A,E
	JR	MSG_A

_SYS01:		;(BDOS)コンソール入力
	CALL	_SYS07
MSG_A:
	CP	3
	JR	Z,MSG_BR
MSGA1:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHPUT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
MSGA2:
	POP	AF
	RET
MSG_BR:
	PUSH	AF
	LD	A,(CSRX)
	CP	2
	JR	C,MSGA2
	POP	AF
MSG_CR:
	PUSH	AF
	LD	A,00DH
	CALL	MSGA1
	LD	A,00AH
	CALL	MSGA1
	POP	AF
	RET

_SYS02:		;(BDOS)コンソール出力
	CALL	BREAK
	JP	_PRINT

_SYS06:		;(BDOS)コンソール直接入出力
	LD	A,E
	INC	A
	JP	Z,_INKEY
	JP	_PRINT

_SYS08:		;(BDOS)エコーなしコンソール入力
	CALL	_SYS07
	CP	3
	CALL	Z,_BREAK
	CP	013H		;CTRL+S
	RET	NZ
PAUSE:
	CALL	KEYBC

BREAK:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,BREAKX
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CALL	C,_BREAK
	POP	AF
	RET
KEYBC:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,KILBUF
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET
FLGET:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	CALL	_FFLUSH
	JR	FLGET1
CBIOS_SWC1	EQU	$-1
	CALL	GETVRAM

	PUSH	HL
	LD	IX,RDVRM
	CALL	CALSLT_R
	POP	HL
	LD	(SWCCHR),A

	PUSH	HL
	LD	A,0FFH
	LD	IX,WRTVRM
	CALL	CALSLT_R
	POP	HL

	PUSH	HL
FLGET1:
	LD	IX,CHGET
	CALL	CALSLT_R
	JR	FLGET2
CBIOS_SWC2	EQU	$-1
	POP	HL

	PUSH	AF
	LD	A,0
SWCCHR	EQU	$-1
	LD	IX,WRTVRM
	CALL	CALSLT_R
	POP	AF
FLGET2:
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CP	3
	RET	NZ
	JP	CPM_BOOT

INKEY:
	CALL	CPM_CONST
	RET	Z
	JR	FLGET

_SYS0A:		;(BDOS)文字列入力
	PUSH	BC
	PUSH	HL
	PUSH	DE
	CALL	GETL
	LD	DE,KBUF
	POP	HL
	PUSH	HL
	LD	C,0
	JR	NC,SAX0
	INC	HL
	INC	HL
	JR	SAX4
SAX0:
	LD	B,(HL)
	INC	HL
SAX1:
	INC	HL
	LD	A,(DE)
	INC	DE
	OR	A
	JR	NZ,SAX2
SAX4:
	LD	(HL),00DH
	JR	SAX3
SAX2:
	LD	(HL),A
	INC	C
	DJNZ	SAX1
SAX3:
	POP	DE
	LD	A,C
	INC	DE
	LD	(DE),A
	DEC	DE
	POP	HL
	POP	BC
	AND	A
	RET
GETL:
	PUSH	IX
	PUSH	IY

	LD	HL,(CSRY)
	LD	(FSTPOS),HL
GETL1:
	CALL	_SYS08
	CP	9
	JR	NZ,GETL1V
	LD	HL,KBUF
	CALL	MSX
	JR	GETL1
GETL1V:
	LD	E,A
	CP	8
	CALL	Z,CTRL02
	CP	020H
	CALL	NC,INSERT
	CALL	MSGA1

	LD	A,E
	CP	00DH
	JR	NZ,GETL1

	LD	DE,KBUF
	LD	A,(LINLEN)
	LD	B,A
	CALL	ZERO_MEMORY_DE

	LD	HL,(FSTPOS)
	LD	A,(CSRY)
	LD	L,A
	PUSH	HL
	CALL	LOC0
	LD	C,L
	LD	B,H
	POP	HL
	LD	A,(LINLEN)
	SUB	H
	DEC	A
	LD	E,A
	LD	HL,KBUF
GETL2:
	CALL	_SCRN
	INC	BC
	LD	(HL),A
	INC	HL
	DEC	E
	JR	NZ,GETL2
	CALL	MSG_CR

	POP	IY
	POP	IX
GETL3:
	DEC	HL
	LD	A,(HL)
	CP	021H
	RET	NC
	LD	(HL),0
	DEC	D
	JR	NZ,GETL3
	RET

_SYS0B:		;(BDOS)コンソール状態のチェック
CONSTX:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHSNS
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	RET

_SYS2A:		;(BDOS)日付の獲得
	LD	C,11		;年/Year→HL
	CALL	RDCLK_BCD
	LD	L,A
	LD	H,0
	LD	A,(EXBRSA)
	OR	A
	JR	Z,SX2A1
	LD	DE,1980		;1980年〜2079年
	ADD	HL,DE
SX2A1:
	LD	C,9		;月/Month→D
	CALL	RDCLK_BCD
	LD	D,A

	LD	C,7		;日/Day→E
	CALL	RDCLK_BCD
	LD	E,A

	LD	C,6		;曜日/Week→A
RDCLK:
	PUSH	IX
	LD	IX,REDCLK
WRCLK1:
	LD	A,(EXBRSA)
	OR	A
	JR	Z,RDCLK1	;MSX1
	PUSH	IY
	LD	IYH,A
	LD	A,B
	CALL	_CALSLT
	POP	IY
RDCLK1:
	POP	IX
	RET
WRCLK:
	PUSH	IX
	LD	IX,WRTCLK
	JR	WRCLK1

RDCLK_BCD:
	CALL	RDCLK		;1の位
	LD	B,A
	INC	C
	CALL	RDCLK		;10の位
	ADD	A,A		;*2
	LD	C,A
	ADD	A,A		;*4
	ADD	A,A		;*8
	ADD	A,C		;*10(8+2)
	ADD	A,B		;1の位
	RET

_SYS2C:		;(BDOS)時刻の獲得
	LD	BC,00115H		;12時間計/24時間計の設定を24時間計に
	CALL	WRCLK
	LD	C,4		;H=時/Hour
	CALL	RDCLK_BCD
	LD	H,A
	LD	C,2		;L=分/Minute
	CALL	RDCLK_BCD
	LD	L,A
	LD	C,0		;D=秒/Second
	CALL	RDCLK_BCD
	LD	D,A
	XOR	A		;E=1/100秒
	LD	E,A
	RET

POS:
	PUSH	AF
	LD	HL,(CSRY)
	LD	A,L
	LD	L,H
	LD	H,A
	INC	L
	INC	H
	POP	AF
	RET

LOC:
	PUSH	AF
	PUSH	HL
	LD	A,L
	LD	L,H
	LD	H,A
	DEC	L
	DEC	H
	PUSH	IX
	LD	IX,POSIT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	AF
	RET
SCRN:
	PUSH	HL
	PUSH	IX

	LD	L,C
	LD	H,B
	LD	IX,RDVRM
	CALL	CALSLT_R

	POP	IX
	POP	HL
	RET

CTRL02:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	ADD	A,2
	LD	B,A	;カーソルより右の文字数
	LD	H,C	;LINLEN
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
C8X1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	DEC	HL
	LD	D,C
	DJNZ	C8X1
	POP	DE
	POP	AF
	RET

INSERT:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	INC	A
	LD	B,A	;カーソルより右の文字数
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
INS1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	INC	HL
	LD	D,C
	DJNZ	INS1
	POP	DE
	POP	AF
	RET

CONOUT1:
	LD	E,C
	JP	_PRINT

GETVRAM:
	LD	HL,(CSRY)
LOC0:
	DEC	L
	DEC	H
	LD	C,H	;CSRX-1
	LD	E,L	;CSRY-1
LOC1:
	LD	A,(LINLEN)
	LD	H,A
	LD	D,0
	LD	L,D	;0
	LD	B,8
LOC2:
	ADD	HL,HL
	JR	NC,LOC3
	ADD	HL,DE
LOC3:
	DJNZ	LOC2
	ADD	HL,BC
	RET

CALSLT_R:
	PUSH	IY
	LD	IY,(EXPTBL-1)	;メインROMスロット
	CALL	_CALSLT
	POP	IY
	RET

;
; インタースロットコール
; ページ3 に配置

AT_GETSLT	EQU	04000H+$$

;
;	現在のスロットを知る
;in
;HL←上位2ビットで切り替えるページを指定
;out
;A→スロット
;※ROM、RAMの両方のルーチンを使うため絶対番地を使わない
_GETSLT:
	PUSH	HL
	IN	A,(0A8H)

	BIT	7,H
	JR	NZ,GETSLT_HIGH
	BIT	6,H
	LD	HL,EXPTBL
	JR	NZ,GETSLT_40H
GETSLT_00H:				;ページ0
	AND	3
	ADD	A,L
	LD	L,A
	BIT	7,(HL)
	JR	Z,GETSLT_1
	ADD	A,SLTTBL-EXPTBL		;拡張スロットをワークアリアから取得
	LD	L,A
	LD	A,(HL)
GETSLT_3:
	RLCA
	RLCA
GETSLT_2:
	AND	00CH
	LD	H,A
	LD	A,L
	SUB	LOW SLTTBL - 080H
	OR	H
	POP	HL
	RET
GETSLT_1:				;スロットは拡張されていない
	SUB	LOW EXPTBL
	POP	HL
	RET

GETSLT_40H:				;ページ1
	RRCA
	RRCA
	AND	3
	ADD	A,L
	LD	L,A
	BIT	7,(HL)
	JR	Z,GETSLT_1
	ADD	A,SLTTBL-EXPTBL
	LD	L,A
	LD	A,(HL)
	JR	GETSLT_2

GETSLT_HIGH:
	BIT	6,H
	LD	HL,EXPTBL
	JR	NZ,GETSLT_C0H
GETSLT_80H:				;ページ2
	RRCA
	RRCA
	RRCA
	RRCA
	AND	3
	ADD	A,L
	LD	L,A
	BIT	7,(HL)
	JR	Z,GETSLT_1
	ADD	A,SLTTBL-EXPTBL
	LD	L,A
	LD	A,(HL)
	RRCA
	RRCA
	JR	GETSLT_2

GETSLT_C0H:				;ページ3
	RLCA
	RLCA
	AND	3
	ADD	A,L
	LD	L,A
	BIT	7,(HL)
	JR	Z,GETSLT_1
	ADD	A,SLTTBL-EXPTBL
	LD	L,A
	LD	A,(HL)
	RLCA
	RLCA
	JR	GETSLT_3

SAME_SLOT_00H:			;ページ0とページ3のスロットが同一
	OUT	(0A8H),A
	LD	A,(0FFFFH)	;拡張スロットの切り替え
	CPL
	AND	0FCH		;ページ0マスク
	OR	C
	LD	(0FFFFH),A
	LD	(HL),A
	RET

ENASLT_HIGH:
	BIT	6,H
	RET	NZ			;ページ3はスロット切り替え不可
;
;ページ2専門のENASLT
;in
;A←スロット
;破壊
;ABCHL
;
ENASLT_80H:
	DI
	LD	L,A
	AND	3
	RLCA
	RLCA
	RLCA
	RLCA
	LD	C,A
	IN	A,(0A8H)
	LD	B,0CFH	;ページ2マスク
	AND	B
	OR	C
	BIT	7,L
	JP	Z,ENASLT_NOEXT
			;拡張スロットの処理
	PUSH	DE
	CALL	ENATBL_BIOS_CHECK
	RLCA
	RLCA
	LD	C,A
	LD	A,E
	CALL	ENASUB
	LD	(HL),E
	POP	DE
	RET
;
;ENASLT
;in
;A←スロット
;HL←上位2ビットで切り替えるページを指定
;破壊
;ABCHL
;
ENASLT:
	BIT	7,H
	JR	NZ,ENASLT_HIGH
	BIT	6,H
	JR	NZ,ENASLT_40H
_ENASLT_00H:
	DI
	LD	H,A
	AND	3
	LD	C,A
	IN	A,(0A8H)
	AND	0FCH	;ページ0マスク
	OR	C
	BIT	7,H
	JR	Z,ENASLT_NOEXT
			;拡張スロットの処理
	PUSH	DE
	LD	B,H
	CALL	ENATBL
	RRCA
	RRCA
	LD	C,A
	LD	A,E
	CP	D
	POP	DE
	JR	Z,SAME_SLOT_00H	;ページ0とページ3のスロットが同一の場合
	LD	H,B
ENASLT_SUB:
	LD	A,H		;ページ3でスロットを指定するためにページ1のROMのルーチンを使う
	PUSH	IX
	LD	IX,ENASLT_00H
INT38H_SUB1:
	PUSH	IY
	LD	IY,(ROM_SLT-1)
	CALL	CALSLT
	POP	IY
	POP	IX
	RET

_ENASLT_00H_S:
	LD	(ENASLT_SP1),SP
	LD	SP,SPBUF
	CALL	ENASLT_SUB
	LD	SP,0
ENASLT_SP1	EQU	$-2
	RET

INT38H_SUB:
	PUSH	IX
	LD	IX,INT38H_ROM
	JR	INT38H_SUB1

ENASLT_BIOS:
	POP	DE
	LD	A,L
	CALL	_ENASLT
	POP	DE
	RET

ENATBL_BIOS_CHECK:
	LD	D,A
	LD	A,(0000H)
	CP	0F3H	;0000H が DI の場合はページ0は BIOS とみなす
	JR	Z,ENASLT_BIOS
	LD	H,L
	LD	A,D
ENATBL:
	LD	D,A	;基本スロットに出力する値
			;対象の拡張スロットを切り替えるために基本スロットを切り替える
	LD	A,H
	AND	3
	LD	C,A	;C=スロット#

	;スロットテーブル
	LD	L,LOW SLTTBL
	ADD	A,L
	LD	L,A
			;対象の拡張スロットを切り替えるために基本スロットを切り替える
	LD	A,C	;C=スロット#
	RRCA
	RRCA		;ページ3
	LD	C,A
	LD	A,D	;基本スロットに出力する値
	AND	03FH	;ページ3マスク
	OR	C
	LD	E,A	;基本スロットでページ3にスロットを割り当てる
	LD	A,H
	AND	00CH
	LD	H,HIGH SLTTBL
	RET
ENASLT_NOEXT_00H:

ENASLT_NOEXT:				;スロットが拡張されていない場合
	OUT	(0A8H),A
	RET

;
;ページ1専門のENASLT
;in
;A←スロット
;破壊
;ABCHL
;
ENASLT_40H:
	DI
	LD	L,A
	AND	3
	RLCA
	RLCA
	LD	C,A
	IN	A,(0A8H)
	LD	B,0F3H	;ページ1マスク
	AND	B
	OR	C
	BIT	7,L
	JR	Z,ENASLT_NOEXT
			;拡張スロットの処理
	PUSH	DE
	CALL	ENATBL_BIOS_CHECK
	LD	C,A
	LD	A,E
	CALL	ENASUB
	LD	(HL),E
	POP	DE
	RET

CALLF:
	EX	(SP),HL
	PUSH	AF
	LD	A,(HL)
	LD	IYH,A
	INC	HL
	LD	A,(HL)
	LD	IXL,A
	INC	HL
	LD	A,(HL)
	LD	IXH,A
	INC	HL
	POP	AF
	EX	(SP),HL
CALSLT:
	DI
	EX	AF,AF'
	PUSH	AF		;裏AFを保存
	PUSH	HL
	LD	HL,08002H
	ADD	HL,SP
	POP	HL
	JR	NC,CALSLT_1
	CALL	CALSLT_2
CALSLT_E:
	EX	AF,AF'
	POP	AF		;保存した裏AF
	EX	AF,AF'
	RET
CALSLT_1:
	LD	(CSSP),SP
	LD	SP,SPBUF
	CALL	CALSLT_2
	LD	SP,0
CSSP	EQU	$-2
	JR	CALSLT_E

CALSLT_2:
	PUSH	HL
	LD	A,IXH
	LD	H,A
	CALL	_GETSLT
	CP	IYH
	JR	Z,CALSLT_3
	PUSH	BC
	PUSH	AF
	LD	A,IYH
	CALL	ENASLT
	POP	AF
	POP	BC
	LD	L,A
	LD	A,IXH
	LD	H,A
	EX	(SP),HL
	EX	AF,AF'
	CALL	JP_IX
	EX	(SP),HL
	PUSH	BC
	EX	AF,AF'
	LD	A,L
	CALL	ENASLT
	EX	AF,AF'
	POP	BC
	POP	HL
	EI
	RET

CALSLT_3:
	POP	HL
	EX	AF,AF'
	JP	(IX)

RDSLT:
	DI
	PUSH	DE
	LD	D,A
	CALL	_GETSLT
	CP	D
	JR	Z,RDSLT1
	LD	E,A
	LD	A,D
	PUSH	HL
	CALL	ENASLT
	POP	HL
	LD	B,(HL)
	PUSH	BC
	LD	A,E
	PUSH	HL
	CALL	ENASLT
	POP	HL
	POP	AF
	POP	DE
	RET
RDSLT1:
	LD	A,(HL)
	POP	DE
	RET

WRSLT:
	DI
	PUSH	DE
	LD	D,A
	CALL	_GETSLT
	CP	D
	JR	Z,WRSLT1
	PUSH	AF
	PUSH	HL
	LD	A,D
	CALL	ENASLT
	POP	HL
	LD	(HL),E
	POP	AF
	PUSH	HL
	CALL	ENASLT
	POP	HL
	POP	DE
	RET

WRSLT1:
	LD	(HL),E
	POP	DE
	RET

INT38H:
	DI
	PUSH	AF
	PUSH	BC
	PUSH	HL
	LD	HL,08002H
	ADD	HL,SP
	JR	C,INT38H1
	LD	(INTSP),SP	;スタックポインタ保存
	LD	SP,SPBUF
	CALL	INT38H_SUB
	LD	SP,0
INTSP	EQU	$-2
	XOR	A
INT38H1:
	CALL	C,INT38H_SUB
	POP	HL
	POP	BC
	POP	AF
	EI
	RET
