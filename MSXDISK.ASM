
;	LSX-Dodgers DISK
;	MSX

;	ROM DISK DRIVER
;	1セクタ可変(256/512/1024)

WTTRK:
FDWT:
	SCF
	RET
FDRD:
	LD	(DRDSP),SP
	LD	A,(DRDSPH)	;スタックがカートリッジと被っていないかチェック
	CP	07FH
	JR	C,FDRD1
	CP	0C1H
	JR	NC,FDRD1
	LD	SP,BUF
FDRD1:
	PUSH	BC
	PUSH	DE

	PUSH	HL
	EX	DE,HL
	LD	B,(IX+DPB_BPS)
EADR1:
	RR	B
	JR	C,EADR2
	ADD	HL,HL
	JR	EADR1
EADR2:
	PUSH	HL	;あとでDEで受け取る
	ADD	HL,HL	;64KB→32KB
	ADD	HL,HL	;32KB→16KB
ASCII16K1:
	ADD	HL,HL	;16KB→8KB	;ASCII-16Kの場合はここがNOPに置き換えられる

	LD	A,(IX+DPB_MAXCYL)	;16KB単位でカートリッジ内のディスクが存在する位置を指定
ASCII16K2:
	ADD	A,A			;ASCII-16Kの場合はここがNOPに置き換えられる
	ADD	A,H
	LD	E,A
	LD	A,(ROM_SLT)
	LD	HL,BANK2_SEL
ROM8000H	EQU	$-2
	CALL	WRSLT

	LD	H,080H
	LD	A,(ROM_SLT)	;カートリッジROMに切り替える
	CALL	ENASLT

	POP	DE
	POP	HL		;HLは読み出し先のメモリアドレス

	LD	A,E
	AND	01FH		;セグメントのサイズでフィルタする(8K:1F/16K:3F)
ASCII16K3	EQU	$-1
	LD	E,0		;DEはROMディスクから読み出すアドレス
	ADD	A,080H
	LD	D,A

	DB	03EH
	AND	A
	LD	(DRD_SWC3),A
	LD	A,H		;読み出し先がカートリッジと被っていないかチェック
	CP	07BH
	JR	C,DRD2
	CP	0C1H
	JR	NC,DRD2
	LD	(DRD_SWC1),HL	;被っている場合はデータバッファをフラッシュして一時的に使う
	LD	HL,DTBUF
	DB	03EH
	SCF
	LD	(DRD_SWC3),A
	CALL	FFLUSHBUF
DRD2:
	LD	A,(IX+DPB_BPS)	;BCは読み出すセクタサイズ
	AND	7
	LD	B,A
	LD	C,E	;E=0

	EX	DE,HL
	LDIR
	PUSH	DE
	LD	H,080H
	LD	A,(0F341H+2)	;メインRAMに戻す
	CALL	ENASLT
DRD_SWC3:
	AND	A		;自己書き換え)被っている場合はSCFになる
	JR	NC,DRD3

	LD	HL,DTBUF
	LD	DE,0
DRD_SWC1	EQU	$-2
	LD	A,(IX+DPB_BPS)	;BCは読み出すセクタサイズ
	AND	7
	LD	B,A
	LD	C,0
	LDIR
	POP	HL
	PUSH	DE
DRD3:
	POP	HL
	POP	DE
	INC	DE
	POP	BC
	DJNZ	FDRD1
	XOR	A
	LD	SP,0
DRDSP	EQU	$-2
DRDSPH	EQU	$-1
	EI
	RET

PDWT:
	SCF
	BIT	7,H
	JR	NZ,PDWX
	CALL	FFLUSHBUF
PDWT1:
	PUSH	BC
	PUSH	DE
	LD	DE,DTBUF
	LD	A,(IX+DPB_BPS)	;BCは読み出すセクタサイズ
	AND	7
	LD	B,A
	LD	C,0
	LDIR
	PUSH	HL
	LD	HL,DTBUF
	LD	B,1
	SCF
	CALL	PDWX
	JR	C,PDRWERR
	POP	HL
	POP	DE
	INC	DE
	POP	BC
	DJNZ	PDWT
	XOR	A
	RET
PDRD:
	AND	A
	BIT	7,H
	JR	NZ,PDWX
	CALL	FFLUSHBUF
PDRD1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,DTBUF
	LD	B,1
	AND	A
	CALL	PDWX
	JR	C,PDRWERR
	POP	DE
	LD	HL,DTBUF
	LD	A,(IX+DPB_BPS)	;BCは読み出すセクタサイズ
	AND	7
	LD	B,A
	LD	C,0
	LDIR
	EX	DE,HL
	POP	DE
	INC	DE
	POP	BC
	DJNZ	PDRD
	XOR	A
	RET
PDRWERR:
	POP	HL
	POP	DE
	POP	BC
	RET
PDWX:					;CF=0 READ CF=1 WRITE
	BIT	4,(IX+DPB_DEVICE)	;デバイス情報
	JR	NZ,PDWX1
	LD	C,(IX+DPB_FATID)	;メディアバイト
PDWX1:
	LD	A,(IX+DPB_UNITNO)	;デバイスドライバ内におけるユニット番号
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	PUSH	IY
	CALL	H_PHYD
	POP	IY
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	RET	C
PDWX2:
	INC	DE
	LD	A,(IX+DPB_BPS)	;BCは読み出すセクタサイズ
	AND	7
	ADD	A,H
	LD	H,A
	DJNZ	PDWX2
	XOR	A
	RET
