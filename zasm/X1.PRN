			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;	LSX-Dodgers for X1/turbo/Z
                      	;	Programmed by
                      	;	Gaku (Lovers/Tablacus)
                      	;X1DEF.ASM
                      	
                      	;	LSX-Dodgers DEF
                      	;	X1/turbo/Z
                      	
  0000                	MAC	EQU	0		;X1
                      	
  CB00                	RUN	EQU	0CB00H
  D106                	BDOS	EQU	0D106H
  EF00                	WORKAD	EQU	0EF00H
  F200                	FATBF	EQU	0F200H
  FA00                	DTBUF	EQU	0FA00H
  FE00                	KEYBF	EQU	0FE00H
  FF00                	KBUF	EQU	0FF00H
  FFCA                	EXTSP	EQU	0FFCAH
  FFD9                	TRAP38	EQU	0FFD9H
  0050                	WIDTH	EQU	80
  0018                	LINE	EQU	24
  0007                	COLORF	EQU	7
  0009                	COMS	EQU	9
                      	
                      	
                      	;LDDEF.ASM
                      	
                      	;	LSX-Dodgers DEF
                      	
  0001                	VER_1   EQU	1
  0004                	VER_2   EQU	4
  0006                	VER_3   EQU	6
  1D00                	MACW	EQU	MAC + 01D00H	;機種フラグとLD判別フラグ
  0000                	MACD	EQU	MAC + 00000H	;機種フラグとデバイスフラグ
  0146                	VER	EQU	VER_1 * 256 + VER_2 * 16 + VER_3
                      	
  0005                	SYSTEM	EQU	00005H
  000F                	JP_HL	EQU	0000FH
  005C                	FCB1	EQU	0005CH
  006C                	FCB2	EQU	0006CH
  005D                	FCB1FN	EQU	FCB1+1
  006D                	FCB2FN	EQU	FCB2+1
  0080                	DTA1	EQU	00080H
                      	
  FFCA                	EXTBIO	EQU	0FFCAH
                      	
                      	
                      	
  CAF9                		ORG	RUN-7
                      	
                      	;	MSX BINARY HEADER
  CAF9  FE            		DB	0FEH
                      	;	MSX BINARY START ADDRESS
  CAFA  00CB          		DW	RUN
                      	;	MSX BINARY END ADDRESS
  CAFC  00F2          		DW	LAST_ADR
                      	;	MSX BINARY EXEC ADDRESS
  CAFE  04CB          		DW	START
                      	;X1INIT.ASM
                      	
                      	;	LSX-Dodgers INIT
                      	;	X1/turbo/Z
                      	
  CB00  001D          		DW	MACW
  CB02  4601          		DW	VER
                      	
  CB04                	START:
  CB04  F3            		DI
  CB05  ED5E          		IM	2
  CB07  31CAFF        		LD	SP,EXTSP
  CB0A  CD18CB        		CALL	INIT		;NZならAUTOEXECを実行
  CB0D  210000        		LD	HL,0
  CB10  E5            		PUSH	HL
  CB11  C8            		RET	Z
  CB12  1192CE        		LD	DE,AUTOD
  CB15  C3FDEF        		JP	_COMANL
                      	
  CB18                	INIT:
  CB18  3E1E          		LD	A,01EH
  CB1A  D300          		OUT	(0),A
                      	
  CB1C  010000        		LD	BC,0
  CB1F  ED438CEF      		LD	(_CTC),BC
  CB23  01040A        		LD	BC,00A04H
  CB26  CD3CCE        		CALL	CHKCTC
  CB29  010407        		LD	BC,00704H
  CB2C  CD3CCE        		CALL	CHKCTC
  CB2F  01A81F        		LD	BC,01FA8H
  CB32  CD3CCE        		CALL	CHKCTC
  CB35  01A01F        		LD	BC,01FA0H
  CB38  CD3CCE        		CALL	CHKCTC
                      	;
  CB3B                	INISIO:
  CB3B  01911F        		LD	BC,01F91H	;INIT SIO
  CB3E  1601          		LD	D,1
  CB40  ED51          		OUT	(C),D
                      	
  CB42  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
  CB44  0E93          		LD	C,093H
  CB46  ED51          		OUT	(C),D
  CB48  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
  CB4A  0E99          		LD	C,099H		;INIT SIO
  CB4C  1601          		LD	D,1
  CB4E  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
  CB50  0E9B          		LD	C,09BH
  CB52  ED51          		OUT	(C),D
  CB54  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
                      	
  CB56  0E80          		LD	C,080H		;INIT DMA
  CB58  2106C3        		LD	HL,0C306H
  CB5B                	INIDMA:
  CB5B  ED61          		OUT	(C),H
  CB5D  2D            		DEC	L
  CB5E  20FB          		JR	NZ,INIDMA
                      	
  CB60  3EE4          		LD	A,0E4H
  CB62  CDFCDE        		CALL	COMOUT
  CB65  AF            		XOR	A
  CB66  CDD6DE        		CALL	OT49SB
                      	
  CB69  3EEF          		LD	A,INTVEC/256
  CB6B  ED47          		LD	I,A
                      	
  CB6D  ED4B8CEF      		LD	BC,(_CTC)
  CB71  0B            		DEC	BC
  CB72  0B            		DEC	BC
                      	
  CB73  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
  CB75  3EC0          		LD	A,CTC0
  CB77  ED79          		OUT	(C),A
                      	
  CB79  0C            		INC	C
  CB7A  3E47          		LD	A,047H
  CB7C  ED79          		OUT	(C),A
  CB7E  3E0D          		LD	A,13		;Baudrate 9600-13
  CB80  ED79          		OUT	(C),A
                      	
  CB82  79            		LD	A,C
  CB83  D610          		SUB	010H
  CB85  4F            		LD	C,A
                      	;	LD	(SIOAD+1),BC
                      	;	LD	(SIOAD0+1),BC
                      	;	LD	(SIOAD1+1),BC
                      	
  CB86  21E2CE        		LD	HL,SIODATA
  CB89                	SINIT1:
  CB89  7E            		LD	A,(HL)
  CB8A  23            		INC	HL
  CB8B  FEFF          		CP	0FFH
  CB8D  2804          		JR	Z,SINIT2
  CB8F  ED79          		OUT	(C),A
  CB91  18F6          		JR	SINIT1
  CB93                	SINIT2:
  CB93  3EE4          		LD	A,0E4H
  CB95  CDFCDE        		CALL	COMOUT
  CB98  3EC8          		LD	A,INTVEC
  CB9A  CDD6DE        		CALL	OT49SB
                      	;
  CB9D  01C41F        		LD	BC,01FC4H
  CBA0  3E0C          		LD	A,00CH
  CBA2  ED79          		OUT	(C),A
                      	
  CBA4  0EC0          		LD	C,0C0H
  CBA6  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
                      	
  CBA8  0C            		INC	C
  CBA9  3E28          		LD	A,40
  CBAB  ED79          		OUT	(C),A
                      	
  CBAD  0C            		INC	C
  CBAE  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
                      	
  CBB0  0C            		INC	C
  CBB1  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
                      	
  CBB3  0EC5          		LD	C,0C5H
  CBB5  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
                      	
  CBB7                	TEXT:
  CBB7  0EB0          		LD	C,0B0H
  CBB9  3E80          		LD	A,080H
  CBBB  ED79          		OUT	(C),A
  CBBD  1607          		LD	D,7
  CBBF  0EB9          		LD	C,0B9H
  CBC1  21DBCE        		LD	HL,TEXTDT
  CBC4                	TEXT1:
  CBC4  04            		INC	B
  CBC5  EDA3          		OUTI
  CBC7  0C            		INC	C
  CBC8  15            		DEC	D
  CBC9  20F9          		JR	NZ,TEXT1
  CBCB  01B01F        		LD	BC,01FB0H
  CBCE  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
                      	
  CBD0  0EC4          		LD	C,0C4H
  CBD2  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
                      	
  CBD4  01011A        		LD	BC,01A01H
  CBD7  ED78          		IN	A,(C)
  CBD9  E608          		AND	8
  CBDB  2809          		JR	Z,PRN
  CBDD  0E03          		LD	C,003H
  CBDF  3E0F          		LD	A,00FH
  CBE1  ED79          		OUT	(C),A
  CBE3  01040A        		LD	BC,00A04H
  CBE6                	PRN:
  CBE6  210000        		LD	HL,0
  CBE9  065C          		LD	B,05CH
  CBEB  3EFF          		LD	A,0FFH		;RST 38H
  CBED  CD54E1        		CALL	FILL_MEMORY
  CBF0  06A4          		LD	B,0A4H
  CBF2  AF            		XOR	A
  CBF3  CD54E1        		CALL	FILL_MEMORY
                      	
  CBF6  3EC3          		LD	A,0C3H		;JP
  CBF8  2103EF        		LD	HL,CPM_WBOOT
  CBFB  320000        		LD	(00000H),A
  CBFE  220100        		LD	(00001H),HL	;IPL
                      	
  CC01  21001D        		LD	HL,MACW	;3 bit7  DPB Compatible LA(1)
  CC04  220300        		LD	(00003H),HL	;  bit6-0 Machine X1(0)
                      	;				;4 LSX-Dodgers(01DH)
  CC07  2106D1        		LD	HL,BDOS
  CC0A  320500        		LD	(00005H),A
  CC0D  220600        		LD	(00006H),HL	;BDOS
                      	
  CC10  21D0DA        		LD	HL,BIOS
  CC13  221900        		LD	(00019H),HL	;BIOS-ROM
                      	
  CC16  21D9FF        		LD	HL,TRAP38	;TRAP38
  CC19  323800        		LD	(00038H),A
  CC1C  223900        		LD	(00039H),HL	;RST 038H
                      	
  CC1F  3EEF          		LD	A,CPM_BOOT/256
  CC21  320B00        		LD	(0000BH),A
                      	
  CC24  3EE9          		LD	A,0E9H		;JP (HL)
  CC26  320F00        		LD	(0000FH),A
                      	
  CC29  3A7DF0        		LD	A,(_FATBF+1)
  CC2C  321300        		LD	(00013H),A
                      	
  CC2F  3A7FF0        		LD	A,(_DTBUF+1)
  CC32  321700        		LD	(00017H),A
                      	
  CC35  3EFE          		LD	A,KEYBF/256
  CC37  321B00        		LD	(0001BH),A
                      	
  CC3A  3EFF          		LD	A,KBUF/256
  CC3C  321F00        		LD	(0001FH),A
                      	
  CC3F  214601        		LD	HL,VER
  CC42  225A00        		LD	(0005AH),HL
                      	
  CC45  3EE6          		LD	A,0E6H
  CC47  CDFCDE        		CALL	COMOUT
  CC4A  CDE1DE        		CALL	IN49SB
  CC4D  F5            		PUSH	AF
  CC4E  CDE1DE        		CALL	IN49SB
  CC51  F1            		POP	AF
  CC52  F5            		PUSH	AF
  CC53  E612          		AND	012H
  CC55  2005          		JR	NZ,KEYR
  CC57  3E01          		LD	A,1
  CC59  3292CD        		LD	(KEYREP+1),A
  CC5C                	KEYR:
  CC5C  F1            		POP	AF
  CC5D  E603          		AND	3
  CC5F  280E          		JR	Z,NON
                      	
  CC61  01D03F        		LD	BC,03FD0H
  CC64  ED49          		OUT	(C),C
  CC66  3E37          		LD	A,037H
  CC68  D3D0          		OUT	(0D0H),A
  CC6A  ED78          		IN	A,(C)
  CC6C  B9            		CP	C
  CC6D  285F          		JR	Z,TURBO
  CC6F                	NON:
  CC6F  21EECE        		LD	HL,AT_SCR1
  CC72  1186DF        		LD	DE,SCR1
  CC75  019400        		LD	BC,SCRNE-SCR1	;転送先のサイズ確認用
  CC78  019100        		LD	BC,AT_SCRE-AT_SCR1
  CC7B  EDB0          		LDIR
                      	
  CC7D  217FCF        		LD	HL,AT_WTTRK
  CC80  1140EE        		LD	DE,WTTRK
  CC83  01B900        		LD	BC,DMAE-WTTRK	;転送先のサイズ確認用
  CC86  01B100        		LD	BC,AT_CPUE-AT_WTTRK
  CC89  EDB0          		LDIR
                      	
  CC8B  2130D0        		LD	HL,AT_EDWT
  CC8E  117BD7        		LD	DE,EDWT
  CC91  014B00        		LD	BC,EMME-EDWT	;転送先のサイズ確認用
  CC94  014600        		LD	BC,AT_EMME-AT_EDWT
  CC97  EDB0          		LDIR
                      	
  CC99  21A1EE        		LD	HL,AT_DRD+AT_R
  CC9C  22ECEF        		LD	(_FDRD+1),HL
  CC9F  2146EE        		LD	HL,AT_DWT+AT_R
  CCA2  22EFEF        		LD	(_FDWT+1),HL
  CCA5  218FD7        		LD	HL,AT_EDRD+AT_RE
  CCA8  2282F1        		LD	(EMMRD),HL
  CCAB  217BD7        		LD	HL,AT_EDWT+AT_RE
  CCAE  2284F1        		LD	(EMMWR),HL
  CCB1  21E4BE        		LD	HL,AT_SCRN-AT_RS
  CCB4  22D1EF        		LD	(_SCRN+1),HL
                      	
  CCB7  3E27          		LD	A,027H			;ノンターボはMFM切り替えを行わない
  CCB9  3212F1        		LD	(DEVICE+012H),A		;Aドライブ
  CCBC  3232F1        		LD	(DEVICE+012H+32),A	;Bドライブ
  CCBF  3252F1        		LD	(DEVICE+012H+32*2),A	;Cドライブ
  CCC2  3272F1        		LD	(DEVICE+012H+32*3),A	;Dドライブ
                      	
  CCC5  210000        		LD	HL,0
  CCC8  2227DF        		LD	(X1PAT),HL
  CCCB  C349CD        		JP	NOBANK
                      	
  CCCE                	TURBO:
  CCCE  F3            		DI			;Check BIOS
  CCCF  061D          		LD	B,01DH
  CCD1  ED79          		OUT	(C),A		;BIOS ON
  CCD3  3A512F        		LD	A,(02F51H)
  CCD6  04            		INC	B		;B=01EH
  CCD7  ED79          		OUT	(C),A		;BIOS OFF
  CCD9  FB            		EI
  CCDA  FEC9          		CP	0C9H
  CCDC  2802          		JR	Z,BIOSOK
  CCDE  1805          		JR	NOBIOS
  CCE0                	BIOSOK:
  CCE0  3EC3          		LD	A,0C3H		;JP
  CCE2  321800        		LD	(00018H),A
  CCE5                	NOBIOS:
  CCE5  3E1F          		LD	A,01FH		;スタートポート
  CCE7  DBF0          		IN	A,(0F0H)
  CCE9  0F            		RRCA
  CCEA  380B          		JR	C,CRT1
  CCEC  21CBCE        		LD	HL,_C8025H
  CCEF  11B0EF        		LD	DE,CRTCD
  CCF2  011000        		LD	BC,16
  CCF5  EDB0          		LDIR
  CCF7                	CRT1:
  CCF7  3E1F          		LD	A,01FH		;スタートポート
  CCF9  DBF0          		IN	A,(0F0H)
  CCFB  CB57          		BIT	2,A
  CCFD  2818          		JR	Z,BANK
                      	
  CCFF  AF            		XOR	A
  CD00                	TZ1:				;前面スイッチが2HDの場合はMFMモードを2HDにしておく
  CD00  F5            		PUSH	AF
  CD01  CDDCEF        		CALL	_GETDPB
  CD04  DD7E12        		LD	A,(IX+012H)	;DPB_12_DEVICE
  CD07  E68F          		AND	08FH
  CD09  FE87          		CP	087H		;フロッピーでMEM切り替えがON
  CD0B  2004          		JR	NZ,TZ2
  CD0D  DDCB0A86      		RES	0,(IX+0AH)	;(DPB_0A_FDMODE)
  CD11                	TZ2:
  CD11  F1            		POP	AF
  CD12  3C            		INC	A
  CD13  FE08          		CP	8
  CD15  38E9          		JR	C,TZ1
                      	
  CD17                	BANK:
  CD17  01000B        		LD	BC,00B00H
  CD1A  21007F        		LD	HL,07F00H
  CD1D  F3            		DI
  CD1E                	BANK1:
  CD1E  ED69          		OUT	(C),L
  CD20  5E            		LD	E,(HL)
  CD21  7B            		LD	A,E
  CD22  C6A5          		ADD	A,0A5H
  CD24  77            		LD	(HL),A
  CD25  BE            		CP	(HL)
  CD26  73            		LD	(HL),E
  CD27  2005          		JR	NZ,BANK2
  CD29  2C            		INC	L
  CD2A  CB65          		BIT	4,L
  CD2C  28F0          		JR	Z,BANK1
  CD2E                	BANK2:
  CD2E  3E10          		LD	A,010H
  CD30  ED79          		OUT	(C),A
  CD32  7D            		LD	A,L
  CD33  B7            		OR	A
  CD34  2813          		JR	Z,NOBANK
  CD36  2600          		LD	H,0
  CD38  29            		ADD	HL,HL	;*2
  CD39  29            		ADD	HL,HL	;*4
  CD3A  29            		ADD	HL,HL	;*8
  CD3B  29            		ADD	HL,HL	;*16
  CD3C  29            		ADD	HL,HL	;*32
  CD3D  2B            		DEC	HL	;-1
  CD3E  2B            		DEC	HL	;-2
  CD3F  2B            		DEC	HL	;-3
  CD40  22A8F1        		LD	(BANKCL),HL
  CD43  CD82CE        		CALL	CALC_FATLN
  CD46  32A0F1        		LD	(BANKFL),A
  CD49                	NOBANK:
  CD49                	EMM:
  CD49  DD2180F1      		LD	IX,EMMFL
  CD4D  CD33CE        		CALL	EADR0
  CD50  ED78          		IN	A,(C)
  CD52  F5            		PUSH	AF
                      	
  CD53  110000        		LD	DE,0
  CD56                	ECHECK1:
  CD56  13            		INC	DE
  CD57  CD17CE        		CALL	EADR2
  CD5A  ED60          		IN	H,(C)
  CD5C  CD17CE        		CALL	EADR2
  CD5F  7C            		LD	A,H
  CD60  C6E5          		ADD	A,0E5H
  CD62  ED79          		OUT	(C),A
  CD64  3C            		INC	A
  CD65  CD33CE        		CALL	EADR0
  CD68  ED79          		OUT	(C),A
  CD6A  3D            		DEC	A
  CD6B  CD17CE        		CALL	EADR2
  CD6E  ED68          		IN	L,(C)
  CD70  CD17CE        		CALL	EADR2
  CD73  ED61          		OUT	(C),H
  CD75  BD            		CP	L
  CD76  2004          		JR	NZ,ECHECK2
  CD78  CB5A          		BIT	3,D
  CD7A  28DA          		JR	Z,ECHECK1
  CD7C                	ECHECK2:
  CD7C  CD33CE        		CALL	EADR0
  CD7F  F1            		POP	AF
  CD80  ED79          		OUT	(C),A
                      	
  CD82  21F7FF        		LD	HL,0-9
  CD85  19            		ADD	HL,DE
  CD86  3009          		JR	NC,NOEMM
  CD88  2288F1        		LD	(EMMCL),HL
  CD8B  CD82CE        		CALL	CALC_FATLN
  CD8E  3280F1        		LD	(EMMFL),A
  CD91                	NOEMM:
  CD91  3E00          	KEYREP:	LD	A,000H
  CD93  B7            		OR	A
  CD94  2807          		JR	Z,KEYR1
  CD96  010000        		LD	BC,0
  CD99  ED438CEF      		LD	(_CTC),BC
  CD9D                	KEYR1:
  CD9D  CD63CE        		CALL	SETCRTC
  CDA0  0130F8        		LD	BC,0-80*25
  CDA3  2ABAEF        		LD	HL,(CRTCD+10)
  CDA6  ED43BAEF      		LD	(CRTCD+10),BC
  CDAA  1E0C          		LD	E,00CH
  CDAC  CDCDEF        		CALL	_PRINT
  CDAF  22BAEF        		LD	(CRTCD+10),HL
                      	
  CDB2  219ECE        		LD	HL,INIMES
  CDB5  CD1DD8        		CALL	MSX
                      	
  CDB8  3A87FF        		LD	A,(0FF87H)
  CDBB  FE08          		CP	8
  CDBD  301E          		JR	NC,INIT0
  CDBF  5F            		LD	E,A
  CDC0  C641          		ADD	A,'A'
  CDC2  329BCE        		LD	(AUTODV),A
  CDC5  0E0E          		LD	C,00EH
  CDC7  CD0500        		CALL	SYSTEM
  CDCA  1C            		INC	E
  CDCB  2185EF        		LD	HL,_SEEKSP	;Disk error Ignore
  CDCE  CBFE          		SET	7,(HL)
  CDD0  0E1B          		LD	C,01BH
  CDD2  CD0500        		CALL	SYSTEM
                      	
  CDD5  2185EF        		LD	HL,_SEEKSP
  CDD8  CBBE          		RES	7,(HL)
                      	
  CDDA  3C            		INC	A
  CDDB  2008          		JR	NZ,INIT1
  CDDD                	INIT0:
  CDDD  1E00          		LD	E,0
  CDDF  0E0E          		LD	C,00EH
  CDE1  CD0500        		CALL	SYSTEM
  CDE4  AF            		XOR	A
  CDE5                	INIT1:
  CDE5  F3            		DI
  CDE6  08            		EX	AF,AF'
  CDE7  2EFE          		LD	L,0-2		;CALLのスタック分減らす
  CDE9  39            		ADD	HL,SP
  CDEA  1100FF        		LD	DE,0FF00H
  CDED  45            		LD	B,L
  CDEE  CDFAD4        		CALL	ZERO_MEMORY_DE
  CDF1  21CAFF        		LD	HL,EXTSP
  CDF4  060F          		LD	B,TRAP38-EXTBIO
  CDF6  3EFF          		LD	A,0FFH		;RST 38H
  CDF8  CD54E1        		CALL	FILL_MEMORY
  CDFB  2176D0        		LD	HL,AT_TRAP38
  CDFE  11D9FF        		LD	DE,TRAP38
  CE01  012700        		LD	BC,AT_TRAPE-AT_TRAP38
  CE04  EDB0          		LDIR
  CE06  08            		EX	AF,AF'
  CE07  B7            		OR	A
  CE08  C8            		RET	Z
                      	
  CE09  3EE6          		LD	A,0E6H
  CE0B  CDFCDE        		CALL	COMOUT
  CE0E  CDE1DE        		CALL	IN49SB
  CE11  CDE1DE        		CALL	IN49SB
  CE14  FE1B          		CP	01BH
  CE16  C9            		RET
                      	
  CE17                	EADR2:
  CE17  D5            		PUSH	DE
  CE18  EB            		EX	DE,HL
  CE19  29            		ADD	HL,HL
  CE1A  29            		ADD	HL,HL
  CE1B  DD4E0D        		LD	C,(IX+00DH)	;DPB_0D_MAXSEC
  CE1E  DD460C        		LD	B,(IX+00CH)	;DPB_0C_MAXCYL
  CE21  09            		ADD	HL,BC
  CE22  DD4E13        		LD	C,(IX+013H)	;DPB_13_UNITNO
  CE25  060D          		LD	B,00DH
  CE27  ED49          		OUT	(C),C
  CE29  0C            		INC	C
  CE2A  ED69          		OUT	(C),L
  CE2C  0C            		INC	C
  CE2D  ED61          		OUT	(C),H
  CE2F  0C            		INC	C
  CE30  EB            		EX	DE,HL
  CE31  D1            		POP	DE
  CE32  C9            		RET
                      	
  CE33                	EADR0:
  CE33  D5            		PUSH	DE
  CE34  110000        		LD	DE,0
  CE37  CD17CE        		CALL	EADR2
  CE3A  D1            		POP	DE
  CE3B  C9            		RET
                      	
  CE3C                	CHKCTC:
  CE3C  C5            		PUSH	BC
  CE3D  110347        		LD	DE,04703H
  CE40                	INICTC1:
  CE40  0C            		INC	C
  CE41  ED51          		OUT	(C),D
  CE43  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令
  CE45  1D            		DEC	E
  CE46  20F8          		JR	NZ,INICTC1
  CE48  C1            		POP	BC
                      	
  CE49  11FA07        		LD	DE,007FAH
  CE4C  ED51          		OUT	(C),D
  CE4E  ED59          		OUT	(C),E
  CE50  ED78          		IN	A,(C)
  CE52  BB            		CP	E
  CE53  C0            		RET	NZ
  CE54  ED51          		OUT	(C),D
  CE56  ED51          		OUT	(C),D
  CE58  ED78          		IN	A,(C)
  CE5A  BA            		CP	D
  CE5B  C0            		RET	NZ
  CE5C  03            		INC	BC
  CE5D  03            		INC	BC
  CE5E  ED438CEF      		LD	(_CTC),BC
  CE62  C9            		RET
                      	
  CE63                	SETCRTC:
  CE63  21B0EF        		LD	HL,CRTCD
  CE66  AF            		XOR	A
  CE67                	SETCRT1:
  CE67  010018        		LD	BC,01800H
  CE6A  ED79          		OUT	(C),A
  CE6C  0C            		INC	C
  CE6D  04            		INC	B
  CE6E  EDA3          		OUTI
  CE70  3C            		INC	A
  CE71  FE0C          		CP	12
  CE73  20F2          		JR	NZ,SETCRT1
  CE75  23            		INC	HL
  CE76  23            		INC	HL
  CE77  01031B        		LD	BC,01A03H+00100H
  CE7A  EDA3          		OUTI
  CE7C  01D020        		LD	BC,01FD0H+00100H
  CE7F  EDA3          		OUTI
  CE81  C9            		RET
                      	
  CE82                	CALC_FATLN:	;FAT12のクラスタ数から必要なFATサイズを求める(セクタサイズ512)
  CE82  5D            		LD	E,L
  CE83  54            		LD	D,H
  CE84  29            		ADD	HL,HL	;*2
  CE85  19            		ADD	HL,DE	;*3
  CE86  CB3C          		SRL	H	;/2
  CE88  CB1D          		RR	L	;1クラスタ辺り1.5バイト必要
  CE8A  11FF01        		LD	DE,511	;切り上げる
  CE8D  19            		ADD	HL,DE
  CE8E  7C            		LD	A,H
  CE8F  CB3F          		SRL	A
  CE91  C9            		RET
                      	
  CE92  4155544F455845	AUTOD:	DB	"AUTOEXEC "
  CE9B  413A00        	AUTODV:	DB	"A:",0
                      	
  CE9E  4C53582D446F64	INIMES:	DB	"LSX-Dodgers for X1/turbo version "
  CEBF  312E          		DB	030H + VER_1, '.'
  CEC1  3436          		DB	030H + VER_2 ,030H + VER_3
  CEC3  2047616B750D0A		DB	' Gaku',0DH,0AH
  CECA  00            		DB	0
                      	
  CECB                	_C8025H:
  CECB  6B5059881B0119		DB	06BH,050H,059H,088H,01BH,001H,019H,01AH
  CED3  000F          		DB	000H,00FH
  CED5  80F8B0FF      		DW	0-80*LINE,0-80
  CED9  0C23          		DB	00CH,023H
                      	
  CEDB  030C0F30333C3F	TEXTDT:	DB	003H,00CH,00FH,030H,033H,03CH,03FH
                      	
  CEE2                	SIODATA:
  CEE2  18            		DB	018H
  CEE3  0100          		DB	1,0
  CEE5  0200          		DB	2,0
  CEE7  03C1          		DB	3,0C1H
  CEE9  0444          		DB	4,044H
  CEEB  05E8          		DB	5,0E8H
  CEED  FF            		DB	0FFH
                      	
                      	;	ノンターボ用パッチ
                      	
  CEEE                	AT_SCR1:
  CEEE  D9            		EXX
  CEEF  C5            		PUSH	BC
  CEF0  ED4BB1EF      		LD	BC,(CRTCD+1)
  CEF4  0620          		LD	B,020H
  CEF6  D9            		EXX
  CEF7  C5            		PUSH	BC
  CEF8  010020        		LD	BC,02000H
                      	
  CEFB  67            		LD	H,A
  CEFC  B7            		OR	A
  CEFD                	AT_SCRUP1:
  CEFD  2815          		JR	Z,AT_SCRCL
  CEFF  C5            		PUSH	BC
  CF00  CBE0          		SET	4,B
  CF02  D9            		EXX
  CF03  C5            		PUSH	BC
  CF04  CBE0          		SET	4,B
  CF06  D9            		EXX
  CF07  CDB7DF        		CALL	AT_UPSUB+AT_RS
  CF0A  D9            		EXX
  CF0B  C1            		POP	BC
  CF0C  D9            		EXX
  CF0D  C1            		POP	BC
  CF0E  CDB7DF        		CALL	AT_UPSUB+AT_RS
  CF11  25            		DEC	H
  CF12  18E9          		JR	AT_SCRUP1
                      	
  CF14                	AT_SCRCL:
  CF14  2AB1EF        		LD	HL,(CRTCD+1)
  CF17  CD4DDE        		CALL	LINECL
  CF1A  C1            		POP	BC
  CF1B  D9            		EXX
  CF1C  C1            		POP	BC
  CF1D  D9            		EXX
  CF1E  C9            		RET
                      	
  CF1F                	AT_UPSUB:
  CF1F  3AB1EF        		LD	A,(CRTCD+1)
  CF22  6F            		LD	L,A
  CF23                	AT_UPSUB1:
  CF23  D9            		EXX
  CF24  ED78          		IN	A,(C)
  CF26  03            		INC	BC
  CF27  D9            		EXX
  CF28  ED79          		OUT	(C),A
  CF2A  03            		INC	BC
  CF2B  2D            		DEC	L
  CF2C  20F5          		JR	NZ,AT_UPSUB1
  CF2E  C9            		RET
                      	
                      	;	FLOPPY DISK DRIVER(CPU)
                      	
  CF2F                	DISKC:
  CF2F  1B            		DEC	DE
  CF30  CB5F          		BIT	3,A
  CF32  C439EE        		CALL	NZ,RNF
  CF35                	DISKD:
  CF35  E5            		PUSH	HL
  CF36  213FEE        		LD	HL,RETRY
  CF39  35            		DEC	(HL)
  CF3A  E1            		POP	HL
  CF3B  203C          		JR	NZ,AT_ERR		;Retry
  CF3D  B7            		OR	A
  CF3E  2839          		JR	Z,AT_ERR		;Error
                      	
  CF40                	AT_DELP:
  CF40  CD39EE        		CALL	RNF
  CF43  CD15EE        		CALL	FDMOFF
  CF46  3A85EF        		LD	A,(_SEEKSP)
  CF49  87            		ADD	A,A
  CF4A  382C          		JR	C,AT_ERRZ
  CF4C                	AT_DELP1:
  CF4C  D5            		PUSH	DE
  CF4D  21C0F0        		LD	HL,DEMES
  CF50  CD1DD8        		CALL	MSX
  CF53  CDA2DB        		CALL	KEYBC
  CF56                	AT_DELP2:
  CF56  CDCAEF        		CALL	_INKEY
  CF59  28FB          		JR	Z,AT_DELP2
                      	
  CF5B  D1            		POP	DE
  CF5C  CDB4E3        		CALL	CAP
  CF5F  FE52          		CP	'R'		;Retry
  CF61  200A          		JR	NZ,AT_DELP3
  CF63  210000        	DHLPAT:	LD	HL,0
  CF66  3E02          		LD	A,2
  CF68  323FEE        		LD	(RETRY),A
  CF6B  B7            		OR	A
  CF6C  C9            		RET
  CF6D                	AT_DELP3:
  CF6D  FE49          		CP	'I'		;Ignore
  CF6F  2805          		JR	Z,AT_IGNORE
  CF71  FE41          		CP	'A'		;Abort
  CF73  20D7          		JR	NZ,AT_DELP1
  CF75  C7            		RST	0
  CF76                	AT_IGNORE:
  CF76  3EFF          		LD	A,0FFH
  CF78                	AT_ERRZ:
  CF78  BF            		CP	A
  CF79                	AT_ERR:
  CF79  3EFF          		LD	A,0FFH
  CF7B  C9            		RET
  CF7C                	AT_SCRN:
  CF7C  ED78          		IN	A,(C)
  CF7E  C9            		RET
  CF7F                	AT_SCRE:
                      	
  CF7F                	AT_WTTRK:
  CF7F  0601          		LD	B,1
  CF81  3EF4          		LD	A,0F4H
  CF83  1802          		JR	AT_WTTRK1
  CF85                	AT_DWT:
  CF85  3EA0          		LD	A,0A0H
  CF87                	AT_WTTRK1:
  CF87  32DDED        		LD	(FDCPAT+1),A
  CF8A                	DWTBL:
  CF8A  78            		LD	A,B
  CF8B  328CEE        		LD	(AT_WTB+1+AT_R),A
  CF8E  3E02          		LD	A,2		;Retry count
  CF90  323FEE        		LD	(RETRY),A
                      	
  CF93                	DWT0:
  CF93  D5            		PUSH	DE
  CF94  E5            		PUSH	HL
  CF95  22FCDF        		LD	(DHLPAT+1+AT_RS),HL
  CF98  CD77ED        		CALL	SEEK		;Write disk
  CF9B  E1            		POP	HL
  CF9C  3838          		JR	C,ERRDW
  CF9E  F3            		DI
  CF9F  3ADDED        		LD	A,(FDCPAT+1)
  CFA2  CD21EE        		CALL	SECSET
  CFA5  0EFB          		LD	C,0FBH
                      	
  CFA7                	DWT1:
  CFA7  56            		LD	D,(HL)
  CFA8                	DWT2:
  CFA8  78            		LD	A,B
  CFA9  DBF8          		IN	A,(0F8H)
  CFAB  0F            		RRCA
  CFAC  3008          		JR	NC,DWT4
  CFAE  0F            		RRCA
  CFAF  30F7          		JR	NC,DWT2
  CFB1                	DWT3:
  CFB1  ED51          		OUT	(C),D
  CFB3  23            		INC	HL
  CFB4  18F1          		JR	DWT1
                      	
  CFB6                	DWT4:
  CFB6  3D            		DEC	A
  CFB7  28F8          		JR	Z,DWT3
  CFB9  3C            		INC	A
  CFBA  FB            		EI
  CFBB  D1            		POP	DE
  CFBC  13            		INC	DE
  CFBD  CD15EE        		CALL	FDMOFF
  CFC0  2808          		JR	Z,DISKOK_WT
  CFC2  CDC7DF        		CALL	DISKC+AT_RS
  CFC5  20CC          		JR	NZ,DWT0
  CFC7  B7            		OR	A
  CFC8  2005          		JR	NZ,ERRW
                      	
  CFCA                	DISKOK_WT:
  CFCA  0601          	AT_WTB:	LD	B,1
  CFCC  10BC          		DJNZ	DWTBL
  CFCE  C9            		RET
                      	
  CFCF                	ERRW:
  CFCF  3EFF          		LD	A,0FFH
  CFD1                	ERRZ:
  CFD1  BF            		CP	A
  CFD2  37            		SCF
  CFD3  C315EE        		JP	FDMOFF
                      	
  CFD6                	ERRDW:
  CFD6  D1            		POP	DE
  CFD7  CDD8DF        		CALL	AT_DELP+AT_RS
  CFDA  20B7          		JR	NZ,DWT0
  CFDC  B7            		OR	A
  CFDD  20F0          		JR	NZ,ERRW
  CFDF  C9            		RET
                      	
  CFE0                	AT_DRD:
  CFE0  3E80          		LD	A,080H
  CFE2  32DDED        		LD	(FDCPAT+1),A
  CFE5                	DRDBL:
  CFE5  78            		LD	A,B
  CFE6  32E3EE        		LD	(AT_RDB+1+AT_R),A
  CFE9  3E02          		LD	A,2		;Retry count
  CFEB  323FEE        		LD	(RETRY),A
  CFEE                	DRD0:
  CFEE  D5            		PUSH	DE
  CFEF  E5            		PUSH	HL
  CFF0  22FCDF        		LD	(DHLPAT+1+AT_RS),HL
  CFF3  CD77ED        		CALL	SEEK		;Read disk
  CFF6  E1            		POP	HL
  CFF7  382D          		JR	C,ERRDR
  CFF9  F3            		DI
  CFFA  3ADDED        		LD	A,(FDCPAT+1)
  CFFD  CD21EE        		CALL	SECSET
  D000  0EFB          		LD	C,0FBH
  D002                	DRD1:
  D002  78            		LD	A,B
  D003  DBF8          		IN	A,(0F8H)
  D005  0F            		RRCA
  D006  3008          		JR	NC,DRD3
  D008  0F            		RRCA
  D009  30F7          		JR	NC,DRD1
  D00B  EDA2          		INI
  D00D  04            		INC	B
  D00E  18F2          		JR	DRD1
                      	
  D010                	DRD3:
  D010  B7            		OR	A
  D011  FB            		EI
  D012  D1            		POP	DE
  D013  13            		INC	DE
  D014  CD15EE        		CALL	FDMOFF
  D017  2808          		JR	Z,DISKOK_RD
  D019  CDC7DF        		CALL	DISKC+AT_RS
  D01C  20D0          		JR	NZ,DRD0
  D01E  B7            		OR	A
  D01F  20AE          		JR	NZ,ERRW
                      	
  D021                	DISKOK_RD:
  D021  0601          	AT_RDB:	LD	B,1
  D023  10C0          		DJNZ	DRDBL
  D025  C9            		RET
                      	
  D026                	ERRDR:
  D026  D1            		POP	DE
  D027  CDD8DF        		CALL	AT_DELP+AT_RS
  D02A  20C2          		JR	NZ,DRD0
  D02C  B7            		OR	A
  D02D  20A0          		JR	NZ,ERRW
  D02F  C9            		RET
                      	
  D030                	AT_CPUE:
                      	
                      	
                      	;	EMM DRIVER(CPU)
                      	
                      	
  D030                	AT_EDWT:
  D030  CDA3D7        		CALL	AT_EADR+AT_RE
  D033                	AT_EDWT0:
  D033  F5            		PUSH	AF
  D034  AF            		XOR	A
  D035                	AT_EDWT1:
  D035  04            		INC	B
  D036  EDA3          		OUTI
  D038  04            		INC	B
  D039  EDA3          		OUTI
  D03B  3D            		DEC	A
  D03C  20F7          		JR	NZ,AT_EDWT1
  D03E  13            		INC	DE
  D03F  F1            		POP	AF
  D040  3D            		DEC	A
  D041  20F0          		JR	NZ,AT_EDWT0
  D043  C9            		RET
                      	
  D044                	AT_EDRD:
  D044  CDA3D7        		CALL	AT_EADR+AT_RE
  D047                	AT_EDRD0:
  D047  F5            		PUSH	AF
  D048  AF            		XOR	A
  D049                	AT_EDRD1:
  D049  EDA2          		INI
  D04B  04            		INC	B
  D04C  EDA2          		INI
  D04E  04            		INC	B
  D04F  3D            		DEC	A
  D050  20F7          		JR	NZ,AT_EDRD1
  D052  13            		INC	DE
  D053  F1            		POP	AF
  D054  3D            		DEC	A
  D055  20F0          		JR	NZ,AT_EDRD0
  D057  C9            		RET
                      	
  D058                	AT_EADR:
  D058  C5            		PUSH	BC
  D059  D5            		PUSH	DE
  D05A  EB            		EX	DE,HL
  D05B  29            		ADD	HL,HL
  D05C  DD4E0D        		LD	C,(IX+00DH)	;DPB_0D_MAXSEC
  D05F  DD460C        		LD	B,(IX+00CH)	;DPB_0C_MAXCYL
  D062  09            		ADD	HL,BC
  D063  DD4E13        		LD	C,(IX+013H)	;DPB_13_UNITNO
  D066  060D          		LD	B,00DH
  D068  ED49          		OUT	(C),C
  D06A  0C            		INC	C
  D06B  ED69          		OUT	(C),L
  D06D  0C            		INC	C
  D06E  ED61          		OUT	(C),H
  D070  0C            		INC	C
  D071  EB            		EX	DE,HL
  D072  D1            		POP	DE
  D073  F1            		POP	AF
  D074  A7            		AND	A	;CF=0
  D075  C9            		RET
  D076                	AT_EMME:
                      	
  D076                	AT_TRAP38:
  D076  210000        		LD	HL,0
  D079  E3            		EX	(SP),HL
  D07A  3E24          		LD	A,'$'
  D07C  CDD9DB        		CALL	MSG_A
  D07F  2B            		DEC	HL
  D080  7C            		LD	A,H
  D081  CDE8FF        		CALL	PRHX+AT_RT
  D084  7D            		LD	A,L
  D085                	PRHX:
  D085  F5            		PUSH	AF
  D086  07            		RLCA
  D087  07            		RLCA
  D088  07            		RLCA
  D089  07            		RLCA
  D08A  CDF1FF        		CALL	PRHX2+AT_RT
  D08D  F1            		POP	AF
  D08E                	PRHX2:
  D08E  E60F          		AND	00FH
  D090  FE0A          		CP	10
  D092  3F            		CCF
  D093  CE30          		ADC	A,'0'
  D095  27            		DAA
  D096  C3D9DB        		JP	MSG_A
  D099                		DS	4
  D09D                	AT_TRAPE:
                      	
  2F63                	AT_RT	EQU	TRAP38-AT_TRAP38
                      	
  D09D                	INITE:
  D09D                		DS	BDOS-INITE
                      	
  D106  C3C6D7        		JP	BDOS0
                      	
                      	;X1CCP.ASM
                      	
                      	;	LSX-Dodgers CCP
                      	;	X1/turbo/Z
                      	
  D109                	WBOOT1:
  D109  F3            		DI
  D10A  ED7B0600      		LD	SP,(SYSTEM+1)
                      	
  D10E  3EEF          		LD	A,INTVEC/256
  D110  ED47          		LD	I,A
                      	
  D112  3EE4          		LD	A,0E4H
  D114  CDFCDE        		CALL	COMOUT
  D117  3EC8          		LD	A,INTVEC
  D119  CDD6DE        		CALL	OT49SB
                      	
  D11C  3ABCEF        		LD	A,(CRTCD+12)
  D11F  B7            		OR	A
  D120  200C          		JR	NZ,SETCRT0
  D122  2180F8        		LD	HL,0-80*LINE
  D125  22BAEF        		LD	(CRTCD+10),HL
  D128  21B0FF        		LD	HL,0-80
  D12B  22BCEF        		LD	(CRTCD+12),HL
  D12E                	SETCRT0:
  D12E  21B0EF        		LD	HL,CRTCD
  D131  AF            		XOR	A
  D132                	SETCRT2:
  D132  010018        		LD	BC,01800H
  D135  ED79          		OUT	(C),A
  D137  0C            		INC	C
  D138  56            		LD	D,(HL)
  D139  FE0D          		CP	13
  D13B  3802          		JR	C,SETCRT3
  D13D  1600          		LD	D,0
  D13F                	SETCRT3:
  D13F  ED51          		OUT	(C),D
  D141  23            		INC	HL
  D142  3C            		INC	A
  D143  FE0E          		CP	14
  D145  20EB          		JR	NZ,SETCRT2
                      	
  D147  01031B        		LD	BC,01A03H+00100H
  D14A  EDA3          		OUTI
  D14C  01D020        		LD	BC,01FD0H+00100H
  D14F  EDA3          		OUTI
  D151  2192EF        		LD	HL,_KEYD
  D154  7E            		LD	A,(HL)
  D155  3600          		LD	(HL),0
  D157  CD9FDB        		CALL	KEYBC_IFBREAK
  D15A  3E04          		LD	A,4
  D15C  CDD9DB        		CALL	MSG_A
                      	
                      	;LDCCP.ASM
                      	
                      	;	LSX-Dodgers CCP
                      	
  D15F                	COMMAND:
  D15F  CD12D2        		CALL	SETDTA1
  D162  3A87EF        		LD	A,(_DVSW)
  D165  C641          		ADD	A,'A'
  D167  CDD9DB        		CALL	MSG_A
  D16A  3E3E          		LD	A,'>'
  D16C  CDD9DB        		CALL	MSG_A
  D16F  3E50          		LD	A,80
  D171  12            		LD	(DE),A
  D172  CD28DC        		CALL	_SYS0A		;(BDOS)文字列入力
  D175  CDBCD3        		CALL	LTNL
                      	
  D178  118200        		LD	DE,DTA1+2
  D17B  CDFDEF        		CALL	_COMANL
  D17E  30DF          		JR	NC,COMMAND
  D180  1179EF        		LD	DE,COMERM
  D183  CD0ED8        		CALL	_SYS09		;(BDOS)文字列出力
  D186  C7            		RST	0
                      	
  D187                	COMANL:
  D187  CDA8E0        		CALL	FILE
  D18A  3A19EF        		LD	A,(FNAME+4)
  D18D  FE20          		CP	020H
  D18F  201C          		JR	NZ,COMB2
  D191  D5            		PUSH	DE
  D192  1115EF        		LD	DE,FNAME
  D195  1A            		LD	A,(DE)
  D196  FE20          		CP	020H
  D198  2844          		JR	Z,SDVSW
  D19A  1B            		DEC	DE
  D19B  1A            		LD	A,(DE)
  D19C  C6FF          		ADD	A,0FFH
  D19E  3809          		JR	C,COMB
  D1A0  13            		INC	DE
                      	
  D1A1  219DD6        		LD	HL,COMTB
  D1A4  0609          		LD	B,COMS
  D1A6  CD2CE3        		CALL	CPNAME
  D1A9                	COMB:
  D1A9  D1            		POP	DE
  D1AA  D20F00        		JP	NC,JP_HL
  D1AD                	COMB2:
  D1AD  EB            		EX	DE,HL
  D1AE  2286D2        		LD	(COMPAT+1),HL
  D1B1  F5            		PUSH	AF
  D1B2  CD32D2        		CALL	CEXE4
  D1B5  F1            		POP	AF
  D1B6  2115EF        		LD	HL,FNAME
  D1B9  116D00        		LD	DE,FCB2+1	;COMMAND NAME
  D1BC  010B00        		LD	BC,11
  D1BF  EDB0          		LDIR
  D1C1  1140D6        		LD	DE,PATHD
  D1C4                	CEX1:
  D1C4  1A            		LD	A,(DE)
  D1C5  FE20          		CP	020H
  D1C7  D8            		RET	C
  D1C8  CD9DE0        		CALL	FILEC
  D1CB  D5            		PUSH	DE
  D1CC  216D00        		LD	HL,FCB2+1	;COMMAND NAME
  D1CF  1115EF        		LD	DE,FNAME
  D1D2  010B00        		LD	BC,11
  D1D5  EDB0          		LDIR
  D1D7  CD32D2        		CALL	CEXE4
  D1DA  D1            		POP	DE
  D1DB  13            		INC	DE
  D1DC  18E6          		JR	CEX1
                      	
  D1DE                	SDVSW:
  D1DE  F1            		POP	AF
  D1DF  3A14EF        		LD	A,(FDRV)
  D1E2  3D            		DEC	A
  D1E3  5F            		LD	E,A
  D1E4  0E0E          		LD	C,00EH		;(BDOS)カレントドライブの設定
  D1E6  1831          		JR	SYSTEM0
                      	
  D1E8                	OPEN1:
  D1E8  2114EF        		LD	HL,FDRV
  D1EB                	OPEN:
  D1EB  0E11          		LD	C,011H		;(BDOS)ファイルの検索
  D1ED                	OPEN3:
  D1ED  D5            		PUSH	DE
  D1EE  111BD6        		LD	DE,DTA_CCP
  D1F1  CD0FD2        		CALL	SETDTA
  D1F4  EB            		EX	DE,HL
  D1F5  CD19D2        		CALL	SYSTEM0
  D1F8  D1            		POP	DE
  D1F9  C9            		RET
                      	
  D1FA                	OPEN2:
  D1FA  0E12          		LD	C,012H
  D1FC  18EF          		JR	OPEN3
                      	
  D1FE                	DEFCB:				;Z=Ok NZ=Error
  D1FE  111BD6        		LD	DE,DTA_CCP
  D201  CD17D2        		CALL	SYSC0F
                      	
  D204  113CD6        		LD	DE,DTA_CCP+33	;(FCB)ランダムレコード
  D207  0604          		LD	B,4
  D209  CDFAD4        		CALL	ZERO_MEMORY_DE
  D20C  110001        		LD	DE,00100H
  D20F                	SETDTA:
  D20F  C30CDA        		JP	_SYS1A		;(BDOS)DTAの設定
                      	
  D212                	SETDTA1:
  D212  118000        		LD	DE,DTA1
  D215  18F8          		JR	SETDTA
                      	
  D217                	SYSC0F:
  D217  0E0F          		LD	C,00FH		;(BDOS)ファイルのオープン
  D219                	SYSTEM0:
  D219  CD0500        		CALL	SYSTEM
  D21C  B7            		OR	A
  D21D  C9            		RET
                      	
  D21E                	C_CD:
  D21E  0E5A          		LD	C,5AH
  D220  18F7          		JR	SYSTEM0
                      	
  D222                	S27BUF:
  D222  3A0700        		LD	A,(SYSTEM+2)
  D225  3D            		DEC	A
  D226  E6F8          		AND	0F8H
  D228  67            		LD	H,A
  D229  2E00          		LD	L,0
  D22B                	S27DTA:
  D22B  111BD6        		LD	DE,DTA_CCP
  D22E                	S27:
  D22E  0E27          		LD	C,027H		;(BDOS)ランダムブロック読み込み
  D230  18E7          		JR	SYSTEM0
                      	
  D232                	CEXE4:
  D232  3A14EF        		LD	A,(FDRV)
  D235  325700        		LD	(00057H),A
  D238  2A22EF        		LD	HL,(FDRV+14)
  D23B  225800        		LD	(00058H),HL
                      	
  D23E  211DEF        		LD	HL,FNAME+8
  D241  7E            		LD	A,(HL)
  D242  FE20          		CP	020H
  D244  2007          		JR	NZ,CEXE7
  D246  3E3F          		LD	A,'?'
  D248  77            		LD	(HL),A
  D249  23            		INC	HL
  D24A  77            		LD	(HL),A
  D24B  23            		INC	HL
  D24C  77            		LD	(HL),A
  D24D                	CEXE7:
  D24D  CDE8D1        		CALL	OPEN1
  D250                	CEXE5:
  D250  C0            		RET	NZ
  D251  2A25D6        		LD	HL,(DTA_CCP+1+9)
  D254  7C            		LD	A,H
  D255  CDB4E3        		CALL	CAP
  D258  67            		LD	H,A
  D259  7D            		LD	A,L
  D25A  CDB4E3        		CALL	CAP
  D25D  6F            		LD	L,A
  D25E  3A24D6        		LD	A,(DTA_CCP+1+8)
  D261  CDB4E3        		CALL	CAP
  D264  D642          		SUB	'B'
  D266  282C          		JR	Z,C_BAT
  D268  3D            		DEC	A		;'C'
  D269  2805          		JR	Z,C_EXE
  D26B                	CEXE6:
  D26B  CDFAD1        		CALL	OPEN2
  D26E  18E0          		JR	CEXE5
                      	
  D270                	C_EXE:
  D270  7C            		LD	A,H
  D271  FE4D          		CP	'M'
  D273  20F6          		JR	NZ,CEXE6
                      	
  D275  CDFED1        		CALL	DEFCB
  D278  CD22D2        		CALL	S27BUF
  D27B  3D            		DEC	A
  D27C  37            		SCF
  D27D  C0            		RET	NZ
  D27E  7C            		LD	A,H
  D27F  B5            		OR	L
  D280  37            		SCF
  D281  C8            		RET	Z
  D282  CD12D2        		CALL	SETDTA1
  D285  110000        	COMPAT:	LD	DE,0				; self-modifying code
  D288  ED7B0600      		LD	SP,(SYSTEM+1)
  D28C  67            		LD	H,A				; A=0 in SETDTA1(_SYS1A)
  D28D  6F            		LD	L,A
  D28E  E5            		PUSH	HL				; push $0000 (reboot address)
  D28F  24            		INC	H
  D290  E5            		PUSH	HL				; push $0100 (TPA address)
  D291  C3C5D4        		JP	SETFCB				; and JP $0100
                      	
  D294                	C_BAT:
  D294  114154        		LD	DE,'A'+'T'*256
  D297  ED52          		SBC	HL,DE
  D299  20D0          		JR	NZ,CEXE6
                      	
  D29B  F1            		POP	AF
  D29C  F1            		POP	AF
  D29D  CDFED1        		CALL	DEFCB
  D2A0  210002        		LD	HL,00200H
  D2A3  CD2BD2        		CALL	S27DTA
  D2A6  C6FE          		ADD	A,0FEH
  D2A8  D8            		RET	C
  D2A9  7D            		LD	A,L
  D2AA  B7            		OR	A
  D2AB  37            		SCF
  D2AC  C8            		RET	Z
  D2AD  3C            		INC	A
  D2AE  F5            		PUSH	AF
                      	
  D2AF  CDA2DB        		CALL	KEYBC
  D2B2  3E06          		LD	A,6
  D2B4  CDD9DB        		CALL	MSG_A
  D2B7  C1            		POP	BC
  D2B8  210001        		LD	HL,00100H
  D2BB                	BAT1:
  D2BB  A7            		AND	A
  D2BC  05            		DEC	B
  D2BD  C8            		RET	Z
  D2BE  7E            		LD	A,(HL)
  D2BF  FE25          		CP	'%'
  D2C1  280C          		JR	Z,BAT2
  D2C3                	BAT1X:
  D2C3  7E            		LD	A,(HL)
  D2C4  23            		INC	HL
  D2C5  FE1A          		CP	01AH
  D2C7  C8            		RET	Z
  D2C8  FE0A          		CP	00AH
  D2CA  C487DB        		CALL	NZ,KEYBS
  D2CD  18EC          		JR	BAT1
                      	
  D2CF                	BAT2:
  D2CF  23            		INC	HL
  D2D0  7E            		LD	A,(HL)
  D2D1  2B            		DEC	HL
  D2D2  D631          		SUB	'1'
  D2D4  38ED          		JR	C,BAT1X
  D2D6  FE09          		CP	9
  D2D8  30E9          		JR	NC,BAT1X
  D2DA  23            		INC	HL
  D2DB  23            		INC	HL
  D2DC  3C            		INC	A
  D2DD  4F            		LD	C,A
  D2DE  ED5B86D2      		LD	DE,(COMPAT+1)
  D2E2                	BAT3:
  D2E2  1A            		LD	A,(DE)
  D2E3  B7            		OR	A
  D2E4  28D5          		JR	Z,BAT1
  D2E6  CD7FE1        		CALL	SPCUT
  D2E9  0D            		DEC	C
  D2EA  2808          		JR	Z,BAT5
  D2EC                	BAT4:
  D2EC  1A            		LD	A,(DE)
  D2ED  FE21          		CP	021H
  D2EF  38F1          		JR	C,BAT3
  D2F1  13            		INC	DE
  D2F2  18F8          		JR	BAT4
  D2F4                	BAT5:
  D2F4  1A            		LD	A,(DE)
  D2F5  FE21          		CP	021H
  D2F7  38C2          		JR	C,BAT1
  D2F9  13            		INC	DE
  D2FA  CD87DB        		CALL	KEYBS
  D2FD  18F5          		JR	BAT5
                      	
  D2FF                	C_DEL:
  D2FF  CDC5D4        		CALL	SETFCB
  D302  CDEFDB        		CALL	_SYS08		;(BDOS)エコーなしコンソール入力
                      	
  D305  0E13          		LD	C,013H
  D307  180A          		JR	CDEL1		;(BDOS)ファイルの削除
                      	
  D309                	C_REN:
  D309  CDC5D4        		CALL	SETFCB
  D30C  3E10          		LD	A,010H		;ディレクトリも対象にする
  D30E  326900        		LD	(FCB1+13),A	;属性
  D311  0E17          		LD	C,017H		;(BDOS)ファイル名の変更
  D313                	CDEL1:
  D313  115C00        		LD	DE,FCB1
  D316  CD0500        		CALL	SYSTEM
  D319  C6FF          		ADD	A,0FFH
  D31B  C9            		RET
                      	
  D31C                	C_DIR:
  D31C  CD9DE0        		CALL	FILEC
  D31F  2115EF        		LD	HL,FDRV+1
  D322  CD10D6        		CALL	CWILD1
  D325  3EF1          		LD	A,0F1H
  D327  3221EF        		LD	(FDRV+13),A
  D32A  CDE8D1        		CALL	OPEN1
  D32D                	CDIR1:
  D32D  B7            		OR	A
  D32E  2008          		JR	NZ,PDSKF
  D330  CD7BD3        		CALL	P_NAME
  D333  CDFAD1        		CALL	OPEN2
  D336  18F5          		JR	CDIR1
                      	
  D338                	PDSKF:
  D338  3A14EF        		LD	A,(FDRV)
  D33B  5F            		LD	E,A
  D33C  0E1B          		LD	C,01BH		;(BDOS)ディスク情報の獲得
  D33E  CD0500        		CALL	SYSTEM
  D341  4F            		LD	C,A		;C←1クラスタ辺りの論理セクタ数
  D342  C601          		ADD	A,001H
  D344  D8            		RET	C		;Aが0FFHだった場合
  D345  3E06          		LD	A,8-2
  D347                	PDS1:				;HL=未使用クラスタの総数
  D347  3C            		INC	A
  D348  CB19          		RR	C
  D34A  30FB          		JR	NC,PDS1
  D34C                	PDS2:				;B←論理セクタのサイズの上位8ビット
  D34C  3C            		INC	A
  D34D  CB18          		RR	B
  D34F  30FB          		JR	NC,PDS2
  D351  47            		LD	B,A
                      	
  D352  110000        		LD	DE,0
  D355                	PDS3:
  D355  29            		ADD	HL,HL
  D356  EB            		EX	DE,HL
  D357  ED6A          		ADC	HL,HL
  D359  EB            		EX	DE,HL
  D35A  10F9          		DJNZ	PDS3
  D35C                	PDSKF1:
  D35C  CDF4D3        		CALL	PRDEC_DEHL
  D35F  1181D6        		LD	DE,FREE
  D362  CD0ED8        		CALL	_SYS09		;(BDOS)文字列出力
  D365  CDE3D3        		CALL	PUTDRV
  D368  3E5C          		LD	A,05CH		;\
  D36A  CDD9DB        		CALL	MSG_A
  D36D  2A22EF        		LD	HL,(FDRV+14)	;ディレクトリのクラスタ番号
  D370  AF            		XOR	A
  D371  11FEFF        		LD	DE,0-2
  D374  19            		ADD	HL,DE
  D375  23            		INC	HL
  D376  DCF0D3        		CALL	C,PRDEC_HL
  D379  1841          		JR	LTNL
                      	
  D37B                	P_NAME:
  D37B  3A1CD6        		LD	A,(DTA_CCP+1)
  D37E  FE2E          		CP	'.'
  D380  C8            		RET	Z
  D381  1199D6        		LD	DE,DIRHD
  D384  CD0ED8        		CALL	_SYS09		;(BDOS)文字列出力
  D387  3A27D6        		LD	A,(DTA_CCP+1+00BH)
  D38A  F5            		PUSH	AF
  D38B  0F            		RRCA
  D38C  9F            		SBC	A,A
  D38D  E60A          		AND	'*'-020H
  D38F  C620          		ADD	A,020H
  D391  CDD9DB        		CALL	MSG_A
  D394  CDE3D3        		CALL	PUTDRV
  D397  211CD6        		LD	HL,DTA_CCP+1
  D39A  CD34D4        		CALL	FPRNT
                      	
  D39D  F1            		POP	AF
  D39E  CB67          		BIT	4,A
  D3A0  2808          		JR	Z,DIR3
  D3A2  118ED6        		LD	DE,DIRMES
  D3A5  CD0ED8        		CALL	_SYS09		;(BDOS)文字列出力
  D3A8  180A          		JR	DIR6
  D3AA                	DIR3:
  D3AA  ED5B3AD6      		LD	DE,(DTA_CCP+1+01EH)
  D3AE  2A38D6        		LD	HL,(DTA_CCP+1+01CH)
  D3B1  CDF4D3        		CALL	PRDEC_DEHL
  D3B4                	DIR6:
  D3B4  3AB1EF        		LD	A,(CRTCD+1)
  D3B7  FE29          		CP	40+1
  D3B9  D456D4        		CALL	NC,PRTTMS
                      	
  D3BC                	LTNL:
  D3BC  1E03          		LD	E,3
  D3BE  C3CDEF        		JP	_PRINT
                      	
  D3C1                	C_PATH:
  D3C1  CD7FE1        		CALL	SPCUT
  D3C4  2140D6        		LD	HL,PATHD
  D3C7  FE21          		CP	021H
  D3C9  300C          		JR	NC,CPATH0
  D3CB                	CPATHP:
  D3CB  7E            		LD	A,(HL)
  D3CC  23            		INC	HL
  D3CD  FE20          		CP	020H
  D3CF  3F            		CCF
  D3D0  30EA          		JR	NC,LTNL
  D3D2  CDD9DB        		CALL	MSG_A
  D3D5  18F4          		JR	CPATHP
  D3D7                	CPATH0:
  D3D7  FE3B          		CP	';'
  D3D9  2001          		JR	NZ,CPATH1
  D3DB  13            		INC	DE
  D3DC                	CPATH1:
  D3DC  EB            		EX	DE,HL
  D3DD  014000        		LD	BC,PATHX
  D3E0  EDB0          		LDIR
  D3E2  C9            		RET
                      	
  D3E3                	PUTDRV:
  D3E3  3A14EF        		LD	A,(FDRV)
  D3E6  C640          		ADD	A,'A'-1
  D3E8  CDD9DB        		CALL	MSG_A
  D3EB  3E3A          		LD	A,':'
  D3ED                	MSG_AR:
  D3ED  C3D9DB        		JP	MSG_A
                      	
  D3F0                	PRDEC_HL:
  D3F0  AF            		XOR	A
  D3F1                	PRDEC_AHL:
  D3F1  5F            		LD	E,A
  D3F2  1600          		LD	D,0
  D3F4                	PRDEC_DEHL:
  D3F4  D5            		PUSH	DE
  D3F5  110FEF        		LD	DE,DECBF
  D3F8  0605          		LD	B,5
  D3FA  CDFAD4        		CALL	ZERO_MEMORY_DE	;A=0
  D3FD  D1            		POP	DE
                      	
  D3FE  0E20          		LD	C,32
  D400                	DEC1:
  D400  29            		ADD	HL,HL
  D401  EB            		EX	DE,HL
  D402  ED6A          		ADC	HL,HL
  D404  EB            		EX	DE,HL
  D405  E5            		PUSH	HL
  D406  2113EF        		LD	HL,DECBF+4
  D409  0605          		LD	B,5
  D40B                	DEC2:
  D40B  7E            		LD	A,(HL)
  D40C  8F            		ADC	A,A
  D40D  27            		DAA
  D40E  77            		LD	(HL),A
  D40F  2B            		DEC	HL
  D410  10F9          		DJNZ	DEC2
  D412  E1            		POP	HL
  D413  0D            		DEC	C
  D414  20EA          		JR	NZ,DEC1
                      	
  D416  210FEF        		LD	HL,DECBF
  D419  3E20          		LD	A,020H
  D41B  0604          		LD	B,4
  D41D                	DEC3:
  D41D  CD2AD4        		CALL	DEC4
  D420  CD2AD4        		CALL	DEC4
  D423  23            		INC	HL
  D424  10F7          		DJNZ	DEC3
  D426                	DECX:
  D426  CD2AD4        		CALL	DEC4
  D429  AF            		XOR	A
  D42A                	DEC4:
  D42A  ED6F          		RLD
  D42C  FE20          		CP	020H
  D42E  2802          		JR	Z,DEC5
  D430  F630          		OR	030H
  D432                	DEC5:
  D432  18B9          		JR	MSG_AR
                      	
  D434                	FPRNT:
  D434  0608          		LD	B,8
  D436  CD47D4        		CALL	P_N1
  D439  7E            		LD	A,(HL)
  D43A  C680          		ADD	A,0A0H-020H
  D43C  FEA0          		CP	0A0H
  D43E  2802          		JR	Z,FPR1
  D440  3E2E          		LD	A,'.'
  D442                	FPR1:
  D442  CDD9DB        		CALL	MSG_A
  D445  0603          		LD	B,3
                      	
  D447                	P_N1:
  D447  C5            		PUSH	BC
  D448  E5            		PUSH	HL
  D449  7E            		LD	A,(HL)
  D44A  CDC0E3        		CALL	CAP3
  D44D  CDD9DB        		CALL	MSG_A
  D450  E1            		POP	HL
  D451  C1            		POP	BC
  D452  23            		INC	HL
  D453  10F2          		DJNZ	P_N1
  D455  C9            		RET
                      	
  D456                	PRTTMS:
  D456  3E20          		LD	A,020H
  D458  CDD9DB        		CALL	MSG_A
                      	
  D45B  2A34D6        		LD	HL,(DTA_CCP+1+24)
  D45E  7D            		LD	A,L
  D45F  B7            		OR	A
  D460  C8            		RET	Z
  D461  CB3C          		SRL	H
  D463  17            		RLA
  D464  17            		RLA
  D465  17            		RLA
  D466  17            		RLA
  D467  E60F          		AND	00FH
  D469  57            		LD	D,A
  D46A  7C            		LD	A,H
  D46B  C650          		ADD	A,80
  D46D  CDB0D4        		CALL	PRDEC_A
  D470  3E2D          		LD	A,'-'
  D472  CDD9DB        		CALL	MSG_A
  D475  7A            		LD	A,D
  D476  CDB0D4        		CALL	PRDEC_A
  D479  3E2D          		LD	A,'-'
  D47B  CDD9DB        		CALL	MSG_A
  D47E  7D            		LD	A,L
  D47F  E61F          		AND	01FH
  D481  CDB0D4        		CALL	PRDEC_A
                      	
  D484  2A32D6        		LD	HL,(DTA_CCP+1+22)
                      	
  D487  3E20          		LD	A,020H
  D489  CDD9DB        		CALL	MSG_A
  D48C  7C            		LD	A,H
  D48D  65            		LD	H,L
  D48E  0603          		LD	B,3
  D490                	PRTTMS1:
  D490  1F            		RRA
  D491  CB1D          		RR	L
  D493  10FB          		DJNZ	PRTTMS1
  D495  E61F          		AND	01FH
  D497  CDB0D4        		CALL	PRDEC_A
  D49A  3E3A          		LD	A,':'
  D49C  CDD9DB        		CALL	MSG_A
  D49F  7D            		LD	A,L
  D4A0  0F            		RRCA
  D4A1  0F            		RRCA
  D4A2  E63F          		AND	03FH
  D4A4  CDB0D4        		CALL	PRDEC_A
  D4A7  3E3A          		LD	A,':'
  D4A9  CDD9DB        		CALL	MSG_A
  D4AC  7C            		LD	A,H
  D4AD  E61F          		AND	01FH
  D4AF  87            		ADD	A,A
                      	
                      	;	PRINT10
                      	
  D4B0                	PRDEC_A:
  D4B0  E5            		PUSH	HL
  D4B1  0608          		LD	B,8
  D4B3  4F            		LD	C,A
  D4B4  AF            		XOR	A
  D4B5                	PRTA1:
  D4B5  CB01          		RLC	C
  D4B7  8F            		ADC	A,A
  D4B8  27            		DAA
  D4B9  10FA          		DJNZ	PRTA1
  D4BB  210FEF        		LD	HL,DECBF
  D4BE  77            		LD	(HL),A
  D4BF  AF            		XOR	A
  D4C0  CD26D4        		CALL	DECX
  D4C3  E1            		POP	HL
  D4C4  C9            		RET
                      	
  D4C5                	SETFCB:
  D4C5  CD7FE1        		CALL	SPCUT
  D4C8  1A            		LD	A,(DE)
  D4C9  FE20          		CP	020H
  D4CB  3801          		JR	C,SETFCBA
  D4CD  1B            		DEC	DE
  D4CE                	SETFCBA:
  D4CE  0624          		LD	B,36
  D4D0  215C00        		LD	HL,FCB1
  D4D3  E5            		PUSH	HL
  D4D4  AF            		XOR	A
  D4D5  CD54E1        		CALL	FILL_MEMORY
  D4D8  E1            		POP	HL
  D4D9  D5            		PUSH	DE
  D4DA  CD78DA        		CALL	_SYS5C		;(BDOS)ファイル名の解析
  D4DD  216C00        		LD	HL,FCB2
  D4E0  CD78DA        		CALL	_SYS5C		;(BDOS)ファイル名の解析
  D4E3  E1            		POP	HL
  D4E4  010050        		LD	BC,05000H
  D4E7  118100        		LD	DE,00081H
  D4EA                	SETFCB1:
  D4EA  7E            		LD	A,(HL)
  D4EB  23            		INC	HL
  D4EC  FE20          		CP	020H
  D4EE  3805          		JR	C,SETFCB2
  D4F0  12            		LD	(DE),A
  D4F1  13            		INC	DE
  D4F2  0C            		INC	C
  D4F3  10F5          		DJNZ	SETFCB1
  D4F5                	SETFCB2:
  D4F5  79            		LD	A,C
  D4F6  328000        		LD	(DTA1),A
  D4F9  04            		INC	B
  D4FA                	ZERO_MEMORY_DE:
  D4FA  AF            		XOR	A
  D4FB                	FILL_MEMORY_DE:
  D4FB  12            		LD	(DE),A
  D4FC  13            		INC	DE
  D4FD  10FC          		DJNZ	FILL_MEMORY_DE
  D4FF  C9            		RET
                      	
  D500                	C_COPY:
  D500  CDC5D4        		CALL	SETFCB
                      	
  D503  1161EF        		LD	DE,BEEPD
  D506  CDA0E0        		CALL	FILEC2
  D509  216C00        		LD	HL,FCB2
  D50C  CD7FDA        		CALL	S29X1
                      	
  D50F  3E10          		LD	A,010H
  D511  326900        		LD	(FCB1+13),A
  D514  216D00        		LD	HL,FCB2+1
  D517  CD10D6        		CALL	CWILD1
  D51A                	COPY0:
  D51A  CD0DD6        		CALL	CWILD
  D51D  215C00        		LD	HL,FCB1
  D520  CDEBD1        		CALL	OPEN
  D523  37            		SCF
  D524                	COPY1:
  D524  C0            		RET	NZ
  D525  CDFED1        		CALL	DEFCB
                      	
  D528  3A28D6        		LD	A,(DTA_CCP+13)
  D52B  CB67          		BIT	4,A
  D52D  2821          		JR	Z,COPY1A
                      	
  D52F  215D00        		LD	HL,FCB1+1
  D532  CDE3E4        		CALL	CHKWILD
  D535  3814          		JR	C,COPY9
                      	
  D537  3E20          		LD	A,020H
  D539  325D00        		LD	(FCB1+1),A
  D53C  2A35D6        		LD	HL,(DTA_CCP+26)
  D53F  23            		INC	HL
  D540  226A00        		LD	(FCB1+14),HL
  D543  18D5          		JR	COPY0
                      	
  D545                	COPY8:
  D545  CD0ED8        		CALL	_SYS09		;(BDOS)文字列出力
  D548  CDBCD3        		CALL	LTNL
  D54B                	COPY9:
  D54B  CDFAD1        		CALL	OPEN2
  D54E  18D4          		JR	COPY1
                      	
  D550                	COPY1A:
  D550  216C00        		LD	HL,FCB2
  D553  1114EF        		LD	DE,FDRV
  D556  011DD6        		LD	BC,DTA_CCP+2
  D559  EDA0          		LDI
  D55B  3E0B          		LD	A,11
  D55D                	COPY2:
  D55D  F5            		PUSH	AF
  D55E  7E            		LD	A,(HL)
  D55F  FE3F          		CP	'?'
  D561  2001          		JR	NZ,COPY3
  D563  0A            		LD	A,(BC)
  D564                	COPY3:
  D564  12            		LD	(DE),A
  D565  03            		INC	BC
  D566  13            		INC	DE
  D567  23            		INC	HL
  D568  F1            		POP	AF
  D569  3D            		DEC	A
  D56A  20F1          		JR	NZ,COPY2
  D56C  010500        		LD	BC,16-11
  D56F  EDB0          		LDIR
  D571                	PUTNAME:
  D571  215D00        		LD	HL,FCB1+1
  D574  CDE3E4        		CALL	CHKWILD
  D577  300B          		JR	NC,PUTN1
  D579  2115EF        		LD	HL,FDRV+1
  D57C  CD34D4        		CALL	FPRNT
  D57F  3E20          		LD	A,020H
  D581  CDD9DB        		CALL	MSG_A
  D584                	PUTN1:
  D584  1114EF        		LD	DE,FDRV
  D587  0E16          		LD	C,016H		;(BDOS)ファイルの作成
  D589  CD19D2        		CALL	SYSTEM0
  D58C  2048          		JR	NZ,COPYE2
  D58E  67            		LD	H,A		;A=0
  D58F  6F            		LD	L,A
  D590  2235EF        		LD	(FDRV+33),HL	;(FCB)ランダムレコード
  D593  2237EF        		LD	(FDRV+35),HL
  D596                	COPY6:
  D596  CD22D2        		CALL	S27BUF
  D599  47            		LD	B,A
  D59A  3C            		INC	A
  D59B  2831          		JR	Z,COPYE
  D59D  7C            		LD	A,H
  D59E  B5            		OR	L
  D59F  280C          		JR	Z,COPY7
  D5A1  1114EF        		LD	DE,FDRV
  D5A4  0E26          		LD	C,026H		;(BDOS)ランダムブロック書き込み
  D5A6  CD19D2        		CALL	SYSTEM0
  D5A9  2023          		JR	NZ,COPYE
  D5AB  10E9          		DJNZ	COPY6
  D5AD                	COPY7:
  D5AD  3A28D6        		LD	A,(DTA_CCP+13)	;(FCB)属性
  D5B0  3221EF        		LD	(FDRV+13),A
  D5B3  212FD6        		LD	HL,DTA_CCP+20	;(FCB)タイムスタンプ
  D5B6  1128EF        		LD	DE,FDRV+20
  D5B9  010400        		LD	BC,4
  D5BC  EDB0          		LDIR
                      	
  D5BE  1114EF        		LD	DE,FDRV
  D5C1  0E10          		LD	C,010H		;(BDOS)ファイルのクローズ
  D5C3  CD19D2        		CALL	SYSTEM0
  D5C6  2006          		JR	NZ,COPYE
                      	
  D5C8  1171EF        		LD	DE,OK
  D5CB  C345D5        		JP	COPY8
                      	
  D5CE                	COPYE:
  D5CE  1114EF        		LD	DE,FDRV
  D5D1  0E13          		LD	C,013H		;(BDOS)ファイルの削除
  D5D3  CD0500        		CALL	SYSTEM
  D5D6                	COPYE2:
  D5D6  37            		SCF
  D5D7  C9            		RET
                      	
  D5D8                	C_TYPE:
  D5D8  CDC5D4        		CALL	SETFCB
  D5DB  215C00        		LD	HL,FCB1
  D5DE  CDEBD1        		CALL	OPEN
  D5E1  37            		SCF
  D5E2                	TYPE1:
  D5E2  C0            		RET	NZ
  D5E3  CDFED1        		CALL	DEFCB
  D5E6  3E06          		LD	A,6
  D5E8  CDD9DB        		CALL	MSG_A
  D5EB                	TYPE2:
  D5EB  CD22D2        		CALL	S27BUF
  D5EE  C6FE          		ADD	A,0FEH
  D5F0  D8            		RET	C
  D5F1  7C            		LD	A,H
  D5F2  B5            		OR	L
  D5F3  2813          		JR	Z,TYPEE
  D5F5  010001        		LD	BC,00100H
  D5F8                	TYPE3:
  D5F8  0A            		LD	A,(BC)
  D5F9  03            		INC	BC
  D5FA  FE1A          		CP	01AH
  D5FC  280A          		JR	Z,TYPEE
  D5FE  CDD9DB        		CALL	MSG_A
  D601  2B            		DEC	HL
  D602  7C            		LD	A,H
  D603  B5            		OR	L
  D604  20F2          		JR	NZ,TYPE3
  D606  18E3          		JR	TYPE2
  D608                	TYPEE:
  D608  CDFAD1        		CALL	OPEN2
  D60B  18D5          		JR	TYPE1
                      	
  D60D                	CWILD:
  D60D  215D00        		LD	HL,FCB1+1
  D610                	CWILD1:
  D610  7E            		LD	A,(HL)
  D611  FE20          		CP	020H
  D613  C0            		RET	NZ
  D614                	CWILD2:
  D614  3E3F          		LD	A,'?'
  D616  060B          		LD	B,11
  D618  C354E1        		JP	FILL_MEMORY
                      	
  D61B                	DTA_CCP:
  D61B                		DS	37
                      	
  0040                	PATHX	EQU	64
  D640                	PATHD:	DS	PATHX
  D680  00            	PATHE:	DB	0
                      	
  D681  20627974657320	FREE:	DB	" bytes free",9,'$'
  D68E  20202020203C44	DIRMES:	DB	"     <DIR>$"
  D699  20203A24      	DIRHD:	DB	"  :$"
                      	
  D69D                	COMTB:
  D69D  44202020      		DB	"D   "	;1
  D6A1  1CD3          		DW	C_DIR
  D6A3  44495220      		DB	"DIR "	;2
  D6A7  1CD3          		DW	C_DIR
  D6A9  434F5059      		DB	"COPY"	;3
  D6AD  00D5          		DW	C_COPY
  D6AF  43442020      		DB	"CD  "	;4
  D6B3  1ED2          		DW	C_CD
  D6B5  44454C20      		DB	"DEL "	;5
  D6B9  FFD2          		DW	C_DEL
  D6BB  50415448      		DB	"PATH"	;6
  D6BF  C1D3          		DW	C_PATH
  D6C1  52454E20      		DB	"REN "	;7
  D6C5  09D3          		DW	C_REN
  D6C7  54595045      		DB	"TYPE"	;8
  D6CB  D8D5          		DW	C_TYPE
  D6CD  52454D20      		DB	"REM "	;9
  D6D1  0BD8          		DW	C_REM
                      	
                      	;X1DISK2.ASM
                      	
                      	;	LSX-Dodgers DISK2
                      	;	X1/turbo/Z
                      	
                      	;	GRAPHIC RAM DISK DRIVER
                      	
                      	
  D6D3                	GDRD:
  D6D3  C5            		PUSH	BC
  D6D4  CDFCD6        		CALL	GADR
  D6D7  F1            		POP	AF
  D6D8                	GDRD1:
  D6D8  EDA2          		INI
  D6DA  04            		INC	B
  D6DB  0C            		INC	C
  D6DC  20FA          		JR	NZ,GDRD1
  D6DE  04            		INC	B
  D6DF  13            		INC	DE
  D6E0  3D            		DEC	A
  D6E1  20F5          		JR	NZ,GDRD1
  D6E3                	GBANK0:
  D6E3  3ABFEF        		LD	A,(WK1FD0)
  D6E6  E6EF          		AND	0EFH		;BANK0
  D6E8  181D          		JR	S1FD0
                      	
  D6EA                	GDWT:
  D6EA  C5            		PUSH	BC
  D6EB  CDFCD6        		CALL	GADR
  D6EE  F1            		POP	AF
  D6EF                	GDWT1:
  D6EF  04            		INC	B
  D6F0  EDA3          		OUTI
  D6F2  0C            		INC	C
  D6F3  20FA          		JR	NZ,GDWT1
  D6F5  04            		INC	B
  D6F6  13            		INC	DE
  D6F7  3D            		DEC	A
  D6F8  20F5          		JR	NZ,GDWT1
  D6FA  18E7          		JR	GBANK0
                      	
  D6FC                	GADR:
  D6FC  0E00          		LD	C,0
  D6FE  7B            		LD	A,E
  D6FF  C640          		ADD	A,040H
  D701  47            		LD	B,A
  D702  3ABFEF        		LD	A,(WK1FD0)
  D705  F610          		OR	010H		;BANK1
  D707                	S1FD0:
  D707  C5            		PUSH	BC
  D708  32BFEF        		LD	(WK1FD0),A
  D70B  01D01F        		LD	BC,01FD0H
  D70E  ED79          		OUT	(C),A
  D710  C1            		POP	BC
  D711  C9            		RET
                      	
                      	
                      	;	BANK RAM DISK DRIVER
                      	
  D712                	BDWT:
  D712  3E            		DB	03EH	;LD A,??
  D713  37            		SCF
  D714  1802          		JR	BRW
                      	
  D716                	BDRD:
  D716  3E            		DB	03EH	;LD A,??
  D717  A7            		AND	A
  D718                	BRW:
  D718  F3            		DI
  D719  3241D7        		LD	(BRWPAT),A
  D71C  ED7361D7      		LD	(BANKSP+1),SP
  D720  3A62D7        		LD	A,(BANKSP+2)
  D723  87            		ADD	A,A
  D724  3803          		JR	C,BRW0
  D726  31CAFF        		LD	SP,EXTSP
  D729                	BRW0:
  D729  D9            		EXX		;裏レジスタ
  D72A  C5            		 PUSH	 BC
  D72B  D5            		 PUSH	 DE
  D72C  01000B        		 LD	 BC,00B00H	 ;バンクメモリ
  D72F  111010        		 LD	 DE,01010H
  D732  D9            		 EXX		;表レジスタ
  D733                	BRW1:
  D733  C5            		PUSH	BC
  D734  D5            		PUSH	DE
  D735  EB            		EX	DE,HL
  D736  29            		ADD	HL,HL
  D737  29            		ADD	HL,HL
  D738  7C            		LD	A,H
  D739  E60F          		AND	00FH	;バンク
  D73B  65            		LD	H,L
  D73C  CB3C          		SRL	H	;/2
  D73E  2E00          		LD	L,0
  D740  D9            		EXX		;裏レジスタ
  D741  A7            	BRWPAT:	 AND	 A	;自己書き換え(AND A:読み出し/SCF:書き込み)
  D742  3808          		 JR	 C,BRWT
  D744  57            		 LD	 D,A	 ;D=バンク
  D745  D9            		 EXX		;表レジスタ
  D746  EB            		EX	DE,HL
  D747  CD65D7        		CALL	BTFR
  D74A  1806          		JR	BRW2
  D74C                	BRWT:
  D74C  5F            		 LD	E,A	 ;E=バンク
  D74D  D9            		 EXX		;表レジスタ
  D74E  CD65D7        		CALL	BTFR
  D751  EB            		EX	DE,HL
  D752                	BRW2:
  D752  D1            		POP	DE
  D753  C1            		POP	BC
  D754  13            		INC	DE
  D755  10DC          		DJNZ	BRW1
                      	
  D757  D9            		EXX		;裏レジスタ
  D758  3E10          		 LD	 A,10H
  D75A  ED79          		 OUT	 (C),A		;メインメモリに切り替える
  D75C  D1            		 POP	 DE
  D75D  C1            		 POP	 BC
  D75E  D9            		 EXX		;表レジスタ
  D75F  AF            		XOR	A
  D760  310000        	BANKSP:	LD	SP,0
  D763  FB            		EI
  D764  C9            		RET
                      	
                      	;メインメモリ←→バンクメモリ 512バイトの転送を行う
                      	; DE:転送元アドレス
                      	; HL:転送先アドレス
                      	; 裏BC:バンク切り替えポート(0B00H)
                      	; 裏D:転送元バンク(0-0FH:10Hはメインメモリ)
                      	; 裏E:転送先バンク(0-0FH:10Hはメインメモリ)
                      	
  D765                	BTFR:
  D765  0600          		LD	B,0	;256回ループ(256*2)
  D767                	BTFR1:
  D767  D9            		EXX		;裏レジスタ
  D768  ED51          		 OUT	 (C),D	 ;BANK(MAIN)
  D76A  D9            		 EXX		;表レジスタ
  D76B  1A            		LD	A,(DE)
  D76C  4F            		LD	C,A
  D76D  13            		INC	DE
  D76E  1A            		LD	A,(DE)
  D76F  13            		INC	DE
  D770  D9            		EXX		;裏レジスタ
  D771  ED59          		 OUT	 (C),E	 ;MAIN(BANK)
  D773  D9            		 EXX		;表レジスタ
  D774  71            		LD	(HL),C
  D775  23            		INC	HL
  D776  77            		LD	(HL),A
  D777  23            		INC	HL
  D778  10ED          		DJNZ	BTFR1
  D77A  C9            		RET
                      	
                      	;	EMM DRIVER(DMA)
                      	
                      	
  D77B                	EDWT:
  D77B  0E0F          		LD	C,15
  D77D  1802          		JR	EDWT1
  D77F                	EDRD:
  D77F  0E0E          		LD	C,14
  D781                	EDWT1:
  D781  C5            		PUSH	BC
  D782  D5            		PUSH	DE
  D783  22C3D7        		LD	(DMAB),HL
  D786  78            		LD	A,B
  D787  87            		ADD	A,A
  D788  3D            		DEC	A
  D789  32BCD7        		LD	(DMALEN+1),A
  D78C  3C            		INC	A
  D78D  84            		ADD	A,H	;HL(アドレス)を先に進めておく
  D78E  67            		LD	H,A
  D78F  EB            		EX	DE,HL
  D790  29            		ADD	HL,HL
  D791  DD460C        		LD	B,(IX+00CH)	;DPB_0C_MAXCYL
  D794  79            		LD	A,C
  D795  DD4E0D        		LD	C,(IX+00DH)	;DPB_0D_MAXSEC
  D798  09            		ADD	HL,BC
  D799  DD4E13        		LD	C,(IX+013H)	;DPB_13_UNITNO
  D79C  060D          		LD	B,00DH
  D79E  ED49          		OUT	(C),C
  D7A0  0C            		INC	C
  D7A1  ED69          		OUT	(C),L
  D7A3  0C            		INC	C
  D7A4  ED61          		OUT	(C),H
                      	
  D7A6  21B7D7        		LD	HL,EDMAD
  D7A9  CDB2DF        		CALL	SETDMA
  D7AC  CDDADF        		CALL	GO_DMA
  D7AF  EB            		EX	DE,HL
  D7B0  D1            		POP	DE
  D7B1  C1            		POP	BC
  D7B2                	DMA_ADD_SEC:
  D7B2  13            		INC	DE
  D7B3  10FD          		DJNZ	DMA_ADD_SEC
  D7B5  AF            		XOR	A
  D7B6  C9            		RET
                      	
  D7B7                	EDMAD:
  D7B7  C3            		DB	0C3H	;WR6 リセット
  D7B8  7D            		DB	07DH	;WR0 ポートA→ポートB 転送
  D7B9  030D          		DW	00D03H	;ポートA開始アドレス EMMリード/ライト
  D7BB  FF01          	DMALEN:	DW	001FFH	;ブロック長(転送サイズ-1)
  D7BD  2C            		DB	02CH	;WR1 ポートAアドレス固定 I/O
  D7BE  50            		DB	050H	;WR2 ポートBアドレスインクリメント メモリ
  D7BF  02            		DB	002H	;ポートBタイミング
  D7C0  80            		DB	080H	;WR3
  D7C1  9A            		DB	09AH	;WR5 WAIT RDY:HIGH
  D7C2  AD            		DB	0ADH	;WR4 連続 ポートB開始アドレス
  D7C3  0000          	DMAB:	DW	0
  D7C5  01            		DB	001H	;WR0 ポートA←ポートB 転送
  D7C6                	EMME:
                      	
  074B                	AT_RE	EQU	EDWT-AT_EDWT
                      	
                      	
                      	;LDOS.ASM
                      	
                      	;	LSX-Dodgers OS
                      	
  D7C6                	BDOS0:
  D7C6  E5            		PUSH	HL
  D7C7  210001        		LD	HL,00100H
  D7CA  39            		ADD	HL,SP
  D7CB  3810          		JR	C,BDOS1X
  D7CD                	BDOS2:
  D7CD  E1            		POP	HL
  D7CE  ED73D9D7      		LD	(BDSP+1),SP
  D7D2  31CAFF        		LD	SP,EXTSP
  D7D5  CDDCD7        		CALL	BDOS1
  D7D8  310000        	BDSP:	LD	SP,0
  D7DB  C9            		RET
  D7DC                	BDOS1:
  D7DC  E5            		PUSH	HL
  D7DD                	BDOS1X:
  D7DD  79            		LD	A,C
  D7DE  FE3C          		CP	03CH
  D7E0  3802          		JR	C,DOS1
  D7E2  3E3C          		LD	A,03CH
  D7E4                	DOS1:
  D7E4  87            		ADD	A,A
  D7E5  6F            		LD	L,A
  D7E6  26F0          		LD	H,STABLE/256
  D7E8  7E            		LD	A,(HL)
  D7E9  2C            		INC	L
  D7EA  66            		LD	H,(HL)
  D7EB  6F            		LD	L,A
  D7EC  E3            		EX	(SP),HL
  D7ED  79            		LD	A,C
  D7EE  C9            		RET
  D7EF                	_DOS2:
  D7EF  FE5A          		CP	05AH		;(BDOS)カレントディレクトリの変更(_CHDIR)
  D7F1  CABCDA        		JP	Z,_SYS5A
  D7F4  FE5C          		CP	05CH		;(BDOS)ファイル名の解析(_PFILE)
  D7F6  CA78DA        		JP	Z,_SYS5C
  D7F9  FE5F          		CP	05FH		;(BDOS)ディスクバッファのフラッシュ(_FLUSH)
  D7FB  CA5BED        		JP	Z,_SYS5F
  D7FE  FE6F          		CP	06FH		;(BDOS)MSX-DOS のバージョン番号の獲得(_DOSVER)
  D800  200A          		JR	NZ,_ERROR
  D802  011D01        		LD	BC,011DH
  D805  114601        		LD	DE,VER
  D808  210000        		LD	HL,MACD
  D80B                	C_REM:
  D80B  AF            		XOR	A
  D80C                	_ERROR:
  D80C  A7            		AND	A
  D80D  C9            		RET
                      	
  D80E                	_SYS09:		;文字列出力
  D80E  D5            		PUSH	DE
  D80F                	_SX09:
  D80F  1A            		LD	A,(DE)
  D810  13            		INC	DE
  D811  FE24          		CP	'$'
  D813  2831          		JR	Z,SX0E2
  D815  CDD9DB        		CALL	MSG_A
  D818  18F5          		JR	_SX09
                      	
  D81A                	MSX1:
  D81A  CDD9DB        		CALL	MSG_A
  D81D                	MSX:
  D81D  7E            		LD	A,(HL)
  D81E  23            		INC	HL
  D81F  B7            		OR	A
  D820  20F8          		JR	NZ,MSX1
  D822  C9            		RET
                      	
  D823                	_SYS0C:		;(BDOS)バージョン番号の獲得
  D823  212200        		LD	HL,00022H
  D826  7D            		LD	A,L
  D827  C9            		RET
                      	
  D828                	_SYS0D:		;(BDOS)ディスクリセット
  D828  CDDFEF        		CALL	_FFLUSH
  D82B  E5            		PUSH	HL
  D82C  218000        		LD	HL,00080H
  D82F  228AEF        		LD	(_DTA),HL
  D832  E1            		POP	HL
  D833  D5            		PUSH	DE
  D834  1E00          		LD	E,0
  D836  3E            		DB	03EH
                      	
  D837                	_SYS0E:		;(BDOS)カレントドライブの設定
  D837  D5            		PUSH	DE
  D838  7B            		LD	A,E
  D839  DDE5          		PUSH	IX
  D83B  CDDCEF        		CALL	_GETDPB
  D83E  DDE1          		POP	IX
  D840  3804          		JR	C,SX0E2
  D842  7B            		LD	A,E
  D843  3287EF        		LD	(_DVSW),A
  D846                	SX0E2:
  D846  3E08          		LD	A,8
  D848  D1            		POP	DE
  D849  C9            		RET
                      	
  D84A                	_SYS0F:		;(BDOS)ファイルのオープン
  D84A  CD91E1        		CALL	SETDRV
  D84D  FD7E1C        		LD	A,(IY+28)	;(FCB)オープンモード
  D850  C602          		ADD	A,002H		;Write
  D852  2848          		JR	Z,FEND0F
  D854  CDDFE4        		CALL	CHKWILDX
                      	
  D857  D4B1E1        		CALL	NC,SOPENX
  D85A                	_S16XX:
  D85A  3840          		JR	C,FEND0F
                      					;Directory location
  D85C  3A9FEF        		LD	A,(_FILEN)
  D85F  FD7719        		LD	(IY+25),A	;(FCB)ディレクトリロケーション
                      	;				_DIRF
  D862  3AA0EF        		LD	A,(_DIRF)
  D865  FD771D        		LD	(IY+29),A	;(FCB)ディレクトリモード
                      	;				_FBPS
  D868  FD731E        		LD	(IY+30),E	;(FCB)ディレクトリポジション
  D86B  FD721F        		LD	(IY+31),D
                      	;				Open(Read)
  D86E  FD361CFF      		LD	(IY+28),0FFH	;(FCB)オープンモード
                      	;				FILENAME
  D872  FDE5          		PUSH	IY
  D874  D1            		POP	DE
  D875  13            		INC	DE
  D876  010B00        		LD	BC,11
  D879  EDB0          		LDIR
                      	;				Attribute
  D87B  7E            		LD	A,(HL)
  D87C  FD770D        		LD	(IY+13),A	;(FCB)属性(アトリビュート)
  D87F  110B00        		LD	DE,11		;Reserved
  D882  19            		ADD	HL,DE
                      						;(FCB)ファイルを最後に変更した時刻
  D883  11EAF0        		LD	DE,SDDATA		;(FCB)ファイルを最後に変更した日付
  D886  060A          		LD	B,SDDATAE-SDDATA	;(FCB)ファイルの先頭クラスタ
  D888                	S16LOOP:				;(FCB)ファイルのサイズ(バイト単位)
  D888  1A            		LD	A,(DE)
  D889  13            		INC	DE
  D88A  3291D8        		LD	(S16PAT+2),A
  D88D  7E            		LD	A,(HL)
  D88E  23            		INC	HL
  D88F  FD7700        	S16PAT:	LD	(IY+0),A
  D892  10F4          		DJNZ	S16LOOP
                      	
  D894  AF            		XOR	A
  D895  FD770C        		LD	(IY+12),A	;(FCB)カレントブロック
  D898  FD2240D9      		LD	(OFCB+1),IY
  D89C                	FEND0F:
  D89C  1845          		JR	FEND10
                      	
  D89E                	_SYS10:		;(BDOS)ファイルのクローズ
  D89E  CD91E1        		CALL	SETDRV
  D8A1  FD7E1C        		LD	A,(IY+28)	;(FCB)オープンモード
  D8A4  D6FE          		SUB	0FEH
  D8A6  37            		SCF
  D8A7  3F            		CCF
  D8A8  2039          		JR	NZ,FEND10
  D8AA                	_S10A:				;Write
  D8AA  FD770E        		LD	(IY+14),A	;(FCB)レコードサイズ/アクセスするディレクトリのクラスタ番号+1
  D8AD  FD770F        		LD	(IY+15),A
                      	
  D8B0  FD341C        		INC	(IY+28)		;0FEH->0FFH ;オープンモード
  D8B3  CDD7E5        		CALL	SETTMS
                      	
  D8B6  FD7E1D        		LD	A,(IY+29)	;(FCB)ディレクトリモード
  D8B9  32A0EF        		LD	(_DIRF),A
  D8BC  E67F          		AND	07FH
  D8BE  3288EA        		LD	(_SECPS+1),A
  D8C1  FD5E1E        		LD	E,(IY+30)	;(FCB)ディレクトリポジション
  D8C4  FD561F        		LD	D,(IY+31)
  D8C7  CDE5E2        		CALL	READ_DIR
  D8CA  3817          		JR	C,FEND10
                      	
  D8CC  3A0FE2        	SDECM1:	LD	A,(SDECPAT+1)	;1セクタ辺りのディレクトリエントリ数
  D8CF  3D            	 	DEC	A		;1024=01FH / 512=00FH / 256=7
  D8D0  FDA619        		AND	(IY+25)		;(FCB)ディレクトリロケーション
  D8D3  6F            		LD	L,A
  D8D4  2600          		LD	H,0
  D8D6  29            		ADD	HL,HL		;*2
  D8D7  29            		ADD	HL,HL		;*4
  D8D8  29            		ADD	HL,HL		;*8
  D8D9  29            		ADD	HL,HL		;*16
  D8DA  29            		ADD	HL,HL		;*32
  D8DB  ED4B7EF0      		LD	BC,(_DTBUF)
  D8DF  09            		ADD	HL,BC
                      	
  D8E0  CDEFE4        		CALL	SDIRENT
  D8E3                	FEND10:
  D8E3  1842          		JR	FEND
                      	
  D8E5                	_SYS11:		;(BDOS)最初のファイルの検索
  D8E5  CD91E1        		CALL	SETDRV
  D8E8  CDDEE1        		CALL	SOPEN
  D8EB                	_S12X1:
  D8EB  383A          		JR	C,FEND
  D8ED  CD7EE2        		CALL	SOPENE2
  D8F0  ED5B8AEF      		LD	DE,(_DTA)
  D8F4  3A88EF        		LD	A,(_DRV)
  D8F7  3C            		INC	A
  D8F8  12            		LD	(DE),A
  D8F9  D5            		PUSH	DE
  D8FA  13            		INC	DE
  D8FB  012000        		LD	BC,32
  D8FE  EDB0          		LDIR
  D900  FD6E0E        		LD	L,(IY+14)	;(FCB)レコードサイズ/アクセスするディレクトリのクラスタ番号+1
  D903  FD660F        		LD	H,(IY+15)
  D906  22F4F0        		LD	(SCDIR),HL
  D909  2A9AEF        		LD	HL,(_FBPS)
  D90C  22F6F0        		LD	(SFBPS),HL
  D90F  2A9FEF        		LD	HL,(_FILEN)
  D912  7C            		LD	A,H
  D913  B7            		OR	A
  D914  2806          		JR	Z,_S12DIR
  D916  3A88EA        		LD	A,(_SECPS+1)
  D919  F680          		OR	080H
  D91B  67            		LD	H,A
  D91C                	_S12DIR:
  D91C  22F8F0        		LD	(SFNDF),HL	;Directory position and Flags
  D91F  E1            		POP	HL
  D920  1139EF        		LD	DE,SFILE
  D923  0E21          		LD	C,33
  D925                	_S11B:
  D925  EDB0          		LDIR
  D927                	FEND:
  D927  9F            		SBC	A,A		;CF=0 A=0 : CF=1 A=0FFH
  D928                	FENDE:
  D928  FDE1          		POP	IY
  D92A  C1            		POP	BC
  D92B  D1            		POP	DE
  D92C  E1            		POP	HL
  D92D  C9            		RET
                      	
  D92E                	_SYS12:		;(BDOS)次のファイルの検索
  D92E  E5            		PUSH	HL
  D92F  D5            		PUSH	DE
  D930  C5            		PUSH	BC
  D931  FDE5          		PUSH	IY
  D933  CD42E2        		CALL	NOPEN
  D936  18B3          		JR	_S12X1
                      	
  D938                	_S16K:
  D938  FD7E1C        		LD	A,(IY+28)	;(FCB)オープンモード
  D93B  FEFD          		CP	0FDH		;Device(0FDH)
  D93D  37            		SCF
  D93E  C8            		RET	Z
                      	
  D93F  110000        	OFCB:	LD	DE,0
  D942  D5            		PUSH	DE
  D943  061A          		LD	B,26		;First cluster
  D945                	S16K1:
  D945  13            		INC	DE
  D946  23            		INC	HL
  D947  10FC          		DJNZ	S16K1
                      	
  D949  1A            		LD	A,(DE)
  D94A  BE            		CP	(HL)
  D94B  2013          		JR	NZ,S16K2
  D94D  13            		INC	DE
  D94E  23            		INC	HL
  D94F  1A            		LD	A,(DE)
  D950  BE            		CP	(HL)
  D951  200D          		JR	NZ,S16K2
                      	
  D953  E1            		POP	HL
  D954  FDE5          		PUSH	IY
  D956  D1            		POP	DE
  D957  1A            		LD	A,(DE)		;Drive
  D958  BE            		CP	(HL)
  D959  2006          		JR	NZ,S16K3
                      					;ここはCFが必ず0
  D95B  ED52          		SBC	HL,DE		;CP HL,DE
  D95D  37            		SCF
  D95E  C0            		RET	NZ
  D95F  E5            		PUSH	HL
  D960                	S16K2:
  D960  E1            		POP	HL
  D961                	S16K3:
  D961  FDE5          		PUSH	IY
  D963  D1            		POP	DE
  D964                	_SYS13:		;(BDOS)ファイルの削除
  D964  CD91E1        		CALL	SETDRV
  D967  CD0FE4        		CALL	KILL
  D96A                	FEND13:
  D96A  18BB          		JR	FEND
                      	
  D96C                	_SYS14:		;(BDOS)シーケンシャルな読み出し
  D96C  CD91E1        		CALL	SETDRV
  D96F  CD09E6        		CALL	PUSHRR
  D972  CD51E6        		CALL	GETSEQ
                      	
  D975  CD6AE6        		CALL	RB_READ
  D978  E5            		PUSH	HL
  D979  CD1DE6        		CALL	SETSEQ
  D97C                	SREAD:
  D97C  E1            		POP	HL
                      	
  D97D  C601          		ADD	A,001H
  D97F  38A6          		JR	C,FEND
                      	
  D981  7D            		LD	A,L
  D982  D601          		SUB	001H
  D984                	FENDX:
  D984  9F            		SBC	A,A
  D985  E603          		AND	3
  D987  1F            		RRA
  D988  189E          		JR	FENDE
                      	
  D98A                	_SYS15:		;(BDOS)シーケンシャルな書き込み
  D98A  CD91E1        		CALL	SETDRV
  D98D  CD09E6        		CALL	PUSHRR
  D990  CD51E6        		CALL	GETSEQ
                      	
  D993  CD63E6        		CALL	RB_WRITE
  D996  E5            		PUSH	HL
  D997  CD1DE6        		CALL	SETSEQ
  D99A                	SWRITE:
  D99A  E1            		POP	HL
                      	
  D99B  C6FF          		ADD	A,0FFH
  D99D  18E5          		JR	FENDX
                      	
  D99F                	_SYS17:		;(BDOS)ファイル名の変更
  D99F  CD91E1        		CALL	SETDRV
  D9A2  CD65E4        		CALL	NAME
  D9A5  18C3          		JR	FEND13
                      	
  D9A7                	_SYS16:		;(BDOS)ファイルの作成
  D9A7  CD91E1        		CALL	SETDRV
                      	
  D9AA  CDDFE4        		CALL	CHKWILDX
  D9AD  38BB          		JR	C,FEND13
  D9AF  FD7E01        		LD	A,(IY+1)	;(FCB)主ファイル名
  D9B2  FE05          		CP	5
  D9B4  2805          		JR	Z,_S16X2
  D9B6  FE21          		CP	021H
  D9B8  DA6AD9        		JP	C,FEND13
  D9BB                	_S16X2:
                      	;				Clear FCB
  D9BB  FDE5          		PUSH	IY
  D9BD  E1            		POP	HL
  D9BE  011000        		LD	BC,16
  D9C1  09            		ADD	HL,BC
  D9C2                	_S16X1:
  D9C2  70            		LD	(HL),B		;B=0
  D9C3  23            		INC	HL
  D9C4  0D            		DEC	C
  D9C5  20FB          		JR	NZ,_S16X1
                      	
  D9C7  CDDCE5        		CALL	SETTMSX
  D9CA  FD360DFF      		LD	(IY+13),0FFH	;(FCB)Attribute
  D9CE  CDB1E1        		CALL	SOPENX
  D9D1  3F            		CCF
  D9D2  CC38D9        		CALL	Z,_S16K
  D9D5  D48FE2        		CALL	NC,COPEN
  D9D8  D4EFE4        		CALL	NC,SDIRENT
  D9DB  C35AD8        		JP	_S16XX
                      	
  D9DE                	_SYS21:		;(BDOS)ランダムな読み出し
                      	
  D9DE  CD91E1        		CALL	SETDRV
  D9E1  CD09E6        		CALL	PUSHRR
  D9E4  CDF2E8        		CALL	GET_RR_AHL
                      	
  D9E7  CD6AE6        		CALL	RB_READ
  D9EA  E5            		PUSH	HL
  D9EB  CD3DE6        		CALL	POPRR
  D9EE  188C          		JR	SREAD
                      	
  D9F0                	_SYS22:		;(BDOS)ランダムな書き込み
  D9F0                	_SYS28:		;(BDOS)ランダムな書き込み・その2
  D9F0  CD91E1        		CALL	SETDRV
  D9F3  CD09E6        		CALL	PUSHRR
  D9F6  CDF2E8        		CALL	GET_RR_AHL
                      	
  D9F9  CD63E6        		CALL	RB_WRITE
  D9FC  E5            		PUSH	HL
  D9FD  CD3DE6        		CALL	POPRR
  DA00  1898          		JR	SWRITE
                      	
  DA02                	_SYS18:		;(BDOS)ログインベクトルの獲得
  DA02  21FF00        		LD	HL,000FFH
  DA05  AF            		XOR	A
  DA06  C9            		RET
                      	
  DA07                	_SYS19:		;(BDOS)カレントドライブ番号の獲得
  DA07  3A87EF        		LD	A,(_DVSW)
  DA0A  A7            		AND	A
  DA0B  C9            		RET
                      	
  DA0C                	_SYS1A:		;(BDOS)DTAの設定
  DA0C  ED538AEF      		LD	(_DTA),DE
  DA10  AF            		XOR	A
  DA11  C9            		RET
                      	
  DA12                	_SYS1B:		;(BDOS)ディスク情報の獲得
  DA12  7B            		LD	A,E
  DA13  CDA4E1        		CALL	GETDRV
  DA16  CDDCEF        		CALL	_GETDPB
  DA19  D4E2EF        		CALL	NC,_RDFATX
  DA1C  210000        		LD	HL,0
  DA1F  D4B5E5        		CALL	NC,DSKF
                      	
  DA22  ED5BB6E5      		LD	DE,(MAXCLP+1)	;DPB_08_MAXCL-1
  DA26  1B            		DEC	DE		;DE←クラスタの総数
  DA27  FD2A7CF0      		LD	IY,(_FATBF)	;IY←メモリ上のFATの先頭アドレス
  DA2B  9F            		SBC	A,A
  DA2C  D8            		RET	C
  DA2D  4F            		LD	C,A		;C=0
  DA2E  DD7E0F        		LD	A,(IX+00FH)	;DPB_0F_BPS
  DA31  E60F          		AND	00FH
  DA33  47            		LD	B,A
  DA34  DD7E07        		LD	A,(IX+7)	;DPB_07_SECPCL
  DA37  C9            		RET
                      	
  DA38                	_SYS1D:		;(BDOS)書き込みが禁止されているディスクの調査
  DA38  AF            		XOR	A
  DA39  67            		LD	H,A
  DA3A  6F            		LD	L,A
  DA3B  C9            		RET
                      	
  DA3C                	_SYS1F:		;(BDOS)ディスク装置のパラメータの読み出し
  DA3C  2100F1        		LD	HL,DEVICE
  DA3F  7B            		LD	A,E
  DA40  C3DCEF        		JP	_GETDPB
                      	
  DA43                	_SYS23:		;(BDOS)ファイルサイズの獲得
  DA43  E5            		PUSH	HL
  DA44  2A1EF0        		LD	HL,(STABLE+2*00FH)	;ファイルのオープン
  DA47  CD0F00        		CALL	JP_HL
  DA4A  3821          		JR	C,_S23X1
                      	
  DA4C  D5            		PUSH	DE		;EX DE,IY
  DA4D  FDE3          		EX	(SP),IY		;
                      	;	POP	DE		;
                      	;	PUSH	DE		;DEを破壊しないように注意vv
  DA4F  FD7E10        		LD	A,(IY+16)	;(FCB)ファイルのサイズ(バイト単位)
  DA52  FD6E11        		LD	L,(IY+17)	;
  DA55  FD6612        		LD	H,(IY+18)	;
  DA58  C5            		PUSH	BC		;
  DA59  FD4613        		LD	B,(IY+19)	;
  DA5C  CD43E6        		CALL	GET_CPM_R_SIZE
  DA5F  C1            		POP	BC
  DA60                	_S24X1:
  DA60  FD7521        		LD	(IY+33),L	;(FCB)Random record
  DA63  FD7422        		LD	(IY+34),H
  DA66  FD7723        		LD	(IY+35),A
                      	;	POP	DE		;^^^^^^^^^^^^^^^^^^^^^^^^
                      	;	PUSH	DE		;EX DE,IY
  DA69  FDE3          		EX	(SP),IY		;
  DA6B  D1            		POP	DE		;
                      	
  DA6C  AF            		XOR	A
  DA6D                	_S23X1:
  DA6D  E1            		POP	HL
  DA6E  C9            		RET
                      	
  DA6F                	_SYS24:		;(BDOS)ランダムレコードフィールドの設定
  DA6F  E5            		PUSH	HL
  DA70  D5            		PUSH	DE		;EX DE,IY
  DA71  FDE3          		EX	(SP),IY		;
                      	;	POP	DE		;
                      	;	PUSH	DE		;DEを破壊しないように注意vv
  DA73  CD51E6        		CALL	GETSEQ
  DA76  18E8          		JR	_S24X1		;^^^^^^^^^^^^^^^^^^^^^^^^
                      	
  DA78                	_SYS5C:		;(BDOS)ファイル名の解析
  DA78  C5            		PUSH	BC
  DA79  E5            		PUSH	HL
  DA7A  CDA8E0        		CALL	FILE
  DA7D  E1            		POP	HL
  DA7E  C1            		POP	BC
  DA7F                	S29X1:
  DA7F  C5            		PUSH	BC
  DA80  D5            		PUSH	DE
  DA81  E5            		PUSH	HL
  DA82  EB            		EX	DE,HL
  DA83  2114EF        		LD	HL,FDRV
  DA86  010D00        		LD	BC,13
  DA89  EDB0          		LDIR
  DA8B  70            		LD	(HL),B		;B=0
  DA8C  0E03          		LD	C,3
  DA8E  EDB0          		LDIR
  DA90  E1            		POP	HL
  DA91  D1            		POP	DE
  DA92  C1            		POP	BC
  DA93  AF            		XOR	A
  DA94  C9            		RET
                      	
  DA95                	_SYS30:		;(BDOS)論理セクタを用いた書き込み
  DA95  C5            		PUSH	BC
  DA96  01F6EB        		LD	BC,DWT24B
  DA99  1804          		JR	S2FX
  DA9B                	_SYS2F:
  DA9B  C5            		PUSH	BC
  DA9C  01E6EB        		LD	BC,DRD24B
  DA9F                	S2FX:
  DA9F  ED43B5DA      		LD	(S2FPAT+1),BC
  DAA3  CDDFEF        		CALL	_FFLUSH
                      	
  DAA6  D5            		PUSH	DE
  DAA7  E5            		PUSH	HL
  DAA8  7D            		LD	A,L		;Drive
  DAA9  CDD9EF        		CALL	_CHGDRV
  DAAC  3809          		JR	C,S30X2
  DAAE  44            		LD	B,H		;Number of clusters
  DAAF  2A8AEF        		LD	HL,(_DTA)
                      	
  DAB2  0E00          		LD	C,0
  DAB4  CDE6EB        	S2FPAT:	CALL	DRD24B
  DAB7                	S30X2:
  DAB7  E1            		POP	HL
  DAB8  D1            		POP	DE
  DAB9  C1            		POP	BC
  DABA  9F            		SBC	A,A
  DABB  C9            		RET
                      	
  DABC                	_SYS5A:		;(BDOS)カレントディレクトリの変更
  DABC  C5            		PUSH	BC
  DABD  D5            		PUSH	DE
  DABE  E5            		PUSH	HL
  DABF  CD9DE0        		CALL	FILEC
  DAC2  210000        		LD	HL,0
  DAC5  1114EF        		LD	DE,FDRV
  DAC8  2A4EF0        		LD	HL,(STABLE+2*027H)	;ランダムブロック読み込み
  DACB  CD0F00        		CALL	JP_HL
  DACE  18E7          		JR	S30X2
                      	
                      	;X1IO.ASM
                      	
                      	;	LSX-Dodgers IO
                      	;	X1/turbo/Z
                      	
  D80C                	_SYS04	EQU	_ERROR		;(BDOS)外部出力
  D80C                	_SYS05	EQU	_ERROR		;(BDOS)プリンタ出力
  D80C                	_SYS1C	EQU	_ERROR		;(BDOS)ディスクの書き込み禁止化
  D80C                	_SYS1E	EQU	_ERROR		;(BDOS)ファイル属性の設定
  D80C                	_SYS20	EQU	_ERROR		;(BDOS)利用者番号の設定と読み出し
  D80C                	_SYS2E	EQU	_ERROR		;(BDOS)ベリファイ・フラグの設定
                      	
  DAD0                	BIOS:
  DAD0  E5            		PUSH	HL
  DAD1  211080        		LD	HL,08010H
  DAD4  39            		ADD	HL,SP
  DAD5  E1            		POP	HL
  DAD6  300E          		JR	NC,BIOS2
  DAD8                	BIOS1:
  DAD8  CDE0DA        		CALL	BIOS3
  DADB  061E          		LD	B,01EH
  DADD  ED79          		OUT	(C),A
  DADF  C9            		RET
  DAE0                	BIOS3:
  DAE0  C5            		PUSH	BC
  DAE1  061D          		LD	B,01DH
  DAE3  ED79          		OUT	(C),A
  DAE5  C9            		RET
  DAE6                	BIOS2:
  DAE6  ED73F1DA      		LD	(SPPAT+1),SP	;スタックがBIOS-ROMと被っている場合
  DAEA  31CAFF        		LD	SP,EXTSP
  DAED  CDD8DA        		CALL	BIOS1
  DAF0  310000        	SPPAT:	LD	SP,0
  DAF3  C9            		RET
                      	
  DAF4                	INT:
  DAF4  F5            		PUSH	AF
  DAF5  E5            		PUSH	HL
  DAF6  ED7307DB      		LD	(INTSP+1),SP
  DAFA  210001        		LD	HL,00100H
  DAFD  39            		ADD	HL,SP
  DAFE  3803          		JR	C,INTP1X
  DB00  31CAFF        		LD	SP,EXTSP
  DB03                	INTP1X:
  DB03  CD0EDB        		CALL	INTP1
  DB06  310000        	INTSP:	LD	SP,0
  DB09                	INTPE:
  DB09  E1            		POP	HL
  DB0A                	INTPE2:
  DB0A  F1            		POP	AF
  DB0B                	INTCTCE:
  DB0B  FB            		EI
  DB0C  ED4D          		RETI
  DB0E                	INTP1:
  DB0E  C5            		PUSH	BC
                      	
  DB0F  CDE1DE        		CALL	IN49SB
  DB12  67            		LD	H,A
  DB13  CDE1DE        		CALL	IN49SB
  DB16  6F            		LD	L,A
                      	
  DB17  FE08          		CP	8		;DELキー
  DB19  2005          		JR	NZ,SCEXE
  DB1B  7C            		LD	A,H
  DB1C  E611          		AND	011H		;CTRLキー + GRAPHキー
  DB1E  2808          		JR	Z,RESET
  DB20                	SCEXE:
  DB20  2292EF        		LD	(_KEYD),HL
  DB23  CD54DB        		CALL	KEYSET1
                      	
  DB26  C1            		POP	BC
  DB27  C9            		RET
                      	
  DB28                	RESET:
  DB28  3E1D          		LD	A,01DH
  DB2A  D300          		OUT	(0),A
  DB2C  C7            		RST	0
                      	
  DB2D                	INTCTC2:
  DB2D  F5            		PUSH	AF
  DB2E  3E00          	INTCPAT:LD	A,000H
  DB30  3D            		DEC	A
  DB31  322FDB        		LD	(INTCPAT+1),A
  DB34  20D4          		JR	NZ,INTPE2
                      	
  DB36  3A92EF        		LD	A,(_KEYD)
  DB39  B7            		OR	A
  DB3A  28CE          		JR	Z,INTPE2
  DB3C                	KEYSET0:
  DB3C  E5            		PUSH	HL
  DB3D  2A90EF        		LD	HL,(_KEYPS)
  DB40  7C            		LD	A,H
  DB41  AD            		XOR	L
  DB42  2006          		JR	NZ,INTC2A
                      	
  DB44  2A92EF        		LD	HL,(_KEYD)
  DB47  CD7EDB        		CALL	KEYSET
  DB4A                	INTC2A:
  DB4A  3A94EF        		LD	A,(_KEYSP)
  DB4D  E60F          		AND	00FH
  DB4F  322FDB        		LD	(INTCPAT+1),A
                      	
  DB52  18B5          		JR	INTPE
                      	
  DB54                	KEYSET1:
  DB54  CB6C          		BIT	5,H
  DB56  F5            		PUSH	AF
  DB57  ED4B8CEF      		LD	BC,(_CTC)
  DB5B  78            		LD	A,B
  DB5C  B1            		OR	C
  DB5D  2006          		JR	NZ,KEYS10E
                      	
  DB5F  F1            		POP	AF
  DB60  201C          		JR	NZ,KEYSET
  DB62  F5            		PUSH	AF
  DB63  18D7          		JR	KEYSET0
  DB65                	KEYS10E:
  DB65  F1            		POP	AF
  DB66  C8            		RET	Z
  DB67  F5            		PUSH	AF
  DB68  3E03          		LD	A,3		;CTC STOP
  DB6A  ED79          		OUT	(C),A
  DB6C  F1            		POP	AF
                      	
  DB6D  B7            		OR	A
  DB6E  C8            		RET	Z
                      	
  DB6F  E5            		PUSH	HL
  DB70  2A94EF        		LD	HL,(_KEYSP)
  DB73  3EA7          		LD	A,0A7H
  DB75  ED79          		OUT	(C),A
  DB77  ED69          		OUT	(C),L
  DB79  7C            		LD	A,H
  DB7A  322FDB        		LD	(INTCPAT+1),A
  DB7D  E1            		POP	HL
                      	
  DB7E                	KEYSET:
  DB7E  7D            		LD	A,L
  DB7F  CB7C          		BIT	7,H
  DB81  CCAFDB        		CALL	Z,FKEY
  DB84  CB74          		BIT	6,H
  DB86  C0            		RET	NZ
  DB87                	KEYBS:
  DB87  B7            		OR	A
  DB88  C8            		RET	Z
  DB89                	KEYBS1:
  DB89  CD9FDB        		CALL	KEYBC_IFBREAK
  DB8C  E5            		PUSH	HL
  DB8D  F5            		PUSH	AF
  DB8E  2A90EF        		LD	HL,(_KEYPS)
  DB91  2C            		INC	L
  DB92  7D            		LD	A,L
  DB93  BC            		CP	H
  DB94  2815          		JR	Z,POP_AF_HL_SCF_RET
  DB96  3290EF        		LD	(_KEYPS),A
  DB99  26FE          		LD	H,KEYBF/256
  DB9B  F1            		POP	AF
  DB9C  77            		LD	(HL),A
  DB9D  E1            		POP	HL
  DB9E  C9            		RET
  DB9F                	KEYBC_IFBREAK:
  DB9F  FE03          		CP	3
  DBA1  C0            		RET	NZ
  DBA2                	KEYBC:
  DBA2  E5            		PUSH	HL
  DBA3  210000        		LD	HL,0
  DBA6  2290EF        		LD	(_KEYPS),HL
  DBA9  E1            		POP	HL
  DBAA  C9            		RET
                      	
  DBAB                	POP_AF_HL_SCF_RET:
  DBAB  F1            		POP	AF
  DBAC  E1            		POP	HL
  DBAD  37            		SCF
  DBAE  C9            		RET
                      	
  DBAF                	FKEY:
  DBAF  CB75          		BIT	6,L
  DBB1  C8            		RET	Z
  DBB2  3E1B          		LD	A,01BH
  DBB4  CD89DB        		CALL	KEYBS1
  DBB7  7D            		LD	A,L
  DBB8  C6AF          		ADD	A,020H-'q'
  DBBA  CB7D          		BIT	7,L
  DBBC  C8            		RET	Z
  DBBD  2E50          		LD	L,'P'
  DBBF  D690          		SUB	0E1H+020H-'q'
  DBC1  2810          		JR	Z,KEYBSL
  DBC3  2C            		INC	L
  DBC4  3D            		DEC	A
  DBC5  280C          		JR	Z,KEYBSL
  DBC7  2C            		INC	L
  DBC8  D609          		SUB	0EBH-0E2H
  DBCA  2807          		JR	Z,KEYBSL
  DBCC  2E43          		LD	L,'C'
  DBCE  3D            		DEC	A
  DBCF  2802          		JR	Z,KEYBSL
  DBD1  2E58          		LD	L,'X'
  DBD3                	KEYBSL:
  DBD3  7D            		LD	A,L
  DBD4  18B3          		JR	KEYBS1
                      	
  DBD6                	_SYS01:		;(BDOS)コンソール入力
  DBD6  CD09EF        		CALL	_SYS07
  DBD9                	MSG_A:
  DBD9  F5            		PUSH	AF
  DBDA  D5            		PUSH	DE
  DBDB  5F            		LD	E,A
  DBDC  CDE2DB        		CALL	_SYS02
  DBDF  D1            		POP	DE
  DBE0  F1            		POP	AF
  DBE1  C9            		RET
                      	
  DBE2                	_SYS02:		;(BDOS)コンソール出力
  DBE2  CDF4DB        		CALL	BREAK
  DBE5  1805          		JR	PRINTX
                      	
  DBE7                	_SYS06:		;(BDOS)コンソール直接入出力
  DBE7  7B            		LD	A,E
  DBE8  3C            		INC	A
  DBE9  CACAEF        		JP	Z,_INKEY
  DBEC                	PRINTX:
  DBEC  C3CDEF        		JP	_PRINT
                      	
  DBEF                	_SYS08:		;(BDOS)エコーなしコンソール入力
  DBEF  CD09EF        		CALL	_SYS07
  DBF2  1804          		JR	BREAK1
  DBF4                	BREAK:
  DBF4  FB            		EI
  DBF5  3A92EF        		LD	A,(_KEYD)
  DBF8                	BREAK1:
  DBF8  FE03          		CP	3		;CTRL+C
  DBFA  CA00EF        		JP	Z,CPM_BOOT
  DBFD  FE13          		CP	013H		;CTRL+S
  DBFF  C0            		RET	NZ
                      	
  DC00                	PAUSE:
  DC00  CDA2DB        		CALL	KEYBC
                      	
  DC03                	FLGET:
  DC03  CDDFEF        		CALL	_FFLUSH
  DC06  C5            		PUSH	BC
  DC07  E5            		PUSH	HL
  DC08  ED4B8EEF      		LD	BC,(_TXADR)
  DC0C  CBE8          		SET	5,B
  DC0E  ED60          		IN	H,(C)
  DC10                	FLGET1:
  DC10  3A93EF        		LD	A,(_KEYD+1)
  DC13  0F            		RRCA
  DC14  0F            		RRCA
  DC15  E610          		AND	010H
  DC17  F608          		OR	8
  DC19  ED68          		IN	L,(C)
  DC1B  B5            		OR	L
  DC1C  ED79          		OUT	(C),A
  DC1E  CDCAEF        		CALL	_INKEY
  DC21  28ED          		JR	Z,FLGET1
  DC23  ED61          		OUT	(C),H
  DC25  E1            		POP	HL
  DC26  C1            		POP	BC
  DC27  C9            		RET
                      	
  DC28                	_SYS0A:		;(BDOS)文字列入力
  DC28  C5            		PUSH	BC
  DC29  E5            		PUSH	HL
  DC2A  D5            		PUSH	DE
  DC2B  CD2EE0        		CALL	GETL
  DC2E  1100FF        		LD	DE,KBUF
  DC31  E1            		POP	HL
  DC32  E5            		PUSH	HL
  DC33  0E00          		LD	C,0
  DC35  3004          		JR	NC,SAX0
  DC37  23            		INC	HL
  DC38  23            		INC	HL
  DC39  1808          		JR	SAX4
  DC3B                	SAX0:
  DC3B  46            		LD	B,(HL)
  DC3C  23            		INC	HL
  DC3D                	SAX1:
  DC3D  23            		INC	HL
  DC3E  1A            		LD	A,(DE)
  DC3F  13            		INC	DE
  DC40  B7            		OR	A
  DC41  2004          		JR	NZ,SAX2
  DC43                	SAX4:
  DC43  360D          		LD	(HL),00DH
  DC45  1804          		JR	SAX3
  DC47                	SAX2:
  DC47  77            		LD	(HL),A
  DC48  0C            		INC	C
  DC49  10F2          		DJNZ	SAX1
  DC4B                	SAX3:
  DC4B  D1            		POP	DE
  DC4C  79            		LD	A,C
  DC4D  13            		INC	DE
  DC4E  12            		LD	(DE),A
  DC4F  1B            		DEC	DE
  DC50  E1            		POP	HL
  DC51  C1            		POP	BC
  DC52  A7            		AND	A
  DC53  C9            		RET
                      	
  DC54                	_SYS0B:		;(BDOS)コンソール状態のチェック
  DC54  CDF4DB        		CALL	BREAK
  DC57  3A92EF        		LD	A,(_KEYD)
  DC5A  B7            		OR	A
  DC5B  C8            		RET	Z
  DC5C                	CONSTX:
  DC5C  CDDFEF        		CALL	_FFLUSH
  DC5F  E5            		PUSH	HL
  DC60  2A90EF        		LD	HL,(_KEYPS)
  DC63  7C            		LD	A,H
  DC64  AD            		XOR	L
  DC65  E1            		POP	HL
  DC66  C6FF          		ADD	A,0FFH
  DC68  9F            		SBC	A,A
  DC69  C9            		RET
                      	
  DC6A                	_SYS2A:		;(BDOS)日付の獲得
  DC6A  C5            		PUSH	BC
  DC6B  3EED          		LD	A,0EDH		;Read calendar
  DC6D  CDFCDE        		CALL	COMOUT
  DC70  CD8DDC        		CALL	IN49SB_BCD	;YY
  DC73  6F            		LD	L,A
  DC74  2600          		LD	H,0
                      	;	LD	DE,1900		;0076CH
                      	;	CP	90		;90以上は1900年代 1990-1999
                      	;	JR	NC,RDDATE1	;90未満は2000年代 2000-2089
                      	;	LD	E,0D0H		;2000 & 0FFH D=007H
  DC76  11D007        		LD	DE,2000		;すべて2000年代で処理 2000-2099
  DC79                	RDDATE1:
  DC79  19            		ADD	HL,DE
  DC7A  CDE1DE        		CALL	IN49SB		;MM
  DC7D  57            		LD	D,A
  DC7E  CD8DDC        		CALL	IN49SB_BCD	;DD
  DC81  5F            		LD	E,A
  DC82  7A            		LD	A,D
  DC83  E607          		AND	7
  DC85  0604          		LD	B,4
  DC87                	RDDATE2:
  DC87  CB3A          		SRL	D
  DC89  10FC          		DJNZ	RDDATE2
  DC8B  C1            		POP	BC
  DC8C  C9            		RET
  DC8D                	IN49SB_BCD:
  DC8D  CDE1DE        		CALL	IN49SB
                      	;------------------------
                      	;BCDの10進数化
                      	;in
                      	;	A  : BCD
                      	;out
                      	;	A  : 10進数
                      	;------------------------
  DC90  4F            		LD	C,A
  DC91  E6F0          		AND	0F0H	;10の位
  DC93  0F            		RRCA
  DC94  47            		LD	B,A
  DC95  0F            		RRCA
  DC96  0F            		RRCA
  DC97  80            		ADD	A,B
  DC98  47            		LD	B,A
  DC99  79            		LD	A,C
  DC9A  E60F          		AND	00FH	;1の位
  DC9C  80            		ADD	A,B
  DC9D  C9            		RET
                      	
  DC9E                	_SYS2C:		;(BDOS)時刻の獲得
  DC9E  C5            		PUSH	BC
  DC9F  3EEF          		LD	A,0EFH		;Read time
  DCA1  CDFCDE        		CALL	COMOUT
  DCA4  CD8DDC        		CALL	IN49SB_BCD	;TT
  DCA7  67            		LD	H,A
  DCA8  CD8DDC        		CALL	IN49SB_BCD	;MM
  DCAB  6F            		LD	L,A
  DCAC  CD8DDC        		CALL	IN49SB_BCD	;SS
  DCAF  57            		LD	D,A
  DCB0  C1            		POP	BC
  DCB1  AF            		XOR	A
  DCB2  C9            		RET
                      	
  DCB3                	ESC1:
  DCB3  7B            		LD	A,E
  DCB4  FE41          		CP	'A'
  DCB6  CAFDDD        		JP	Z,CTRL1E
  DCB9  FE42          		CP	'B'
  DCBB  CA67DF        		JP	Z,CTRL1F
  DCBE  FE43          		CP	'C'
  DCC0  CA08DE        		JP	Z,CTRL1C
  DCC3  FE44          		CP	'D'
  DCC5  CAF4DD        		JP	Z,CTRL1D
  DCC8  FE45          		CP	'E'
  DCCA  2802          		JR	Z,CT0CX
  DCCC  FE6A          		CP	'j'
  DCCE                	CT0CX:
  DCCE  CA3ADF        		JP	Z,CTRL0C
  DCD1  FE48          		CP	'H'
  DCD3  CA82DE        		JP	Z,CTRL0B
  DCD6  FE4A          		CP	'J'
  DCD8  CA3DDF        		JP	Z,CTRL06
  DCDB  FE4B          		CP	'K'
  DCDD  CA41DE        		JP	Z,CTRL05
  DCE0  FE4C          		CP	'L'
  DCE2  CA7CDF        		JP	Z,CTRL0E
  DCE5  FE54          		CP	'T'
  DCE7  CA41DE        		JP	Z,CTRL05
  DCEA  FE78          		CP	'x'
  DCEC  2804          		JR	Z,ESC1X
  DCEE  FE79          		CP	'y'
  DCF0  2002          		JR	NZ,ESC1Y
  DCF2                	ESC1X:
  DCF2  3601          		LD	(HL),1
  DCF4                	ESC1Y:
  DCF4  FE3D          		CP	'='
  DCF6  2804          		JR	Z,ESC1W
  DCF8  FE59          		CP	'Y'
  DCFA  2002          		JR	NZ,ESC1Z
  DCFC                	ESC1W:
  DCFC  3602          		LD	(HL),2
  DCFE                	ESC1Z:
  DCFE  FE20          		CP	020H
  DD00  3802          		JR	C,ESC1C
  DD02  87            		ADD	A,A
  DD03  D0            		RET	NC
  DD04                	ESC1C:
  DD04  E1            		POP	HL
  DD05  184F          		JR	ANK
                      	
  DD07                	ESC:
  DD07  2167DD        		LD	HL,PRINTE5
  DD0A  E5            		PUSH	HL
  DD0B  212DDD        		LD	HL,ESCFLG+1
  DD0E  3600          		LD	(HL),0
  DD10  3D            		DEC	A
  DD11  28A0          		JR	Z,ESC1
  DD13  3D            		DEC	A
  DD14  2009          		JR	NZ,ESC2
  DD16  3603          		LD	(HL),3
  DD18  7B            		LD	A,E
  DD19  D620          		SUB	020H
  DD1B  3222DD        		LD	(ESCYP+1),A
  DD1E  C9            		RET
  DD1F                	ESC2:
  DD1F  3D            		DEC	A
  DD20  C0            		RET	NZ
  DD21  2600          	ESCYP:	LD	H,0
  DD23  7B            		LD	A,E
  DD24  D620          		SUB	020H
  DD26  6F            		LD	L,A
  DD27  C3D6EF        		JP	_LOC
                      	
  DD2A                	PRINT:
  DD2A  C5            		PUSH	BC
  DD2B  E5            		PUSH	HL
  DD2C  3E00          	ESCFLG:	LD	A,000H
  DD2E  B7            		OR	A
  DD2F  20D6          		JR	NZ,ESC
                      	
  DD31  3E1F          		LD	A,01FH
  DD33  BB            		CP	E
  DD34  3035          		JR	NC,CTRL
  DD36  0105DF        	INSFLG:	LD	BC,INSERT
                      	
  DD39  3A1800        		LD	A,(00018H)
  DD3C  3C            		INC	A
  DD3D  2817          		JR	Z,ANK
  DD3F  3E00          	KANFLG:	LD	A,000H
  DD41  B7            		OR	A
  DD42  203A          		JR	NZ,KANJI2
  DD44  7B            		LD	A,E
  DD45  FE80          		CP	080H
  DD47  380D          		JR	C,ANK
  DD49  FEA0          		CP	0A0H
  DD4B  3804          		JR	C,KANJI1
  DD4D  FEE0          		CP	0E0H
  DD4F  3805          		JR	C,ANK
  DD51                	KANJI1:
  DD51  3240DD        		LD	(KANFLG+1),A
  DD54  1811          		JR	PRINTE5
  DD56                	ANK:
  DD56  ED4B8EEF      		LD	BC,(_TXADR)
  DD5A  78            		LD	A,B
  DD5B  F638          		OR	038H
  DD5D  47            		LD	B,A
  DD5E  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令	kanji
  DD60  CB98          		RES	3,B
  DD62  ED59          		OUT	(C),E		;Text
  DD64                	KANJIE:
  DD64  CDC9DD        		CALL	PRINTE7
  DD67                	PRINTE5:
  DD67  E1            		POP	HL
  DD68  C1            		POP	BC
  DD69  AF            		XOR	A
  DD6A  C9            		RET
                      	
  DD6B                	CTRL:
  DD6B  CDB0DD        		CALL	KANCLR
  DD6E  7B            		LD	A,E
  DD6F  87            		ADD	A,A
  DD70  F680          		OR	080H
  DD72  6F            		LD	L,A
  DD73  26F0          		LD	H,CTRLTB/256
  DD75  7E            		LD	A,(HL)
  DD76  2C            		INC	L
  DD77  66            		LD	H,(HL)
  DD78  6F            		LD	L,A
  DD79  CD0F00        		CALL	JP_HL
  DD7C  18E9          		JR	PRINTE5
                      	
  DD7E                	KANJI2:
  DD7E  D5            		PUSH	DE
  DD7F  57            		LD	D,A
  DD80  01812F        		LD	BC,02F81H	;SFTJIS
  DD83  DF            		RST	018H
  DD84  01C32F        		LD	BC,02FC3H	;Instead or JISVRM
  DD87  DF            		RST	018H
                      	
  DD88  ED4B8EEF      		LD	BC,(_TXADR)
  DD8C  78            		LD	A,B
  DD8D  F638          		OR	038H
  DD8F  47            		LD	B,A
  DD90  ED51          		OUT	(C),D		;kanji
  DD92  CB98          		RES	3,B
  DD94  ED59          		OUT	(C),E		;Text
  DD96  CDC9DD        		CALL	PRINTE7
  DD99  ED4B8EEF      		LD	BC,(_TXADR)
  DD9D  78            		LD	A,B
  DD9E  F638          		OR	038H
  DDA0  47            		LD	B,A
  DDA1  CBF2          		SET	6,D
  DDA3  ED51          		OUT	(C),D		;Kanji
  DDA5  CB98          		RES	3,B
  DDA7  ED59          		OUT	(C),E		;Text
  DDA9  D1            		POP	DE
  DDAA  AF            		XOR	A
  DDAB  3240DD        		LD	(KANFLG+1),A
  DDAE  18B4          		JR	KANJIE
                      	
  DDB0                	KANCLR:
  DDB0  3A40DD        		LD	A,(KANFLG+1)
  DDB3  B7            		OR	A
  DDB4  C8            		RET	Z
  DDB5  ED4B8EEF      		LD	BC,(_TXADR)
  DDB9  78            		LD	A,B
  DDBA  F638          		OR	038H
  DDBC  47            		LD	B,A
  DDBD  AF            		XOR	A
  DDBE  3240DD        		LD	(KANFLG+1),A
  DDC1  ED79          		OUT	(C),A		;Kanji
  DDC3  CB98          		RES	3,B
  DDC5  3E20          		LD	A,020H
  DDC7  ED79          		OUT	(C),A		;Text
  DDC9                	PRINTE7:
  DDC9  CBA0          		RES	4,B
  DDCB                	PRINTE6:
  DDCB  3A96EF        		LD	A,(_COLORF)
  DDCE                	PRINTE4:
  DDCE  ED79          		OUT	(C),A		;Color
  DDD0  78            		LD	A,B
  DDD1  E607          		AND	7
  DDD3  47            		LD	B,A
  DDD4                	PRINTE2:
  DDD4  03            		INC	BC
  DDD5                	PRINTE3:
  DDD5  2ABAEF        		LD	HL,(CRTCD+10)
  DDD8  A7            		AND	A
  DDD9  ED4A          		ADC	HL,BC
  DDDB                	PRINTE8:
  DDDB  2805          		JR	Z,PRINT2
  DDDD  ED438EEF      		LD	(_TXADR),BC
  DDE1                	CTRL00:
  DDE1  C9            		RET
                      	
  DDE2                	PRINT2:
  DDE2  CD82DF        		CALL	SCR
  DDE5  2ABCEF        		LD	HL,(CRTCD+12)
  DDE8  09            		ADD	HL,BC
  DDE9                	SET_TXADR_HL:
  DDE9  228EEF        		LD	(_TXADR),HL
  DDEC  C9            		RET
                      	
  DDED                	CTRL08:
  DDED  3A36DD        		LD	A,(INSFLG)
  DDF0  FECD          		CP	0CDH		;CALL ????
  DDF2  2829          		JR	Z,CTRL02
  DDF4                	CTRL1D:
  DDF4  2A8EEF        		LD	HL,(_TXADR)
  DDF7  7C            		LD	A,H
  DDF8  B5            		OR	L
  DDF9  C8            		RET	Z
  DDFA  2B            		DEC	HL
  DDFB  18EC          		JR	SET_TXADR_HL
                      	
  DDFD                	CTRL1E:
  DDFD  ED4B8EEF      		LD	BC,(_TXADR)
  DE01  2ABCEF        		LD	HL,(CRTCD+12)
  DE04  09            		ADD	HL,BC
  DE05  D0            		RET	NC
  DE06  18E1          		JR	SET_TXADR_HL
                      	
  DE08                	CTRL1C:
  DE08  ED4B8EEF      		LD	BC,(_TXADR)
  DE0C  18C6          		JR	PRINTE2
                      	
  DE0E                	CTRL09:
  DE0E  ED4B8EEF      		LD	BC,(_TXADR)
  DE12  79            		LD	A,C
  DE13  E6F8          		AND	0F8H
  DE15  C608          		ADD	A,8
  DE17  4F            		LD	C,A
  DE18  88            		ADC	A,B
  DE19  91            		SUB	C
  DE1A  47            		LD	B,A
  DE1B  18B8          		JR	PRINTE3
                      	
  DE1D                	CTRL02:
  DE1D  CDF4DD        		CALL	CTRL1D
  DE20  C8            		RET	Z
  DE21  D5            		PUSH	DE
  DE22  CDD3EF        		CALL	_POS
  DE25  3AB1EF        		LD	A,(CRTCD+1)
  DE28  95            		SUB	L
  DE29  4F            		LD	C,A
  DE2A  0D            		DEC	C
  DE2B  0638          		LD	B,038H
  DE2D  2A8EEF        		LD	HL,(_TXADR)
  DE30  09            		ADD	HL,BC
  DE31  44            		LD	B,H
  DE32  4D            		LD	C,L
  DE33  110020        		LD	DE,02000H	;D:Text E:kanji
  DE36  2A96EF        		LD	HL,(_COLORF)	;L:Color
  DE39                	C8X1:
  DE39  CD21DF        		CALL	C12S
  DE3C  0B            		DEC	BC
  DE3D  20FA          		JR	NZ,C8X1
  DE3F  D1            		POP	DE
  DE40  C9            		RET
                      	
  DE41                	CTRL05:
  DE41  CDD3EF        		CALL	_POS
  DE44  3AB1EF        		LD	A,(CRTCD+1)
  DE47  95            		SUB	L
  DE48  6F            		LD	L,A
  DE49                	C08X1:
  DE49  ED4B8EEF      		LD	BC,(_TXADR)
  DE4D                	LINECL:
  DE4D  2620          		LD	H,020H
  DE4F  3A96EF        		LD	A,(_COLORF)
  DE52  CBE8          		SET	5,B
  DE54                	LINECL1:
  DE54  CBD8          		SET	3,B
  DE56  CBE0          		SET	4,B
  DE58  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令	kanji
  DE5A  CB98          		RES	3,B
  DE5C  ED61          		OUT	(C),H		;Text
  DE5E  CBA0          		RES	4,B
  DE60  ED79          		OUT	(C),A		;Color
  DE62  03            		INC	BC
  DE63  2D            		DEC	L
  DE64  20EE          		JR	NZ,LINECL1
  DE66  C9            		RET
                      	
  DE67                	CTRL0D:
  DE67  CDD3EF        		CALL	_POS
  DE6A  2E00          		LD	L,0
  DE6C                	LOC:
  DE6C  C5            		PUSH	BC
  DE6D  E5            		PUSH	HL
  DE6E  CDB0DD        		CALL	KANCLR
  DE71  CD8FDE        		CALL	LOC1
  DE74  228EEF        		LD	(_TXADR),HL
  DE77  E1            		POP	HL
  DE78  C1            		POP	BC
  DE79  C9            		RET
                      	
  DE7A                	CTRL12:
  DE7A  2136DD        		LD	HL,INSFLG
  DE7D  7E            		LD	A,(HL)
  DE7E  EECC          		XOR	0CCH		;CD:CALL ???? 1:LD BC,????
  DE80  77            		LD	(HL),A
  DE81  C9            		RET
                      	
  DE82                	CTRL0B:
  DE82  210000        		LD	HL,0
  DE85  228EEF        		LD	(_TXADR),HL
  DE88  C9            		RET
                      	
  DE89                	CTRL1B:
  DE89  3E01          		LD	A,1
  DE8B  322DDD        		LD	(ESCFLG+1),A
  DE8E  C9            		RET
                      	
  DE8F                	LOC1:
  DE8F  D5            		PUSH	DE
  DE90  4D            		LD	C,L
  DE91  0608          		LD	B,8
  DE93  5C            		LD	E,H
  DE94  1600          		LD	D,0
  DE96  2AB0EF        		LD	HL,(CRTCD)
  DE99  6A            		LD	L,D
  DE9A                	LOC2:
  DE9A  29            		ADD	HL,HL
  DE9B  3001          		JR	NC,LOC3
  DE9D  19            		ADD	HL,DE
  DE9E                	LOC3:
  DE9E  10FA          		DJNZ	LOC2
  DEA0  09            		ADD	HL,BC
  DEA1  D1            		POP	DE
  DEA2  C9            		RET
                      	
  DEA3                	CONOUT1:
  DEA3  CDB5DE        		CALL	CURSOR
  DEA6  59            		LD	E,C
  DEA7  CDCDEF        		CALL	_PRINT
  DEAA  7B            		LD	A,E
  DEAB  FE0A          		CP	00AH
  DEAD  2006          		JR	NZ,CURSOR
  DEAF  CD67DE        		CALL	CTRL0D
  DEB2  CD41DE        		CALL	CTRL05
  DEB5                	CURSOR:
  DEB5  C5            		PUSH	BC
  DEB6  ED4B8EEF      		LD	BC,(_TXADR)
  DEBA  CBE8          		SET	5,B
  DEBC  ED78          		IN	A,(C)
  DEBE  EE18          		XOR	018H
  DEC0  ED79          		OUT	(C),A
  DEC2  C1            		POP	BC
  DEC3  C9            		RET
                      	
  DEC4                	CTRL07:
  DEC4  2161EF        		LD	HL,BEEPD
  DEC7                	BEEP1:
  DEC7  7E            		LD	A,(HL)
  DEC8  23            		INC	HL
  DEC9  FEFF          		CP	0FFH
  DECB  C8            		RET	Z
  DECC  F3            		DI
  DECD  061C          		LD	B,01CH
  DECF  ED79          		OUT	(C),A
  DED1  EDA3          		OUTI
  DED3  FB            		EI
  DED4  18F1          		JR	BEEP1
                      	
  DED6                	OT49SB:
  DED6  C5            		PUSH	BC
  DED7  CDF2DE        		CALL	CANW
  DEDA  010019        		LD	BC,01900H
  DEDD  ED79          		OUT	(C),A
  DEDF  C1            		POP	BC
  DEE0  C9            		RET
                      	
  DEE1                	IN49SB:
  DEE1  C5            		PUSH	BC
  DEE2  01011A        		LD	BC,01A01H
  DEE5                	CANR:
  DEE5  ED78          		IN	A,(C)
  DEE7  E620          		AND	020H
  DEE9  20FA          		JR	NZ,CANR
  DEEB  010019        		LD	BC,01900H
  DEEE  ED78          		IN	A,(C)
  DEF0  C1            		POP	BC
  DEF1  C9            		RET
                      	
  DEF2                	CANW:
  DEF2  01011A        		LD	BC,01A01H
  DEF5  ED48          		IN	C,(C)
  DEF7  CB71          		BIT	6,C
  DEF9  20F7          		JR	NZ,CANW
  DEFB  C9            		RET
                      	
  DEFC                	COMOUT:
  DEFC  FB            		EI
  DEFD  CDD6DE        		CALL	OT49SB
  DF00  CDF2DE        		CALL	CANW
  DF03  F3            		DI
  DF04  C9            		RET
                      	
  DF05                	INSERT:
  DF05  D5            		PUSH	DE
  DF06  CDD3EF        		CALL	_POS
  DF09  3AB1EF        		LD	A,(CRTCD+1)
  DF0C  95            		SUB	L
  DF0D  ED4B8EEF      		LD	BC,(_TXADR)
  DF11  110020        		LD	DE,02000H	;D:Text E:Kanji
  DF14  2A96EF        		LD	HL,(_COLORF)	;L:Color
  DF17  CBE8          		SET	5,B
  DF19                	C12X1:
  DF19  CD21DF        		CALL	C12S
  DF1C  03            		INC	BC
  DF1D  20FA          		JR	NZ,C12X1
  DF1F  D1            		POP	DE
  DF20  C9            		RET
                      	
  DF21                	C12S:
  DF21  CBE0          		SET	4,B
  DF23  CBD8          		SET	3,B
  DF25  ED60          		IN	H,(C)
  DF27  ED59          	X1PAT:	OUT	(C),E
  DF29  5C            		LD	E,H
  DF2A  CB98          		RES	3,B
  DF2C  ED60          		IN	H,(C)
  DF2E  ED51          		OUT	(C),D
  DF30  54            		LD	D,H
  DF31  CBA0          		RES	4,B
  DF33  ED60          		IN	H,(C)
  DF35  ED69          		OUT	(C),L
  DF37  6C            		LD	L,H
  DF38  3D            		DEC	A
  DF39  C9            		RET
                      	
  DF3A                	CTRL0C:
  DF3A  CD82DE        		CALL	CTRL0B
  DF3D                	CTRL06:
  DF3D  ED4B8EEF      		LD	BC,(_TXADR)
  DF41                	C1AX1:
  DF41  78            		LD	A,B
  DF42  F638          		OR	038H
  DF44  47            		LD	B,A
  DF45  ED71          		DB	0EDH,071H	;OUT (C),0	Z80未定義命令	kanji
  DF47  CB98          		RES	3,B
  DF49  3E20          		LD	A,020H
  DF4B  ED79          		OUT	(C),A		;Text
  DF4D  CBA0          		RES	4,B
  DF4F  3A96EF        		LD	A,(_COLORF)
  DF52  ED79          		OUT	(C),A		;Color
  DF54  03            		INC	BC
  DF55  CBA8          		RES	5,B
  DF57  2ABAEF        		LD	HL,(CRTCD+10)
  DF5A  09            		ADD	HL,BC
  DF5B  30E4          		JR	NC,C1AX1
  DF5D  C9            		RET
                      	
  DF5E                	CTRL04:
  DF5E  CDD3EF        		CALL	_POS
  DF61  7D            		LD	A,L
  DF62  B7            		OR	A
  DF63  C8            		RET	Z
  DF64                	CTRL03:
  DF64  CD67DE        		CALL	CTRL0D
  DF67                	CTRL0A:
  DF67                	CTRL1F:
  DF67  ED4B8EEF      		LD	BC,(_TXADR)
  DF6B  2AB1EF        		LD	HL,(CRTCD+1)
  DF6E  2600          		LD	H,0
  DF70  09            		ADD	HL,BC
  DF71  44            		LD	B,H
  DF72  4D            		LD	C,L
  DF73  2ABAEF        		LD	HL,(CRTCD+10)
  DF76  09            		ADD	HL,BC
  DF77  3F            		CCF
  DF78  9F            		SBC	A,A
  DF79  C3DBDD        		JP	PRINTE8
                      	
  DF7C                	CTRL0E:
  DF7C  CDD3EF        		CALL	_POS
  DF7F  7C            		LD	A,H
  DF80  1804          		JR	SCR1
  DF82                	SCR:
  DF82  3A97EF        		LD	A,(_LINE)
  DF85  3D            		DEC	A
  DF86                	SCR1:
  DF86  C5            		PUSH	BC
  DF87  D5            		PUSH	DE
  DF88  F5            		PUSH	AF
  DF89  3E07          		LD	A,7
  DF8B  21BCDF        		LD	HL,SCRDMAD
  DF8E  CDB2DF        		CALL	SETDMA
  DF91  F1            		POP	AF
  DF92  210038        		LD	HL,03800H
                      	
  DF95                	SCRUP1:
  DF95  B7            		OR	A
  DF96  284D          		JR	Z,SCRCL
  DF98  F5            		PUSH	AF
  DF99  CDC3DF        		CALL	UPSUB		;kanji
  DF9C  3ABCEF        		LD	A,(CRTCD+12)
  DF9F  5F            		LD	E,A
  DFA0  16F7          		LD	D,0F7H
  DFA2  19            		ADD	HL,DE
  DFA3  D5            		PUSH	DE
  DFA4  CDC3DF        		CALL	UPSUB		;Text
  DFA7  D1            		POP	DE
  DFA8  19            		ADD	HL,DE
  DFA9  CDC3DF        		CALL	UPSUB		;Color
  DFAC  CBE4          		SET	4,H
  DFAE  F1            		POP	AF
  DFAF  3D            		DEC	A
  DFB0  18E3          		JR	SCRUP1
                      	
  DFB2                	SETDMA:
  DFB2  01871F        		LD	BC,01F87H
  DFB5                	SDMA:
  DFB5  04            		INC	B
  DFB6  EDA3          		OUTI
  DFB8  3D            		DEC	A
  DFB9  20FA          		JR	NZ,SDMA
  DFBB  C9            		RET
                      	
  DFBC                	SCRDMAD:
  DFBC  C3            		DB	0C3H		;WR6 リセット
  DFBD  9A            		DB	09AH		;WR5 ストップ WAIT READY:HIGH
  DFBE  65            		DB	065H		;WR0 ブロック長(LH)
  DFBF  4F00          		DW	79
  DFC1  1C            		DB	01CH		;WR1 ポートAインクリメント I/O
  DFC2  18            		DB	018H		;WR0 ポートAアドレス(LH)
                      	
  DFC3                	UPSUB:
  DFC3  3AB1EF        		LD	A,(CRTCD+1)
  DFC6  5F            		LD	E,A
  DFC7  1600          		LD	D,0
  DFC9  3ECD          		LD	A,0CDH		;WR4 バースト ポートB開始アドレス(LH)
  DFCB  ED79          		OUT	(C),A
  DFCD  ED69          		OUT	(C),L		;SOURCE
  DFCF  ED61          		OUT	(C),H
  DFD1  19            		ADD	HL,DE
  DFD2  3E1D          		LD	A,01DH		;WR0 ポートA開始アドレス(LH) ポートA→ポートB 転送
  DFD4  ED79          		OUT	(C),A
  DFD6  ED69          		OUT	(C),L		;DEST
  DFD8  ED61          		OUT	(C),H
  DFDA                	GO_DMA:
  DFDA  3ECF          		LD	A,0CFH
  DFDC  ED79          		OUT	(C),A		;WR6 ロード
  DFDE  3EB3          		LD	A,0B3H
  DFE0  ED79          		OUT	(C),A		;WR6 強制RDY
  DFE2  ED49          		OUT	(C),C		;WR6 イネーブル
  DFE4  C9            		RET
                      	
  DFE5                	SCRCL:
  DFE5  44            		LD	B,H
  DFE6  4D            		LD	C,L
  DFE7  2AB1EF        		LD	HL,(CRTCD+1)
  DFEA  CD4DDE        		CALL	LINECL
  DFED  D1            		POP	DE
  DFEE  C1            		POP	BC
  DFEF  C9            		RET
                      	
  DFF0                	SCRN:
  DFF0  D5            		PUSH	DE
  DFF1  ED58          		IN	E,(C)		;Text
  DFF3  3A1800        		LD	A,(00018H)
  DFF6  3C            		INC	A
  DFF7  281E          		JR	Z,SCRN2
  DFF9  CBD8          		SET	3,B
  DFFB  ED50          		IN	D,(C)		;Kanji
  DFFD  2816          		JR	Z,SCRN1
  DFFF  AF            		XOR	A
  E000  C5            		PUSH	BC
  E001  013730        		LD	BC,03037H	;VRMJIS
  E004  DF            		RST	018H
  E005  01522F        		LD	BC,02F52H	;JISSFT
  E008  DF            		RST	018H
  E009  C1            		POP	BC
  E00A  ED78          		IN	A,(C)
  E00C  CB98          		RES	3,B
  E00E  E640          		AND	040H
  E010  2005          		JR	NZ,SCRN2
  E012  7A            		LD	A,D
  E013  D1            		POP	DE
  E014  C9            		RET
  E015                	SCRN1:
  E015  CB98          		RES	3,B
  E017                	SCRN2:
  E017  7B            		LD	A,E
  E018  D1            		POP	DE
  E019  C9            		RET
  E01A                	SCRNE:
  E01A                	POS:
  E01A  2A8EEF        		LD	HL,(_TXADR)
  E01D  C5            		PUSH	BC
  E01E  ED4BBCEF      		LD	BC,(CRTCD+12)
  E022  AF            		XOR	A
  E023                	POS1:
  E023  09            		ADD	HL,BC
  E024  3C            		INC	A
  E025  38FC          		JR	C,POS1
  E027  ED42          		SBC	HL,BC
  E029  3D            		DEC	A
  E02A  67            		LD	H,A
  E02B  C1            		POP	BC
  E02C  A7            		AND	A
  E02D  C9            		RET
                      	
  E02E                	GETL:
  E02E  CDD3EF        		CALL	_POS
  E031  E5            		PUSH	HL
  E032  3ECD          		LD	A,0CDH		;CALL ????
  E034  3236DD        		LD	(INSFLG),A
  E037                	GETL1:
  E037  CDEFDB        		CALL	_SYS08
  E03A  FE09          		CP	9
  E03C  2008          		JR	NZ,GETL1V
  E03E  2100FF        		LD	HL,KBUF
  E041  CD1DD8        		CALL	MSX
  E044  18F1          		JR	GETL1
  E046                	GETL1V:
  E046  5F            		LD	E,A
  E047  CDCDEF        		CALL	_PRINT
                      	
  E04A  7B            		LD	A,E
  E04B  FE0D          		CP	00DH
  E04D  20E8          		JR	NZ,GETL1
  E04F  3E01          		LD	A,1		;LD BC,????
  E051  3236DD        		LD	(INSFLG),A
  E054  1100FF        		LD	DE,KBUF
  E057  3AB1EF        		LD	A,(CRTCD+1)
  E05A  47            		LD	B,A
  E05B  CDFAD4        		CALL	ZERO_MEMORY_DE
                      	
  E05E  D1            		POP	DE
  E05F  CDD3EF        		CALL	_POS
  E062  6B            		LD	L,E
  E063  3AB1EF        		LD	A,(CRTCD+1)
  E066  95            		SUB	L
  E067  57            		LD	D,A
  E068  CD8FDE        		CALL	LOC1
  E06B  4D            		LD	C,L
  E06C  7C            		LD	A,H
  E06D  F630          		OR	030H
  E06F  47            		LD	B,A
  E070  5A            		LD	E,D
  E071  2100FF        		LD	HL,KBUF
  E074                	GETL2:
  E074  CDD0EF        		CALL	_SCRN
  E077  03            		INC	BC
  E078  77            		LD	(HL),A
  E079  23            		INC	HL
  E07A  1D            		DEC	E
  E07B  20F7          		JR	NZ,GETL2
  E07D                	GETL3:
  E07D  2B            		DEC	HL
  E07E  7E            		LD	A,(HL)
  E07F  FE21          		CP	021H
  E081  D0            		RET	NC
  E082  3600          		LD	(HL),0
  E084  15            		DEC	D
  E085  20F6          		JR	NZ,GETL3
  E087  C9            		RET
                      	
  E088                	INKEY:
  E088  FB            		EI
  E089  E5            		PUSH	HL
  E08A  2A90EF        		LD	HL,(_KEYPS)
  E08D  7C            		LD	A,H
  E08E  AD            		XOR	L
  E08F  2809          		JR	Z,INKEY1
  E091  7C            		LD	A,H
  E092  3C            		INC	A
  E093  3291EF        		LD	(_KEYPS+1),A
  E096  6F            		LD	L,A
  E097  26FE          		LD	H,KEYBF/256
  E099  7E            		LD	A,(HL)
  E09A                	INKEY1:
  E09A  E1            		POP	HL
  E09B  B7            		OR	A
  E09C  C9            		RET
                      	
  1098                	AT_RS	EQU	SCR1-AT_SCR1
                      	
                      	;LDFILE.ASM
                      	
                      	;	LSX-Dodgers FILE
                      	
  E09D                	FILEC:
  E09D  CDA8E0        		CALL	FILE
  E0A0                	FILEC2:
  E0A0  3A15EF        		LD	A,(FDRV+1)
  E0A3  FE20          		CP	020H
  E0A5  C8            		RET	Z
  E0A6  1839          		JR	SETDIR1
                      	
  E0A8                	FILE:
  E0A8  AF            		XOR	A
  E0A9  3214EF        		LD	(FDRV),A
  E0AC  67            		LD	H,A
  E0AD  6F            		LD	L,A
  E0AE  2222EF        		LD	(FDRV+14),HL
  E0B1  CD7FE1        		CALL	SPCUT
  E0B4  CD64E1        		CALL	CCHK3
  E0B7  2812          		JR	Z,DEVI1
  E0B9  13            		INC	DE
  E0BA  1A            		LD	A,(DE)
  E0BB  1B            		DEC	DE
  E0BC  FE3A          		CP	':'
  E0BE  200B          		JR	NZ,DEVI1
  E0C0  1A            		LD	A,(DE)		;DRIVE
  E0C1  CDB4E3        		CALL	CAP
  E0C4  D640          		SUB	'@'
  E0C6  13            		INC	DE
  E0C7  13            		INC	DE
  E0C8  3214EF        		LD	(FDRV),A
  E0CB                	DEVI1:
  E0CB  1A            		LD	A,(DE)
  E0CC  D65C          		SUB	05CH		;\
  E0CE  2009          		JR	NZ,NOROOT
  E0D0  6F            		LD	L,A		;A=0でH=0のはずなのでHL=0
  E0D1  222EEF        		LD	(FDRV+26),HL
  E0D4  2C            		INC	L
  E0D5  2222EF        		LD	(FDRV+14),HL
  E0D8  13            		INC	DE
  E0D9                	NOROOT:
                      	
  E0D9                	SETDIR:
  E0D9  CD05E1        		CALL	FILED
  E0DC  1A            		LD	A,(DE)
  E0DD  FE5C          		CP	05CH		;\
  E0DF  C0            		RET	NZ
  E0E0  13            		INC	DE
  E0E1                	SETDIR1:
  E0E1  3E10          		LD	A,010H		;Directory
  E0E3  3221EF        		LD	(FDRV+13),A
  E0E6  D5            		PUSH	DE
  E0E7  1114EF        		LD	DE,FDRV
  E0EA  2A1EF0        		LD	HL,(STABLE+2*00FH)
  E0ED  CD0F00        		CALL	JP_HL
  E0F0  D1            		POP	DE
  E0F1  B7            		OR	A
  E0F2  C0            		RET	NZ
                      	
  E0F3  3A21EF        		LD	A,(FDRV+13)
  E0F6  CB67          		BIT	4,A
  E0F8  C8            		RET	Z
                      	
  E0F9  CD4CE1        		CALL	FILEI
  E0FC  2A2EEF        		LD	HL,(FDRV+26)
  E0FF  23            		INC	HL
  E100  2222EF        		LD	(FDRV+14),HL
  E103  18D4          		JR	SETDIR
                      	
  E105                	FILED:
  E105  CD4CE1        		CALL	FILEI
  E108  2115EF        		LD	HL,FNAME
                      	
  E10B  0608          		LD	B,8
  E10D                	FILE2:
  E10D  CD59E1        		CALL	CCHKF
  E110  C8            		RET	Z
  E111  FE2A          		CP	'*'
  E113  282E          		JR	Z,FILE9
  E115  FE2E          		CP	'.'
  E117  280C          		JR	Z,FILE4
  E119  77            		LD	(HL),A
  E11A  23            		INC	HL
  E11B  10F0          		DJNZ	FILE2
                      	
  E11D                	FILE3:
  E11D  CD59E1        		CALL	CCHKF
  E120  C8            		RET	Z
  E121  FE2E          		CP	'.'
  E123  20F8          		JR	NZ,FILE3
                      	
  E125                	FILE4:
  E125  211DEF        		LD	HL,FNAME+8
  E128  0603          		LD	B,3
                      	
  E12A                	FILE4L:
  E12A  CD59E1        		CALL	CCHKF
  E12D  C8            		RET	Z
  E12E  FE2E          		CP	'.'
  E130  2008          		JR	NZ,FILE12
  E132  3215EF        		LD	(FNAME),A	;Parent directory(..)
  E135  3216EF        		LD	(FNAME+1),A
  E138  3E20          		LD	A,020H
  E13A                	FILE12:
  E13A  FE2A          		CP	'*'
  E13C  280A          		JR	Z,FILE10
  E13E  77            		LD	(HL),A
  E13F  23            		INC	HL
  E140  10E8          		DJNZ	FILE4L
  E142  C9            		RET
                      	
  E143                	FILE9:				;名前の*処理、名前の残りを?で埋める
  E143  CD48E1        		CALL	FILE10
  E146  18D5          		JR	FILE3
                      	
  E148                	FILE10:
  E148  3E3F          		LD	A,'?'
  E14A  1808          		JR	FILL_MEMORY
  E14C                	FILEI:				;名前＋拡張子をスペースで初期化
  E14C  3E20          		LD	A,020H
  E14E  01000B        		LD	BC,11*256	;C=0にしておく
  E151  2115EF        		LD	HL,FNAME
  E154                	FILL_MEMORY:			;HLからBバイトAで埋める
  E154  77            		LD	(HL),A
  E155  23            		INC	HL
  E156  10FC          		DJNZ	FILL_MEMORY
  E158  C9            		RET
                      	
  E159                	CCHKF:
  E159  1A            		LD	A,(DE)
  E15A  CD61E1        		CALL	CCHK2
  E15D  C8            		RET	Z
  E15E  C3CCE4        		JP	FKAN
                      	
  E161                	CCHK2:
  E161  0C            		INC	C
  E162  0D            		DEC	C
  E163  C0            		RET	NZ
  E164                	CCHK3:				;ZF=1 で使えない文字
  E164  FE2B          		CP	'+'
  E166  C8            		RET	Z
  E167  FE2C          		CP	','
  E169  C8            		RET	Z
  E16A  FE2F          		CP	'/'
  E16C  C8            		RET	Z
  E16D  FE3A          		CP	':'
  E16F  C8            		RET	Z
  E170  FE3B          		CP	';'
  E172  C8            		RET	Z
  E173  FE3D          		CP	'='
  E175  C8            		RET	Z
  E176  FE5C          		CP	05CH	;\
  E178  C8            		RET	Z
  E179  FE20          		CP	020H
  E17B  D0            		RET	NC
  E17C  BF            		CP	A		;Z=1
  E17D  C9            		RET
                      	
  E17E                	SS1:
  E17E  13            		INC	DE
  E17F                	SPCUT:				;スペースをカット
  E17F  1A            		LD	A,(DE)
  E180  FE20          		CP	020H
  E182  28FA          		JR	Z,SS1
  E184  C9            		RET
                      	
  E185                	SETDRVF:
  E185  1114EF        		LD	DE,FDRV
  E188                	SETDRV0:
  E188  CD91E1        		CALL	SETDRV
  E18B  FDE1          		POP	IY
  E18D  C1            		POP	BC
  E18E  D1            		POP	DE
  E18F  E1            		POP	HL
  E190  C9            		RET
                      	
  E191                	SETDRV:
  E191  E3            		EX	(SP),HL		;HL=RETRN ADDRESS
  E192  D5            		PUSH	DE		;PUSH HL/DE/BC/IY
  E193  C5            		PUSH	BC
  E194  1A            		LD	A,(DE)
  E195  D5            		PUSH	DE
  E196  FDE3          		EX	(SP),IY
                      	
  E198  CDA4E1        		CALL	GETDRV
  E19B  3C            		INC	A
  E19C  FD7700        		LD	(IY+0),A	;(FCB)ドライブ番号
  E19F  3D            		DEC	A
  E1A0  CDD9EF        		CALL	_CHGDRV
                      	
  E1A3  E9            		JP	(HL)
                      	
  E1A4                	GETDRV:
  E1A4  E67F          		AND	07FH
  E1A6  D601          		SUB	001H
  E1A8  3003          		JR	NC,GETDRV1
  E1AA  3A87EF        		LD	A,(_DVSW)	;Current Drive
  E1AD                	GETDRV1:
  E1AD  3288EF        		LD	(_DRV),A
  E1B0  C9            		RET
                      	
  E1B1                	SOPENX:
  E1B1  1139EF        		LD	DE,SFILE
  E1B4  1A            		LD	A,(DE)
  E1B5  FDBE00        		CP	(IY+0)		;(FCB)ドライブ番号
  E1B8  2024          		JR	NZ,SOPEN
  E1BA  13            		INC	DE
  E1BB  FDE5          		PUSH	IY
  E1BD  E1            		POP	HL
  E1BE  23            		INC	HL
  E1BF  CD4CE3        		CALL	CPFILE
  E1C2  201A          		JR	NZ,SOPEN
                      	
  E1C4  2AF4F0        		LD	HL,(SCDIR)
  E1C7  FD750E        		LD	(IY+14),L	;(FCB)アクセスするディレクトリのクラスタ番号+1
  E1CA  FD740F        		LD	(IY+15),H
                      	
  E1CD  2AF8F0        		LD	HL,(SFNDF)	;Direcroty location and Flags
  E1D0  229FEF        		LD	(_FILEN),HL
                      	
  E1D3  ED5BF6F0      		LD	DE,(SFBPS)
  E1D7  2139EF        		LD	HL,SFILE
  E1DA  36FF          		LD	(HL),0FFH
  E1DC  23            		INC	HL
  E1DD  C9            		RET
  E1DE                	SOPEN:
  E1DE  21F4E2        		LD	HL,FILESE
  E1E1                	SOPENC:
  E1E1  221AE2        		LD	(OPENPAT+1),HL
                      	
  E1E4  CDE2EF        		CALL	_RDFATX		;Detect Media
  E1E7  3856          		JR	C,RDDERR
                      	
  E1E9  AF            		XOR	A
  E1EA  329FEF        		LD	(_FILEN),A
  E1ED  CDF9E3        		CALL	LDDIRX1		;A=0で呼び出す
  E1F0  2818          		JR	Z,SDIRX1
                      	
  E1F2  CDE5E2        		CALL	READ_DIR	;Sub Directory
  E1F5  3808          		JR	C,SDIRX0
  E1F7  2A7EF0        		LD	HL,(_DTBUF)
  E1FA  7E            		LD	A,(HL)
  E1FB  FE2E          		CP	'.'
  E1FD  280F          		JR	Z,SOPEN1
  E1FF                	SDIRX0:
  E1FF  AF            		XOR	A
  E200  32A0EF        		LD	(_DIRF),A
  E203  FD360E01      		LD	(IY+14),1	;(FCB)レコードサイズ
  E207  FD770F        		LD	(IY+15),A
  E20A                	SDIRX1:
  E20A  ED5BFCF0      		LD	DE,(_DIRPS)	;Root Directory
  E20E                	SOPEN1:
  E20E  0E20          	SDECPAT:LD	C,32		;自己書き換え 1セクタ辺りのディレクトリエントリ数  1024=32 / 512=16 / 256=8
  E210                	SOPEN1X:
  E210  CDE5E2        		CALL	READ_DIR	;FILE SEARCH
  E213  382A          		JR	C,RDDERR
  E215  2A7EF0        		LD	HL,(_DTBUF)
  E218                	SOPEN2:
  E218  D5            		PUSH	DE
  E219  CDF4E2        	OPENPAT:CALL	FILESE		;(self-modifying code)
  E21C  D1            		POP	DE
  E21D  386E          		JR	C,SOPENE
  E21F  281B          		JR	Z,SCF_FF_RET
  E221                	SOPEN3:
  E221  3AA0EF        		LD	A,(_DIRF)	;ディレクトリか判別
  E224  B7            		OR	A
  E225  200C          		JR	NZ,SOPEN5
  E227  13            		INC	DE		;ルートディレクトリ
  E228  E5            		PUSH	HL
  E229  210C00        	MD_PAT:	LD	HL,12		;(self-modifying code)MAXDIR
  E22C  ED52          		SBC	HL,DE		;CF=0 なので SUB HL,DE
  E22E  E1            		POP	HL
  E22F  20DD          		JR	NZ,SOPEN1
  E231  1807          		JR	SOPEN6
  E233                	SOPEN5:
  E233  CD90EA        		CALL	GNCLX		;END DIR
  E236  D8            		RET	C
  E237  CDACE3        		CALL	ENDCL
  E23A                	SOPEN6:
  E23A  38D2          		JR	C,SOPEN1
  E23C                	SCF_FF_RET:
  E23C  37            		SCF			;CF=1
  E23D  9F            		SBC	A,A		;A=0FFH
  E23E  C9            		RET
                      	
  E23F                	RDDERR:
  E23F  BF            		CP	A		;READ ERR CF=1 ZF=1
  E240  37            		SCF
  E241  C9            		RET
                      	
  E242                	NOPEN:
  E242  21F4E2        		LD	HL,FILESE
  E245  221AE2        		LD	(OPENPAT+1),HL
  E248  FD2A98EF      		LD	IY,(_FCB)
  E24C  CDDFE4        		CALL	CHKWILDX
  E24F  30EB          		JR	NC,SCF_FF_RET
  E251  FD7E00        		LD	A,(IY+0)	;(FCB)ドライブ番号
  E254  3D            		DEC	A
  E255  3288EF        		LD	(_DRV),A
  E258  CDD9EF        		CALL	_CHGDRV
  E25B  D8            		RET	C
  E25C  2AF8F0        		LD	HL,(SFNDF)	;Direcroty location and Flags
  E25F  229FEF        		LD	(_FILEN),HL
                      	
  E262  CDF4E3        		CALL	LDDIRX
  E265  ED5B9AEF      		LD	DE,(_FBPS)
  E269  CDE5E2        		CALL	READ_DIR
  E26C  38D1          		JR	C,RDDERR
  E26E  3A9EEF        		LD	A,(_FBCNT)
  E271  3D            		DEC	A
  E272  28AD          		JR	Z,SOPEN3
  E274                	NOPEN2:
  E274  2A9CEF        		LD	HL,(_FBAD)
  E277  012000        		LD	BC,020H
  E27A  09            		ADD	HL,BC
  E27B  4F            		LD	C,A
  E27C  189A          		JR	SOPEN2
                      	
  E27E                	SOPENE2:
  E27E  229CEF        		LD	(_FBAD),HL
  E281  79            		LD	A,C
  E282  329EEF        		LD	(_FBCNT),A
  E285  ED539AEF      		LD	(_FBPS),DE
  E289  FD2298EF      		LD	(_FCB),IY
  E28D                	SOPENE:
  E28D  AF            		XOR	A
  E28E  C9            		RET
                      	
  E28F                	COPEN:
  E28F  FD360D20      		LD	(IY+13),020H	;(FCB)属性(アトリビュート)
                      	
  E293  2111E3        		LD	HL,NEXTSE
  E296  CDE1E1        		CALL	SOPENC
  E299  D0            		RET	NC
  E29A  C8            		RET	Z
  E29B  3AA0EF        		LD	A,(_DIRF)	;ディレクトリか判別
  E29E  B7            		OR	A
  E29F  37            		SCF
  E2A0  C8            		RET	Z		;ルートディレクトリ
  E2A1  0601          		LD	B,1
  E2A3  CD3DE5        		CALL	WRDF
  E2A6  D8            		RET	C
  E2A7  ED5BFEF0      		LD	DE,(_CLPS)
  E2AB  D5            		PUSH	DE
  E2AC  CD75E3        		CALL	DCPAT
  E2AF  CD1BE9        		CALL	DRDNX
  E2B2  2A7EF0        		LD	HL,(_DTBUF)	;ディレクトリエントリを0クリア
  E2B5  3A86E8        		LD	A,(SECSZ+2)	;1024=4 / 512=2 / 256=1
  E2B8  47            		LD	B,A
  E2B9  AF            		XOR	A
  E2BA  4F            		LD	C,A
  E2BB                	COPENCL:
  E2BB  77            		LD	(HL),A
  E2BC  EDA1          		CPI			;HL=HL+1 BC=BC-1 BCが0の場合にP/V=0
  E2BE  EABBE2        		JP	PE,COPENCL
                      	
  E2C1  3A93E3        		LD	A,(SPCPAT+1)	;1クラスタの論理セクタ数
  E2C4  3D            		DEC	A
  E2C5  2819          		JR	Z,COPENE
  E2C7  3E01          		LD	A,1		;1セクタ辺りのセクタ数が2以上の場合
  E2C9  3288EA        		LD	(_SECPS+1),A
  E2CC  D1            		POP	DE		;DE=(_CLPS)
  E2CD  D5            		PUSH	DE
  E2CE                	COPEN1:
  E2CE  D5            		PUSH	DE		;データバッファに入らないセクタもゼロクリア
  E2CF  C5            		PUSH	BC
  E2D0  2A7EF0        		LD	HL,(_DTBUF)
  E2D3  CD8FE3        		CALL	CLUSTX
  E2D6  CDF4EB        		CALL	DWT24
  E2D9  C1            		POP	BC
  E2DA  D1            		POP	DE
  E2DB  CD87EA        		CALL	NEXTX
  E2DE  20EE          		JR	NZ,COPEN1
  E2E0                	COPENE:
  E2E0  2A7EF0        		LD	HL,(_DTBUF)
  E2E3  D1            		POP	DE
  E2E4  C9            		RET
                      	
  E2E5                	READ_DIR:
  E2E5  ED53FEF0      		LD	(_CLPS),DE
  E2E9  C5            		PUSH	BC
  E2EA  D5            		PUSH	DE
  E2EB  CD75E3        		CALL	DCPAT
  E2EE  CDFBE8        		CALL	DRDX
  E2F1  D1            		POP	DE
  E2F2  C1            		POP	BC
  E2F3  C9            		RET
                      	
  E2F4                	FILESE:
  E2F4  7E            		LD	A,(HL)
  E2F5  B7            		OR	A
  E2F6  C8            		RET	Z		;END:ZF=1 CF=0
  E2F7  FEE5          		CP	0E5H
  E2F9  280E          		JR	Z,FILESE1
  E2FB  FDE5          		PUSH	IY
  E2FD  D1            		POP	DE
  E2FE  13            		INC	DE
  E2FF  E5            		PUSH	HL
  E300  CD4CE3        		CALL	CPFILE
  E303  CC6DE3        		CALL	Z,CPFILE2
  E306  E1            		POP	HL
  E307  37            		SCF
  E308  C8            		RET	Z		;!!!:(ZF=1) CF=1
  E309                	FILESE1:
  E309  CD20E3        		CALL	FNEXT
  E30C  20E6          		JR	NZ,FILESE
  E30E                	ZF0_CF0_AFF_RET:
  E30E  F6FF          		OR	0FFH		;ZF=0 CF=0
  E310  C9            		RET
                      	
  E311                	NEXTSE:
  E311  7E            		LD	A,(HL)
  E312  B7            		OR	A
  E313  37            		SCF
  E314  C8            		RET	Z		;!!!:ZF=1 CF=1
  E315  FEE5          		CP	0E5H
  E317  37            		SCF
  E318  C8            		RET	Z		;!!!:(ZF=1) CF=1
  E319  CD20E3        		CALL	FNEXT
  E31C  20F3          		JR	NZ,NEXTSE
  E31E  18EE          		JR	ZF0_CF0_AFF_RET
                      	
  E320                	FNEXT:
  E320  E5            		PUSH	HL
  E321  219FEF        		LD	HL,_FILEN
  E324  34            		INC	(HL)
  E325  E1            		POP	HL
  E326  112000        		LD	DE,020H
  E329  19            		ADD	HL,DE
  E32A  0D            		DEC	C
  E32B  C9            		RET
                      	
  E32C                	CPNAME:
  E32C  C5            		PUSH	BC
  E32D  D5            		PUSH	DE
  E32E  E5            		PUSH	HL
  E32F  CD46E3        		CALL	CPFILE4
  E332  E1            		POP	HL
  E333  23            		INC	HL
  E334  23            		INC	HL
  E335  23            		INC	HL
  E336  23            		INC	HL
  E337  D1            		POP	DE
  E338  C1            		POP	BC
  E339  2005          		JR	NZ,CPNAME1
  E33B  7E            		LD	A,(HL)
  E33C  23            		INC	HL
  E33D  66            		LD	H,(HL)
  E33E  6F            		LD	L,A
  E33F  C9            		RET
  E340                	CPNAME1:
  E340  23            		INC	HL
  E341  23            		INC	HL
  E342  10E8          		DJNZ	CPNAME
  E344  37            		SCF
  E345  C9            		RET
                      	
  E346                	CPFILE4:
  E346  C5            		PUSH	BC
  E347  010004        		LD	BC,00400H
  E34A  1804          		JR	CPSTR1
  E34C                	CPFILE:
  E34C  C5            		PUSH	BC
  E34D  01000B        		LD	BC,00B00H
  E350                	CPSTR1:
  E350  1A            		LD	A,(DE)
  E351  FE3F          		CP	'?'		;Wild card
  E353  2812          		JR	Z,CPSTR2
  E355  7E            		LD	A,(HL)
  E356  CDC5E4        		CALL	FKANC
  E359  E5            		PUSH	HL
  E35A  67            		LD	H,A
  E35B  1A            		LD	A,(DE)
  E35C  CB01          		RLC	C
  E35E  CDC5E4        		CALL	FKANC
  E361  CB09          		RRC	C
  E363  BC            		CP	H		;CP (HL),(DE)
  E364  E1            		POP	HL
  E365  2004          		JR	NZ,CPSTR3
  E367                	CPSTR2:
  E367  13            		INC	DE
  E368  23            		INC	HL
  E369  10E5          		DJNZ	CPSTR1
  E36B                	CPSTR3:
  E36B  C1            		POP	BC
  E36C  C9            		RET
                      	
  E36D                	CPFILE2:
  E36D  FD7E0D        		LD	A,(IY+13)	;(FCB)属性(アトリビュート)
  E370  F6E1          		OR	0E1H
  E372  2F            		CPL
  E373  A6            		AND	(HL)
  E374  C9            		RET
                      	
  E375                	DCPAT:
  E375  0E00          		LD	C,0
  E377  2A7EF0        		LD	HL,(_DTBUF)
  E37A  3AA0EF        		LD	A,(_DIRF)	;ディレクトリか判別
  E37D  B7            		OR	A
  E37E  C8            		RET	Z		;ルートディレクトリの場合はCレジスタを0にしておく(CDEがセクタ番号)
  E37F  3A93E3        		LD	A,(SPCPAT+1)
  E382  4F            		LD	C,A
  E383  3A88EA        		LD	A,(_SECPS+1)
  E386  B9            		CP	C
  E387  3801          		JR	C,DC1
  E389  AF            		XOR	A
  E38A                	DC1:
  E38A  F680          		OR	080H
  E38C  32A0EF        		LD	(_DIRF),A	;bit0-6:セクタインデックス
  E38F                	CLUSTX:
  E38F  E5            		PUSH	HL
  E390  EB            		EX	DE,HL
  E391  AF            		XOR	A
  E392  0E01          	SPCPAT:	LD	C,1		;自己書き換え（1クラスタの論理セクタ数）
  E394                	L_CLDBL:
  E394  CB19          		RR	C		;->CF
  E396  3804          		JR	C,E_CLDBL
  E398  29            		ADD	HL,HL		;*2
  E399  8F            		ADC	A,A
  E39A  18F8          		JR	L_CLDBL
  E39C                	E_CLDBL:
  E39C  4F            		LD	C,A
  E39D  3A88EA        		LD	A,(_SECPS+1)
  E3A0  B5            		OR	L
  E3A1  6F            		LD	L,A
  E3A2  110800        	CLSPAT:	LD	DE,8	;+8 (2D)  !ADDCL (self-modifying code)
  E3A5  AF            		XOR	A
  E3A6  19            		ADD	HL,DE	;データ格納領域の先頭論理セクタ番号
  E3A7  89            		ADC	A,C
  E3A8  4F            		LD	C,A
  E3A9  EB            		EX	DE,HL
  E3AA  E1            		POP	HL
  E3AB  C9            		RET
                      	
  E3AC                	ENDCL:
  E3AC  7A            		LD	A,D	;END CLUSTER
  E3AD  FE01          	CLPAT1:	CP	1	;164=356	(self-modifying code)
  E3AF  C0            		RET	NZ
  E3B0  7B            		LD	A,E
  E3B1  FE64          	CLPAT2:	CP	064H	;		(self-modifying code)
  E3B3  C9            		RET
                      	
  E3B4                	CAP:
  E3B4  FE61          		CP	'a'
  E3B6  D8            		RET	C
  E3B7  FE7B          		CP	'z'+1
  E3B9  D0            		RET	NC
  E3BA  D620          		SUB	020H
  E3BC  C9            		RET
  E3BD                	CAP2:
  E3BD  CDB4E3        		CALL	CAP
  E3C0                	CAP3:
  E3C0  CDC9E3        		CALL	CAP4
  E3C3  FE21          		CP	021H
  E3C5  D0            		RET	NC
  E3C6  3EA0          		LD	A,0A0H
  E3C8  C9            		RET
  E3C9                	CAP4:
  E3C9  FE05          		CP	5
  E3CB  C0            		RET	NZ
  E3CC  3EE5          		LD	A,0E5H
  E3CE  C9            		RET
                      	
  E3CF                	CHDIR:
  E3CF  CD2BED        		CALL	GETDPBD
  E3D2  381D          		JR	C,CHDIR2
  E3D4  DD751A        		LD	(IX+01AH),L		;_SDIR_
  E3D7  DD741B        		LD	(IX+01BH),H		;_SDIR_+1
  E3DA  1815          		JR	CHDIR2		;POP_IX_RET
                      	
  E3DC                	LDDIR:
  E3DC  CD2BED        		CALL	GETDPBD
  E3DF  DD5E1A        		LD	E,(IX+01AH)		;_SDIR_
  E3E2  DD561B        		LD	D,(IX+01BH)		;_SDIR_+1
  E3E5  13            		INC	DE
  E3E6  FD730E        		LD	(IY+14),E	;(FCB)アクセスするディレクトリのクラスタ番号+1
  E3E9  FD720F        		LD	(IY+15),D
  E3EC  1B            		DEC	DE
  E3ED  AF            		XOR	A
  E3EE  32A0EF        		LD	(_DIRF),A
  E3F1                	CHDIR2:
  E3F1  DDE1          		POP	IX
  E3F3  C9            		RET
                      	
  E3F4                	LDDIRX:
  E3F4  3AA0EF        		LD	A,(_DIRF)	;(FCB)アクセスするディレクトリのセクタインデックス
  E3F7  E67F          		AND	07FH
  E3F9                	LDDIRX1:
  E3F9  3288EA        		LD	(_SECPS+1),A
  E3FC  FD5E0E        		LD	E,(IY+14)	;(FCB)アクセスするディレクトリのクラスタ番号+1
  E3FF  FD560F        		LD	D,(IY+15)
  E402  1B            		DEC	DE
  E403  CDACE3        		CALL	ENDCL
  E406  D4DCE3        		CALL	NC,LDDIR
  E409                	LDDIRS:
  E409  7A            		LD	A,D
  E40A  B3            		OR	E
  E40B  32A0EF        		LD	(_DIRF),A	;ディレクトリか判別
  E40E  C9            		RET
                      	
  E40F                	KILL:
  E40F  CDDFE4        		CALL	CHKWILDX
  E412  3834          		JR	C,KILLW
  E414  CDB1E1        		CALL	SOPENX
  E417                	KILLS:
  E417  3E1F          		LD	A,01FH
  E419  D45BE4        		CALL	NC,CHKATT
  E41C  D8            		RET	C
  E41D                	KILLSX:
  E41D  36E5          		LD	(HL),0E5H	;DIR
  E41F  CDB9E4        		CALL	WTDB
  E422  111A00        		LD	DE,01AH
  E425  19            		ADD	HL,DE
  E426  5E            		LD	E,(HL)
  E427  23            		INC	HL
  E428  56            		LD	D,(HL)
  E429                	KILLS1:
  E429  CDACE3        		CALL	ENDCL
  E42C  D0            		RET	NC		;CF=0
  E42D  21FEFF        		LD	HL,0-2
  E430  19            		ADD	HL,DE
  E431  D0            		RET	NC		;DE= 0 or 1
  E432  D5            		PUSH	DE
  E433  CDF7EF        		CALL	_GNCL
  E436  ED53FEF0      		LD	(_CLPS),DE
  E43A  D1            		POP	DE
  E43B  210000        		LD	HL,0
  E43E  D4FAEF        		CALL	NC,_SNCL
  E441  D8            		RET	C
  E442  ED5BFEF0      		LD	DE,(_CLPS)	;FAT
  E446  18E1          		JR	KILLS1
                      	
  E448                	KILLW:
  E448  CDDEE1        		CALL	SOPEN
  E44B                	KILLW1:
  E44B  380B          		JR	C,KILLW2
  E44D  CD7EE2        		CALL	SOPENE2
  E450  CD17E4        		CALL	KILLS
  E453  CD42E2        		CALL	NOPEN
  E456  18F3          		JR	KILLW1
  E458                	KILLW2:
  E458  C8            		RET	Z
  E459  3F            		CCF
  E45A  C9            		RET
                      	
  E45B                	CHKATT:
  E45B  E5            		PUSH	HL
  E45C  110B00        		LD	DE,00BH
  E45F  19            		ADD	HL,DE
  E460  A6            		AND	(HL)
  E461  E1            		POP	HL
  E462  C8            		RET	Z
  E463  37            		SCF
  E464  C9            		RET
                      	
  E465                	NAME:
  E465  CDDFE4        		CALL	CHKWILDX
  E468  D8            		RET	C
  E469  110400        		LD	DE,4
  E46C  19            		ADD	HL,DE
  E46D  2285E4        		LD	(NAMEP+2),HL
  E470  23            		INC	HL
  E471  CDE3E4        		CALL	CHKWILD
  E474  D8            		RET	C
                      	
  E475  CDB1E1        		CALL	SOPENX
  E478  3E0F          		LD	A,00FH
  E47A  D45BE4        		CALL	NC,CHKATT
  E47D  D8            		RET	C
                      	
  E47E  FD7E00        		LD	A,(IY+0)
  E481  FDE5          		PUSH	IY
  E483  FD210000      	NAMEP:	LD	IY,0		;自己書き換え
  E487  FD7700        		LD	(IY+0),A
  E48A  CDB1E1        		CALL	SOPENX
  E48D  FDE3          		EX	(SP),IY
  E48F  3F            		CCF
  E490  D4DEE1        		CALL	NC,SOPEN
  E493  EB            		EX	DE,HL
  E494  E1            		POP	HL
  E495  D8            		RET	C
                      	
  E496                	SETNAME:
  E496  01000B        		LD	BC,11*256
  E499  23            		INC	HL
  E49A  7E            		LD	A,(HL)
  E49B  FEE5          		CP	0E5H
  E49D  2004          		JR	NZ,SNAME1
  E49F  3E05          		LD	A,5
  E4A1  180E          		JR	SNAME3
  E4A3                	SNAME1:
  E4A3  7E            		LD	A,(HL)
  E4A4  0C            		INC	C
  E4A5  0D            		DEC	C
  E4A6  2009          		JR	NZ,SNAME3
  E4A8  CDB4E3        		CALL	CAP
  E4AB  FEA0          		CP	0A0H
  E4AD  2002          		JR	NZ,SNAME3
  E4AF  3E20          		LD	A,020H
  E4B1                	SNAME3:
  E4B1  12            		LD	(DE),A
  E4B2  7E            		LD	A,(HL)
  E4B3  23            		INC	HL
  E4B4  CDCCE4        		CALL	FKAN
  E4B7  10EA          		DJNZ	SNAME1
  E4B9                	WTDB:
  E4B9  3EFF          		LD	A,0FFH
  E4BB  3239EF        		LD	(SFILE),A
  E4BE                	SWTDBF:
  E4BE  3E01          		LD	A,1
  E4C0  32A4EF        		LD	(_WTDBF),A
  E4C3  AF            		XOR	A
  E4C4  C9            		RET
                      	
  E4C5                	FKANC:
  E4C5  CB41          		BIT	0,C
  E4C7  CCBDE3        		CALL	Z,CAP2
  E4CA  1801          		JR	FKANX
  E4CC                	FKAN:			;漢字チェック
  E4CC  13            		INC	DE
  E4CD                	FKANX:
  E4CD  CB41          		BIT	0,C
  E4CF  CB81          		RES	0,C
  E4D1  C0            		RET	NZ
  E4D2  FE80          		CP	080H
  E4D4  D8            		RET	C
  E4D5  FEA0          		CP	0A0H
  E4D7  3803          		JR	C,FKAN1
  E4D9  FEE0          		CP	0E0H
  E4DB  D8            		RET	C
  E4DC                	FKAN1:
  E4DC  0C            		INC	C
  E4DD  A7            		AND	A
  E4DE  C9            		RET
                      	
  E4DF                	CHKWILDX:
  E4DF  FDE5          		PUSH	IY
  E4E1  E1            		POP	HL
  E4E2  23            		INC	HL
  E4E3                	CHKWILD:
  E4E3  060B          		LD	B,11
  E4E5  3E3F          		LD	A,'?'
  E4E7                	CHKWIL1:
  E4E7  BE            		CP	(HL)
  E4E8  23            		INC	HL
  E4E9  37            		SCF
  E4EA  C8            		RET	Z
  E4EB  10FA          		DJNZ	CHKWIL1
  E4ED  AF            		XOR	A
  E4EE  C9            		RET
                      	
  E4EF                	SDIRENT:		;IY=FCB HL=DIR
  E4EF  D5            		PUSH	DE
  E4F0  E5            		PUSH	HL
  E4F1  FDE5          		PUSH	IY
  E4F3  D1            		POP	DE
  E4F4  EB            		EX	DE,HL
  E4F5  CD96E4        		CALL	SETNAME
                      	;				Attribute
  E4F8  FD7E0D        		LD	A,(IY+13)	;(FCB)属性(アトリビュート)
  E4FB  12            		LD	(DE),A
  E4FC  13            		INC	DE
                      	;				Reserved
  E4FD  AF            		XOR	A
  E4FE  060A          		LD	B,10
  E500                	SDE1:
  E500  12            		LD	(DE),A
  E501  13            		INC	DE
  E502  10FC          		DJNZ	SDE1
                      						;(FCB)更新時刻
  E504  21EAF0        		LD	HL,SDDATA		;(FCB)更新年月日
  E507  060A          		LD	B,SDDATAE-SDDATA	;(FCB)ファイルの先頭クラスタ
  E509                	SDLOOP:					;(FCB)ファイルのサイズ(バイト単位)
  E509  7E            		LD	A,(HL)
  E50A  23            		INC	HL
  E50B  3210E5        		LD	(SDPAT+2),A
  E50E  FD7E00        	SDPAT:	LD	A,(IY+0)	;LD A,(IY+A)
  E511  12            		LD	(DE),A
  E512  13            		INC	DE
  E513  10F4          		DJNZ	SDLOOP
                      	
  E515  AF            		XOR	A
  E516  E1            		POP	HL
  E517  D1            		POP	DE
  E518  C9            		RET
                      	
  E519                	WOPEN:
  E519  FD7E0D        		LD	A,(IY+13)	;(FCB)属性(アトリビュート)
  E51C  E61F          		AND	01FH
  E51E  37            		SCF
  E51F  C0            		RET	NZ
  E520  FD361CFE      		LD	(IY+28),0FEH	;(FCB)オープンモード
  E524                	TOPEN2:
  E524  AF            		XOR	A
  E525                	TOPEN:				;Initializes the time stamp
  E525  FD3617FF      		LD	(IY+23),0FFH	;(FCB)更新時刻
  E529  C9            		RET
                      	
  E52A                	WRDFX:
  E52A  3A93E3        		LD	A,(SPCPAT+1)	;1クラスタの論理セクタ
  E52D                	L_WRFX:
  E52D  1F            		RRA			;->CF
  E52E  3806          		JR	C,E_WRFX
  E530  CB39          		SRL	C
  E532  CB1C          		RR	H		;CH=CH/2
  E534  18F7          		JR	L_WRFX
  E536                	E_WRFX:
  E536  44            		LD	B,H
  E537  04            		INC	B
  E538  3E37          		LD	A,037H		;SCF
  E53A  3218E9        		LD	(DRDN1),A
                      	
  E53D                	WRDF:				;Bクラスタ分FATを確保する
  E53D  110200        		LD	DE,2
  E540  AF            		XOR	A
  E541  3288EA        		LD	(_SECPS+1),A
  E544                	WRDF1:
  E544  C5            		PUSH	BC
  E545  D5            		PUSH	DE
  E546  CDF7EF        		CALL	_GNCL
  E549  381C          		JR	C,WRDF3
  E54B  7A            		LD	A,D		;空いているかチェック
  E54C  B3            		OR	E
  E54D  2027          		JR	NZ,WRDF4
  E54F  E1            		POP	HL		;HL=空いているクラスタ
  E550  E5            		PUSH	HL
  E551  ED5BFEF0      		LD	DE,(_CLPS)	;DE=元の(_CLPS)
  E555  22FEF0        		LD	(_CLPS),HL
  E558  7A            		LD	A,D		;元の(_CLPS)が 0 か?
  E559  B3            		OR	E
  E55A  2008          		JR	NZ,WRDF2
  E55C  FD751A        		LD	(IY+26),L	;(FCB)ファイルの先頭クラスタ
  E55F  FD741B        		LD	(IY+27),H	;元が空の場合はFCBにクラスタをセットする
  E562  1803          		JR	WRDF3
  E564                	WRDF2:
  E564  CDFAEF        		CALL	_SNCL
  E567                	WRDF3:
  E567  D1            		POP	DE
  E568  C1            		POP	BC
  E569  D8            		RET	C
  E56A  100C          		DJNZ	WRDF5		;ここでループカウンタチェック
  E56C  ED5BFEF0      		LD	DE,(_CLPS)
  E570  21FFFF        		LD	HL,0FFFFH	;FATエントリ終了(EOCマーク)
  E573  C3FAEF        		JP	_SNCL
                      	
  E576                	WRDF4:				;DEクラスタが空じゃないので次に移動する
  E576  D1            		POP	DE
  E577  C1            		POP	BC
  E578                	WRDF5:
  E578  13            		INC	DE
  E579  CDACE3        		CALL	ENDCL
  E57C  38C6          		JR	C,WRDF1
  E57E  37            		SCF
  E57F  C9            		RET
                      	
  E580                	RWXR:
  E580  3A86E8        		LD	A,(SECSZ+2)	;1024=4 / 512=2 / 256=1
  E583                	L_RWX:
  E583  1F            		RRA		;->CF
  E584  3808          		JR	C,E_RWX
  E586  CB38          		SRL	B	;BCH=BCH/2
  E588  CB19          		RR	C	;
  E58A  CB1C          		RR	H	;
  E58C  18F5          		JR	L_RWX
  E58E                	E_RWX:
  E58E  FD5E1A        		LD	E,(IY+26)	;(FCB)ファイルの先頭クラスタ
  E591  FD561B        		LD	D,(IY+27)
  E594  AF            		XOR	A
  E595  3288EA        		LD	(_SECPS+1),A
  E598                	RWX1:
  E598  ED53FEF0      		LD	(_CLPS),DE
  E59C  7A            		LD	A,D
  E59D  B3            		OR	E
  E59E  2813          		JR	Z,SCF_RET	;RET DE==0 => CF=1
                      	
  E5A0  78            		LD	A,B
  E5A1  B1            		OR	C
  E5A2  B4            		OR	H
  E5A3  C8            		RET	Z		;RET BCH==0 => CF=0
                      	
  E5A4  CD90EA        		CALL	GNCLX
  E5A7  D8            		RET	C
  E5A8  7C            		LD	A,H		;
  E5A9  25            		DEC	H		;
  E5AA  B7            		OR	A		;DEC BCH
  E5AB  2001          		JR	NZ,RWX2		;
  E5AD  0B            		DEC	BC		;
  E5AE                	RWX2:
  E5AE  CDACE3        		CALL	ENDCL
  E5B1  38E5          		JR	C,RWX1
  E5B3                	SCF_RET:
  E5B3  37            		SCF
  E5B4  C9            		RET
                      	
  E5B5                	DSKF:
  E5B5  110000        	MAXCLP:	LD	DE,0
  E5B8  2AACEF        		LD	HL,(_FAKEFREE)
  E5BB  7C            		LD	A,H
  E5BC  B5            		OR	L
  E5BD  2803          		JR	Z,DSKF1
  E5BF  110100        		LD	DE,1
  E5C2                	DSKF1:
  E5C2  D5            		PUSH	DE
  E5C3  CDF7EF        		CALL	_GNCL
  E5C6  380C          		JR	C,POPSCF
  E5C8  7A            		LD	A,D
  E5C9  B3            		OR	E
  E5CA  2001          		JR	NZ,DSKF2
  E5CC  23            		INC	HL
  E5CD                	DSKF2:
  E5CD  D1            		POP	DE
  E5CE  1B            		DEC	DE
  E5CF  7A            		LD	A,D
  E5D0  B3            		OR	E
  E5D1  20EF          		JR	NZ,DSKF1
  E5D3  C9            		RET
                      	
  E5D4                	POPSCF:
  E5D4  F1            		POP	AF
  E5D5  37            		SCF
  E5D6  C9            		RET
                      	
  E5D7                	SETTMS:
  E5D7  FD7E17        		LD	A,(IY+23)	;(FCB)ファイルを最後に変更した時刻
  E5DA  3C            		INC	A
  E5DB  C0            		RET	NZ		;ファイルが更新されていない
  E5DC                	SETTMSX:			;FCBに日付時刻をセットする
  E5DC  CD9EDC        		CALL	_SYS2C		;(BDOS)時刻の獲得
                      					;H←時  L←分  D←秒
  E5DF  CB25          		SLA	L		;   *2
  E5E1  CB25          		SLA	L		;   *4
  E5E3  29            		ADD	HL,HL		;*2 *8
  E5E4  29            		ADD	HL,HL		;*4 *16
  E5E5  29            		ADD	HL,HL		;*8 *32
  E5E6  7A            		LD	A,D
  E5E7  0F            		RRCA			;bit.0->7->->->0->C
  E5E8  E61F          		AND	01FH
  E5EA  B5            		OR	L
  E5EB  FD7716        		LD	(IY+22),A	;(FCB)ファイルを最後に変更した時刻
  E5EE  FD7417        		LD	(IY+23),H
                      	
  E5F1  CD6ADC        		CALL	_SYS2A		;(BDOS)日付の獲得
                      					;HL←年  D←月  E←日
  E5F4  0144F8        		LD	BC,0-1980
  E5F7  09            		ADD	HL,BC
  E5F8  65            		LD	H,L
                      	
  E5F9  7A            		LD	A,D
  E5FA  87            		ADD	A,A		;*2
  E5FB  87            		ADD	A,A		;*4
  E5FC  87            		ADD	A,A		;*8
  E5FD  87            		ADD	A,A		;*16
  E5FE  6F            		LD	L,A
  E5FF  29            		ADD	HL,HL		;*32
  E600  7D            		LD	A,L
  E601  B3            		OR	E
  E602  FD7714        		LD	(IY+20),A	;(FCB)ファイルを最後に変更した日付
  E605  FD7415        		LD	(IY+21),H
  E608  C9            		RET
                      	
  E609                	PUSHRR:
  E609  CD0FE6        		CALL	SUBRR
  E60C                	POPRR1:
  E60C  EDB0          		LDIR
  E60E  C9            		RET
                      	
  E60F                	SUBRR:
  E60F  FDE5          		PUSH	IY
  E611  E1            		POP	HL
  E612  112100        		LD	DE,33
  E615  19            		ADD	HL,DE
  E616  115AEF        		LD	DE,HLBUF
  E619  010300        		LD	BC,3
  E61C  C9            		RET
                      	
  E61D                	SETSEQ:
  E61D  F5            		PUSH	AF		;AHL=Random record
  E61E  C5            		PUSH	BC
  E61F  FD7E21        		LD	A,(IY+33)	;(FCB)ランダムレコード
  E622  FD6E22        		LD	L,(IY+34)
  E625  FD6623        		LD	H,(IY+35)
  E628  010000        	RR36:	LD	BC,0		;(IY+36)
                      	
  E62B  CD43E6        		CALL	GET_CPM_R_SIZE
                      	
  E62E  29            		ADD	HL,HL
  E62F  8F            		ADC	A,A
  E630  CB3D          		SRL	L		;L=L/2
  E632  FD7520        		LD	(IY+32),L	;(FCB)カレントレコード
  E635  FD740C        		LD	(IY+12),H	;(FCB)カレントブロック
  E638  FD7718        		LD	(IY+24),A	;(FCB)カレントブロック上位
  E63B  C1            		POP	BC
  E63C  F1            		POP	AF
                      	
  E63D                	POPRR:
  E63D  CD0FE6        		CALL	SUBRR
  E640  EB            		EX	DE,HL
  E641  18C9          		JR	POPRR1
                      	
  E643                	GET_CPM_R_SIZE:		;バイト単位からCP/Mのレコード単位を求める（1レコード=128バイト）
  E643  87            		ADD	A,A		;in BHLA => out AHL
  E644  ED6A          		ADC	HL,HL
  E646  CB18          		RR	B
  E648  B7            		OR	A
  E649  78            		LD	A,B		;ここでフラグは変化しない
  E64A  C8            		RET	Z
  E64B  2C            		INC	L		;INC AHL
  E64C  C0            		RET	NZ		;
  E64D  24            		INC	H		;
  E64E  C0            		RET	NZ		;
  E64F  3C            		INC	A		;
  E650  C9            		RET
                      	
  E651                	GETSEQ:
  E651  FD6E20        		LD	L,(IY+32)	;(FCB)カレントレコード
  E654  FD660C        		LD	H,(IY+12)	;(FCB)カレントブロック
  E657  FD7E18        		LD	A,(IY+24)	;(FCB)カレントブロック上位
                      	
  E65A  CB25          		SLA	L	;L=L*2
                      	
  E65C  CB3F          		SRL	A	;
  E65E  CB1C          		RR	H	;AHL=AHL/2
  E660  CB1D          		RR	L	;
  E662  C9            		RET
                      	
  E663                	RB_WRITE:
  E663  C5            		PUSH	BC
  E664  ED4B4CF0      		LD	BC,(STABLE+2*026H)	;(BDOS)ランダムブロック書き込み
  E668  1805          		JR	RBRW
  E66A                	RB_READ:
  E66A  C5            		PUSH	BC
  E66B  ED4B4EF0      		LD	BC,(STABLE+2*027H)	;(BDOS)ランダムブロック読み込み
  E66F                	RBRW:
  E66F  CB3F          		SRL	A	;AHLA' = HL*128
  E671  F5            		PUSH	AF
  E672  CB1C          		RR	H	;AHL=AHL/2
  E674  CB1D          		RR	L	;
  E676  3E00          		LD	A,0
  E678  1F            		RRA			;A'HLA
  E679  FD7721        		LD	(IY+33),A	;(FCB)ランダムレコード
  E67C  FD7522        		LD	(IY+34),L
  E67F  FD7423        		LD	(IY+35),H
  E682  FD7E24        		LD	A,(IY+36)	;ランダムレコードを4バイトで扱うとCP/M用のコマンドで
  E685  32A3E6        		LD	(PBRSWC+3),A	;デフォルトのFCBとデフォルトのDTAを使用しているコマンドで
  E688  F1            		POP	AF		;ファイル読み込みの際に先頭の1バイトが化けるので保存
  E689  FD7724        		LD	(IY+36),A	;
  E68C  218000        		LD	HL,128
                      	
  E68F  FDE5          		PUSH	IY
  E691  D1            		POP	DE
  E692  D5            		PUSH	DE
  E693  CDA5E6        		CALL	JP_BC
  E696  FDE1          		POP	IY
  E698  FD4624        		LD	B,(IY+36)
  E69B  ED4329E6      		LD	(RR36+1),BC
  E69F  C1            		POP	BC
  E6A0  FD362400      	PBRSWC:	LD	(IY+36),0	;自己書き換え	ここでランダムレコードを復元
  E6A4  C9            		RET
  E6A5                	JP_BC:
  E6A5  C5            		PUSH	BC
  E6A6  C9            		RET
                      	
  E23C                	_SYS03	EQU	SCF_FF_RET	;(BDOS)外部入力
  E23C                	_SYS2B	EQU	SCF_FF_RET	;(BDOS)日付の設定
  E23C                	_SYS2D	EQU	SCF_FF_RET	;(BDOS)時刻の設定
  E23C                	_SYS39	EQU	SCF_FF_RET	;(BDOS)サブディレクトリの作成
  E23C                	_SYS3A	EQU	SCF_FF_RET	;(BDOS)サブディレクトリの削除
                      	
                      	
                      	;LDFILE2.ASM
                      	
                      	;	LSX-Dodgers FILE2
                      	
                      					;Write random block
  E6A7                	_SYS26:		;(BDOS)ランダムブロック書き込み
  E6A7  AF            		XOR	A
  E6A8  3218E9        		LD	(DRDN1),A	;NOP
  E6AB  22C9E8        		LD	(LEFT+1),HL	;HL←書き込むレコード数
  E6AE  225DEF        		LD	(LEFTX),HL
  E6B1  CD91E1        		CALL	SETDRV
                      	
  E6B4  CD28E8        		CALL	RBX0
  E6B7  DA4EE7        		JP	C,RBWERR
  E6BA  CD19E5        		CALL	WOPEN
  E6BD  DA4EE7        		JP	C,RBWERR
  E6C0  7C            		LD	A,H
  E6C1  B5            		OR	L
  E6C2  CA47E7        		JP	Z,RBW1
                      	
  E6C5  2B            		DEC	HL
                      	
  E6C6  CDE5E8        		CALL	GET_RR_BCDE	;BCDE=Random record
                      	
  E6C9  19            		ADD	HL,DE		;BCHL=HL+BCDE
  E6CA  3001          		JR	NC,S26X1	;
  E6CC  03            		INC	BC		;
  E6CD                	S26X1:
  E6CD  CD80E5        		CALL	RWXR
  E6D0  DC2AE5        		CALL	C,WRDFX
  E6D3  DA4EE7        		JP	C,RBWERR
                      	
  E6D6  CD57E8        		CALL	RBX2
  E6D9  281A          		JR	Z,RBWB		;NZ=セクタ以下の端数がある
  E6DB  CD6EE8        		CALL	RBXA
  E6DE  386E          		JR	C,RBWERR
  E6E0  EB            		EX	DE,HL
  E6E1  C5            		PUSH	BC
  E6E2  EDB0          		LDIR
  E6E4  225FEF        		LD	(DTAX),HL
                      	
  E6E7  CDBEE4        		CALL	SWTDBF		;バッファデータは更新された
                      	
  E6EA  2A5DEF        		LD	HL,(LEFTX)
  E6ED  D1            		POP	DE
  E6EE  ED52          		SBC	HL,DE
  E6F0  225DEF        		LD	(LEFTX),HL
  E6F3  2830          		JR	Z,RBWEND
                      	
  E6F5                	RBWB:
  E6F5  CDA4E8        		CALL	RBXB
  E6F8  2813          		JR	Z,RBWC
  E6FA                	RBWB1:
  E6FA  C5            		PUSH	BC
  E6FB  D5            		PUSH	DE
  E6FC  CD8FE3        		CALL	CLUSTX
  E6FF  CDF4EB        		CALL	DWT24
  E702  D1            		POP	DE
  E703  C1            		POP	BC
  E704  3848          		JR	C,RBWERR
  E706  CD90EA        		CALL	GNCLX
  E709  3843          		JR	C,RBWERR
  E70B  10ED          		DJNZ	RBWB1
  E70D                	RBWC:
  E70D  CDBCE8        		CALL	RBXC
  E710  2813          		JR	Z,RBWEND
  E712  C5            		PUSH	BC
  E713  CD8FE3        		CALL	CLUSTX
  E716  CD17E9        		CALL	DRDN
  E719  C1            		POP	BC
  E71A  3832          		JR	C,RBWERR
  E71C  ED5B7EF0      		LD	DE,(_DTBUF)
  E720  EDB0          		LDIR
  E722  CDBEE4        		CALL	SWTDBF		;バッファデータは更新された
  E725                	RBWEND:
  E725  CDC8E8        		CALL	RBXEND
                      	
  E728  CD37E8        		CALL	RBX1
  E72B  301A          		JR	NC,RBW1
  E72D  2AC9E8        		LD	HL,(LEFT+1)
  E730  FD5E10        		LD	E,(IY+16)	;(FCB)ファイルのサイズ(バイト単位)
  E733  FD5611        		LD	D,(IY+17)	;DE=File size
  E736  19            		ADD	HL,DE
  E737  FD7510        		LD	(IY+16),L	;(FCB)ファイルのサイズ(バイト単位)
  E73A  FD7411        		LD	(IY+17),H
  E73D  3008          		JR	NC,RBW1
  E73F  FD3412        		INC	(IY+18)
  E742  2003          		JR	NZ,RBW1
  E744  FD3413        		INC	(IY+19)
  E747                	RBW1:
  E747  FDE1          		POP	IY
  E749  C1            		POP	BC
  E74A  D1            		POP	DE
  E74B  E1            		POP	HL
  E74C                	DD_NUL:
  E74C  AF            		XOR	A
  E74D  C9            		RET
                      	
  E74E                	RBWERR:
  E74E  FDE5          		PUSH	IY
  E750  D1            		POP	DE
  E751  2A20F0        		LD	HL,(STABLE+2*010H)
  E754  CD0F00        		CALL	JP_HL
  E757                	RBRERR1:
  E757  3E01          		LD	A,001H
  E759                	RBRERR2:
  E759  FDE1          		POP	IY
  E75B  C1            		POP	BC
  E75C  D1            		POP	DE
  E75D  E1            		POP	HL
  E75E  37            		SCF
  E75F  210000        		LD	HL,0
  E762  C9            		RET
                      	
  E763                	RBRERR:
  E763  3EFF          		LD	A,0FFH
  E765  18F2          		JR	RBRERR2
                      	
  E767                	RBRFL:
  E767  3E00          	RBRFLP:	LD	A,000H
  E769  FE0D          		CP	00DH
  E76B  2005          		JR	NZ,RBRFL1
  E76D  D5            		PUSH	DE
  E76E  1E0A          		LD	E,00AH
  E770  1805          		JR	RBRFL2
  E772                	RBRFL1:
  E772  D5            		PUSH	DE
  E773  CD09EF        		CALL	_SYS07
  E776  5F            		LD	E,A
  E777                	RBRFL2:
  E777  CDCDEF        		CALL	_PRINT
  E77A  7B            		LD	A,E
  E77B  D1            		POP	DE
  E77C  3268E7        		LD	(RBRFLP+1),A
  E77F  C9            		RET
  E780                	DDX:
  E780  FD7E01        		LD	A,(IY+1)	;(FCB)主ファイル名
  E783  CDB4E3        		CALL	CAP
  E786  FE41          		CP	'A'
  E788  C9            		RET
                      	
                      					;Read random block
  E789                	_SYS27:		;(BDOS)ランダムブロック読み込み
  E789  225DEF        		LD	(LEFTX),HL
  E78C  CD91E1        		CALL	SETDRV
                      	
  E78F  FDCB0D66      		BIT	4,(IY+13)	;(FCB)属性(アトリビュート)
  E793  C216E8        		JP	NZ,RBRDIR	;ディレクトリ
  E796  CD28E8        		CALL	RBX0
  E799  DA63E7        		JP	C,RBRERR
  E79C  CD37E8        		CALL	RBX1
  E79F  DA57E7        		JP	C,RBRERR1
  E7A2  EB            		EX	DE,HL
  E7A3  ED52          		SBC	HL,DE		;CP 00HL,BCDE
  E7A5  19            		ADD	HL,DE
  E7A6  79            		LD	A,C
  E7A7  DE00          		SBC	A,0
  E7A9  78            		LD	A,B
  E7AA  DE00          		SBC	A,0
  E7AC  3801          		JR	C,RBR1
  E7AE  EB            		EX	DE,HL
  E7AF                	RBR1:
  E7AF  9F            		SBC	A,A
  E7B0  E601          		AND	1
  E7B2  3214E8        		LD	(RBRAP+1),A
                      	
  E7B5  7C            		LD	A,H
  E7B6  B5            		OR	L
  E7B7  2854          		JR	Z,RBREND0
                      	
  E7B9  22C9E8        		LD	(LEFT+1),HL	;Read record byte
  E7BC  225DEF        		LD	(LEFTX),HL
                      	
  E7BF  CD57E8        		CALL	RBX2
  E7C2  2819          		JR	Z,RBRB
  E7C4  CD6EE8        		CALL	RBXA
  E7C7  DA63E7        		JP	C,RBRERR
  E7CA  C5            		PUSH	BC
  E7CB  EDB0          		LDIR
  E7CD  ED535FEF      		LD	(DTAX),DE
  E7D1  2A5DEF        		LD	HL,(LEFTX)
  E7D4  D1            		POP	DE
  E7D5  A7            		AND	A
  E7D6  ED52          		SBC	HL,DE
  E7D8  225DEF        		LD	(LEFTX),HL
  E7DB  282D          		JR	Z,RBREND
                      	
  E7DD                	RBRB:
  E7DD  CDA4E8        		CALL	RBXB
  E7E0  2812          		JR	Z,RBRC
  E7E2                	RBRB1:
  E7E2  C5            		PUSH	BC
  E7E3  D5            		PUSH	DE
  E7E4  CD8FE3        		CALL	CLUSTX
  E7E7  CDE4EB        		CALL	DRD24
  E7EA  D1            		POP	DE
  E7EB  C1            		POP	BC
  E7EC  D490EA        		CALL	NC,GNCLX
  E7EF  DA63E7        		JP	C,RBRERR
  E7F2  10EE          		DJNZ	RBRB1
  E7F4                	RBRC:
  E7F4  CDBCE8        		CALL	RBXC
  E7F7  2811          		JR	Z,RBREND
  E7F9  C5            		PUSH	BC
  E7FA  CD8FE3        		CALL	CLUSTX
  E7FD  CDFBE8        		CALL	DRDX
  E800  C1            		POP	BC
  E801  DA63E7        		JP	C,RBRERR
  E804  EB            		EX	DE,HL
  E805  2A7EF0        		LD	HL,(_DTBUF)
  E808  EDB0          		LDIR
  E80A                	RBREND:
  E80A  CDC8E8        		CALL	RBXEND
  E80D                	RBREND0:
  E80D  FDE1          		POP	IY
  E80F  C1            		POP	BC
  E810  D1            		POP	DE
  E811  F1            		POP	AF	;(HL)
  E812  AF            		XOR	A
  E813  3E00          	RBRAP:	LD	A,000H
  E815  C9            		RET
                      	
  E816                	RBRDIR:
  E816  FD6E1A        		LD	L,(IY+26)	;(FCB)ファイルの先頭クラスタ
  E819  FD661B        		LD	H,(IY+27)
  E81C  CDCFE3        		CALL	CHDIR
  E81F  AF            		XOR	A
  E820  67            		LD	H,A
  E821  6F            		LD	L,A
  E822  3C            		INC	A
  E823  3214E8        		LD	(RBRAP+1),A
  E826  18E5          		JR	RBREND0
                      	
  E828                	RBX0:
  E828  2A8AEF        		LD	HL,(_DTA)
  E82B  225FEF        		LD	(DTAX),HL
  E82E  2A5DEF        		LD	HL,(LEFTX)
  E831  FD7E1C        		LD	A,(IY+28)	;(FCB)オープンモード
  E834  D6FD          		SUB	0FDH
  E836  C9            		RET
                      	
  E837                	RBX1:
  E837  E5            		PUSH	HL		;Record byte
                      					;AHL=File size
  E838  FD6E10        		LD	L,(IY+16)	;ファイルのサイズ(バイト単位)
  E83B  FD6611        		LD	H,(IY+17)	;
  E83E  FD7E12        		LD	A,(IY+18)	;
                      	
  E841  CDE5E8        		CALL	GET_RR_BCDE	;BCDE=Random record
                      	
  E844  A7            		AND	A
  E845  ED52          		SBC	HL,DE
  E847  99            		SBC	A,C
  E848  4F            		LD	C,A
  E849  FD7E13        		LD	A,(IY+19)	;ファイルのサイズ
  E84C  98            		SBC	A,B
  E84D  D1            		POP	DE
                      	
  E84E  D8            		RET	C
  E84F  47            		LD	B,A
  E850  B1            		OR	C
  E851  EB            		EX	DE,HL	;BCDE=File byte	HL=Record byte
  E852  B2            		OR	D
  E853  B3            		OR	E
  E854  C0            		RET	NZ
  E855  37            		SCF
  E856  C9            		RET
                      	
  E857                	RBX2:				;Cluster settings
  E857  FD6622        		LD	H,(IY+34)	;(+33)(FCB)ランダムレコード
  E85A  FD4E23        		LD	C,(IY+35)
  E85D  FD4624        		LD	B,(IY+36)
  E860  CD80E5        		CALL	RWXR
  E863  3A86E8        		LD	A,(SECSZ+2)	;1024=4 / 512=2 / 256=1
  E866  3D            		DEC	A
  E867  FDA622        		AND	(IY+34)
  E86A  FDB621        		OR	(IY+33)
  E86D  C9            		RET
                      	
  E86E                	RBXA:
  E86E  C5            		PUSH	BC
  E86F  D5            		PUSH	DE
  E870  CD8FE3        		CALL	CLUSTX
  E873  CDFBE8        		CALL	DRDX
  E876  D1            		POP	DE
  E877  C1            		POP	BC
  E878  D8            		RET	C
  E879  CD90EA        		CALL	GNCLX
  E87C  D8            		RET	C
  E87D  ED53FEF0      		LD	(_CLPS),DE
                      	
  E881  FD4E21        		LD	C,(IY+33)	;(FCB)ランダムレコード
  E884  210004        	SECSZ:	LD	HL,0400H	;自己書き換え 1024=0400H / 512=0200H ここに1セクタのサイズが格納される
  E887  7C            		LD	A,H
  E888  3D            		DEC	A		;1024=3 / 512=1
  E889  FDA622        		AND	(IY+34)		;(FCB)ランダムレコード
  E88C  47            		LD	B,A		;BCに1セクタ以下の端数が入る
  E88D  ED42          		SBC	HL,BC		;残りを計算
                      	
  E88F  ED5B5DEF      		LD	DE,(LEFTX)
  E893  ED52          		SBC	HL,DE		;CP HL,DE
  E895  19            		ADD	HL,DE		;
  E896  3801          		JR	C,RBXA1		;DEとHLを比較して大きい方をHLに
  E898  EB            		EX	DE,HL
  E899                	RBXA1:
  E899  E5            		PUSH	HL
  E89A  2A7EF0        		LD	HL,(_DTBUF)
  E89D  09            		ADD	HL,BC
  E89E  ED5B5FEF      		LD	DE,(DTAX)
  E8A2  C1            		POP	BC
  E8A3  C9            		RET
                      	
  E8A4                	RBXB:
  E8A4  2A5FEF        		LD	HL,(DTAX)
  E8A7  ED5BFEF0      		LD	DE,(_CLPS)
  E8AB  3A5EEF        		LD	A,(LEFTX+1)
  E8AE  47            		LD	B,A
  E8AF  3A86E8        		LD	A,(SECSZ+2)	;1024=4 / 512=2 / 256=1
  E8B2                	RBXB1:
  E8B2  1F            		RRA		;->CF
  E8B3  3804          		JR	C,RBXB2
  E8B5  CB38          		SRL	B	;B=B/2
  E8B7  18F9          		JR	RBXB1
  E8B9                	RBXB2:
  E8B9  78            		LD	A,B
  E8BA  B7            		OR	A
  E8BB  C9            		RET
                      	
  E8BC                	RBXC:
  E8BC  ED4B5DEF      		LD	BC,(LEFTX)
  E8C0  3A86E8        		LD	A,(SECSZ+2)	;1024=4 / 512=2 / 256=1
  E8C3  3D            		DEC	A
  E8C4  A0            		AND	B
  E8C5  47            		LD	B,A		;1セクタ以下の端数がない場合はZ
  E8C6  B1            		OR	C
  E8C7  C9            		RET
                      	
  E8C8                	RBXEND:
  E8C8  110000        	LEFT:	LD	DE,0
  E8CB  AF            		XOR	A
  E8CC  FD6E21        		LD	L,(IY+33)	;(FCB)ランダムレコード
  E8CF  FD6622        		LD	H,(IY+34)
  E8D2  19            		ADD	HL,DE
  E8D3  FD7521        		LD	(IY+33),L	;(FCB)ランダムレコード
  E8D6  FD7422        		LD	(IY+34),H
  E8D9  3008          		JR	NC,RBXEND1
  E8DB  FD3423        		INC	(IY+35)
  E8DE  2003          		JR	NZ,RBXEND1
  E8E0  FD3424        		INC	(IY+36)
  E8E3                	RBXEND1:
  E8E3  EB            		EX	DE,HL		;HL=Read byte
  E8E4  C9            		RET
                      	
  E8E5                	GET_RR_BCDE:			;BCDE=Random record
  E8E5  FD5E21        		LD	E,(IY+33)	;(FCB)ランダムレコード
  E8E8  FD5622        		LD	D,(IY+34)
  E8EB  FD4E23        		LD	C,(IY+35)
  E8EE  FD4624        		LD	B,(IY+36)
  E8F1  C9            		RET
                      	
  E8F2                	GET_RR_AHL:
  E8F2  FD6E21        		LD	L,(IY+33)	;(FCB)Random record
  E8F5  FD6622        		LD	H,(IY+34)	;
  E8F8  FD7E23        		LD	A,(IY+35)	;
                      	
                      	
                      	
                      	;LDDIO.ASM
                      	
                      	;	LSX-Dodgers DIO
                      	
  E8FB                	DRDX:
  E8FB  CD39E9        		CALL	DRDY
  E8FE  C8            		RET	Z
  E8FF  CD1FE9        		CALL	DRDX1		;データバッファの情報を保存
  E902  D8            		RET	C
  E903  E5            		PUSH	HL
  E904  C5            		PUSH	BC
  E905  D5            		PUSH	DE
  E906  2A7EF0        		LD	HL,(_DTBUF)
  E909  3A62E9        		LD	A,(_DBS24+1)
  E90C  4F            		LD	C,A
  E90D  CDE4EB        		CALL	DRD24
  E910  DC34E9        		CALL	C,DRDNE
  E913                	POP_DE_BC_HL_RET:
  E913  D1            		POP	DE
  E914  C1            		POP	BC
  E915  E1            		POP	HL
  E916  C9            		RET
                      	
                      	;	CDE:セクタ番号
  E917                	DRDN:
  E917  AF            		XOR	A
  E918  00            	DRDN1:	NOP			;自己書き換え NOP / SCF
  E919  30E0          		JR	NC,DRDX
  E91B                	DRDNX:
  E91B  CD39E9        		CALL	DRDY
  E91E  C8            		RET	Z
  E91F                	DRDX1:
  E91F  CD4FE9        		CALL	DWTX
                      					;データバッファの読み込んだドライブ・セクタ情報を保存
  E922  ED53A6EF      		LD	(_DBSEC),DE
  E926  79            		LD	A,C
  E927  3262E9        		LD	(_DBS24+1),A
                      	
  E92A  3A88EF        		LD	A,(_DRV)
  E92D  32A5EF        		LD	(_DBDRV),A
  E930  CDD9EF        		CALL	_CHGDRV
  E933  D0            		RET	NC
  E934                	DRDNE:
  E934  9F            		SBC	A,A		;CF=1ならばA=0FFH
  E935  32A5EF        		LD	(_DBDRV),A
  E938  C9            		RET
                      	
                      	;	CDE:セクタ番号
  E939                	DRDY:
  E939  3A62E9        		LD	A,(_DBS24+1)
  E93C  B9            		CP	C
  E93D  C0            		RET	NZ
                      	
  E93E  E5            		PUSH	HL
  E93F  3A88EF        		LD	A,(_DRV)
  E942  21A5EF        		LD	HL,_DBDRV
  E945  AE            		XOR	(HL)
  E946  2005          		JR	NZ,POP_HL_RET
                      	
  E948  2AA6EF        		LD	HL,(_DBSEC)
  E94B  ED52          		SBC	HL,DE		;CF=0
  E94D                	POP_HL_RET:
  E94D  E1            		POP	HL
  E94E  C9            		RET
                      	
  E94F                	DWTX:
  E94F  3AA4EF        		LD	A,(_WTDBF)
  E952  B7            		OR	A
  E953  C8            		RET	Z
  E954  AF            		XOR	A
  E955  32A4EF        		LD	(_WTDBF),A
                      	
  E958  E5            		PUSH	HL
  E959  C5            		PUSH	BC
  E95A  D5            		PUSH	DE
  E95B  3AA5EF        		LD	A,(_DBDRV)
  E95E  CDD9EF        		CALL	_CHGDRV
  E961  0E00          	_DBS24:	LD	C,0
  E963  ED5BA6EF      		LD	DE,(_DBSEC)
  E967  2A7EF0        		LD	HL,(_DTBUF)
  E96A  D4F4EB        		CALL	NC,DWT24
  E96D                	POP_DE_BC_HL_RET2:
  E96D  18A4          		JR	POP_DE_BC_HL_RET
                      	
  E96F                	RDFATX:
  E96F  E5            		PUSH	HL
  E970  3A88EF        		LD	A,(_DRV)
  E973  21AAEF        		LD	HL,_FATDRV
  E976  AE            		XOR	(HL)
  E977  28D4          		JR	Z,POP_HL_RET
                      	
  E979  C5            		PUSH	BC
  E97A  D5            		PUSH	DE
  E97B  DDE5          		PUSH	IX
  E97D  CD99E9        		CALL	WTFATX
  E980  3813          		JR	C,POP_IX_DE_BC_HL_RET
                      	
  E982  AF            		XOR	A
  E983  32ABEF        		LD	(_FATIX),A
  E986  3A88EF        		LD	A,(_DRV)
  E989  32AAEF        		LD	(_FATDRV),A
  E98C  CDE5EF        		CALL	_RDFAT
  E98F                	RDFATE2:
  E98F  3004          		JR	NC,POP_IX_DE_BC_HL_RET
  E991  9F            		SBC	A,A		;A=0xFF
  E992  32AAEF        		LD	(_FATDRV),A
  E995                	POP_IX_DE_BC_HL_RET:
  E995  DDE1          		POP	IX
  E997  18D4          		JR	POP_DE_BC_HL_RET2
                      	
  E999                	WTFATX:
  E999  3AA2EF        		LD	A,(_WTFATF)
  E99C  B7            		OR	A
  E99D  C8            		RET	Z
  E99E  E5            		PUSH	HL
  E99F  3AAAEF        		LD	A,(_FATDRV)
  E9A2  21A5EF        		LD	HL,_DBDRV
  E9A5  AE            		XOR	(HL)
  E9A6  CC4FE9        		CALL	Z,DWTX
  E9A9  38A2          		JR	C,POP_HL_RET
  E9AB  C5            		PUSH	BC
  E9AC  D5            		PUSH	DE
  E9AD  DDE5          		PUSH	IX
  E9AF  CD01EC        		CALL	WTFAT
  E9B2  AF            		XOR	A
  E9B3  32A2EF        		LD	(_WTFATF),A
  E9B6  18DD          		JR	POP_IX_DE_BC_HL_RET
                      	
  E9B8                	NCL1:
  E9B8  7A            		LD	A,D
  E9B9  B3            		OR	E
  E9BA  37            		SCF
  E9BB  C8            		RET	Z
                      	
  E9BC  7A            		LD	A,D
  E9BD  CB3F          		SRL	A	;/2
  E9BF  CB3F          		SRL	A	;/2
                      	
  E9C1  E5            		PUSH	HL
  E9C2  32E1E9        		LD	(NCLPAT+2),A	;_FATIX
  E9C5  2AAAEF        		LD	HL,(_FATDRV)	;L=_FATDRV H=_FATIX
  E9C8  BC            		CP	H
  E9C9  3A88EF        		LD	A,(_DRV)	;この間でフラグは変化しない
  E9CC  32E0E9        		LD	(NCLPAT+1),A	;_FATDRV
  E9CF  2005          		JR	NZ,NCL2		;FATIXが違う
  E9D1  BD            		CP	L
  E9D2  2002          		JR	NZ,NCL2		;ドライブが違う
  E9D4  E1            		POP	HL
  E9D5  C9            		RET
  E9D6                	NCL2:
  E9D6  C5            		PUSH	BC
  E9D7  D5            		PUSH	DE
  E9D8  DDE5          		PUSH	IX
  E9DA  CD99E9        		CALL	WTFATX
  E9DD  38B6          		JR	C,POP_IX_DE_BC_HL_RET
  E9DF  010000        	NCLPAT:	LD	BC,0		;自己書き換え C=_FATDRV B=_FATIX
  E9E2  ED43AAEF      		LD	(_FATDRV),BC
  E9E6  CDD6EB        		CALL	RDFATS
  E9E9  18A4          		JR	RDFATE2
                      	
  E9EB                	NCL3:
  E9EB  CB9A          		RES	3,D
  E9ED  CB92          		RES	2,D
  E9EF  6B            		LD	L,E
  E9F0  62            		LD	H,D
  E9F1  CB3C          		SRL	H	;
  E9F3  CB1D          		RR	L	;HLA=HLA/2
  E9F5  1F            		RRA		;
  E9F6  F5            		PUSH	AF
  E9F7  3AABEF        		LD	A,(_FATIX)
  E9FA  0F            		RRCA
  E9FB  300B          		JR	NC,NCL3X1
  E9FD  3A86E8        		LD	A,(SECSZ+2)
  EA00  FE04          		CP	4		;1セクタ1024バイトの場合は奇数インデックスの場合に先頭を512バイトずらす
  EA02  2004          		JR	NZ,NCL3X1
  EA04  010002        		LD	BC,512
  EA07  09            		ADD	HL,BC
  EA08                	NCL3X1:
  EA08  F1            		POP	AF
  EA09  ED4B7CF0      		LD	BC,(_FATBF)
  EA0D  19            		ADD	HL,DE
  EA0E  09            		ADD	HL,BC
  EA0F  17            		RLA
  EA10  C9            		RET
                      	
  EA11                	GNCL:
  EA11  CDB8E9        		CALL	NCL1		;GET NEXT CLUSTER
  EA14  D8            		RET	C
  EA15  C5            		PUSH	BC
  EA16  E5            		PUSH	HL
  EA17  CDEBE9        		CALL	NCL3
  EA1A  3809          		JR	C,GNC1
  EA1C  5E            		LD	E,(HL)
  EA1D  23            		INC	HL
  EA1E  7E            		LD	A,(HL)
  EA1F  E60F          		AND	00FH
  EA21  57            		LD	D,A
  EA22  E1            		POP	HL
  EA23  C1            		POP	BC
  EA24  C9            		RET
  EA25                	GNC1:
  EA25  7E            		LD	A,(HL)
  EA26  23            		INC	HL
  EA27  56            		LD	D,(HL)
  EA28  0604          		LD	B,4
  EA2A                	GNC1L:
  EA2A  CB3A          		SRL	D	;DA=DA/2
  EA2C  1F            		RRA		;
  EA2D  10FB          		DJNZ	GNC1L
                      	
  EA2F  5F            		LD	E,A
  EA30  E1            		POP	HL
  EA31  C1            		POP	BC
  EA32  A7            		AND	A
  EA33  C9            		RET
                      	
  EA34                	SNCL:
  EA34  CDB8E9        		CALL	NCL1
  EA37  D8            		RET	C
                      	;				SET NEXT CLUSTER
  EA38  E5            		PUSH	HL
  EA39  C5            		PUSH	BC
  EA3A  D5            		PUSH	DE
  EA3B  E5            		PUSH	HL
  EA3C  CDEBE9        		CALL	NCL3
  EA3F  D1            		POP	DE
                      	;CF=ODD
  EA40  7E            		LD	A,(HL)
  EA41  73            		LD	(HL),E
  EA42  3806          		JR	C,SNC1
                      	;EVEN
                      	;M[0] = E
                      	;M[1] = (M[1] & 0xF0) | (D & 0x0F)
  EA44  23            		INC	HL
  EA45  ED67          		RRD		;M={A[3:0],E[3:0]}
  EA47  7A            		LD	A,D
  EA48  1804          		JR	SNC11
  EA4A                	SNC1:
                      	;ODD
                      	;HL[0] = (HL[0]&0x0F) | (E<<4)
                      	;HL[1] = DE>>4
  EA4A  ED6F          		RLD		;M={D[3:0],E[7:4]}
  EA4C  23            		INC	HL
  EA4D  72            		LD	(HL),D	;M={D[3:0],E[7:4]}
  EA4E                	SNC11:
  EA4E  ED6F          		RLD		;M={M[7:4],D[3:0]}
  EA50  B7            		OR	A
  EA51  D1            		POP	DE
  EA52  C1            		POP	BC
  EA53  E1            		POP	HL
  EA54                	FAT_CHANGED:
  EA54  3E01          		LD	A,1
  EA56  32A2EF        		LD	(_WTFATF),A
  EA59  C9            		RET
                      	
  EA5A                	N16CL3:
  EA5A  C5            		PUSH	BC
  EA5B  6B            		LD	L,E
  EA5C  7A            		LD	A,D
  EA5D  E603          		AND	3
  EA5F  67            		LD	H,A
  EA60  29            		ADD	HL,HL
  EA61  ED4B7CF0      		LD	BC,(_FATBF)
  EA65  09            		ADD	HL,BC
  EA66  C1            		POP	BC
  EA67  C9            		RET
                      	
  EA68                	GNCL16:
  EA68  CDB8E9        		CALL	NCL1		;GET NEXT CLUSTER for FAT16
  EA6B  D8            		RET	C
  EA6C  E5            		PUSH	HL
  EA6D  CD5AEA        		CALL	N16CL3
  EA70  5E            		LD	E,(HL)
  EA71  23            		INC	HL
  EA72  56            		LD	D,(HL)
  EA73  E1            		POP	HL
  EA74  C9            		RET
                      	
  EA75                	SNCL16:
  EA75  CDB8E9        		CALL	NCL1		;GET NEXT CLUSTER for FAT16
  EA78  D8            		RET	C
  EA79  D5            		PUSH	DE
  EA7A  E5            		PUSH	HL
  EA7B  CD5AEA        		CALL	N16CL3
  EA7E  D1            		POP	DE		;HL
  EA7F  73            		LD	(HL),E
  EA80  23            		INC	HL
  EA81  72            		LD	(HL),D
  EA82  6B            		LD	L,E
  EA83  62            		LD	H,D
  EA84  D1            		POP	DE
  EA85  18CD          		JR	FAT_CHANGED
                      	
                      	;------------------------
                      	;次のセクタを探す際に_SECPSの値をチェック
                      	;in
                      	;	DE : セクタ番号
                      	;out
                      	;	DE : 次のセクタ番号
                      	;note
                      	;
                      	;------------------------
  EA87                	NEXTX:
  EA87  3E00          	_SECPS:	LD	A,0	;自己書き換え
  EA89  3C            		INC	A
  EA8A  E600          	NCPAT:	AND	0	;自己書き換え　1クラスタの論理セクタ数-1
  EA8C  3288EA        		LD	(_SECPS+1),A
  EA8F  C9            		RET
                      	
  EA90                	GNCLX:
  EA90  CD87EA        		CALL	NEXTX
  EA93  C0            		RET	NZ	;1クラスタのセクタ未満の場合は戻る
  EA94  C3F7EF        		JP	_GNCL	;次のクラスタを探す
                      	
  EA97                	RDFAT:
  EA97  3E80          		LD	A,080H
  EA99  3270EF        		LD	(CHECK),A
  EA9C                	RDFAT1:
  EA9C  3A88EF        		LD	A,(_DRV)
  EA9F  CD34ED        		CALL	CHGDRVR
  EAA2  D8            		RET	C
  EAA3  DD7E12        		LD	A,(IX+012H)	;DPB_12_DEVICE
  EAA6  CB6F          		BIT	5,A
  EAA8  2843          		JR	Z,RDFAT0X
  EAAA  2170EF        		LD	HL,CHECK
  EAAD  A6            		AND	(HL)
  EAAE  77            		LD	(HL),A
  EAAF  110000        		LD	DE,0		;BPB
  EAB2  2A7CF0        		LD	HL,(_FATBF)
  EAB5  CDE2EB        		CALL	DRD16
  EAB8  3024          		JR	NC,GETBPB
  EABA  2170EF        		LD	HL,CHECK
  EABD  CB06          		RLC	(HL)
  EABF  3F            		CCF
  EAC0  D8            		RET	C
  EAC1  DDCB0A1E      		RR	(IX+00AH)	;DPB_0A_FDMODE
  EAC5  3F            		CCF			;フロッピーディスクのMFMモードを切り替える
  EAC6  DDCB0A16      		RL	(IX+00AH)	;DPB_0A_FDMODE
  EACA  3EFF          		LD	A,0FFH
  EACC  3289EF        		LD	(_DSK),A
  EACF  3A84EF        		LD	A,(_DRIVE)
  EAD2  E603          		AND	3
  EAD4  F680          		OR	080H
  EAD6  6F            		LD	L,A
  EAD7  26EF          		LD	H,_CYL0/256
  EAD9  3EFF          		LD	A,0FFH
  EADB  77            		LD	(HL),A
  EADC  18BE          		JR	RDFAT1
  EADE                	GETBPB:
  EADE  FDE5          		PUSH	IY
  EAE0  FD2A7CF0      		LD	IY,(_FATBF)	;BPB
  EAE4  CDF1EF        		CALL	_BPB2DPB
  EAE7  FDE1          		POP	IY
  EAE9  D8            		RET	C
  EAEA  CD8AEC        		CALL	CHGDRV2
  EAED                	RDFAT0X:
  EAED  CDD6EB        		CALL	RDFATS
  EAF0  D8            		RET	C
  EAF1  DDBE06        		CP	(IX+6)		;DPB_06_FATID
  EAF4  C8            		RET	Z
  EAF5  DDCB126E      		BIT	5,(IX+012H)	;DPB_12_DEVICE
  EAF9  C0            		RET	NZ		;BPBを取得している場合はFATIDをチェックしない
  EAFA  37            		SCF
  EAFB  C9            		RET
                      	
  EAFC                	BPB2DPB:
  EAFC  FD7E00        		LD	A,(IY+0)	;BS_JmpBoot
  EAFF  FEEB          		CP	0EBH		;Short jump
  EB01  2808          		JR	Z,BPBOK
  EB03  FEE9          		CP	0E9H		;Near jump
  EB05  2804          		JR	Z,BPBOK
  EB07  FE60          		CP	060H		;X68k
  EB09  37            		SCF
  EB0A  C0            		RET	NZ
  EB0B                	BPBOK:
  EB0B  FD7E0D        		LD	A,(IY+13)	;BPB_SecPerClus
  EB0E  DD7707        		LD	(IX+7),A	;DPB_07_SECPCL
                      	
  EB11  FD7E16        		LD	A,(IY+22)	;BPB_FATSz16
  EB14  DD7700        		LD	(IX+0),A	;DPB_00_FATLN
  EB17  FD7E17        		LD	A,(IY+23)	;BPB_FATSz16
  EB1A  DD7701        		LD	(IX+1),A	;DPB_00_FATLN
                      	
  EB1D  FD6E0E        		LD	L,(IY+14)	;BPB_RsvdSecCnt
  EB20  FD660F        		LD	H,(IY+15)
  EB23  DD750E        		LD	(IX+00EH),L	;DPB_0E_FATPS
  EB26  FD5E16        		LD	E,(IY+22)	;BPB_FATSz16
  EB29  FD5617        		LD	D,(IY+23)
  EB2C  FD4610        		LD	B,(IY+16)	;BPB_NumFATs
  EB2F                	BPBDP1:				;BPB_FATSz16 * BPB_NumFATs
  EB2F  19            		ADD	HL,DE
  EB30  10FD          		DJNZ	BPBDP1
  EB32                	BPBDP2:
  EB32  DD7510        		LD	(IX+010H),L	;DPB_10_DIRPS
  EB35  DD7411        		LD	(IX+011H),H
  EB38  E5            		PUSH	HL
                      	
  EB39  FD6E11        		LD	L,(IY+17)	;BPB_RootEntCnt
  EB3C  FD6612        		LD	H,(IY+18)
  EB3F  FD7E0C        		LD	A,(IY+12)	;BPB_BytsPerSec
  EB42  0606          		LD	B,6
  EB44                	BPBBPS1:
  EB44  05            		DEC	B
  EB45  0F            		RRCA
  EB46  30FC          		JR	NC,BPBBPS1
  EB48                	BPBDE1:
  EB48  29            		ADD	HL,HL
  EB49  10FD          		DJNZ	BPBDE1
                      	
  EB4B  7C            		LD	A,H
  EB4C  DD770B        		LD	(IX+00BH),A	;DPB_0B_DIRSCNT
                      	
  EB4F  D1            		POP	DE		;DPB_10_DIRPS
  EB50  6C            		LD	L,H
  EB51  2600          		LD	H,0
  EB53  19            		ADD	HL,DE		;MAXDIR
  EB54  E5            		PUSH	HL
  EB55  FD4E0D        		LD	C,(IY+13)	;BPB_SecPerClus
  EB58  CB21          		SLA	C		;*2
  EB5A  AF            		XOR	A
  EB5B  47            		LD	B,A
  EB5C  ED42          		SBC	HL,BC
  EB5E  DD7514        		LD	(IX+014H),L	;DPB_14_ADDCL16
  EB61  DD7415        		LD	(IX+015H),H
                      	
  EB64  D1            		POP	DE		;DE=DPB_0B_MAXDIR
  EB65  FD6E13        		LD	L,(IY+19)	;BPB_TotSec16
  EB68  FD6614        		LD	H,(IY+20)
  EB6B  7C            		LD	A,H
  EB6C  B5            		OR	L
  EB6D  3E00          		LD	A,0		;LDでフラグは変化しない。BPB_TotSec16を AHL で扱う為、Aに0を入れる
  EB6F  2010          		JR	NZ,BPB16BIT	;BPB_TotSec16が「0」の場合はBPB_TotSec32で計算
  EB71  FD7E23        		LD	A,(IY+35)	;念のために総セクタ数が24ビットを超える場合はエラーを返す
  EB74  B7            		OR	A
  EB75  37            		SCF
  EB76  C0            		RET	NZ
  EB77  FD6E20        		LD	L,(IY+32)	;BPB_TotSec32 は AHL の24ビットで扱う
  EB7A  FD6621        		LD	H,(IY+33)	;1クラスタの論理セクタ数(DPB_07_SECPCL)が最大128なので
  EB7D  FD7E22        		LD	A,(IY+34)	;24ビットを超えると必ずクラスタ数が16ビットを超えてしまうのでこれで十分
  EB80  A7            		AND	A		;CF=0
  EB81                	BPB16BIT:
  EB81  ED52          		SBC	HL,DE
  EB83  DE00          		SBC	A,0
  EB85  FD460D        		LD	B,(IY+13)	;BPB_SecPerClus
  EB88                	BPBTC1:				;総クラスタ数(AHL) = 総セクタ数(AHL) / セクタサイズ(B)
  EB88  CB18          		RR	B		;->CY
  EB8A  3808          		JR	C,BPBTC2
  EB8C  CB3F          		SRL	A
  EB8E  CB1C          		RR	H		;AHL=AHL/2
  EB90  CB1D          		RR	L
  EB92  18F4          		JR	BPBTC1
  EB94                	BPBTC2:
  EB94  B7            		OR	A
  EB95  37            		SCF
  EB96  C0            		RET	NZ		;念のため(クラスタ数が16ビットを超える場合)
  EB97  23            		INC	HL
  EB98  23            		INC	HL
  EB99  DD7508        		LD	(IX+8),L	;DPB_08_MAXCL
  EB9C  DD7409        		LD	(IX+9),H
                      	
  EB9F  FD7E15        		LD	A,(IY+21)	;BPB_Media
  EBA2  DD7706        		LD	(IX+6),A	;DPB_06_FATID
                      	
  EBA5  DDCB0A7E      		BIT	7,(IX+00AH)	;DPB_0A_FDMODE
  EBA9  2817          		JR	Z,NOT_FD
  EBAB  2E28          		LD	L,40		;MAXCYLの設定（要検討）ここから下はフロッピーのみ書き換える
  EBAD  FEFD          		CP	0FDH	;2D
  EBAF  2808          		JR	Z,SETCYL
  EBB1  2E4D          		LD	L,77
  EBB3  FEFE          		CP	0FEH	;2HD
  EBB5  2802          		JR	Z,SETCYL
  EBB7  2E50          		LD	L,80	;その他2DD等
  EBB9                	SETCYL:
  EBB9  DD750C        		LD	(IX+00CH),L	;DPB_0C_MAXCYL
  EBBC  FD7E18        		LD	A,(IY+24)	;BPB_SecPerTr
  EBBF  DD770D        		LD	(IX+00DH),A	;DPB_0D_MAXSEC	;ここまでフロッピー専用
  EBC2                	NOT_FD:
  EBC2  FD7E10        		LD	A,(IY+16)	;BPB_NumFATs
  EBC5  FE02          		CP	2		;>2:NC
  EBC7  FD7E0C        		LD	A,(IY+12)	;BPB_BytsPerSec
  EBCA  3802          		JR	C,BPBFAT1
  EBCC  F680          		OR	080H
  EBCE                	BPBFAT1:
  EBCE  DD770F        		LD	(IX+00FH),A	;DPB_0F_BPS
                      	
  EBD1  E67F          		AND	07FH		;セクタバッファが1KBしかないので1KBを超える場合はCF=1でエラーにする
  EBD3  C6FB          		ADD	A,0-5		;0-4:CF=0 5以上:CF=1
  EBD5  C9            		RET
                      	
  EBD6                	RDFATS:
  EBD6  CD1CEC        		CALL	FATSETUP
  EBD9  D8            		RET	C
  EBDA  CDE6EB        		CALL	DRD24B
  EBDD  2A7CF0        		LD	HL,(_FATBF)
  EBE0  7E            		LD	A,(HL)
  EBE1  C9            		RET
                      	
  EBE2                	DRD16:
  EBE2  0E00          		LD	C,0
  EBE4                	DRD24:
  EBE4  0601          		LD	B,1
  EBE6                	DRD24B:
  EBE6  DDE5          		PUSH	IX
  EBE8  DD2AA8EF      		LD	IX,(_DPB)
  EBEC  CD54EE        	DRDPAT:	CALL	FDRD		;自己書き換え（ディスク読み込み）
  EBEF                	POP_IX_RET:
  EBEF  DDE1          		POP	IX
  EBF1  C9            		RET
                      	
  EBF2                	DWT16:
  EBF2  0E00          		LD	C,0
  EBF4                	DWT24:
  EBF4  0601          		LD	B,1
  EBF6                	DWT24B:
  EBF6  DDE5          		PUSH	IX
  EBF8  DD2AA8EF      		LD	IX,(_DPB)
  EBFC  CD46EE        	DWTPAT:	CALL	FDWT		;自己書き換え（ディスク書き込み）
  EBFF  18EE          		JR	POP_IX_RET
                      	
  EC01                	WTFAT:
  EC01  CD1CEC        		CALL	FATSETUP
  EC04  D4F6EB        		CALL	NC,DWT24B
  EC07  D8            		RET	C
  EC08  DDCB0F7E      		BIT	7,(IX+00FH)	;DPB_0F_BPS
  EC0C  C8            		RET	Z		;予備FATが無い
  EC0D  CD23EC        		CALL	FATS2
  EC10  E5            		PUSH	HL		;予備FAT
  EC11  DD6E00        		LD	L,(IX+0)	;DPB_00_FATLN
  EC14  DD6601        		LD	H,(IX+1)
  EC17  19            		ADD	HL,DE
  EC18  EB            		EX	DE,HL
  EC19  E1            		POP	HL
  EC1A  18DA          		JR	DWT24B
                      	;------------------------
                      	;FATのセットアップ
                      	;out
                      	;	B  : FATセクタ数
                      	;	DE : FAT先頭セクタ番号
                      	;	HL : FATバッファポインタ
                      	;	CF : 1=ドライブ切り替えエラー
                      	;note
                      	;	FATサイズがFATバッファを超える場合は
                      	;	対象クラスタ領域==(_FATIX)によって
                      	;	FAT12:対象クラスタ1024毎に1.5KBを切り替える
                      	;	FAT16:対象クラスタ1024毎に2KBを切り替える
                      	;------------------------
  EC1C                	FATSETUP:
  EC1C  3AAAEF        		LD	A,(_FATDRV)
  EC1F  CD34ED        		CALL	CHGDRVR		;ドライブ切り替え
  EC22  D8            		RET	C
  EC23                	FATS2:
  EC23  DD7E0F        		LD	A,(IX+00FH)	;DPB_0F_BPS 512=2 1024=4
  EC26  0F            		RRCA
  EC27  CD50EC        	FATSX:	CALL	FATSETUP12	;自己書き換え
  EC2A  210000        		LD	HL,0
  EC2D  4A            		LD	C,D
  EC2E  54            		LD	D,H
  EC2F  3AABEF        		LD	A,(_FATIX)	;範囲FAT12:0-3/FAT16:0-63(0-03FH):65535*2/2048
  EC32  47            		LD	B,A
  EC33  04            		INC	B
  EC34  DD7E00        		LD	A,(IX+0)	;DPB_00_FATLN
  EC37                	FAT_SKP:
  EC37  05            		DEC	B
  EC38  2805          		JR	Z,FAT1
  EC3A  19            		ADD	HL,DE
  EC3B  93            		SUB	E
  EC3C  30F9          		JR	NC,FAT_SKP
  EC3E  C9            		RET
  EC3F                	FAT1:
  EC3F  EB            		EX	DE,HL
  EC40  B9            		CP	C		;C=FATバッファに読み込めるセクタ数
  EC41  3801          		JR	C,FAT2
  EC43  79            		LD	A,C
  EC44                	FAT2:
  EC44  47            		LD	B,A
                      	
  EC45  2AFAF0        		LD	HL,(_FATPS)	;fat sector pos
  EC48  19            		ADD	HL,DE
  EC49  EB            		EX	DE,HL
  EC4A  2A7CF0        		LD	HL,(_FATBF)
  EC4D  AF            		XOR	A		;CF=0
  EC4E  4F            		LD	C,A
  EC4F  C9            		RET
                      	;
                      	;	FATバッファに読み込める最大のセクタ数と_FATIXで進めるセクタ数
                      	;	FATSETUP* (FAT12/FAT16)
                      	;out
                      	;	D : FATバッファに読み込める最大のセクタ数
                      	;	E : _FATIXで進めるセクタ数
  EC50                	FATSETUP12:
  EC50  110606        		LD	DE,0606H	;256
  EC53  D8            		RET	C
  EC54  110303        		LD	DE,0303H	;512
  EC57  0F            		RRCA
  EC58  D8            		RET	C
  EC59  110102        		LD	DE,0201H	;1024
  EC5C  C9            		RET
                      	
  EC5D                	FATSETUP16:
  EC5D  110808        		LD	DE,0808H	;256
  EC60  D8            		RET	C
  EC61  110404        		LD	DE,0404H
  EC64  0F            		RRCA			;512
  EC65  D8            		RET	C
  EC66  110202        		LD	DE,0202H	;1024
  EC69  C9            		RET
                      	
  EC6A                	CHGDRV:
  EC6A  E5            		PUSH	HL
  EC6B  2189EF        		LD	HL,_DSK
  EC6E  BE            		CP	(HL)
  EC6F  280A          		JR	Z,CHGDRVE
  EC71                	CHGDRV1:
  EC71  DDE5          		PUSH	IX
  EC73  CD7DEC        		CALL	CHGDRV0
  EC76  3A89EF        		LD	A,(_DSK)
  EC79  DDE1          		POP	IX
  EC7B                	CHGDRVE:
  EC7B  E1            		POP	HL
  EC7C  C9            		RET
                      	
  EC7D                	CHGDRV0:
  EC7D  6F            		LD	L,A
  EC7E  CDDCEF        		CALL	_GETDPB
  EC81  D8            		RET	C
  EC82  DD22A8EF      		LD	(_DPB),IX
  EC86  7D            		LD	A,L
  EC87  3289EF        		LD	(_DSK),A
  EC8A                	CHGDRV2:
  EC8A  C5            		PUSH	BC
  EC8B  D5            		PUSH	DE
  EC8C  ED73E9EC      		LD	(SPPAT2+1),SP
  EC90  F3            		DI
  EC91  DDF9          		LD	SP,IX
                      	
  EC93  E1            		POP	HL		;DPB_00_FATLN
  EC94  E1            		POP	HL		;DPB_02_DRD
  EC95  22EDEB        		LD	(DRDPAT+1),HL
                      	
  EC98  E1            		POP	HL
  EC99  22FDEB        		LD	(DWTPAT+1),HL	;DPB_04_DWT
                      	
  EC9C  E1            		POP	HL		;L=DPB_06_FATID H=DPB_07_SECPCL
  EC9D  7C            		LD	A,H
  EC9E  3293E3        		LD	(SPCPAT+1),A	;1クラスタの論理セクタ数
  ECA1  3D            		DEC	A
  ECA2  328BEA        		LD	(NCPAT+1),A
                      	
  ECA5  E1            		POP	HL		;DPB_08_MAXCL
  ECA6  7D            		LD	A,L
  ECA7  32B2E3        		LD	(CLPAT2+1),A
  ECAA  7C            		LD	A,H
  ECAB  32AEE3        		LD	(CLPAT1+1),A
  ECAE  2B            		DEC	HL
  ECAF  22B6E5        		LD	(MAXCLP+1),HL
                      	
  ECB2  E1            		POP	HL		;L=DPB_0A_FDMODE H=DPB_0B_DIRSCNT
  ECB3  7D            		LD	A,L
  ECB4  F6FE          		OR	0FEH
  ECB6  32EFED        		LD	(FDMODE+1),A
  ECB9  07            		RLCA
  ECBA  E603          		AND	3
  ECBC  32D7ED        		LD	(FSPAT+1),A
  ECBF  4C            		LD	C,H
                      	
  ECC0  E1            		POP	HL		;L=DPB_0C_MAXCYL H=DPB_0D_MAXSEC
  ECC1  7D            		LD	A,L
  ECC2  32A1EF        		LD	(_MAXCYL),A
  ECC5  7C            		LD	A,H
  ECC6  3280ED        		LD	(MAXSEC+1),A
                      	
  ECC9  E1            		POP	HL		;L=DPB_0E_FATPS H=DPB_0F_BPS
  ECCA  7C            		LD	A,H		;DPB_0F_BPS
  ECCB  E607          		AND	7
  ECCD  3286E8        		LD	(SECSZ+2),A	;1セクタのサイズの上位1バイト 0x400 - 0x200 - 0x100
                      					;1セクタ512 2D/2DD/2HC/2HQ(2HD1.44MB)/EMM/BRAM等
                      					;1セクタ1024 2HD/2HDE98
                      					;1セクタ256 GRAM等
                      					;1024	512	256
                      					;4	2	1
  ECD0  87            		ADD	A,A		;8	4	2
  ECD1  87            		ADD	A,A		;010H	8	4
  ECD2  87            		ADD	A,A		;020H	010H	8
  ECD3  320FE2        		LD	(SDECPAT+1),A	;1セクタ辺りのディレクトリエントリ数
                      	
  ECD6  2600          		LD	H,0
  ECD8  22FAF0        		LD	(_FATPS),HL
                      	
  ECDB  E1            		POP	HL		;L=DPB_10_DIRPS
  ECDC  22FCF0        		LD	(_DIRPS),HL
                      	
  ECDF  E1            		POP	HL		;L=DPB_12_DEVICE H=DPB_13_UNITNO
  ECE0  7C            		LD	A,H
  ECE1  3284EF        		LD	(_DRIVE),A
                      	
  ECE4  E1            		POP	HL		;DPB_14_ADDCL16
  ECE5  22A3E3        		LD	(CLSPAT+1),HL
                      	
  ECE8  310000        	SPPAT2:	LD	SP,0		;元のSPを復元
  ECEB  FB            		EI
  ECEC  2AFCF0        		LD	HL,(_DIRPS)
  ECEF  0600          		LD	B,0
  ECF1  09            		ADD	HL,BC
  ECF2  222AE2        		LD	(MD_PAT+1),HL
                      	
  ECF5  2AB6E5        		LD	HL,(MAXCLP+1);
  ECF8  11F60F        		LD	DE,4086
  ECFB  B7            		OR	A
  ECFC  ED52          		SBC	HL,DE
  ECFE  300F          		JR	NC,SETFAT16
  ED00  2111EA        		LD	HL,GNCL		;FAT12
  ED03  1134EA        		LD	DE,SNCL
  ED06  0150EC        		LD	BC,FATSETUP12
  ED09  DDCB0FAE      		RES	5,(IX+00FH)	;DPB_0F_BPS(FAT12)
  ED0D  180D          		JR	EXTRA1
  ED0F                	SETFAT16:
  ED0F  2168EA        		LD	HL,GNCL16	;FAT16
  ED12  1175EA        		LD	DE,SNCL16
  ED15  015DEC        		LD	BC,FATSETUP16
  ED18  DDCB0FEE      		SET	5,(IX+00FH)	;DPB_0F_BPS(FAT16)
  ED1C                	EXTRA1:
  ED1C  22F8EF        		LD	(GNCPAT+1),HL
  ED1F  ED53FBEF      		LD	(SNCPAT+1),DE
  ED23  ED4328EC      		LD	(FATSX+1),BC
  ED27  D1            		POP	DE
  ED28  C1            		POP	BC
  ED29  AF            		XOR	A
  ED2A  C9            		RET
                      	
  ED2B                	GETDPBD:
  ED2B  DDE3          		EX	(SP),IX
  ED2D  DDE5          		PUSH	IX
  ED2F  3A88EF        		LD	A,(_DRV)
  ED32  1807          		JR	GETDPB1
                      	
  ED34                	CHGDRVR:
  ED34  CDD9EF        		CALL	_CHGDRV
  ED37  D8            		RET	C
  ED38  3A89EF        		LD	A,(_DSK)
  ED3B                	GETDPB1:
  ED3B  C3DCEF        		JP	_GETDPB
                      	
  ED3E                	GETDPB:
  ED3E  FE08          		CP	8
  ED40  3F            		CCF
  ED41  D8            		RET	C
  ED42  0F            		RRCA
  ED43  0F            		RRCA
  ED44  0F            		RRCA
  ED45  DD            		DB	0DDH		;Z80未定義命令
  ED46  6F            		LD	L,A		;LD	IXL,A
  ED47  DD            		DB	0DDH		;Z80未定義命令
  ED48  26F1          		LD	H,DEVICE/256	;LD	IXH,DEVICE/256
  ED4A  DD7E00        		LD	A,(IX+0)	;DPB_00_FATLN
  ED4D  B7            		OR	A		;CF=0の為
  ED4E  DDCB0F6E      		BIT	5,(IX+00FH)	;DPB_0F_BPS
  ED52  C0            		RET	NZ		;FAT16
  ED53  DDCB126E      		BIT	5,(IX+012H)	;DPB_12_DEVICE
  ED57  C0            		RET	NZ		;BPB
  ED58  FE01          		CP	001H		;A=0 THEN CF=1
  ED5A  C9            		RET
                      	
  ED5B                	_SYS5F:
  ED5B  AF            		XOR	A
  ED5C                	FFLUSH:
  ED5C  F5            		PUSH	AF
  ED5D  3EFF          		LD	A,0FFH
  ED5F  3239EF        		LD	(SFILE),A
  ED62  CD4FE9        		CALL	DWTX
  ED65  3EFF          		LD	A,0FFH
  ED67  32A5EF        		LD	(_DBDRV),A
  ED6A  CD99E9        		CALL	WTFATX
  ED6D  AF            		XOR	A
  ED6E  32ABEF        		LD	(_FATIX),A
  ED71  2F            		CPL
  ED72  32AAEF        		LD	(_FATDRV),A
  ED75  F1            		POP	AF
  ED76  C9            		RET
                      	
                      	
                      	;X1DISK.ASM
                      	
                      	;	LSX-Dodgers DISK
                      	;	X1/turbo/Z
                      	
  ED77                	SEEK:
  ED77  7A            		LD	A,D
  ED78  B3            		OR	E
  ED79  3E01          		LD	A,001H
  ED7B  2811          		JR	Z,DIV3
                      	
  ED7D  0610          		LD	B,16
  ED7F  0E00          	MAXSEC:	LD	C,000H
  ED81  AF            		XOR	A
  ED82  EB            		EX	DE,HL
  ED83                	DIV1:
  ED83  29            		ADD	HL,HL
  ED84  8F            		ADC	A,A
  ED85  B9            		CP	C
  ED86  3802          		JR	C,DIV2
  ED88  91            		SUB	C
  ED89  2C            		INC	L
  ED8A                	DIV2:
  ED8A  10F7          		DJNZ	DIV1
                      	
  ED8C  3C            		INC	A	;最小のセクタは１固定
  ED8D  55            		LD	D,L	;Quotient(TRACK)
  ED8E                	DIV3:
  ED8E  5F            		LD	E,A	;Remainder
  ED8F  CB3A          		SRL	D	;CYLINDER
  ED91  9F            		SBC	A,A
  ED92  E610          		AND	010H	;SIDE
  ED94  4F            		LD	C,A
                      	
  ED95  3A84EF        		LD	A,(_DRIVE)
  ED98  FE04          		CP	4
  ED9A  3F            		CCF
  ED9B  D8            		RET	C
  ED9C  B1            		OR	C
  ED9D  321BEE        		LD	(MOPAT+1),A	;Motor off data
  EDA0  01FC0F        		LD	BC,00FFCH
  EDA3  F680          		OR	080H		;Motor on
  EDA5  ED79          		OUT	(C),A
                      	
  EDA7  CDEDED        		CALL	SETMODE
  EDAA  D8            		RET	C
                      	
  EDAB  CD30EE        		CALL	DRIVE
  EDAE  FEFF          		CP	0FFH		;A=0FFH Unknown CYLINDER
  EDB0  CC0FEE        		CALL	Z,FDCRES	;Restore
  EDB3  0EF9          		LD	C,0F9H
  EDB5  ED79          		OUT	(C),A		;Currrent CYLINDER
  EDB7  92            		SUB	D
  EDB8  C8            		RET	Z		;No seek
                      	
  EDB9  3A86EF        		LD	A,(_ISEEK)
  EDBC  4F            		LD	C,A
                      	
  EDBD  3AA1EF        		LD	A,(_MAXCYL)	;Max CYLINDER
  EDC0  3D            		DEC	A
  EDC1  BA            		CP	D		;Over CYLINDER
  EDC2  D8            		RET	C
                      	
  EDC3  B9            		CP	C		;for Built-in 2DD
  EDC4  3803          		JR	C,SETM2
  EDC6  78            		LD	A,B		;B=00FH
  EDC7  DBFE          		IN	A,(0FEH)		;2HD mode for 2DD seek
  EDC9                	SETM2:
  EDC9  78            		LD	A,B
  EDCA  DBFD          		IN	A,(0FDH)		;MFM mode
                      	
  EDCC  0EFB          		LD	C,0FBH
  EDCE  ED51          		OUT	(C),D
  EDD0  72            		LD	(HL),D
  EDD1  0E18          		LD	C,018H		;Seek
  EDD3                	FDCCMD2:
  EDD3  3A85EF        		LD	A,(_SEEKSP)
  EDD6  E6FF          	FSPAT:	AND	0FFH
  EDD8  B1            		OR	C
                      	
  EDD9                	FDCCMD:
  EDD9  CD26EE        		CALL	COMSET
                      	
  EDDC  3E80          	FDCPAT:	LD	A,080H		;FDC COMMAND(Self-modifying code)
  EDDE  E620          		AND	020H		;Write data Write track
  EDE0  3C            		INC	A
                      	
  EDE1                	SST:
  EDE1  0E00          		LD	C,0
  EDE3                	SST1:
  EDE3  0D            		DEC	C		; 4
  EDE4  20FD          		JR	NZ,SST1		;12 * 256 / 4 = About 1ms
  EDE6  3D            		DEC	A
  EDE7  20FA          		JR	NZ,SST1
                      	
  EDE9  3C            		INC	A		;A=1
  EDEA  CDF8ED        		CALL	READY
  EDED                	SETMODE:
  EDED  78            		LD	A,B		;B=00FH
  EDEE  DBFF          	FDMODE:	IN	A,(0FFH)	;Floppy mode(2D/2HD) (Self-modifying code)
                      	
  EDF0  78            		LD	A,B
  EDF1  DBFD          		IN	A,(0FDH)	;MFM mode
                      	
  EDF3  CD2AEE        		CALL	DELAY0		;Head select time
  EDF6  3E81          		LD	A,081H
  EDF8                	READY:
  EDF8  3202EE        		LD	(WAITA+1),A
  EDFB  E5            		PUSH	HL
  EDFC  0EF8          		LD	C,0F8H
  EDFE  61            		LD	H,C
  EDFF                	READY2:
  EDFF  ED78          		IN	A,(C)
  EE01  E681          	WAITA:	AND	081H		;NOT READY,BUSY
  EE03  2808          		JR	Z,READYE
  EE05  E3            		EX	(SP),HL		;wait 19clock
  EE06  E3            		EX	(SP),HL		; //
  EE07  2B            		DEC	HL
  EE08  7C            		LD	A,H
  EE09  B5            		OR	L
  EE0A  20F3          		JR	NZ,READY2
  EE0C  37            		SCF
  EE0D                	READYE:
  EE0D  E1            		POP	HL
  EE0E  C9            		RET
                      	
  EE0F                	FDCRES:
  EE0F  3600          		LD	(HL),0
  EE11  0E08          		LD	C,8
  EE13  18BE          		JR	FDCCMD2
                      	
  EE15                	FDMOFF:
  EE15  F5            		PUSH	AF
  EE16  C5            		PUSH	BC
  EE17  01FC0F        		LD	BC,00FFCH	;Motor off
  EE1A  3E00          	MOPAT:	LD	A,000H		;(Self-rewriting)
  EE1C  ED79          		OUT	(C),A
  EE1E  C1            		POP	BC
  EE1F  F1            		POP	AF
  EE20  C9            		RET
                      	
  EE21                	SECSET:
  EE21  01FA0F        		LD	BC,00FFAH
  EE24  ED59          		OUT	(C),E
  EE26                	COMSET:
  EE26  0EF8          		LD	C,0F8H
  EE28  ED79          		OUT	(C),A
  EE2A                	DELAY0:
  EE2A  3E1A          		LD	A,26
  EE2C                	DELAY:
  EE2C  3D            		DEC	A
  EE2D  20FD          		JR	NZ,DELAY
  EE2F  C9            		RET
                      	
  EE30                	DRIVE:
  EE30  2A84EF        		LD	HL,(_DRIVE)
  EE33  26EF          		LD	H,_CYL0/256
  EE35  CBFD          		SET	7,L
  EE37  7E            		LD	A,(HL)
  EE38  C9            		RET
                      	
  EE39                	RNF:
  EE39  CD30EE        		CALL	DRIVE
  EE3C  36FF          		LD	(HL),0FFH
  EE3E  C9            		RET
                      	
  EE3F  02            	RETRY:	DB	2
                      	
                      	
                      	;	FLOPPY DISK DRIVER(DMA)
                      	
                      	
  EE40                	WTTRK:
  EE40  0601          		LD	B,1
  EE42  3EF4          		LD	A,0F4H
  EE44  1802          		JR	WTTRK1
  EE46                	FDWT:
  EE46  3EA0          		LD	A,0A0H
  EE48                	WTTRK1:
  EE48  32DDED        		LD	(FDCPAT+1),A
  EE4B  3E10          		LD	A,16
  EE4D  180C          		JR	DISK
                      	
  EE4F                	DISKOK:
  EE4F  0601          	FDCNT:	LD	B,1
  EE51  100B          		DJNZ	DISK1
  EE53  C9            		RET
                      	
  EE54                	FDRD:
  EE54  3E80          		LD	A,080H
  EE56  32DDED        		LD	(FDCPAT+1),A
  EE59  3E0E          		LD	A,14
                      	
  EE5B                	DISK:
  EE5B  3272EE        		LD	(DMAPAT+1),A
  EE5E                	DISK1:
  EE5E  78            		LD	A,B
  EE5F  3250EE        		LD	(FDCNT+1),A
  EE62  22F4EE        		LD	(DMAD+11),HL
  EE65                	DISKR:
  EE65  3E02          		LD	A,2		;Retry count
  EE67  323FEE        		LD	(RETRY),A
  EE6A                	DISK2:
  EE6A  D5            		PUSH	DE
  EE6B  CD77ED        		CALL	SEEK
  EE6E  3843          		JR	C,ERRF
  EE70  F3            		DI
                      	
  EE71  3E0E          	DMAPAT:	LD	A,14		;自己書き換え
  EE73  21E9EE        		LD	HL,DMAD
  EE76  CDB2DF        		CALL	SETDMA
  EE79  ED49          		OUT	(C),C		;OUT 01F87H,087H WR6 イネーブル
  EE7B  3ADDED        		LD	A,(FDCPAT+1)
  EE7E  C5            		PUSH	BC
  EE7F  CD21EE        		CALL	SECSET
  EE82  2AF4EE        		LD	HL,(DMAHL)
  EE85  23            		INC	HL
  EE86  1106BB        		LD	DE,0BB06H	;READ DMA
                      	
  EE89  3E03          		LD	A,3
  EE8B  CDF8ED        		CALL	READY
  EE8E  ED78          		IN	A,(C)
                      	
  EE90  C1            		POP	BC
  EE91  ED51          		OUT	(C),D		;読み出しマスク設定
  EE93  ED59          		OUT	(C),E		;バイトカウンタ(LH)
  EE95  ED58          		IN	E,(C)
  EE97  ED50          		IN	D,(C)
  EE99  19            		ADD	HL,DE
  EE9A  D1            		POP	DE
  EE9B  13            		INC	DE
  EE9C  FB            		EI
  EE9D  CD15EE        		CALL	FDMOFF
  EEA0  B7            		OR	A
  EEA1  28AC          		JR	Z,DISKOK
  EEA3  1B            		DEC	DE
  EEA4  CB67          		BIT	4,A
  EEA6  C439EE        		CALL	NZ,RNF
  EEA9                	DISKE2:
  EEA9  213FEE        		LD	HL,RETRY
  EEAC  35            		DEC	(HL)
  EEAD  20BB          		JR	NZ,DISK2
  EEAF  B7            		OR	A
  EEB0  2834          		JR	Z,ERR
  EEB2  D5            		PUSH	DE
  EEB3                	ERRF:
  EEB3  D1            		POP	DE
  EEB4                	DELP:
  EEB4  CD39EE        		CALL	RNF
  EEB7  CD15EE        		CALL	FDMOFF
  EEBA  3A85EF        		LD	A,(_SEEKSP)
  EEBD  87            		ADD	A,A
  EEBE  9F            		SBC	A,A
  EEBF  3825          		JR	C,ERR
  EEC1                	DELP1:
  EEC1  D5            		PUSH	DE
  EEC2  21C0F0        		LD	HL,DEMES
  EEC5  CD1DD8        		CALL	MSX
  EEC8  CDA2DB        		CALL	KEYBC
  EECB                	DELP2:
  EECB  CDCAEF        		CALL	_INKEY
  EECE  28FB          		JR	Z,DELP2
                      	
  EED0  D1            		POP	DE
  EED1  CDB4E3        		CALL	CAP
  EED4  FE52          		CP	'R'		;Retry
  EED6  CA65EE        		JP	Z,DISKR
  EED9  FE49          		CP	'I'		;Ignore
  EEDB  2807          		JR	Z,IGNORE
  EEDD  FE41          		CP	'A'		;Abort
  EEDF  20E0          		JR	NZ,DELP1
  EEE1  C300EF        		JP	CPM_BOOT
  EEE4                	IGNORE:
  EEE4  3EFF          		LD	A,0FFH
  EEE6                	ERR:
  EEE6  BF            		CP	A
  EEE7  37            		SCF
  EEE8  C9            		RET
                      	
                      				;X1turbo DISK DMA
  EEE9  C3            	DMAD:	DB	0C3H	;WR6 リセット
  EEEA  7D            		DB	07DH	;WR0 ポートA→ポートB 転送
  EEEB  FB0F          		DW	00FFBH	;ポートA開始アドレス FDC データレジスタ
  EEED  FFFF          		DW	0FFFFH	;ブロック長(転送サイズ-1)
  EEEF  2C            		DB	02CH	;WR1 ポートAアドレス固定 I/O
  EEF0  10            		DB	010H	;WR2 ポートBインクリメント
  EEF1  80            		DB	080H	;WR3
  EEF2  92            		DB	092H	;WR5 WAIT RDY:LOW
  EEF3  8D            		DB	08DH	;WR4 バイト ポートB開始アドレス(LH)
  EEF4  0000          	DMAHL:	DW	0	;ポートB開始アドレス
  EEF6  CF            		DB	0CFH	;WR6 ロード
  EEF7  01            		DB	001H	;WR0 ポートB←ポートB 転送
  EEF8  CF            		DB	0CFH	;WR6 ロード
                      	
  EEF9                	DMAE:
                      	
  1EC1                	AT_R	EQU	WTTRK-AT_WTTRK
                      	
                      	;LDWORK.ASM
                      	
                      	;	LSX-Dodgers WORK0
                      	;
                      	;	CP/M互換BIOS
                      	;	BOOT:アドレス下位1バイトが0になるように
  EEF9                	AT_WORK:
  EEF9                		DS	WORKAD-AT_WORK
                      	
  EF00                	CPM_BOOT:
  EF00  C30000        		JP	0
  EF03                	_SYS00:		;(BDOS)プログラム終了
  EF03                	CPM_WBOOT:
  EF03  C309D1        		JP	WBOOT1
  EF06                	CPM_CONST:
  EF06  C35CDC        		JP	CONSTX
  EF09                	_SYS07:		;(BDOS)コンソール直接入力
  EF09                	CPM_CONIN:
  EF09  C303DC        		JP	FLGET
  EF0C                	CPM_CONOUT:
  EF0C  C3A3DE        		JP	CONOUT1
                      	
  EF0F                	DECBF:
  EF0F                		DS	5
  EF14  00            	FDRV	DB	0
  EF15                	FNAME	DS	36
  EF39                	SFILE:	DS	33		;DRV FILENAME
  EF5A                	HLBUF:	DS	3		;Random record buffer
                      	
  EF5D  0000          	LEFTX:	DW	0
  EF5F  0000          	DTAX:	DW	0
                      	
  EF61  008E01000B000C	BEEPD:	DB	0,08EH,1,0,00BH,0,00CH,010H
  EF69  073E08100D00FF		DB	007H,03EH,008H,010H,00DH,0,0FFH
                      	
  EF70  00            	CHECK:	DB	0
                      	
  EF71  4F4B24        	OK:	DB	"OK$"
  EF74                		DS	5
  EF79  4572726F722124	COMERM:	DB	"Error!$"
                      	;	システムワークエリア
                      	;	_CYL0:アドレス下位1バイトが080Hになるように
  EF80  FF            	_CYL0:	DB	0FFH	;Cylinder
  EF81  FF            	_CYL1:	DB	0FFH	;Cylinder
  EF82  FF            	_CYL2:	DB	0FFH	;Cylinder
  EF83  FF            	_CYL3:	DB	0FFH	;Cylinder
  EF84  00            	_DRIVE:	DB	0	;unit number
  EF85  00            	_SEEKSP:DB	0	;Seek speed
  EF86  FF            	_ISEEK:	DB	0FFH	;
  EF87  00            	_DVSW:	DB	0
  EF88  00            	_DRV:	DB	0
  EF89  FF            	_DSK:	DB	0FFH
  EF8A  8000          	_DTA:	DW	00080H
  EF8C  0000          	_CTC:	DW	0
  EF8E  0000          	_TXADR:	DW	0
  EF90  0000          	_KEYPS:	DW	0
  EF92  00FF          	_KEYD:	DW	0FF00H	;キーデータ
  EF94  C210          	_KEYSP:	DW	010C2H
  EF96  07            	_COLORF:DB	COLORF	;色
  EF97  18            	_LINE:	DB	LINE
                      	
  EF98  0000          	_FCB:	DW	0
  EF9A  0000          	_FBPS:	DW	0	;SEARCH FILES
  EF9C  0000          	_FBAD:	DW	0	;    //
  EF9E  00            	_FBCNT:	DB	0	;    //
  EF9F  00            	_FILEN:	DB	0
  EFA0  00            	_DIRF:	DB	0	;bit7:ディレクトリ bit0-6:セクタインデックス
  EFA1  00            	_MAXCYL:DB	0
  EFA2  00            	_WTFATF:DB	0	;FATバッファの更新フラグ
  EFA3                			DS	1
  EFA4  00            	_WTDBF:	DB	0	;データバッファの更新フラグ
  EFA5  FF            	_DBDRV:	DB	0FFH	;データバッファにあるデータを読み込んだドライブ
  EFA6  0000          	_DBSEC:	DW	0	;データバッファにあるデータを読み込んだセクタ番号
  EFA8  0000          	_DPB:	DW	0
  EFAA                	_FATDRV:
  EFAA  FF            		DB	0FFH	;FATバッファに読み込んでいるドライブ
  EFAB  00            	_FATIX:	DB	0	;FATバッファに読み込んでいるインデックス
  EFAC                	_FAKEFREE:
  EFAC  0000          		DW	0	;偽の残量（FATサイズが大きくなると重くなるのでその対策）
                      	
  EFAE                		DS	2
                      	
  EFB0  6F5059381F0219	CRTCD:	DB	06FH,WIDTH,059H,038H,01FH,002H,019H,01CH
  EFB8  0007          		DB	000H,007H
  EFBA  80F8B0FF      		DW	0-WIDTH*LINE,0-WIDTH
  EFBE  0C            		DB	00CH
  EFBF  20            	WK1FD0:	DB	020H
                      	
  EFC0  0BDB          	CTC0:	DW	INTCTCE
  EFC2  0BDB          	CTC1:	DW	INTCTCE
  EFC4  2DDB          	CTC2:	DW	INTCTC2
  EFC6  0BDB          	CTC3:	DW	INTCTCE
  EFC8  F4DA          	INTVEC:	DW	INT
                      	
                      	;	LSX-Dodgers 独自BIOS
                      	
  EFCA  C388E0        	_INKEY:	JP	INKEY
  EFCD  C32ADD        	_PRINT:	JP	PRINT
  EFD0  C3F0DF        	_SCRN:	JP	SCRN
  EFD3  C31AE0        	_POS:	JP	POS
  EFD6  C36CDE        	_LOC:	JP	LOC
  EFD9                	_CHGDRV:
  EFD9  C36AEC        		JP	CHGDRV
  EFDC                	_GETDPB:
  EFDC  C33EED        		JP	GETDPB
  EFDF                	_FFLUSH:
  EFDF  C35CED        		JP	FFLUSH
  EFE2                	_RDFATX:
  EFE2  C36FE9        		JP	RDFATX
  EFE5  C397EA        	_RDFAT:	JP	RDFAT
  EFE8  C340EE        	_WTTRK:	JP	WTTRK
  EFEB  C354EE        	_FDRD:	JP	FDRD
  EFEE  C346EE        	_FDWT:	JP	FDWT
  EFF1                	_BPB2DPB:
  EFF1  C3FCEA        		JP	BPB2DPB
  EFF4                		DS	3
  EFF7                	_GNCL:
  EFF7  C311EA        	GNCPAT:	JP	GNCL		; self-modifying code
  EFFA                	_SNCL:
  EFFA  C334EA        	SNCPAT:	JP	SNCL		; self-modifying code
  EFFD  C387D1        	_COMANL:JP	COMANL
                      	
  F000                	_WE:
                      	
                      	;LDCALL.ASM
                      	
                      	;	LSX-Dodgers システムコール(BDOS)
                      	;
                      	;	LSX-Dodgers SYSTEM CALL
                      	;	STABLE:アドレス下位1バイトが0になるように
                      	
  F000                	STABLE:
                      	;0
  F000  03EFD6DBE2DB3C		DW	_SYS00,_SYS01,_SYS02,_SYS03
  F008  0CD80CD8E7DB09		DW	_SYS04,_SYS05,_SYS06,_SYS07
  F010  EFDB0ED828DC54		DW	_SYS08,_SYS09,_SYS0A,_SYS0B
  F018  23D828D837D84A		DW	_SYS0C,_SYS0D,_SYS0E,_SYS0F
                      	;1
  F020  9ED8E5D82ED964		DW	_SYS10,_SYS11,_SYS12,_SYS13
  F028  6CD98AD9A7D99F		DW	_SYS14,_SYS15,_SYS16,_SYS17
  F030  02DA07DA0CDA12		DW	_SYS18,_SYS19,_SYS1A,_SYS1B
  F038  0CD838DA0CD83C		DW	_SYS1C,_SYS1D,_SYS1E,_SYS1F
                      	;2
  F040  0CD8DED9F0D943		DW	_SYS20,_SYS21,_SYS22,_SYS23
  F048  6FDA0CD8A7E689		DW	_SYS24,_ERROR,_SYS26,_SYS27
  F050  F0D978DA6ADC3C		DW	_SYS28,_SYS5C,_SYS2A,_SYS2B
  F058  9EDC3CE20CD89B		DW	_SYS2C,_SYS2D,_SYS2E,_SYS2F
                      	;3
  F060  95DA0CD80CD80C		DW	_SYS30,_ERROR,_ERROR,_ERROR
  F068  0CD80CD80CD80C		DW	_ERROR,_ERROR,_ERROR,_ERROR
  F070  0CD83CE23CE2BC		DW	_ERROR,_SYS39,_SYS3A,_SYS5A
                      	;DOS2
  F078  EFD7          		DW	_DOS2
                      	
  F07A                		DS	2
  F07C  00F2          	_FATBF:	DW	FATBF
  F07E  00FA          	_DTBUF:	DW	DTBUF
                      	
                      	;	コントロールコードテーブル
                      	;	CTRLTB:アドレス下位1バイトが080Hになるように
                      	
  F080                	CTRLTB:
  F080  E1DDE1DD1DDE64		DW	CTRL00,CTRL00,CTRL02,CTRL03	;  A,B,C
  F088  5EDF41DE3DDFC4		DW	CTRL04,CTRL05,CTRL06,CTRL07	;D,E,F,G
  F090  EDDD0EDE67DF82		DW	CTRL08,CTRL09,CTRL0A,CTRL0B	;H,I,J,K
  F098  3ADF67DE7CDFE1		DW	CTRL0C,CTRL0D,CTRL0E,CTRL00	;L,M,N,O
  F0A0  E1DDE1DD7ADEE1		DW	CTRL00,CTRL00,CTRL12,CTRL00	;P,Q,R,S
  F0A8  E1DDE1DDE1DDE1		DW	CTRL00,CTRL00,CTRL00,CTRL00	;T,U,V,W
  F0B0  E1DDE1DD3ADF89		DW	CTRL00,CTRL00,CTRL0C,CTRL1B	;X,Y,Z,ESC
  F0B8  08DEF4DDFDDD67		DW	CTRL1C,CTRL1D,CTRL1E,CTRL1F	;→,←,↑,↓
                      	
  F0C0  0307          	DEMES:	DB	3,7
  F0C2  44657669636520		DB	"Device I/O Error",5,3
  F0D4  41626F72742052		DB	"Abort Retry Ignore?",5,3,0
                      	
  F0EA  1617          	SDDATA:	DB	22,23		;(FCB)ファイルを最後に変更した時刻
  F0EC  1415          		DB	20,21		;(FCB)ファイルを最後に変更した日付
  F0EE  1A1B          		DB	26,27		;(FCB)ファイルの先頭クラスタ
  F0F0  10111213      		DB	16,17,18,19	;(FCB)ファイルのサイズ(バイト単位)
  F0F4                	SDDATAE:
                      	
  F0F4                	SCDIR:	DS	2		;+14 +15
  F0F6                	SFBPS:	DS	2		;FBPS
  F0F8                	SFNDF:	DS	2		;FILEN DIRF
                      	
  F0FA  0000          	_FATPS:	DW	0
  F0FC  0000          	_DIRPS:	DW	0
  F0FE  0000          	_CLPS:	DW	0
                      	
  F100                	_PE:
                      	
                      	;X1DPB.ASM
                      	
                      	;	LSX-Dodgers DPB
                      	;	X1/turbo/Z
                      	
  F100                	DEVICE:
                      	
                      	;	A:
                      	
  F100  0200          		DW	2	;DPB_00_FATLN
  F102  EBEF          		DW	_FDRD	;DPB_02_DRD
  F104  EEEF          		DW	_FDWT	;DPB_04_DWT
  F106  FD            		DB	0FDH	;DPB_06_FATID
  F107  02            		DB	2	;DPB_07_SECPCL
  F108  6401          		DW	356	;DPB_08_MAXCL
  F10A  FF            		DB	0FFH	;DPB_0A_FDMODE	0FFH=2D / 0FEH=2HD
  F10B  07            		DB	7	;DPB_0B_DIRSCNT
  F10C  28            		DB	40	;DPB_0C_MAXCYL
  F10D  09            		DB	9	;DPB_0D_MAXSEC	Number of sectors
  F10E  01            		DB	1	;DPB_0E_FATPS
  F10F  82            		DB	082H	;DPB_0F_BPS
  F110  0500          		DW	5	;DPB_10_DIRPS
  F112  A7            		DB	0A7H	;DPB_12_DEVICE	Device Number
  F113  00            		DB	0	;DPB_13_UNITNO
  F114  0800          		DW	8	;DPB_14_ADDCL16
  F116  0000          		DW	0	;DPB_16
  F118  0000          		DW	0	;DPB_18
  F11A  0000          		DW	0	;DPB_1A_SDIR
  F11C  46444430      		DB	"FDD0"	;DPB_1C_NAME
                      	
                      	;	B:
                      	
  F120  0200          		DW	2	;DPB_00_FATLN
  F122  EBEF          		DW	_FDRD	;DPB_02_DRD
  F124  EEEF          		DW	_FDWT	;DPB_04_DWT
  F126  FD            		DB	0FDH	;DPB_06_FATID
  F127  02            		DB	2	;DPB_07_SECPCL
  F128  6401          		DW	356	;DPB_08_MAXCL
  F12A  FF            		DB	0FFH	;DPB_0A_FDMODE	0FFH=2D / 0FEH=2HD
  F12B  07            		DB	7	;DPB_0B_DIRSCNT
  F12C  28            		DB	40	;DPB_0C_MAXCYL
  F12D  09            		DB	9	;DPB_0D_MAXSEC	Number of sectors
  F12E  01            		DB	1	;DPB_0E_FATPS
  F12F  82            		DB	082H	;DPB_0F_BPS
  F130  0500          		DW	5	;DPB_10_DIRPS
  F132  A7            		DB	0A7H	;DPB_12_DEVICE	Device Number
  F133  01            		DB	1	;DPB_13_UNITNO
  F134  0800          		DW	8	;DPB_14_ADDCL16
  F136  0000          		DW	0	;DPB_16
  F138  0000          		DW	0	;DPB_18
  F13A  0000          		DW	0	;DPB_1A_SDIR
  F13C  46444431      		DB	"FDD1"	;DPB_1C_NAME
                      	
                      	;	C:
                      	
  F140  0200          		DW	2	;DPB_00_FATLN
  F142  EBEF          		DW	_FDRD	;DPB_02_DRD
  F144  EEEF          		DW	_FDWT	;DPB_04_DWT
  F146  FD            		DB	0FDH	;DPB_06_FATID
  F147  02            		DB	2	;DPB_07_SECPCL
  F148  6401          		DW	356	;DPB_08_MAXCL
  F14A  FF            		DB	0FFH	;DPB_0A_FDMODE	0FFH=2D / 0FEH=2HD
  F14B  07            		DB	7	;DPB_0B_DIRSCNT
  F14C  28            		DB	40	;DPB_0C_MAXCYL
  F14D  09            		DB	9	;DPB_0D_MAXSEC	Number of sectors
  F14E  01            		DB	1	;DPB_0E_FATPS
  F14F  82            		DB	082H	;DPB_0F_BPS
  F150  0500          		DW	5	;DPB_10_DIRPS
  F152  A7            		DB	0A7H	;DPB_12_DEVICE	Device Number
  F153  02            		DB	2	;DPB_13_UNITNO
  F154  0800          		DW	8	;DPB_14_ADDCL16
  F156  0000          		DW	0	;DPB_16
  F158  0000          		DW	0	;DPB_18
  F15A  0000          		DW	0	;DPB_1A_SDIR
  F15C  46444432      		DB	"FDD2"	;DPB_1C_NAME
                      	
                      	;	D:
                      	
  F160  0200          		DW	2	;DPB_00_FATLN
  F162  EBEF          		DW	_FDRD	;DPB_02_DRD
  F164  EEEF          		DW	_FDWT	;DPB_04_DWT
  F166  FD            		DB	0FDH	;DPB_06_FATID
  F167  02            		DB	2	;DPB_07_SECPCL
  F168  6401          		DW	356	;DPB_08_MAXCL
  F16A  FF            		DB	0FFH	;DPB_0A_FDMODE	0FFH=2D / 0FEH=2HD
  F16B  07            		DB	7	;DPB_0B_DIRSCNT
  F16C  28            		DB	40	;DPB_0C_MAXCYL
  F16D  09            		DB	9	;DPB_0D_MAXSEC	Number of sectors
  F16E  01            		DB	1	;DPB_0E_FATPS
  F16F  82            		DB	082H	;DPB_0F_BPS
  F170  0500          		DW	5	;DPB_10_DIRPS
  F172  A7            		DB	0A7H	;DPB_12_DEVICE
  F173  03            		DB	3	;DPB_13_UNITNO
  F174  0800          		DW	8	;DPB_14_ADDCL16
  F176  0000          		DW	0	;DPB_16
  F178  0000          		DW	0	;DPB_18
  F17A  0000          		DW	0	;DPB_1A_SDIR
  F17C  46444433      		DB	"FDD3"	;DPB_1C_NAME
                      	
                      	;	E:
                      	
  F180  0000          	EMMFL:	DW	0	;DPB_00_FATLN
  F182  7FD7          	EMMRD:	DW	EDRD	;DPB_02_DRD
  F184  7BD7          	EMMWR:	DW	EDWT	;DPB_04_DWT
  F186  FE            		DB	0FEH	;DPB_06_FATID
  F187  02            		DB	2	;DPB_07_SECPCL
  F188  0000          	EMMCL:	DW	0	;DPB_08_MAXCL
  F18A  00            		DB	0	;DPB_0A_FDMODE
  F18B  0C            		DB	12	;DPB_0B_DIRSCNT
  F18C  00            		DB	0	;DPB_0C_MAXCYL
  F18D  00            		DB	0	;DPB_0D_MAXSEC
  F18E  02            		DB	2	;DPB_0E_FATPS
  F18F  02            		DB	2	;DPB_0F_BPS
  F190  0A00          		DW	10	;DPB_10_DIRPS
  F192  06            		DB	6	;DPB_12_DEVICE
  F193  00            		DB	0	;DPB_13_UNITNO
  F194  1200          		DW	18	;DPB_14_ADDCL16
  F196  0000          		DW	0	;DPB_16
  F198  0000          		DW	0	;DPB_18
  F19A  0000          		DW	0	;DPB_1A_SDIR
  F19C  454D4D30      		DB	"EMM0"	;DPB_1C_NAME
                      	
                      	;	F:
                      	
  F1A0  0000          	BANKFL:	DW	0	;DPB_00_FATLN
  F1A2  16D7          		DW	BDRD	;DPB_02_DRD
  F1A4  12D7          		DW	BDWT	;DPB_04_DWT
  F1A6  F9            		DB	0F9H	;DPB_06_FATID
  F1A7  02            		DB	2	;DPB_07_SECPCL
  F1A8  0000          	BANKCL:	DW	0	;DPB_08_MAXCL
  F1AA  00            		DB	0	;DPB_0A_FDMODE
  F1AB  08            		DB	8	;DPB_0B_DIRSCNT
  F1AC  00            		DB	0	;DPB_0C_MAXCYL
  F1AD  01            		DB	1	;DPB_0D_MAXSEC
  F1AE  00            		DB	0	;DPB_0E_FATPS
  F1AF  02            		DB	2	;DPB_0F_BPS
  F1B0  0200          		DW	2	;DPB_10_DIRPS
  F1B2  0B            		DB	00BH	;DPB_12_DEVICE
  F1B3  00            		DB	0	;DPB_13_UNITNO
  F1B4  0600          		DW	6	;DPB_14_ADDCL16
  F1B6  0000          		DW	0	;DPB_16
  F1B8  0000          		DW	0	;DPB_18
  F1BA  0000          		DW	0	;DPB_1A_SDIR
  F1BC  4252414D      		DB	"BRAM"	;DPB_1C_NAME
                      	
                      	;	G:
                      	
  F1C0  0200          		DW	2	;DPB_00_FATLN
  F1C2  D3D6          		DW	GDRD	;DPB_02_DRD
  F1C4  EAD6          		DW	GDWT	;DPB_04_DWT
  F1C6  FA            		DB	0FAH	;DPB_06_FATID
  F1C7  01            		DB	1	;DPB_07_SECPCL
  F1C8  B800          		DW	184	;DPB_08_MAXCL
  F1CA  00            		DB	0	;DPB_0A_FDMODE
  F1CB  07            		DB	7	;DPB_0B_DIRSCNT
  F1CC  00            		DB	0	;DPB_0C_MAXCYL
  F1CD  01            		DB	1	;DPB_0D_MAXSEC
  F1CE  01            		DB	1	;DPB_0E_FATPS
  F1CF  01            		DB	1	;DPB_0F_BPS
  F1D0  0300          		DW	3	;DPB_10_DIRPS
  F1D2  05            		DB	5	;DPB_12_DEVICE
  F1D3  01            		DB	1	;DPB_13_UNITNO
  F1D4  0800          		DW	8	;DPB_14_ADDCL16
  F1D6  0000          		DW	0	;DPB_16
  F1D8  0000          		DW	0	;DPB_18
  F1DA  0000          		DW	0	;DPB_1A_SDIR
  F1DC  4752414D      		DB	"GRAM"	;DPB_1C_NAME
                      	
                      	;	H:
  F1E0                		DS	31
  F1FF  00            		DB	0
                      	
                      	
  F200                	LAST_ADR:
                      	
  F200                		END
