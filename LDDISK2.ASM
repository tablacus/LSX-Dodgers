
;	LSX-Dodgers DISK2


;	GRAPHIC RAM DISK DRIVER


GDRD:
	PUSH	BC
	CALL	GADR
	POP	AF
GDRD1:
	INI
	INC	B
	INC	C
	JR	NZ,GDRD1
	INC	B
	INC	DE
	DEC	A
	JR	NZ,GDRD1
GBANK0:
	LD	A,(WK1FD0)
	AND	0EFH		;BANK0
	JR	S1FD0

GDWT:
	PUSH	BC
	CALL	GADR
	POP	AF
GDWT1:
	INC	B
	OUTI
	INC	C
	JR	NZ,GDWT1
	INC	B
	INC	DE
	DEC	A
	JR	NZ,GDWT1
	JR	GBANK0

GADR:
	LD	C,0
	LD	A,E
	ADD	A,040H
	LD	B,A
	LD	A,(WK1FD0)
	OR	010H		;BANK1
S1FD0:
	PUSH	BC
	LD	(WK1FD0),A
	LD	BC,01FD0H
	OUT	(C),A
	POP	BC
	RET


;	BANK RAM DISK DRIVER

BDRD:
	LD	(BANKSP+1),SP
	LD	A,(BANKSP+2)
	ADD	A,A
	JR	C,BDRD1
	LD	SP,EXTSP
BDRD1:
	PUSH	DE
	EXX		;裏レジスタ
	 PUSH	 BC
	 PUSH	 DE
	 CALL	 BADR
	CALL	BTFR
	EXX		;裏レジスタ
BDWTE:
	 POP	 DE
	 POP	 BC
	 EXX		;表レジスタ
	POP	DE
	XOR	A
BANKSP:	LD	SP,0
	EI
	RET

BDWT:
	LD	(BANKSP+1),SP
	LD	A,(BANKSP+2)
	ADD	A,A
	JR	C,BDWT1
	LD	SP,EXTSP
BDWT1:
	PUSH	DE
	EXX		;裏レジスタ
	 PUSH	 BC
	 PUSH	 DE
	 CALL	 BADR
	EXX		;裏レジスタ
	 LD	D,E
	 LD	E,A
	 EXX		;表レジスタ
	EX	DE,HL
	CALL	BTFR
	EX	DE,HL
	EXX		;裏レジスタ
	 OUT	 (C),D	 ;MAIN
	 JR	 BDWTE

BADR:
	 EXX		;表レジスタ
	LD	A,E
	ADD	A,A
	RL	D
	ADD	A,A
	RL	D
	PUSH	AF
	ADD	A,A
	LD	A,D
	ADC	A,A
	AND	00FH
	EXX		;裏レジスタ
	 LD	 BC,00B00H
	 LD	 D,A	  ;BANK
	 LD	 E,010H	  ;MAIN
	 EXX		;表レジスタ
	POP	DE
	LD	E,0
	RES	7,D
	RET

BTFR:
	DI
BTFR0:
	PUSH	BC
	PUSH	DE
	LD	B,0	;256回ループ(256*4)
BTFR1:
	EXX		;裏レジスタ
	 OUT	 (C),D	 ;BANK(MAIN)
	 EXX		;表レジスタ
	EX	(SP),HL
	LD	A,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	EX	(SP),HL
	EXX		;裏レジスタ
	 OUT	 (C),E	 ;MAIN(BANK)
	 EXX		;表レジスタ
	LD	(HL),A
	INC	HL
	LD	(HL),C
	INC	HL
	LD	(HL),D
	INC	HL
	LD	(HL),E
	INC	HL
	DJNZ	BTFR1
	POP	DE
	INC	DE
	POP	BC
	DJNZ	BTFR0
	RET

;	EMM DRIVER(DMA)


EDWT:
	LD	C,15
	JR	EDWT1
EDRD:
	LD	C,14
EDWT1:
	PUSH	BC
	PUSH	DE
	LD	(DMAB),HL
	LD	A,B
	ADD	A,A
	ADD	A,A
	PUSH	AF
	DEC	A
	LD	(DMALEN+1),A
	LD	A,C
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,(_MAXCYL-1)
	LD	C,0
	ADD	HL,BC
	LD	BC,(_DRIVE)
	LD	B,00DH
	OUT	(C),C
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H

	LD	HL,EDMAD
	CALL	SETDMA
	CALL	GO_DMA
	EX	DE,HL
	POP	AF
	ADD	A,H
	LD	L,A
	POP	DE
	POP	BC
DMA_ADD_SEC:
	INC	DE
	DJNZ	DMA_ADD_SEC
	XOR	A
	RET

EDMAD:
	DB	0C3H	;WR6 リセット
	DB	07DH	;WR0 ポートA→ポートB 転送
	DW	00D03H	;ポートA開始アドレス EMMリード/ライト
DMALEN:	DW	003FFH	;ブロック長(転送サイズ-1)
	DB	02CH	;WR1 ポートAアドレス固定 I/O
	DB	050H	;WR2 ポートBアドレスインクリメント メモリ
	DB	002H	;ポートBタイミング
	DB	080H	;WR3
	DB	09AH	;WR5 WAIT RDY:HIGH
	DB	0ADH	;WR4 連続 ポートB開始アドレス
DMAB:	DW	0
	DB	001H	;WR0 ポートB←ポートB 転送
	DS	1	;ノンターボ版との差分
EMME:

AT_RE	EQU	EDWT-AT_EDWT

