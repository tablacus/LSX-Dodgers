
;	LSX-Dodgers INIT

	JP	START
MACHINE:DW	MAC
VERSION:DW	VER

START:
	DI
	IM	2
	LD	SP,STACK
	LD	A,01EH
	OUT	(0),A

	LD	BC,0
	LD	(_CTC),BC
	LD	BC,00A04H
	CALL	CHKCTC
	LD	BC,00704H
	CALL	CHKCTC
	LD	BC,01FA8H
	CALL	CHKCTC
	LD	BC,01FA0H
	CALL	CHKCTC
;
INISIO:
	LD	BC,01F91H	;INIT SIO
	LD	D,1
	OUT	(C),D
	DB	0EDH,071H
	LD	C,093H
	OUT	(C),D
	DB	0EDH,071H
	LD	C,099H		;INIT SIO
	LD	D,1
	OUT	(C),D
	DB	0EDH,071H
	LD	C,09BH
	OUT	(C),D
	DB	0EDH,071H

	LD	C,080H		;INIT DMA
	LD	HL,0C306H
INIDMA:
	OUT	(C),H
	DEC	L
	JR	NZ,INIDMA

	LD	A,0E4H
	CALL	COMOUT
	XOR	A
	CALL	OT49SB

	LD	A,INTVEC/256
	LD	I,A

	LD	BC,(_CTC)
	DEC	BC
	DEC	BC

	DB	0EDH,071H
	LD	A,CTC0
	OUT	(C),A

	INC	C
	LD	A,047H
	OUT	(C),A
	LD	A,13		;Baudrate 9600-13
	OUT	(C),A

	LD	A,C
	SUB	010H
	LD	C,A
	LD	(SIOAD+1),BC
	LD	(SIOAD0+1),BC
	LD	(SIOAD1+1),BC

	LD	HL,SIODATA
SINIT1:
	LD	A,(HL)
	INC	HL
	CP	0FFH
	JR	Z,SINIT2
	OUT	(C),A
	JR	SINIT1
SINIT2:
	LD	A,0E4H
	CALL	COMOUT
	LD	A,INTVEC
	CALL	OT49SB
;
	LD	BC,01FC4H
	LD	A,00CH
	OUT	(C),A

	LD	C,0C0H
	DB	0EDH,071H

	INC	C
	LD	A,40
	OUT	(C),A

	INC	C
	DB	0EDH,071H

	INC	C
	DB	0EDH,071H

	LD	C,0C5H
	DB	0EDH,071H

TEXT:
	LD	C,0B0H
	LD	A,080H
	OUT	(C),A
	LD	D,7
	LD	C,0B9H
	LD	HL,TEXTDT
TEXT1:
	INC	B
	OUTI
	INC	C
	DEC	D
	JR	NZ,TEXT1
	LD	BC,01FB0H
	DB	0EDH,071H

	LD	C,0C4H
	DB	0EDH,071H

	LD	BC,01A01H
	IN	A,(C)
	AND	8
	JR	Z,PRN
	LD	C,003H
	LD	A,00FH
	OUT	(C),A
	LD	BC,00A04H
PRN:
	LD	DE,1
	LD	H,D
	LD	L,D
	LD	BC,07FH
	LD	(HL),0FFH
	LDIR
	LD	BC,07FH
	LD	(HL),0
	LDIR

	LD	A,0C3H		;JP
	LD	HL,WBOOT
	LD	(00000H),A
	LD	(00001H),HL	;IPL

	LD	HL,(MACHINE)	;3 bit7  DPB Original(1)
	LD	(00003H),HL	;  bit6-0 Machine X1(0)
;				;4 LSX-Dodgers(01DH)
	LD	HL,BDOS
	LD	(00005H),A
	LD	(00006H),HL	;BDOS

	LD	HL,BIOS
	LD	(00018H),A	;BIOS-ROM
	LD	(00019H),HL

	LD	HL,0
	LD	(00038H),A	;RST038H
	LD	(00039H),HL

	LD	A,STABLE/256
	LD	(0000BH),A

	LD	A,0E9H		;JP (HL)
	LD	(0000FH),A

	LD	A,(_FATBF+1)
	LD	(00013H),A

	LD	A,(_DTBUF+1)
	LD	(00017H),A

	LD	A,KEYBF/256
	LD	(0001BH),A

	LD	A,KBUF/256
	LD	(0001FH),A

	LD	HL,(VERSION)
	LD	(0005AH),HL

	LD	A,0E6H
	CALL	COMOUT
	CALL	IN49SB
	PUSH	AF
	CALL	IN49SB
	POP	AF
	PUSH	AF
	AND	012H
	JR	NZ,KEYR
	LD	A,1
	LD	(KEYREP+1),A
KEYR:
	POP	AF
	AND	3
	JR	Z,NON

	LD	BC,03FD0H
	OUT	(C),C
	LD	A,037H
	OUT	(0D0H),A
	IN	A,(C)
	CP	C
	JR	Z,TURBO
NON:
	DEC	A		;A=0FFH
	LD	(00018H),A	;BIOS-ROM

	LD	HL,@SCR1
	LD	DE,SCR1
	LD	BC,@SCRE-@SCR1
	LDIR

	LD	HL,@WTTRK
	LD	DE,WTTRK
	LD	BC,@CPUE-@WTTRK
	LDIR

	LD	HL,@DRD+@R
	LD	(_FDRD+1),HL
	LD	HL,@DWT+@R
	LD	(_FDWT+1),HL
	LD	HL,@EDRDC+@R
	LD	(EMMRD),HL
	LD	HL,@EDWTC+@R
	LD	(EMMWR),HL

	LD	HL,DWTPAT+@R
	LD	(@FDCPAT+1),HL

	LD	HL,0
	LD	(X1PAT),HL

	LD	A,0AFH		;XOR	A
	LD	(X1KPAT),A

	LD	HL,SCRNX
	LD	(_SCRN+1),HL

	CALL	SETCRTC
	LD	B,010H		;PALET
	DB	0EDH,071H
	INC	B
	DB	0EDH,071H
	INC	B
	DB	0EDH,071H
	INC	B
	DB	0EDH,071H
	JP	NOBANK

TURBO:
	LD	A,01FH
	IN	A,(0F0H)
	RRCA
	JR	C,CRT1
	LD	HL,C8025H
	LD	DE,CRTCD
	LD	BC,16
	LDIR
CRT1:
	CALL	SETCRTC
	LD	BC,010AAH	;PALET
	OUT	(C),C
	LD	BC,011CCH
	OUT	(C),C
	LD	BC,012F0H
	OUT	(C),C
	INC	B
	DB	0EDH,071H

	LD	A,01FH
	IN	A,(0F0H)
	BIT	2,A
	JR	Z,BANK

	XOR	A
TZ1:
	PUSH	AF
	CALL	_GETDPB
	LD	A,(IX+012H)
	CP	087H
	JR	NZ,TZ2
	PUSH	IX
	POP	DE
	LD	HL,M2HD
	LDI
	LDI
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	LD	BC,12
	LDIR
TZ2:
	POP	AF
	INC	A
	CP	8
	JR	C,TZ1

BANK:
	LD	BC,00B00H
	LD	HL,07F00H
	DI
BANK1:
	OUT	(C),L
	LD	E,(HL)
	LD	A,E
	ADD	A,0A5H
	LD	(HL),A
	CP	(HL)
	LD	(HL),E
	JR	NZ,BANK2
	INC	L
	BIT	4,L
	JR	Z,BANK1
BANK2:
	LD	A,010H
	OUT	(C),A
	LD	A,L
	OR	A
	JR	Z,NOBANK
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	DEC	HL
	DEC	HL
	DEC	HL
	LD	(BANKCL),HL
	LD	A,1
	LD	(BANKFL),A
NOBANK:
EMM:
	CALL	EADR0
	IN	A,(C)
	PUSH	AF

	LD	DE,0
ECHECK1:
	INC	DE
	CALL	EADR2
	IN	H,(C)
	CALL	EADR2
	LD	A,H
	ADD	A,0E5H
	OUT	(C),A
	INC	A
	CALL	EADR0
	OUT	(C),A
	DEC	A
	CALL	EADR2
	IN	L,(C)
	CALL	EADR2
	OUT	(C),H
	CP	L
	JR	NZ,ECHECK2
	BIT	3,D
	JR	Z,ECHECK1
ECHECK2:
	CALL	EADR0
	POP	AF
	OUT	(C),A

	LD	HL,0-9
	ADD	HL,DE
	JR	NC,NOEMM
	LD	(EMMCL),HL
	LD	A,3
	LD	(EMMFL),A
NOEMM:
KEYREP:	LD	A,000H
	OR	A
	JR	Z,KEYR1
	LD	BC,0
	LD	(_CTC),BC
KEYR1:
	LD	BC,0-80*25
	LD	HL,(CRTCD+10)
	LD	(CRTCD+10),BC
	LD	E,00CH
	CALL	_PRINT
	LD	(CRTCD+10),HL

	LD	HL,INIMES
INIMES1:
	LD	A,(HL)
	INC	HL
	OR	A
	JR	Z,INIMES2
	LD	E,A
	CALL	_PRINT
	JR	INIMES1
INIMES2:
	LD	A,(0FF87H)
	CP	8
	JR	NC,INIT0
	LD	E,A
	ADD	A,"A"
	LD	(AUTODV),A
	LD	C,00EH
	CALL	SYSTEM
	INC	E
	LD	HL,_SEEKSP	;Disk error Ignore
	SET	7,(HL)
	LD	C,01BH
	CALL	SYSTEM
	LD	HL,_SEEKSP
	RES	7,(HL)

	INC	A
	JR	NZ,INIT1
INIT0:
	LD	E,0
	LD	C,00EH
	CALL	SYSTEM
	XOR	A
INIT1:
	LD	HL,0FF00H
	LD	DE,0FF01H
	LD	BC,000FFH
	LD	(HL),L
	LDIR

	LD	HL,0
	PUSH	HL
	OR	A
	RET	Z

	LD	A,0E6H
	CALL	COMOUT
	CALL	IN49SB
	CALL	IN49SB
	CP	01BH
	RET	Z
	LD	DE,AUTOD
	JP	_COMANL

EADR2:
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,(EMMBL-1)
	LD	C,0
	ADD	HL,BC
	LD	BC,(EMMDV)
	LD	B,00DH
	OUT	(C),C
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H
	INC	C
	EX	DE,HL
	POP	DE
	RET

EADR0:
	LD	BC,(EMMDV)
	LD	B,00DH
	DB	0EDH,071H
	INC	C
	DB	0EDH,071H
	INC	C
	DB	0EDH,071H
	INC	C
	RET

CHKCTC:
	PUSH	BC
	LD	DE,04703H
INICTC1:
	INC	C
	OUT	(C),D
	DB	0EDH,071H
	DEC	E
	JR	NZ,INICTC1
	POP	BC

	LD	DE,007FAH
	OUT	(C),D
	OUT	(C),E
	IN	A,(C)
	CP	E
	RET	NZ
	OUT	(C),D
	OUT	(C),D
	IN	A,(C)
	CP	D
	RET	NZ
	INC	BC
	INC	BC
	LD	(_CTC),BC
	RET

SETCRTC:
	LD	HL,CRTCD
	XOR	A
SETCRT1:
	LD	BC,01800H
	OUT	(C),A
	INC	C
	INC	B
	OUTI
	INC	A
	CP	12
	JR	NZ,SETCRT1
	INC	HL
	INC	HL
	LD	BC,01A03H+00100H
	OUTI
	LD	BC,01FD0H+00100H
	OUTI
	RET

AUTOD:	DB	"AUTOEXEC "
AUTODV:	DB	"A:",0

INIMES:	DB	"¥LSX-Dodgers for X1/turbo version "
;	DB	'0'+VER/256(MOD)16,'.'			;‚È‚º‚©‚¤‚Ü‚­“®‚©‚È‚¢
;	DB	'0'+VER/16(MOD)16,'0'+VER(MOD)16	;‚È‚º‚©‚¤‚Ü‚­“®‚©‚È‚¢
	DB	'1.24'
	DB	' Gaku (Lovers/Tablacus)',0DH,0AH
	DB	0

C8025H:	DB	06BH,050H,059H,088H,01BH,001H,019H,01AH
	DB	000H,00FH
	DW	0-80*LINE,0-80
	DB	00CH,0A3H

TEXTDT:	DB	003H,00CH,00FH,030H,033H,03CH,03FH

SIODATA:
	DB	018H
	DB	1,0
	DB	2,0
	DB	3,0C1H
	DB	4,044H
	DB	5,0E8H
	DB	0FFH

@SCR1:
	EXX
	PUSH	BC
	LD	BC,02000H+80
	EXX
	PUSH	BC
	LD	BC,02000H

	LD	H,A
	OR	A
@SCRUP1:
	JR	Z,@SCRCL
	PUSH	BC
	SET	4,B
	EXX
	PUSH	BC
	SET	4,B
	EXX
	CALL	@UPSUB+@R
	EXX
	POP	BC
	EXX
	POP	BC
	CALL	@UPSUB+@R
	DEC	H
	JR	@SCRUP1

@SCRCL:
	LD	HL,02000H+16
	LD	A,(_COLORF)
@SCRCL1:
	SET	4,B
	OUT	(C),H
	RES	4,B
	OUT	(C),A
	INC	BC
	SET	4,B
	OUT	(C),H
	RES	4,B
	OUT	(C),A
	INC	BC
	SET	4,B
	OUT	(C),H
	RES	4,B
	OUT	(C),A
	INC	BC
	SET	4,B
	OUT	(C),H
	RES	4,B
	OUT	(C),A
	INC	BC
	SET	4,B
	OUT	(C),H
	RES	4,B
	OUT	(C),A
	INC	BC
	DEC	L
	JR	NZ,@SCRCL1
	POP	BC
	EXX
	POP	BC
	EXX
	RET

@SCRE:

;	FLOPPY DISK DRIVER(CPU)

@WTTRK:
	LD	A,0F0H
	JR	@WTTRK1

@DWT:
	LD	A,0A0H
@WTTRK1:
	LD	(DWTPAT+1+@R),A

	LD	A,2		;Retry count
	LD	(RETRY),A

DWT0:
	PUSH	DE
	PUSH	HL
	LD	(DHLPAT+1+@R),HL
	CALL	SEEK		;Write disk
	POP	HL
	JR	C,ERRDW
	DI
DWTPAT:	LD	A,0A0H
	CALL	SECSET
	LD	C,0FBH

DWT1:
	LD	D,(HL)
DWT2:
	LD	A,B
	IN	A,(0F8H)
	RRCA
	JR	NC,DWT4
	RRCA
	JR	NC,DWT2
DWT3:
	OUT	(C),D
	INC	HL
	JR	DWT1

DWT4:
	DEC	A
	JR	Z,DWT3
	INC	A
	EI
	POP	DE
	INC	DE
	CALL	FDMOFF
	RET	Z
	CALL	DISKC+@R
	JR	NZ,DWT0
	OR	A
	RET	Z
ERRW:
	LD	A,0FFH
ERRZ:
	CP	A
	SCF
	JP	FDMOFF

ERRDW:
	POP	DE
	CALL	@DELP+@R
	JR	NZ,DWT0
	OR	A
	JR	NZ,ERRW
	RET

@DRD:
	LD	A,080H
	LD	(DRDPAT+1+@R),A
	LD	(DWTPAT+1+@R),A

	LD	A,2		;Retry count
	LD	(RETRY),A

DRD0:
	PUSH	DE
	PUSH	HL
	LD	(DHLPAT+1+@R),HL
	CALL	SEEK		;Read disk
	POP	HL
	JR	C,ERRDR
	DI
DRDPAT:
	LD	A,080H
	CALL	SECSET
	LD	C,0FBH
DRD1:
	LD	A,B
	IN	A,(0F8H)
	RRCA
	JR	NC,DRD3
	RRCA
	JR	NC,DRD1
	INI
	INC	B
	JR	DRD1

DRD3:
	OR	A
	EI
	POP	DE
	INC	DE
	CALL	FDMOFF
	RET	Z
	CALL	DISKC+@R
	JR	NZ,DRD0
	OR	A
	JR	NZ,ERRW
	RET

ERRDR:
	POP	DE
	CALL	@DELP+@R
	JR	NZ,DRD0
	OR	A
	JR	NZ,ERRW
	RET

DISKC:
	DEC	DE
	BIT	3,A
	CALL	NZ,RNF
DISKD:
	PUSH	HL
	LD	HL,RETRY
	DEC	(HL)
	POP	HL
	JR	NZ,@ERR		;Retry
	OR	A
	JR	Z,@ERR		;Error

@DELP:
	CALL	RNF
	CALL	FDMOFF
	LD	A,(_SEEKSP)
	ADD	A,A
	JR	C,@ERRZ
@DELP1:
	PUSH	DE
	LD	HL,DEMES
	CALL	MSX
	CALL	KEYBC
@DELP2:
	CALL	INKEY
	JR	Z,@DELP2

	POP	DE
	CALL	CAP
	CP	"R"
	JR	NZ,@DELP3
DHLPAT:	LD	HL,0
	LD	A,2
	LD	(RETRY),A
	OR	A
	RET
@DELP3:
	CP	"I"
	JR	Z,@IGNORE
	CP	"A"
	JR	NZ,@DELP1
	JP	BOOT
@IGNORE:
	LD	A,0FFH
@ERRZ:
	CP	A
@ERR:
	LD	A,0FFH
	RET


;	EMM DRIVER(CPU)


@EDRDC:
	CALL	@EADR+@R
@EDRDC1:
	INI
	INC	B
	INI
	INC	B
	INI
	INC	B
	INI
	INC	B
	DEC	A
	JR	NZ,@EDRDC1
	XOR	A
	RET

@EDWTC:
	CALL	@EADR+@R
@EDWTC1:
	INC	B
	OUTI
	INC	B
	OUTI
	INC	B
	OUTI
	INC	B
	OUTI
	DEC	A
	JR	NZ,@EDWTC1
	XOR	A
	RET

@EADR:
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,(_MAXCYL-1)
	LD	C,0
	ADD	HL,BC
	LD	B,00DH
	OUT	(C),C
	INC	C
	OUT	(C),L
	INC	C
	OUT	(C),H
	INC	C
	EX	DE,HL
	POP	DE
	INC	DE
	XOR	A
	RET

@UPSUB:
	LD	L,10
@UPSUB1:
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	EXX
	IN	A,(C)
	INC	BC
	EXX
	OUT	(C),A
	INC	BC
	DEC	L
	JP	NZ,@UPSUB1+@R
	RET

@CPUE:
INITE:
	DS	0CC06H-INITE
