
;	LSX-Dodgers DIO

DRDX:
	CALL	DRDY
	RET	Z
	CALL	DRDX1		;データバッファの情報を保存
	RET	C
	PUSH	HL
	PUSH	BC
	PUSH	DE
	LD	HL,(_DTBUF)
	CALL	DRD_DPB
	CALL	C,DRDNE
POP_DE_BC_HL_RET:
	POP	DE
	POP	BC
	POP	HL
	RET

DRDN:
	XOR	A
DRDN1:	NOP			;自己書き換え NOP / SCF
	JR	NC,DRDX
DRDNX:
	CALL	DRDY
	RET	Z
DRDX1:				;データバッファの情報を保存
	CALL	DWTX
	LD	(_DBSEC),DE
	LD	A,(_DRV)
	LD	(_DBDRV),A
	CALL	_CHGDRV
	RET	NC
DRDNE:
	SBC	A,A
	LD	(_DBDRV),A
	RET

DRDY:
	PUSH	HL
	LD	A,(_DRV)
	LD	HL,_DBDRV
	XOR	(HL)
	JR	NZ,POP_HL_RET

	LD	HL,(_DBSEC)
	SBC	HL,DE		;CF=0
POP_HL_RET:
	POP	HL
	RET

DWTX:
	LD	A,(_WTDBF)
	OR	A
	RET	Z
	XOR	A
	LD	(_WTDBF),A

	PUSH	HL
	PUSH	BC
	PUSH	DE
	LD	A,(_DBDRV)
	CALL	_CHGDRV
	LD	DE,(_DBSEC)
	LD	HL,(_DTBUF)
	CALL	NC,DWT_DPB
	JR	POP_DE_BC_HL_RET

RDFATX:
	PUSH	HL
	LD	A,(_DRV)
	LD	HL,_FATDRV
	XOR	(HL)
	JR	Z,POP_HL_RET

	PUSH	BC
	PUSH	DE
	PUSH	IX
	CALL	WTFATX
	JR	C,POP_IX_DE_BC_HL_RET

	XOR	A
	LD	(_FATIX),A
	LD	A,(_DRV)
	LD	(_FATDRV),A
	CALL	_RDFAT
RDFATE2:
	JR	NC,POP_IX_DE_BC_HL_RET
	SBC	A,A		;A=0xFF
	LD	(_FATDRV),A
POP_IX_DE_BC_HL_RET:
	POP	IX
	JR	POP_DE_BC_HL_RET

WTFATX:
	LD	A,(_WTFATF)
	OR	A
	RET	Z
	PUSH	HL
	LD	A,(_FATDRV)
	LD	HL,_DBDRV
	XOR	(HL)
	CALL	Z,DWTX
	JR	C,POP_HL_RET
	PUSH	BC
	PUSH	DE
	PUSH	IX
	CALL	WTFAT
	XOR	A
	LD	(_WTFATF),A
	JR	POP_IX_DE_BC_HL_RET

NCL1:
	LD	A,D
	OR	E
	SCF
	RET	Z

	PUSH	HL
	LD	A,D
	AND	8		;DE(targetCluster >= 0x800)
	LD	(NCLPAT+2),A
	LD	HL,(_FATDRV)	;L=_FATDRV H=FATIX
	CP	H
	LD	A,(_DRV)	;この間でフラグは変化しない
	LD	(NCLPAT+1),A	;
	JR	NZ,NCL2		;FATIXが違う
	CP	L
	JR	NZ,NCL2		;ドライブが違う
	POP	HL
	RET
NCL2:
	PUSH	BC
	PUSH	DE
	PUSH	IX
	CALL	WTFATX
	JR	C,POP_IX_DE_BC_HL_RET
NCLPAT:	LD	BC,0
	LD	(_FATDRV),BC
	CALL	RDFATS
	JR	RDFATE2

NCL3:
	RES	3,D
	LD	L,E
	LD	H,D
	SRL	H	;
	RR	L	;HLA=HLA/2
	RRA		;
	LD	BC,(_FATBF)
	ADD	HL,DE
	ADD	HL,BC
	RLA
	RET

GNCL:
	CALL	NCL1		;GET NEXT CLUSTER
	RET	C
	PUSH	BC
	PUSH	HL
	CALL	NCL3
	JR	C,GNC1
	LD	E,(HL)
	INC	HL
	LD	A,(HL)
	AND	00FH
	LD	D,A
	POP	HL
	POP	BC
	RET
GNC1:
	LD	A,(HL)
	INC	HL
	LD	D,(HL)
	LD	B,4
GNC1L:
	SRL	D	;DA=DA/2
	RRA		;
	DJNZ	GNC1L

	LD	E,A
	POP	HL
	POP	BC
	AND	A
	RET

SNCL:
	CALL	NCL1
	RET	C
	LD	A,1
	LD	(_WTFATF),A
;				SET NEXT CLUSTER
	PUSH	HL
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	NCL3
	POP	DE
;CF=ODD
	LD	A,(HL)
	LD	(HL),E
	JR	C,SNC1
;EVEN
;M[0] = E
;M[1] = (M[1] & 0xF0) | (D & 0x0F)
	INC	HL
	RRD		;M={A[3:0],E[3:0]}
	LD	A,D
	JR	SNC11
SNC1:
;ODD
;HL[0] = (HL[0]&0x0F) | (E<<4)
;HL[1] = DE>>4
	RLD		;M={D[3:0],E[7:4]}
	INC	HL
	LD	(HL),D	;M={D[3:0],E[7:4]}
SNC11:
	RLD		;M={M[7:4],D[3:0]}
	OR	A
	POP	DE
	POP	BC
	POP	HL
	RET


;------------------------
;次のセクタを探す際に「1クラスタの論理セクタ数」を掛けてまとめて扱う
;in
;	DE : セクタ番号
;out
;	DE : まとめた値
;note
;	「FAT12」最大12ビット + 「1クラスタの論理セクタ数」4ビットで計16ビットとして扱う
;------------------------
GNCLX:
	LD	A,E
	INC	A
NCPAT:	AND	0	;自己書き換え　1クラスタの論理セクタ数-1
	JR	Z,GNCLX1
	INC	DE	;クラスタを跨がない場合は単純に+1する
	RET
GNCLX1:
	CALL	DIVSPC	;クラスタを跨ぐ場合は
	CALL	_GNCL	;次のクラスタを探す

;------------------------
;クラスタと「1クラスタの論理セクタ数」を掛ける
;in
;	DE : セクタ番号
;out
;	DE : 掛けられた値
;note
;	DE以外のレジスタとフラグは保持する
;------------------------
MULSPC:
	PUSH	AF
	EX	DE,HL
SPCPAT:	LD	A,1		;自己書き換え（1クラスタの論理セクタ数）
CLDBL:
	RRCA			;->CF
	JR	C,CLDBL_RET
	ADD	HL,HL		;*2
	JR	CLDBL
CLDBL_RET:
	EX	DE,HL
POP_AF_RET
	POP	AF
	RET

;------------------------
;「1クラスタの論理セクタ数」を割って論理セクタだけに戻す
;in
;	DE : 掛けられた値
;out
;	DE : セクタ番号
;note
;	DE以外のレジスタとフラグは保持する
;------------------------
DIVSPC:
	PUSH	AF
	LD	A,(SPCPAT+1)	;1クラスタの論理セクタ
CLDIV1:
	RRCA			;->CF
	JR	C,POP_AF_RET
	SRL	D		;DE=DE/2
	RR	E		;
	JR	CLDIV1

RDFAT0X:
	CALL	RDFATS
	RET	C
	CP	(IX+1)		;DPB_01_FATID
	RET	Z
	SCF
	RET

RDFAT:
	LD	A,(_DRV)
	CALL	CHGDRVR
	RET	C
	LD	A,(IX+012H)	;DPB_12_DEVNO
	CP	087H
	JR	NZ,RDFAT0X

	LD	A,07FH
	LD	(CHECK),A	;A=07FH
	BIT	0,(IX+00AH)	;DPB_0A_FDMODE
	JR	Z,X2HX
E2DX:
	LD	HL,M2D
	CALL	DRDF
	JR	C,E2HX
	CP	0FDH		;2D
	RET	Z
	CP	0F9H		;2DD9
	JP	Z,E2DD9
	CP	0FBH		;2DD8
	SCF
	RET	NZ		;NOT 2DD
	LD	HL,M2DD8
CHGTYPE:
	PUSH	DE
	CALL	CHGDV
	LD	A,(_DRIVE)
	AND	3
	OR	080H
	LD	L,A
	LD	H,_CYL0/256
	LD	A,0FFH
	LD	(HL),A
	POP	DE
	RET

CHGDV:
	PUSH	IX
	POP	DE
	LDI
	LDI
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	LD	BC,12
	LDIR
CHGDV2:
	LD	A,0FFH
	LD	(_DSK),A
	LD	A,(_DRV)
	JP	_CHGDRV

E2HX:
	INC	A
	RET	Z
	LD	HL,CHECK
	RLC	(HL)
	RET	C
X2HX:
	LD	HL,M2HD
	LD	DE,0
	CALL	DRDD
	JR	NC,X2HX1
	INC	A
	RET	Z
	LD	HL,CHECK
	RLC	(HL)
	RET	C
	JR	E2DX
X2HX1:
	INC	HL
	LD	A,(HL)
	LD	B,0FEH
	CP	00CH
	JR	Z,X2HDE
	LD	B,0F8H
	SUB	020H		;020H
	JR	Z,X2HDE
	INC	A		;01FH
	JR	Z,X2HDE
	INC	A		;01EH
	JR	NZ,C2HX		;2HD/2HC/2HQ
X2HS:
	LD	HL,M2HS
	CALL	X2HSS
	RET	C
	LD	HL,(_FATBF)
	INC	HL
	LD	A,(HL)		;FATBF+1
	CP	80
	CCF
	RET	NC
	CP	90
	RET	NC
	LD	(IX+00CH),A	;DPB_0C_MAXCYL
	SUB	80
	ADD	A,A	;*2
	LD	C,A
	ADD	A,A	;*4
	ADD	A,A	;*8
	ADD	A,A	;*16
	ADD	A,C	;*18
	ADD	A,098H
	LD	(IX+8),A	;DPB_08_MAXCL_LOW
	LD	A,5
	ADC	A,0
	LD	(IX+9),A	;DPB_08_MAXCL_HIGH
	JR	CHGDV2
X2HDE:
	LD	HL,M2HDE
	INC	HL
	LD	(HL),B
	DEC	HL
X2HSS:
	CALL	DRDF
	RET	C
	CP	(IX+1)		;DPB_01_FATID
	SCF
	RET	NZ
	CALL	INCDRD
	JR	NC,DRD_DPB
	RET

E2DD9:
	LD	HL,M2DD
	CALL	CHGTYPE
	LD	HL,(_FATBF)
INCDRD:
	INC	H
	INC	H
	INC	H
	INC	H
DRD_DPB:
	PUSH	IX
	LD	IX,(_DPB)
DRDPAT:	CALL	FDRD		;自己書き換え（ディスク読み込み）
POP_IX_RET:
	POP	IX
	RET

DRDF:
	LD	DE,(_FATPS)
DRDD:
	CALL	CHGTYPE
	LD	HL,(_FATBF)
	PUSH	HL
	CALL	DRD_DPB
	POP	HL
	RET	C
	LD	A,(HL)
	RET

C2HX:
	CALL	RDFATS
	RET	C
	CP	0FEH	;2HD
	RET	Z
	LD	HL,M2HQ
	CP	0F0H
	JR	Z,X2HQ
	CP	0F9H
	SCF
	RET	NZ
	LD	HL,M2HC
X2HQ:
	CALL	CHGTYPE
	CALL	FATS2
	INC	H	;先頭の1セクタはすでに読み込まれているのでスキップ
	INC	H
	DEC	B
	INC	DE
	JR	RDFATL
RDFATS:
	CALL	FATS
	RET	C
RDFATL:
	PUSH	BC
	CALL	DRD_DPB
	POP	BC
	RET	C
	DJNZ	RDFATL
	LD	HL,(_FATBF)
	LD	A,(HL)
	RET

DWT_DPB:
	PUSH	IX
	LD	IX,(_DPB)
DWTPAT:	CALL	FDWT		;自己書き換え（ディスク書き込み）
	JR	POP_IX_RET

_GNCL:
GNCPAT:	JP	GNCL		;自己書き換え

_SNCL:
SNCPAT:	JP	SNCL		;自己書き換え

WTFAT:
	CALL	FATS
	CALL	NC,WTFATL
	RET	C
	BIT	7,(IX+00FH)	;DPB_0F_BPS
	RET	Z		;予備FATが無い
	CALL	FATS2
	LD	A,(IX+0)	;DPB_00_FATLN
WTFAT2:
	INC	DE
	DEC	A
	JR	NZ,WTFAT2
WTFATL:
	PUSH	BC
	CALL	DWT_DPB
	POP	BC
	RET	C
	DJNZ	WTFATL
	RET
;------------------------
;FATのセットアップ
;out
;	B  : FATセクタ数
;	DE : FAT先頭セクタ番号
;	HL : FATバッファポインタ
;	CF : 1=ドライブ切り替えエラー
;note
;	FATサイズがFATバッファを超える場合は
;	対象クラスタ領域==(_FATIX)によって
;	FATの前半3KBと後半3KBを切り替える
;------------------------
FATS:
	LD	A,(_FATDRV)
	CALL	CHGDRVR		;ドライブ切り替え
	RET	C
FATS2:
	LD	DE,(_FATPS)	;fat sector pos
	LD	HL,(_FATBF)
FATSX:	JP	FATS_FAT12	;自己書き換え

FATS_FAT12:
	LD	B,(IX+00FH)	;DPB_0F_BPS
	LD	A,12		;3072 / 256 FATバッファに読み込めるセクタ数
L_FATS2:
	RR	B		;->CF
	JR	C,E_FATS2
	RRA			;CF=0 なので A=A/2
	JR	L_FATS2
E_FATS2:
	LD	B,(IX+0)	;DPB_00_FATLN
	CP	B
	RET	NC		;DPB_00_FATLN x sectorSize < fatBufSize
;targetCluster
	LD	C,A		;C = skipCount
	LD	A,(_FATIX)	;(targetCluster >= 0x800)
	OR	A
	JR	Z,FATS3
;(targerCluster>=0x800)
FAT_SKP:
	INC	DE		;FATPS++
	DEC	B		;FATSIZE--
	DEC	C		;skipCnt--
	JR	NZ,FAT_SKP
	LD	C,B
FATS3:
	LD	B,C		;3 or 6 or 12
	XOR	A
	RET

CHGDRV:
	AND	07FH
	PUSH	HL
	LD	HL,_DSK
	CP	(HL)
	JR	Z,CHGDRVE
CHGDRV1:
	PUSH	IX
	CALL	CHGDRV0
	LD	A,(_DSK)
	POP	IX
CHGDRVE:
	POP	HL
	RET

CHGDRV0:
	LD	L,A
	CALL	_GETDPB
	RET	C

	LD	(_DPB),IX
	LD	A,L
	LD	(_DSK),A

	DI
	LD	(SPPAT2+1),SP

	LD	SP,IX

	POP	HL		;L=DPB_00_FATLN H=DPB_01_FATID
	POP	HL		;DPB_02_DRD
	LD	(DRDPAT+1),HL

	POP	HL
	LD	(DWTPAT+1),HL	;DPB_04_DWT

	POP	HL		;L=DPB_06_ADDCL H=DPB_07_SECPCL
	LD	A,L
	LD	(CLSPAT+1),A
	LD	A,H
	LD	(SPCPAT+1),A	;1クラスタの論理セクタ数
	DEC	A
	LD	(NCPAT+1),A

	POP	HL		;DPB_08_MAXCL
	LD	A,L
	LD	(CLPAT2+1),A
	LD	A,H
	LD	(CLPAT1+1),A
	DEC	HL
	LD	(MAXCLP+1),HL

	POP	HL		;L=DPB_0A_FDMODE H=DPB_0B_MAXDIR
	LD	A,L
	OR	0FEH
	LD	(FDMODE+1),A
	RLCA
	AND	3
	LD	(FSPAT+1),A
	LD	A,H
	LD	(RTEPAT+1),A	;DPB_0B_MAXDIR

	POP	HL		;L=DPB_0C_MAXCYL H=DPB_0D_MAXSEC
	LD	A,L
	LD	(_MAXCYL),A
	LD	A,H
	LD	(MAXSEC+1),A

	POP	HL		;L=DPB_0E_FATPS H=DPB_0F_BPS
	LD	A,H		;DPB_0F_BPS
	AND	7
	LD	(SECSZ+2),A	;1セクタのサイズの上位1バイト 0x400 - 0x200 - 0x100
	DEC	A		;1セクタ512 2D/2DD/2HC/2HQ(2HD1.44MB)等
				;1セクタ1024 2HD/EMM/BRAM
				;1セクタ256 GRAM等
				;1024	512	256
	LD	(SECSZM1+2),A	;3	1	0	1セクタのサイズ-1したものの上位1バイト (0x3FF - 0x1FF - 0xFF)
	INC	A		;4	2	1
	ADD	A,A		;8	4	2
	ADD	A,A		;010H	8	4
	ADD	A,A		;020H	010H	8
	LD	(SDECPAT+1),A	;1セクタ辺りのディレクトリエントリ数
	DEC	A		;01FH	00FH	7
	LD	(SDECM1+1),A	;1セクタ辺りのディレクトリエントリ数-1

	LD	H,0
	LD	(_FATPS),HL

	POP	HL		;L=DPB_10_DIRPS H=DPB_11_MINSEC
	LD	A,H
	LD	(SECPAT+1),A
	LD	H,0
	LD	(_DIRPS),HL

	POP	HL		;L=DPB_12_DEVNO H=DPB_13_UNITNO
	LD	A,H
	LD	(_DRIVE),A

	POP	HL		;DPB_14_EXTRA
	LD	A,H
	OR	L
	JR	NZ,EXTRA
	LD	HL,GNCL		;FAT12(default)
	LD	(GNCPAT+1),HL
	LD	HL,SNCL
	LD	(SNCPAT+1),HL
	LD	HL,FATS_FAT12
	JR	EXTRA1
EXTRA:
	LD	(GNCPAT+1),HL
	INC	HL
	INC	HL
	INC	HL
	LD	(SNCPAT+1),HL
	INC	HL
	INC	HL
	INC	HL
EXTRA1:
	LD	(FATSX+1),HL

SPPAT2:	LD	SP,0		;元のSPを復元
	EI
	XOR	A
	RET

GETDPBD:
	EX	(SP),IX
	PUSH	IX
	LD	A,(_DRV)
	JR	GETDPB1

CHGDRVR:
	CALL	_CHGDRV
	RET	C
	LD	A,(_DSK)
GETDPB1:
	JP	_GETDPB

GETDPB:
	CP	8
	CCF
	RET	C
	ADD	A,A	;*2
	ADD	A,A	;*4
	ADD	A,A	;*8
	ADD	A,A	;*16
	ADD	A,A	;*32
;	LD	IXL,A		;OHM-Z80固有マクロ
	DB	0DDH		;Z80未定義命令
	LD	L,A		;
;	LD	IXH,DEVICE/256	;OHM-Z80固有マクロ
	DB	0DDH		;Z80未定義命令
	LD	H,DEVICE/256	;
	LD	A,(IX+0)	;DPB_00_FATLN
	CP	001H		;A=0 THEN CF=1
	RET

FFLUSH:
	PUSH	AF
	LD	A,0FFH
	LD	(SFILE),A
	CALL	DWTX
	LD	A,0FFH
	LD	(_DBDRV),A
	CALL	WTFATX
	XOR	A
	LD	(_FATIX),A
	CPL
	LD	(_FATDRV),A
	POP	AF
	RET

